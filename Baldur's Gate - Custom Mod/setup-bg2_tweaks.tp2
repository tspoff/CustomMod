// GAME_IS returns totlm for iwdee; will have to update my req_pred once WeiDU has an iwdee entry
// currently using howparty.2da file detection for iwdee

BACKUP ~bg2_tweaks/backup~ // location to store files for
AUTHOR ~pcamagna@yahoo.com~ // email address displayed if install fails

ALWAYS

  ACTION_IF NOT VARIABLE_IS_SET cd_always THEN BEGIN

    OUTER_SET cd_always = 1 // just do this once per install

    ACTION_FOR_EACH file IN iplot01k.itm iplot04g.itm iplot04h.itm iplot04i.itm xr2400.are xr2600.are BEGIN
      DISABLE_FROM_KEY "%file%"
    END

    // Borrowed from Edwin Romance: convert strings to UTF-8 for BGEE/BG2EE
    INCLUDE ~%MOD_FOLDER%/lib/handle_charsets.tpa~
    ACTION_DEFINE_ARRAY cdnoconvert BEGIN END
    ACTION_DEFINE_ARRAY cdreload BEGIN ~0ikmorph~ ~description_updates~ ~ikmorph~ ~p#khalid~ ~setup~ END
    LAF HANDLE_CHARSETS
      INT_VAR
        infer_charset = 1
      STR_VAR
        tra_path = EVAL ~%MOD_FOLDER%/languages~
        // charset_table = cdcharsets // Included for illustrative purposes.
        noconvert_array = cdnoconvert
        reload_array = cdreload
    END
  
  END

END

VERSION ~v16~

README ~bg2_tweaks/readme-bg2tweaks.html~

AUTO_TRA ~bg2_tweaks/languages/%s~

LANGUAGE
  ~English~
  ~english~
  ~bg2_tweaks/languages/english/description_updates.tra~
  ~bg2_tweaks/languages/english/setup.tra~
LANGUAGE
  ~Czech (Translation by Razfallow and Vlasák)~
  ~czech~
  ~bg2_tweaks/languages/english/setup.tra~
  ~bg2_tweaks/languages/czech/setup.tra~
LANGUAGE
  ~Francais (Translation by Anomaly, Elgaern the Dragon Slayer, Isaya and elminster)~
  ~french~
  ~bg2_tweaks/languages/english/description_updates.tra~
  ~bg2_tweaks/languages/english/setup.tra~
  ~bg2_tweaks/languages/french/description_updates.tra~
  ~bg2_tweaks/languages/french/setup.tra~
LANGUAGE
  ~Deutsch (Uebersetzung von Leonardo Watson, Caswallon und Jester)~
  ~german~
  ~bg2_tweaks/languages/english/description_updates.tra~
  ~bg2_tweaks/languages/english/setup.tra~
  ~bg2_tweaks/languages/german/description_updates.tra~
  ~bg2_tweaks/languages/german/setup.tra~
LANGUAGE
  ~Italiano (translation by Andrea Colombo; original EoU by Antonio Favata)~
  ~italian~
  ~bg2_tweaks/languages/english/description_updates.tra~
  ~bg2_tweaks/languages/english/setup.tra~
  ~bg2_tweaks/languages/italian/description_updates.tra~
  ~bg2_tweaks/languages/italian/setup.tra~
LANGUAGE
  ~Korean (Translation by web2air)~
  ~korean~
  ~bg2_tweaks/languages/english/setup.tra~
  ~bg2_tweaks/languages/korean/setup.tra~
LANGUAGE
  ~Polski (Translation by yarpen, Artanis, dragionian, Evendur, Grjgori, and Neminus)~
  ~polish~
  ~bg2_tweaks/languages/english/setup.tra~
  ~bg2_tweaks/languages/polish/setup.tra~
LANGUAGE
  ~Russian (Translation by Creepin, Domi, Kulyok, Serdrick, and aerie.ru)~
  ~russian~
  ~bg2_tweaks/languages/english/setup.tra~
  ~bg2_tweaks/languages/russian/setup.tra~
LANGUAGE
  ~Espanol (traducido por Clan DLAN)~
  ~spanish~
  ~bg2_tweaks/languages/english/description_updates.tra~
  ~bg2_tweaks/languages/english/setup.tra~
  ~bg2_tweaks/languages/spanish/description_updates.tra~
  ~bg2_tweaks/languages/spanish/setup.tra~
LANGUAGE
  ~Chinese (translated by Miranda)~
  ~chinese~
  ~bg2_tweaks/languages/english/description_updates.tra~
  ~bg2_tweaks/languages/english/setup.tra~
  ~bg2_tweaks/languages/chinese/description_updates.tra~
  ~bg2_tweaks/languages/chinese/setup.tra~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Remove Helmet Animations                         \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @1000 DESIGNATED 10
GROUP @11

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt00010.g3~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_SHORT 0x1c itemtype ELSE 0
  READ_SHORT 0x22 anim ELSE 0
  PATCH_IF (itemtype = 7) && ! (anim=0x5759 || anim=0x575a) BEGIN
    WRITE_SHORT 0x22 0
  END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Change Imoen's Avatar to Mage                    \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @2000 DESIGNATED 20
GROUP @11
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee~ @25

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt00020.g3~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~ // Imoen animation adjustments
  READ_LONG   0x08 "name"  ELSE 0
  READ_SHORT  0x28 "anim"  ELSE 0
  READ_BYTE  0x273 "class" ELSE 0
  PATCH_IF ((("%name%" = 16453) OR ("%name%" = 16454)) AND ("%class%" = 13) AND ("%anim%" = 25360)) BEGIN
    WRITE_SHORT 0x28 25104
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Change Nalia's Avatar to Thief                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @3000 DESIGNATED 30
GROUP @11
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee~ @25

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt00030.g3~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~ // Nalia animation adjustments
  READ_LONG   0x08 "name"  ELSE 0
  READ_SHORT  0x28 "anim"  ELSE 0
  READ_BYTE  0x273 "class" ELSE 0
  PATCH_IF ((("%name%" = 9102) OR ("%name%" = 9103)) AND ("%class%" = 13) AND ("%anim%" = 25104)) BEGIN
    WRITE_SHORT 0x28 25360
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Change Viconia's Skin Color to Dark Blue         \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @4000 DESIGNATED 40
GROUP @11
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bgee bg2ee~ @25

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt00040.g3~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~ // Viconia skin color change
  READ_LONG 0x08 "name" ELSE 0
  PATCH_IF (("%name%" = 6132) OR ("%name%" = 9489) OR ("%name%" = 9508)) BEGIN
    WRITE_BYTE 0x2F 96
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Avatar Morphing Script                           \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @5000 DESIGNATED 50
GROUP @11
REQUIRE_PREDICATE NOT MOD_IS_INSTALLED ~1pp/1pp.tp2~ ~113~ @24 // don't install if 1pp smart avatar/armor switching installed

// adds ToB trigger/actions to SoA
INCLUDE ~bg2_tweaks/lib/tob2soa.tpa~

COMPILE ~bg2_tweaks/baf/0ikmorph.baf~
        ~bg2_tweaks/baf/ikmorph.baf~
        ~bg2_tweaks/dlg/ikmorph.d~

MOVE ~override/0ikmorph.bcs~ ~scripts/0ikmorph.bs~

APPEND ~scrpdesc.2da~ ~0IKMorph IKMorph1 IKMorph2~

COPY_EXISTING ~scrpdesc.2da~ ~override~
  REPLACE ~IKMorph1~ @5001
  REPLACE ~IKMorph2~ @5002

COPY ~bg2_tweaks/cre/ikmorph.cre~  ~override~
     ~bg2_tweaks/eff/ikmorph.eff~  ~override~
     ~bg2_tweaks/spl/ikmorph.spl~  ~override~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Weapon Animation Tweaks                          \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @6000 DESIGNATED 60
GROUP @11

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt00060.g3~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~ // animation adjustments
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
    READ_BYTE  0x18 "flags"
    READ_ASCII 0x22 "anim" (2)
    READ_BYTE  0x31 "prof"
    READ_LONG  0x64 "abil_off"
    READ_SHORT 0x68 "abil_num"
    PATCH_IF ((("%prof%" = 100) OR ("%prof%" = 101)) AND ("%anim%" STRING_COMPARE_CASE "ms" = 0)) BEGIN
      WRITE_ASCII 0x22 "MC"
    END
    PATCH_IF ((("%flags%" BAND 0b00000010) = 0b00000000) AND   // one-handed...
              (("%anim%" STRING_COMPARE_CASE "cl" = 0) OR      // club
               ("%anim%" STRING_COMPARE_CASE "mc" = 0) OR      // mace
               ("%anim%" STRING_COMPARE_CASE "ms" = 0))) BEGIN // or morningstar
      FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN
        READ_BYTE ("%abil_off%" + ("%index%" * 0x38)) "type"
        PATCH_IF ("%type%" = 1) BEGIN
          WRITE_SHORT ("%abil_off%" + 0x2C + ("%index%" * 0x38)) 50 // Overhead
          WRITE_SHORT ("%abil_off%" + 0x2E + ("%index%" * 0x38)) 50 // Backhand
          WRITE_SHORT ("%abil_off%" + 0x30 + ("%index%" * 0x38))  0 // Thrusting
        END
      END
    END ELSE
    PATCH_IF ((("%anim%" STRING_COMPARE_CASE "sp" = 0) AND (("%flags%" BAND 0b00000010) = 0b00000010)) OR     // two-handed spears
              (("%anim%" STRING_COMPARE_CASE "ss" = 0) AND (("%flags%" BAND 0b00000010) = 0b00000000))) BEGIN // one-handed short swords
      FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN
        READ_BYTE ("%abil_off%" + ("%index%" * 0x38)) "type"
        PATCH_IF ("%type%" = 1) BEGIN
          WRITE_SHORT ("%abil_off%" + 0x2C + ("%index%" * 0x38)) 10 // Overhead
          WRITE_SHORT ("%abil_off%" + 0x2E + ("%index%" * 0x38)) 20 // Backhand
          WRITE_SHORT ("%abil_off%" + 0x30 + ("%index%" * 0x38)) 70 // Thrusting
        END
      END
    END ELSE
    PATCH_IF (("%anim%" STRING_COMPARE_CASE "qs" = 0) AND (("%flags%" BAND 0b00000010) = 0b00000010)) BEGIN // two-handed staves
      FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN
        READ_BYTE ("%abil_off%" + ("%index%" * 0x38)) "type"
        PATCH_IF ("%type%" = 1) BEGIN
          WRITE_SHORT ("%abil_off%" + 0x2C + ("%index%" * 0x38)) 20 // Overhead
          WRITE_SHORT ("%abil_off%" + 0x2E + ("%index%" * 0x38)) 70 // Backhand
          WRITE_SHORT ("%abil_off%" + 0x30 + ("%index%" * 0x38)) 10 // Thrusting
        END
      END
    END
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Icewind Dale Casting Graphics                    \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @7000 DESIGNATED 70
GROUP @11
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bgee bg2ee~ @25

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt00070.g3~

COPY ~bg2_tweaks/bam/cgabjura.bam~ ~override~
     ~bg2_tweaks/bam/cgaltera.bam~ ~override~
     ~bg2_tweaks/bam/cgconjur.bam~ ~override~
     ~bg2_tweaks/bam/cgdivina.bam~ ~override~
     ~bg2_tweaks/bam/cgenchan.bam~ ~override~
     ~bg2_tweaks/bam/cgillusi.bam~ ~override~
     ~bg2_tweaks/bam/cginvoca.bam~ ~override~
     ~bg2_tweaks/bam/cgnecrom.bam~ ~override~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Restore SoA Logo for ToB Games                             \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @8000 DESIGNATED 80
GROUP @11
REQUIRE_PREDICATE GAME_IS ~tob~ @25

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt00080.g3~

//Force the game to use the SoA files in the biffs
MOVE ~override/loadcntr.bam~ ~bg2_tweaks/backup~
     ~override/gprogbar.mos~ ~bg2_tweaks/backup~
     ~override/gtrspsk.mos~  ~bg2_tweaks/backup~
     ~override/gtrspsk2.mos~ ~bg2_tweaks/backup~
     ~override/stoneopt.mos~ ~bg2_tweaks/backup~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Remove icons added by equipping items            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @9000 DESIGNATED 90
GROUP @11

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt00090.g3~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = 142 END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Commoners use drab colors                        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @10000 DESIGNATED 100
GROUP @11
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bgee bg2ee~ @25

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt00100.g3~

COPY_EXISTING ~randcolr.2da~ ~override~
  SET_2DA_ENTRY_LATER ~randcolr~  3  9 ~114~ // AthPeasantMajor
  SET_2DA_ENTRY_LATER ~randcolr~  4  9 ~107~
  SET_2DA_ENTRY_LATER ~randcolr~  5  9  ~36~
  SET_2DA_ENTRY_LATER ~randcolr~  6  9  ~37~
  SET_2DA_ENTRY_LATER ~randcolr~  7  9  ~38~
  SET_2DA_ENTRY_LATER ~randcolr~  8  9  ~39~
  SET_2DA_ENTRY_LATER ~randcolr~  9  9  ~40~
  SET_2DA_ENTRY_LATER ~randcolr~ 10  9  ~41~
  SET_2DA_ENTRY_LATER ~randcolr~ 11  9  ~42~
  SET_2DA_ENTRY_LATER ~randcolr~ 12  9  ~43~
  SET_2DA_ENTRY_LATER ~randcolr~  3 10  ~63~ // AthPeasantMinor
  SET_2DA_ENTRY_LATER ~randcolr~  4 10  ~64~
  SET_2DA_ENTRY_LATER ~randcolr~  5 10  ~65~
  SET_2DA_ENTRY_LATER ~randcolr~  6 10  ~66~
  SET_2DA_ENTRY_LATER ~randcolr~  7 10  ~87~
  SET_2DA_ENTRY_LATER ~randcolr~  8 10  ~91~
  SET_2DA_ENTRY_LATER ~randcolr~  9 10  ~92~
  SET_2DA_ENTRY_LATER ~randcolr~ 10 10  ~93~
  SET_2DA_ENTRY_LATER ~randcolr~ 11 10  ~94~
  SET_2DA_ENTRY_LATER ~randcolr~ 12 10  ~95~
  SET_2DA_ENTRY_LATER ~randcolr~  3 13 ~114~ // TradePeasantMajor
  SET_2DA_ENTRY_LATER ~randcolr~  4 13 ~107~
  SET_2DA_ENTRY_LATER ~randcolr~  5 13  ~36~
  SET_2DA_ENTRY_LATER ~randcolr~  6 13  ~37~
  SET_2DA_ENTRY_LATER ~randcolr~  7 13  ~38~
  SET_2DA_ENTRY_LATER ~randcolr~  8 13  ~39~
  SET_2DA_ENTRY_LATER ~randcolr~  9 13  ~40~
  SET_2DA_ENTRY_LATER ~randcolr~ 10 13  ~41~
  SET_2DA_ENTRY_LATER ~randcolr~ 11 13  ~42~
  SET_2DA_ENTRY_LATER ~randcolr~ 12 13  ~43~
  SET_2DA_ENTRY_LATER ~randcolr~  3 14  ~63~ // TradePeasantMinor
  SET_2DA_ENTRY_LATER ~randcolr~  4 14  ~64~
  SET_2DA_ENTRY_LATER ~randcolr~  5 14  ~65~
  SET_2DA_ENTRY_LATER ~randcolr~  6 14  ~66~
  SET_2DA_ENTRY_LATER ~randcolr~  7 14  ~87~
  SET_2DA_ENTRY_LATER ~randcolr~  8 14  ~91~
  SET_2DA_ENTRY_LATER ~randcolr~  9 14  ~92~
  SET_2DA_ENTRY_LATER ~randcolr~ 10 14  ~93~
  SET_2DA_ENTRY_LATER ~randcolr~ 11 14  ~94~
  SET_2DA_ENTRY_LATER ~randcolr~ 12 14  ~95~
  SET_2DA_ENTRIES_NOW ~randcolr~ 1
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Better Icons                                     \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @11000 DESIGNATED 110
GROUP @11

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt00110.g3~

COPY ~bg2_tweaks/bam/cdgchan.bam~  ~override~
     ~bg2_tweaks/bam/cdgelchn.bam~ ~override~
     ~bg2_tweaks/bam/cdgleat.bam~  ~override~
     ~bg2_tweaks/bam/gclck01.bam~  ~override~
     ~bg2_tweaks/bam/gclck02.bam~  ~override~
     ~bg2_tweaks/bam/gleat01.bam~  ~override~
     ~bg2_tweaks/bam/iclck09.bam~  ~override~
     ~bg2_tweaks/bam/iclck10.bam~  ~override~
     ~bg2_tweaks/bam/iclck11.bam~  ~override~
     ~bg2_tweaks/bam/iclck12.bam~  ~override~
     ~bg2_tweaks/bam/iclck13.bam~  ~override~
     ~bg2_tweaks/bam/iclck15.bam~  ~override~
     ~bg2_tweaks/bam/iclck16.bam~  ~override~
     ~bg2_tweaks/bam/iclck17.bam~  ~override~

ACTION_CLEAR_ARRAY cd_armor
INCLUDE ~bg2_tweaks/lib/common_armor_list.tpa~ // common armor types

ACTION_PHP_EACH cd_armor AS item => type BEGIN

  ACTION_IF (((type = 130) OR (type = 203) OR (type = 303) OR (type = 330)) AND (FILE_EXISTS_IN_GAME "%item%.itm")) BEGIN // chain mail

    COPY_EXISTING ~%item%.itm~ ~override~
      READ_ASCII 0x44 icon ELSE 0
      PATCH_IF (("%icon%" STRING_COMPARE_CASE "gchan01" = 0) OR
                ("%icon%" STRING_COMPARE_CASE "_gchan01" = 0)) BEGIN
        WRITE_ASCII 0x44 ~cdgchan~ #8
      END
      BUT_ONLY

  END

  ACTION_IF ((type = 304) AND (FILE_EXISTS_IN_GAME "%item%.itm")) BEGIN // elven chain

    COPY_EXISTING ~%item%.itm~ ~override~
      READ_ASCII 0x44 icon ELSE 0
      PATCH_IF (("%icon%" STRING_COMPARE_CASE "gchan01" = 0) OR
                ("%icon%" STRING_COMPARE_CASE "_gchan01" = 0)) BEGIN
        WRITE_ASCII 0x44 ~cdgelchn~
      END
      BUT_ONLY

  END

  ACTION_IF (((type = 200) OR (type = 300)) AND (FILE_EXISTS_IN_GAME "%item%.itm")) BEGIN // leather

    COPY_EXISTING ~%item%.itm~ ~override~
      READ_ASCII 0x44 icon ELSE 0
      PATCH_IF (("%icon%" STRING_COMPARE_CASE "gleat01" = 0) OR
                ("%icon%" STRING_COMPARE_CASE "_gleat01" = 0)) BEGIN
        WRITE_ASCII 0x44 ~cdgleat~ #8
      END
      BUT_ONLY
      
  END

END

// if Tutu, move BG2 icons to Tutu defaults
ACTION_IF FILE_EXISTS_IN_GAME ~_gleat01.bam~ THEN BEGIN

  COPY_EXISTING ~gleat01.bam~ ~override/_gleat01.bam~
                ~gchan01.bam~ ~override/_gchan01.bam~

END

// Valygar's armor should use chain graphic on avatars
ACTION_IF FILE_EXISTS_IN_GAME ~npchan.itm~ BEGIN

  COPY_EXISTING ~npchan.itm~ ~override~
    WRITE_ASCII 0x22 ~3A~ #2
    BUT_ONLY_IF_IT_CHANGES

END

ACTION_FOR_EACH item IN quiver02 quiver04 BEGIN

  ACTION_IF ((FILE_EXISTS_IN_GAME ~%item%.itm~) AND (FILE_EXISTS_IN_GAME ~iquiver0.bam~)) THEN BEGIN
  
    COPY_EXISTING ~%item%.itm~ ~override~
      WRITE_ASCII 0x3a ~iquiver0~ #8
      BUT_ONLY_IF_IT_CHANGES

  END

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Change Avatar When Wearing Robes or Armor        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @12000 DESIGNATED 120
GROUP @11
REQUIRE_PREDICATE NOT MOD_IS_INSTALLED ~1pp/1pp.tp2~ ~113~ @24 // don't install if 1pp smart avatar/armor switching installed

ACTION_FOR_EACH file IN lcarmor lcarmor2 lcarmorc lcarmorf lcarmort lcrobe lcrobe2 BEGIN

  COPY ~bg2_tweaks/spl/%file%.spl~ ~override~

END

ACTION_FOR_EACH file IN  fc_dwar2 fc_dwarf fc_el2 fc_elf fc_gnom2 fc_gnome fc_half2 fc_halfl
  fc_hfor2 fc_hforc fc_huma2 fc_human ff_dwar2 ff_dwarf ff_el2 ff_elf ff_gnom2 ff_gnome
  ff_half2 ff_halfl ff_hfor2 ff_hforc ff_huma2 ff_human fm_dwar2 fm_dwarf fm_el2 fm_elf
  fm_gnom2 fm_gnome fm_half2 fm_halfl fm_huma2 fm_human ft_dwar2 ft_dwarf ft_el2 ft_elf
  ft_gnom2 ft_gnome ft_half2 ft_halfl ft_hfor2 ft_hforc ft_huma2 ft_human lcarmor2 lcarmorc
  lcarmorf lcarmort lcrobe2 mc_dwar2 mc_dwarf mc_el2 mc_elf mc_gnom2 mc_gnome mc_half2
  mc_halfl mc_hfor2 mc_hforc mc_huma2 mc_human mf_dwar2 mf_dwarf mf_el2 mf_elf mf_gnom2
  mf_gnome mf_half2 mf_halfl mf_hfor2 mf_hforc mf_huma2 mf_human mm_dwar2 mm_dwarf mm_el2
  mm_elf mm_gnom2 mm_gnome mm_half2 mm_halfl mm_huma2 mm_human mt_dwar2 mt_dwarf mt_el2
  mt_elf mt_gnom2 mt_gnome mt_half2 mt_halfl mt_hfor2 mt_hforc mt_huma2 mt_human
BEGIN

  COPY ~bg2_tweaks/eff/%file%.eff~ ~override~

END

// make monks immune to avatar shifting
COPY_EXISTING ~spcl812.spl~ ~override~ // movement rate bonus spell
  PATCH_FOR_EACH res IN lcarmor lcrobe BEGIN
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 2 parameter1 = `0 timing = 9 STR_VAR resource = "%res%" END
  END
  BUT_ONLY

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_SHORT 0x1c itemtype ELSE 0
  PATCH_IF (itemtype = 2) BEGIN
    READ_ASCII 0x22 anim (2)
    PATCH_IF ("%anim%" STRING_COMPARE_REGEXP "[234][Ww]" = 0) BEGIN
      LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 146 target = 1 parameter1 = 1 parameter2 = 1 timing = 2 STR_VAR resource = "lcrobe" END
    END ELSE BEGIN
      LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 146 target = 1 parameter1 = 1 parameter2 = 1 timing = 2 STR_VAR resource = "lcarmor" END
    END
  END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Force all dialogue to pause                      \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @13000 DESIGNATED 130
GROUP @11

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt00130.g3~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.dlg$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x2f) THEN BEGIN // protects against invalid files
    READ_LONG  0x0c offset_state ELSE 0
    PATCH_IF (offset_state = 0x30) BEGIN // adds pause field to older dlg formats
      PATCH_FOR_EACH offset IN 0x18 0x20 0x28 BEGIN // loop through state, response trigger offsets
        READ_LONG offset loop_off
        READ_LONG (offset + 0x04) loop_num
        FOR (index = 0 ; index < loop_num ; ++index) BEGIN
          WRITE_LONG (loop_off + (index * 0x08)) (THIS + 0x04)
        END
      END
      // push all other offsets back 0x04
      PATCH_FOR_EACH offset IN 0x0c 0x14 0x18 0x20 0x28 BEGIN
        WRITE_LONG offset (THIS + 0x04)
      END
      INSERT_BYTES 0x30 0x04
    END ELSE
    PATCH_IF (offset_state = 0x34) BEGIN // otherwise just sets to pause
      WRITE_LONG 0x30 0
    END
  END
  BUT_ONLY

// pausing actually causing issues with drow duels, so:
ACTION_IF FILE_EXISTS_IN_GAME ~udlesa.bcs~ BEGIN

  COPY_EXISTING ~udlesa.bcs~ ~override~
    DECOMPILE_BCS_TO_BAF
      REPLACE_TEXTUALLY ~\(IF[%TAB% %LNL%%MNL%%WNL%]+!CombatCounter(0)[%TAB% %LNL%%MNL%%WNL%]+Global("DuelOn","AR2202",0)\)\([%TAB% %LNL%%MNL%%WNL%]+\)\(THEN\)~
        ~\1\2Global("PlayerDuelingLesaonar","GLOBAL",0)\2\3~
    COMPILE_BAF_TO_BCS
    BUT_ONLY_IF_IT_CHANGES

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Fix Boo's terrible, horrible, awful squeak       \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @14000 DESIGNATED 140
GROUP @11
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bgee bg2ee~ @25

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt00140.g3~

COPY ~bg2_tweaks/wav/gam_48.wav~ ~override~
/*
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Unique item names                                \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// unique name                                      \\\\\
/////                                                  \\\\\

BEGIN @15001 DESIGNATED 150
SUBCOMPONENT @15000
GROUP @11
REQUIRE_PREDICATE FILE_EXISTS ~bg2_tweaks/languages/%LANGUAGE%/unique.tra~ @7 // skip if tra file for language not available

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt00150.g3~

LOAD_TRA ~bg2_tweaks/languages/%LANGUAGE%/unique.tra~

INCLUDE ~bg2_tweaks/lib/unique.tpa~

/////                                                  \\\\\
///// unique name                                      \\\\\
/////                                                  \\\\\

BEGIN @15100 DESIGNATED 151
SUBCOMPONENT @15000
GROUP @11
REQUIRE_PREDICATE FILE_EXISTS ~bg2_tweaks/languages/%LANGUAGE%/uniqueplus.tra~ @7 // skip if tra file for language not available

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt00151.g3~

LOAD_TRA ~bg2_tweaks/languages/%LANGUAGE%/uniqueplus.tra~

INCLUDE ~bg2_tweaks/lib/unique.tpa~

/////                                                  \\\\\
///// generic names                                    \\\\\
/////                                                  \\\\\

BEGIN @15200 DESIGNATED 152
SUBCOMPONENT @15000
GROUP @11
REQUIRE_PREDICATE FILE_EXISTS ~bg2_tweaks/languages/%LANGUAGE%/uniquegeneric.tra~ @7 // skip if tra file for language not available

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt00152.g3~

LOAD_TRA ~bg2_tweaks/languages/%LANGUAGE%/uniquegeneric.tra~

INCLUDE ~bg2_tweaks/lib/unique.tpa~
*/
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// More Interjections                               \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @101000 DESIGNATED 1010
GROUP @13

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt01010.g3~

ACTION_IF FILE_EXISTS_IN_GAME ~mrimrin1.itm~ THEN BEGIN // imoen romance compatibility code

  COPY_EXISTING ~imoen2j.dlg~ ~override~
    DECOMPILE_DLG_TO_D
      REPLACE_TEXTUALLY ~!I[fs]ValidForPartyDialog\(ue\)?(Player\([2-6]\))~ ~!Range(Player\2,30)~
    COMPILE_D_TO_DLG
    BUT_ONLY_IF_IT_CHANGES

END

COPY_EXISTING_REGEXP GLOB ~^.+\.bcs$~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~I[fs]ValidForPartyDialog\(ue\)?(~
                      ~InParty(~
  COMPILE_BAF_TO_BCS
  IF ~16552~
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~trigger.ids~ ~override~
  REPLACE_TEXTUALLY ~0x40A8~ ~0x4043~
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Alter HP Triggers for NPC Wounded Dialogues      \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @102000 DESIGNATED 1020
GROUP @13
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee~ @25

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt01020.g3~

ACTION_IF NOT FILE_EXISTS_IN_GAME ~shoutids.ids~ THEN BEGIN // if no shoutids, make one

  COPY ~bg2_tweaks/ids/shoutids.ids~ ~override~

END

// alter triggers in dialogues...
COMPILE ~bg2_tweaks/dlg/hptriggers.d~

// and corresponding triggers in scripts
COPY_EXISTING ~edwin.bcs~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~HPPercentLT(Myself,15)~ ~HPPercentLT(Myself,50)~
  COMPILE_BAF_TO_BCS
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~jaheira.bcs~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~HPLT("Aerie",10)~ ~HPPercentLT("Aerie",50)~
    REPLACE_TEXTUALLY ~HPLT("Nalia",10)~ ~HPPercentLT("Nalia",50)~
    REPLACE_TEXTUALLY ~HPLT(Myself,15)~  ~HPPercentLT(Myself,50)~
    REPLACE_TEXTUALLY ~HPGT(Myself,14)~  ~!HPPercentLT(Myself,50)~
  COMPILE_BAF_TO_BCS
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~mazzy.bcs~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~HPPercentLT("Valygar",20)~ ~HPPercentLT("Valygar",50)~
  COMPILE_BAF_TO_BCS
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~minsc.bcs~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~HPLT(Myself,20)~    ~HPPercentLT(Myself,50)~
    REPLACE_TEXTUALLY ~HPGT(Myself,19)~    ~!HPPercentLT(Myself,50)~
    REPLACE_TEXTUALLY ~HPLT("Keldorn",20)~ ~HPPercentLT("Keldorn",50)~
  COMPILE_BAF_TO_BCS
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~nalia.bcs~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~HPPercentLT("Aerie",25)~ ~HPPercentLT("Aerie",50)~
  COMPILE_BAF_TO_BCS
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~yoshimo.bcs~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~HPLT("Mazzy",20)~ ~HPPercentLT("Mazzy",50)~
  COMPILE_BAF_TO_BCS
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Reveal wilderness areas on map before chapter 6            \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @103000 DESIGNATED 1030
GROUP @13
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee~ @25

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt01030.g3~

ACTION_IF ((FILE_EXISTS_IN_GAME ~cdbehbla.pro~) OR (GAME_IS ~bg2ee~)) THEN BEGIN // easy fix if fixed worldmap present

  COPY_EXISTING ~worldmap.wmp~ ~override~
    READ_LONG 0x30 "area_num"
    READ_LONG 0x34 "area_off"
    FOR (index = 0 ; index < area_num ; index = index + 1) BEGIN
      READ_ASCII ("%area_off%" + ("%index%" * 0xf0)) "areafile"
      PATCH_IF (("ar1700" STRING_COMPARE_CASE "%areafile%" = 0) OR     // small teeth pass
                ("ar1800" STRING_COMPARE_CASE "%areafile%" = 0) OR     // north forest
                ("ar2600" STRING_COMPARE_CASE "%areafile%" = 0)) BEGIN // forest of tethir
        READ_BYTE  ("%area_off%" + 0x30 + ("%index%" * 0xf0)) "flags"
        WRITE_BYTE ("%area_off%" + 0x30 + ("%index%" * 0xf0)) ("%flags%" BOR 0b00000010)
      END
    END
    BUT_ONLY_IF_IT_CHANGES

END ELSE BEGIN

  // adds new entrance points to wmp areas that don't have one already
  COPY_EXISTING ~ar1700.are~ ~override~
                ~ar1800.are~ ~override~
                ~ar2600.are~ ~override~
    READ_LONG 0x68 ent_off
    PATCH_IF ("ar1700.are" STRING_COMPARE_CASE ~%SOURCE_FILE%~ = 0) BEGIN
      SET x_coord = 3375
      SET y_coord = 142
    END ELSE
    PATCH_IF ("ar1800.are" STRING_COMPARE_CASE ~%SOURCE_FILE%~ = 0) BEGIN
      SET x_coord = 1325
      SET y_coord = 56
    END ELSE BEGIN
      SET x_coord = 3856
      SET y_coord = 115
    END
    WRITE_LONG 0x6c (THIS + 1)
    INSERT_BYTES  (ent_off       ) 0x68
      WRITE_ASCII (ent_off       ) ~CDExit~ // entrance name
      WRITE_SHORT (ent_off + 0x20) x_coord
      WRITE_SHORT (ent_off + 0x22) y_coord
    // fix offsets
    PATCH_FOR_EACH test_off IN 0x54 0x5c 0x60 0x68 0x6c 0x70 0x78 0x7c 0x84 0x88 0xa0 0xa8 0xb0 0xb8 0xbc 0xc0 0xc4 BEGIN  
      READ_LONG test_off val
      PATCH_IF (val < ent_off) BEGIN
        WRITE_LONG test_off (test_off + 0x68) 
      END
    END
    BUT_ONLY

  // need to add link if fixed worldmap not installed
  COPY_EXISTING ~worldmap.wmp~ ~override~
    READ_LONG  0x30 area_num
    READ_LONG  0x34 area_off
    READ_LONG  0x38 link_off
    READ_LONG  0x3c link_num
    WRITE_LONG 0x3c (link_num + 1)
    SET link_idx_nsrt = 999999
    FOR (index = 0 ; index < area_num ; ++index) BEGIN
      READ_ASCII (area_off + (index * 0xf0)) areafile
      PATCH_IF ("ar0020" STRING_COMPARE_CASE "%areafile%" = 0) BEGIN // City Gates
        SET area_to = index
      END
      PATCH_IF ("ar1800" STRING_COMPARE_CASE "%areafile%" = 0) BEGIN    // north forest
        WRITE_BYTE (area_off + 0x30 + (index * 0xf0)) (THIS | BIT1)
        SET nf_idx = index
      END
      PATCH_IF ("ar2600" STRING_COMPARE_CASE "%areafile%" = 0) BEGIN // forest of tethir
        WRITE_BYTE (area_off + 0x30 + (index * 0xf0)) (THIS | BIT1)
        SET fot_idx = index
      END
      READ_LONG  (area_off + 0x50 + (index * 0xf0)) link_idx_1
      PATCH_IF ("ar1700" STRING_COMPARE_CASE "%areafile%" = 0) BEGIN // Small Teeth Pass
        SET link_idx_nsrt = link_idx_1
        WRITE_BYTE (area_off + 0x30 + (index * 0xf0)) (THIS | BIT1)
        WRITE_LONG (area_off + 0x54 + (index * 0xf0)) (THIS + 1)
        SET stp_idx = index
      END
      PATCH_IF (link_idx_nsrt < link_idx_1) BEGIN // adjusts indices for links after the insert
        WRITE_LONG  (area_off + 0x50 + (index * 0xf0)) (link_idx_1 + 1)
      END
      READ_LONG  (area_off + 0x58 + (index * 0xf0)) link_idx_2
      PATCH_IF (link_idx_nsrt < link_idx_2) BEGIN // adjusts indices for links after the insert
        WRITE_LONG  (area_off + 0x58 + (index * 0xf0)) (link_idx_2 + 1)
      END
      READ_LONG  (area_off + 0x60 + (index * 0xf0)) link_idx_3
      PATCH_IF (link_idx_nsrt < link_idx_3) BEGIN // adjusts indices for links after the insert
        WRITE_LONG  (area_off + 0x60 + (index * 0xf0)) (link_idx_3 + 1)
      END
      READ_LONG  (area_off + 0x68 + (index * 0xf0)) link_idx_4
      PATCH_IF (link_idx_nsrt < link_idx_4) BEGIN // adjusts indices for links after the insert
        WRITE_LONG  (area_off + 0x68 + (index * 0xf0)) (link_idx_4 + 1)
      END
    END
    FOR (index2 = 0 ; index2 < link_num ; ++index2) BEGIN
      READ_LONG (link_off +        (index2 * 0xd8)) target // target index
      READ_LONG (link_off + 0x04 + (index2 * 0xd8)) exit   // exit point
      PATCH_IF ((exit = 0) AND
                ((target = stp_idx) OR
                 (target = nf_idx) OR
                 (target = fot_idx))) BEGIN
        WRITE_ASCII (link_off + 0x04 + (index2 * 0xd8)) "CDExit"   // exit point
      END
    END
    INSERT_BYTES  (link_off +        (link_idx_nsrt * 0xd8)) 0xd8
      WRITE_LONG  (link_off +        (link_idx_nsrt * 0xd8)) "%area_to%" // target index
      WRITE_ASCII (link_off + 0x04 + (link_idx_nsrt * 0xd8)) ~ExitNE~    // exit point
      WRITE_LONG  (link_off + 0x24 + (link_idx_nsrt * 0xd8)) 5           // travel time
      WRITE_LONG  (link_off + 0x28 + (link_idx_nsrt * 0xd8)) 2           // unk, changed for consistency
    FOR (index3 = 0 ; index3 < link_num ; ++index3) BEGIN
      READ_LONG  (link_off +        (index3 * 0xd8)) target // target index
      READ_ASCII (link_off + 0x04 + (index3 * 0xd8)) exit   // exit point
      PATCH_IF (("" STRING_COMPARE_CASE "%exit%" = 0) AND
                ((target = stp_idx) OR
                 (target = nf_idx) OR
                 (target = fot_idx))) BEGIN
        WRITE_ASCII (link_off + 0x04 + (index3 * 0xd8)) "CDExit"   // exit point
      END
    END
    BUT_ONLY

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Open up cloakwood                                          \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                            \\\\\
///// first area only                                            \\\\\
/////                                                            \\\\\

BEGIN @103501 DESIGNATED 1035
SUBCOMPONENT @103500
GROUP @13
REQUIRE_PREDICATE GAME_IS ~tutu tutu_totsc bgt bgee~ @25 // Tutu, BGEE or BGT only
REQUIRE_PREDICATE ((NOT FILE_EXISTS_IN_GAME ~X#BG1NPCCloakwood.G3~) AND
                   (NOT FILE_EXISTS_IN_GAME ~X#BG1NPCCloakLodge.G3~)) @16 // already installed by BG1 NPC

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt01035.g3~

ACTION_IF GAME_IS ~tutu tutu_totsc~ BEGIN
  OUTER_SPRINT "area_res_match"  "fw2200" // tutu
  OUTER_SPRINT "area_res_match2" "fw2100" // tutu
END ELSE BEGIN
  ACTION_IF GAME_IS ~bgt~ BEGIN
    OUTER_SPRINT "area_res_match"  "ar7000" // bgt
    OUTER_SPRINT "area_res_match2" "ar8800" // bgt
  END ELSE BEGIN
    OUTER_SPRINT "area_res_match"  "ar2200" // bgee
    OUTER_SPRINT "area_res_match2" "ar2100" // bgee
  END
END

COPY_EXISTING ~worldmap.wmp~ ~override~
  READ_LONG  0x30 "area_num"
  READ_LONG  0x34 "area_off"
  FOR (index = 0; index < area_num; index = index + 1) BEGIN
    READ_ASCII ("%area_off%" + 0x08 + (0xf0 * "%index%")) "area_res"
    PATCH_IF ("%area_res%" STRING_COMPARE_CASE "%area_res_match%" = 0) BEGIN // cloakwood 1
      READ_BYTE  ("%area_off%" + 0x30 + (0xf0 * "%index%")) "flags"
      WRITE_BYTE ("%area_off%" + 0x30 + (0xf0 * "%index%")) ("%flags%" BOR 0b00000010) // adds 'reveal thru linked area' flag
    END ELSE
    PATCH_IF ("%area_res%" STRING_COMPARE_CASE "%area_res_match2%" = 0) BEGIN // cloakwood 2
      READ_BYTE  ("%area_off%" + 0x30 + (0xf0 * "%index%")) "flags"
      WRITE_BYTE ("%area_off%" + 0x30 + (0xf0 * "%index%")) ("%flags%" BAND 0b11111101) // removes 'reveal thru linked area' flag
    END
  END
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING_REGEXP GLOB ~^\(_?ar1901\|ar8701\)\.bcs$~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~RevealAreaOnMap("FW2200")~ ~RevealAreaOnMap("FW2200") RevealAreaOnMap("FW2100")~
    REPLACE_TEXTUALLY ~RevealAreaOnMap("AR2200")~ ~RevealAreaOnMap("AR2200") RevealAreaOnMap("AR2100")~
    REPLACE_TEXTUALLY ~RevealAreaOnMap("AR7000")~ ~RevealAreaOnMap("AR7000") RevealAreaOnMap("AR8800")~
  COMPILE_BAF_TO_BCS
  BUT_ONLY_IF_IT_CHANGES

/////                                                            \\\\\
///// all cloakwood save mines                                   \\\\\
/////                                                            \\\\\

BEGIN @103600 DESIGNATED 1036
SUBCOMPONENT @103500
GROUP @13
REQUIRE_PREDICATE GAME_IS ~tutu tutu_totsc bgt bgee~ @25 // Tutu, BGEE or BGT only
REQUIRE_PREDICATE ((NOT FILE_EXISTS_IN_GAME ~X#BG1NPCCloakwood.G3~) AND
                   (NOT FILE_EXISTS_IN_GAME ~X#BG1NPCCloakLodge.G3~)) @16 // already installed by BG1 NPC

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt01036.g3~

ACTION_IF GAME_IS ~tutu tutu_totsc~ BEGIN
  OUTER_SPRINT "area_res_match"  "fw2200" // tutu
  OUTER_SPRINT "area_res_match2" "fw1800" // tutu
END ELSE BEGIN
  ACTION_IF GAME_IS ~bgt~ BEGIN
    OUTER_SPRINT "area_res_match"  "ar7000" // bgt
    OUTER_SPRINT "area_res_match2" "ar8600" // bgt
  END ELSE BEGIN
    OUTER_SPRINT "area_res_match"  "ar2200" // bgee
    OUTER_SPRINT "area_res_match2" "ar1800" // bgee
  END
END

COPY_EXISTING ~worldmap.wmp~ ~override~
  READ_LONG  0x30 "area_num"
  READ_LONG  0x34 "area_off"
  FOR (index = 0; index < area_num; index = index + 1) BEGIN
    READ_ASCII ("%area_off%" + 0x08 + (0xf0 * "%index%")) "area_res"
    PATCH_IF ("%area_res%" STRING_COMPARE_CASE "%area_res_match%" = 0) BEGIN // cloakwood 1
      READ_BYTE  ("%area_off%" + 0x30 + (0xf0 * "%index%")) "flags"
      WRITE_BYTE ("%area_off%" + 0x30 + (0xf0 * "%index%")) ("%flags%" BOR 0b00000010) // adds 'reveal thru linked area' flag
    END ELSE
    PATCH_IF ("%area_res%" STRING_COMPARE_CASE "%area_res_match2%" = 0) BEGIN // cloakwood mines
      READ_BYTE  ("%area_off%" + 0x30 + (0xf0 * "%index%")) "flags"
      WRITE_BYTE ("%area_off%" + 0x30 + (0xf0 * "%index%")) ("%flags%" BAND 0b11111101) // removes 'reveal thru linked area' flag
    END
  END
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING_REGEXP GLOB ~^\(_?ar1901\|ar8701\)\.bcs$~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~RevealAreaOnMap("FW2200")~ ~RevealAreaOnMap("FW2200") RevealAreaOnMap("FW1800")~
    REPLACE_TEXTUALLY ~RevealAreaOnMap("AR2200")~ ~RevealAreaOnMap("AR2200") RevealAreaOnMap("AR1800")~
    REPLACE_TEXTUALLY ~RevealAreaOnMap("AR7000")~ ~RevealAreaOnMap("AR7000") RevealAreaOnMap("AR8600")~
  COMPILE_BAF_TO_BCS
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Improved Athkatla City Guard                               \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @104000 DESIGNATED 1040
GROUP @13
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee~ @25
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~bpduearc.spl~ @16 // non-BP

COPY_EXISTING ~amncen1.cre~  ~override~ // amnish centurion
              ~amng1.cre~    ~override~ // amnish soldier
              ~amng2.cre~    ~override~ // amnish soldier
              ~amnleg01.cre~ ~override~ // amnish legionary
              ~amnleg1.cre~  ~override~ // amnish legionary
              ~amnlegs.cre~  ~override~ // amnish legionary
              ~bamng01.cre~  ~override~ // amnish guard
              ~bamng02.cre~  ~override~ // amnish guard
              ~bodyg1.cre~   ~override~ // amnish bodyguard
              ~bodyg2.cre~   ~override~ // amnish bodyguard
              ~circg1.cre~   ~override~ // amnish soldier
              ~civamng1.cre~ ~override~ // amnish soldier
              ~docsol01.cre~ ~override~ // amnish soldier
              ~gguard01.cre~ ~override~ // amnish soldier
              ~guamur.cre~   ~override~ // amnish soldier
              ~hactong.cre~  ~override~ // amnish bodyguard
              ~isamng1.cre~  ~override~ // amnish soldier
              ~isamng2.cre~  ~override~ // amnish soldier
              ~isamng3.cre~  ~override~ // amnish soldier
              ~isamng4.cre~  ~override~ // amnish soldier
              ~slamng01.cre~ ~override~ // amnish soldier
              ~slcent.cre~   ~override~ // amnish centurion
              ~stealgrd.cre~ ~override~ // amnish soldier
              ~stolegrd.cre~ ~override~ // amnish soldier
              ~temamn01.cre~ ~override~ // amnish guard
              ~vvamn1.cre~   ~override~ // amnish soldier
              ~vvamn2.cre~   ~override~ // amnish soldier
  READ_SHORT  0x24  "current_hp"
  READ_SHORT  0x26  "max_hp"
  READ_SHORT  0x46  "natural_ac"
  READ_SHORT  0x48  "effective_ac"
  READ_SHORT  0x52  "guard_thac0"
  READ_BYTE   0x54  "save_death"
  READ_BYTE   0x55  "save_wands"
  READ_BYTE   0x56  "save_polymorph"
  READ_BYTE   0x57  "save_breath"
  READ_BYTE   0x58  "save_spell"
  WRITE_SHORT 0x24  ("%current_hp%" + 10)    // Current HP
  WRITE_SHORT 0x26  ("%max_hp%" + 10)        // Max HP
  WRITE_SHORT 0x46  ("%natural_ac%" - 1)     // Natural AC
  WRITE_SHORT 0x48  ("%effective_ac%" - 1)   // Effective AC
  WRITE_SHORT 0x52  ("%guard_thac0%" - 1)    // thac0
  WRITE_BYTE  0x54  ("%save_death%" - 1)     // save vs death
  WRITE_BYTE  0x55  ("%save_wands%" - 1)     // save vs wands
  WRITE_BYTE  0x56  ("%save_polymorph%" - 1) // save vs polymorph
  WRITE_BYTE  0x57  ("%save_breath%" - 1)    // save vs breath
  WRITE_BYTE  0x58  ("%save_spell%" - 1)     // save vs spell
  //search for first empty script slot and overwrite
  FOR (index = 0 ; index < 5 ; index = index + 1) BEGIN
    READ_ASCII (0x248 + ("%index%" * 0x08)) "script"
    PATCH_IF (("None" STRING_COMPARE_CASE "%script%" = 0) OR
              (""     STRING_COMPARE_CASE "%script%" = 0)) BEGIN
      WRITE_ASCII (0x248 + ("%index%" * 0x08)) ~a#amng~ #8
      SET "index" = 5 // kills loop
    END ELSE
    PATCH_IF ("%index%" = 4) BEGIN // if we get here, no script slot open so we overwrite general script
      WRITE_ASCII 0x260 ~a#amng~ #8 // override script
    END
  END
  BUT_ONLY_IF_IT_CHANGES

// Amnish Soldier
COPY_EXISTING ~amng1.cre~ ~override/a#amng.cre~
  SAY NAME1 @104001
  SAY NAME2 @104001
  WRITE_ASCII 0x280 ~a#amng~ #8

//Amnish Soldier - Hostile
COPY_EXISTING ~a#amng.cre~ ~override/a#amngh.cre~
  WRITE_BYTE  0x270 255          // Allegiance enemy
  WRITE_ASCII 0x280 ~a#amngh~ #8 // Death Variable

//Sanctioned Wizard
COPY ~bg2_tweaks/cre/a#amnm.cre~ ~override~
  SAY NAME1 @104002
  SAY NAME2 @104002

//Sanctioned Wizard - Hostile
COPY_EXISTING ~a#amnm.cre~ ~override/a#amnmh.cre~
  WRITE_BYTE  0x270 255          // Allegiance enemy
  WRITE_ASCII 0x280 ~a#amnmh~ #8 // Death Variable

//Sanctioned Wizard
COPY ~bg2_tweaks/cre/a#amnp.cre~ ~override~
  SAY NAME1 @104003
  SAY NAME2 @104003

//Sanctioned Wizard - Hostile
COPY_EXISTING ~a#amnp.cre~ ~override/a#amnph.cre~
  WRITE_BYTE  0x270 255          // Allegiance enemy
  WRITE_ASCII 0x280 ~a#amnph~ // Death Variable

COMPILE ~bg2_tweaks/baf/a#amng.baf~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Gradual Drow item disintegration                           \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @105000 DESIGNATED 1050
GROUP @13
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee~ @25

EXTEND_BOTTOM ~ar2500.bcs~ ~bg2_tweaks/baf/ar2500.baf~
EXTEND_BOTTOM ~baldur.bcs~ ~bg2_tweaks/baf/baldur.baf~

ACTION_IF FILE_EXISTS_IN_GAME ~baldur25.bcs~ THEN BEGIN // extend ToB game script if present

  EXTEND_BOTTOM ~baldur25.bcs~ ~bg2_tweaks/baf/baldur.baf~

END

COPY ~bg2_tweaks/spl/cdsw1h01.spl~ ~override~
  WRITE_ASCII 0xae ~dwsw1h01~
  SAY 0xfe @105001

COPY_EXISTING ~cdsw1h01.spl~ ~override/cdsw1h02.spl~
  WRITE_ASCII 0xae ~dwsw1h02~

COPY_EXISTING ~cdsw1h01.spl~ ~override/cdblun01.spl~
  WRITE_ASCII 0xae ~dwblun01~

COPY_EXISTING ~cdsw1h01.spl~ ~override/cdhalb01.spl~
  WRITE_ASCII 0xae ~dwhalb01~

COPY_EXISTING ~cdsw1h01.spl~ ~override/cdsper01.spl~
  WRITE_ASCII 0xae ~dwsper01~

COPY ~bg2_tweaks/spl/cdbreak1.spl~ ~override~ // armor
  SAY 0xce @105001

COPY_EXISTING ~cdbreak1.spl~ ~override/cdbreak4.spl~  // cloak
  WRITE_LONG 0x9e 4

COPY_EXISTING ~cdbreak1.spl~ ~override/cdbreak9.spl~  // shield
  WRITE_LONG 0x9e 9

// melee weapons have chance of failing when used in combat
COPY_EXISTING ~dwsw1h01.itm~ ~override~
              ~dwsw1h02.itm~ ~override~
              ~dwblun01.itm~ ~override~
              ~dwhalb01.itm~ ~override~
              ~dwsper01.itm~ ~override~
  READ_LONG  0x64 "abil_off" ELSE 0
  READ_SHORT 0x68 "abil_num" ELSE 0
  READ_LONG  0x6a "fx_off"   ELSE 0
  SET "abil_length" = 0x38
  SET "fx_delta" = 0
  FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN // start iterating through abilities
    READ_SHORT  ("%abil_off%" +        ("%abil_length%" * "%index%")) "type"
    READ_SHORT  ("%abil_off%" + 0x1e + ("%abil_length%" * "%index%")) "abil_fx_num"
    READ_SHORT  ("%abil_off%" + 0x20 + ("%abil_length%" * "%index%")) "abil_fx_idx"
    SET "abil_fx_idx" = ("%abil_fx_idx%" + "%fx_delta%")
    WRITE_SHORT ("%abil_off%" + 0x20 + ("%abil_length%" * "%index%")) ("%abil_fx_idx%")
    PATCH_IF ("%type%" = 1) BEGIN // melee
      INSERT_BYTES            ("%fx_off%"        + (0x30 * "%abil_fx_idx%")) 0x30
        WRITE_SHORT           ("%fx_off%"        + (0x30 * "%abil_fx_idx%")) 146 // opcode: 146 cast spell
        WRITE_BYTE            ("%fx_off%" + 0x02 + (0x30 * "%abil_fx_idx%")) 1   // target: self
        WRITE_LONG            ("%fx_off%" + 0x04 + (0x30 * "%abil_fx_idx%")) 1   // cast at level: 1
        WRITE_BYTE            ("%fx_off%" + 0x0c + (0x30 * "%abil_fx_idx%")) 1   // timing: instant permanent
        WRITE_BYTE            ("%fx_off%" + 0x12 + (0x30 * "%abil_fx_idx%")) 1   // probability: 1%
        WRITE_EVALUATED_ASCII ("%fx_off%" + 0x14 + (0x30 * "%abil_fx_idx%")) ~cd%SOURCE_RES%~ // name of spell to cast (same as item name)
      SET "abil_fx_num" = "%abil_fx_num%" + 1
      SET "fx_delta" = "%fx_delta%" + 1
    END
    WRITE_SHORT  ("%abil_off%" + 0x1e + ("%abil_length%" * "%index%")) "%abil_fx_num%"
  END
  BUT_ONLY_IF_IT_CHANGES

// armor/shield/cloak has a chance to break when attacked
COPY_EXISTING ~dwchan01.itm~ ~override~
              ~dwchan02.itm~ ~override~
              ~dwclck01.itm~ ~override~
              ~dwplat01.itm~ ~override~
              ~dwshld01.itm~ ~override~
              ~misc9w.itm~   ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
    READ_SHORT  0x1c "type"
    READ_LONG   0x64 "abil_off"
    READ_SHORT  0x68 "abil_num"
    READ_LONG   0x6a "fx_off"
    READ_SHORT  0x70 "fx_num"
    WRITE_SHORT 0x70 ("%fx_num%" + 1)
    FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN
      READ_SHORT  ("%abil_off%" + 0x20 + (0x38 * "%index%")) "abil_fx_idx"
      WRITE_SHORT ("%abil_off%" + 0x20 + (0x38 * "%index%")) ("%abil_fx_idx%" + 1)
    END
    INSERT_BYTES  ("%fx_off%"       ) 0x30
      WRITE_SHORT ("%fx_off%"       ) 232          // effect type - 232 cast spell on condition
      WRITE_BYTE  ("%fx_off%" + 0x02) 1            // target - 1 targetself
      WRITE_LONG  ("%fx_off%" + 0x04) 1            // cast at level - 1
      WRITE_BYTE  ("%fx_off%" + 0x0c) 2            // duration - 2 instant while equipped
      WRITE_BYTE  ("%fx_off%" + 0x12) 1            // probability 1 - 1%
      PATCH_IF ("%type%" = 2) BEGIN                // if armor
        WRITE_ASCII ("%fx_off%" + 0x14) ~cdbreak1~ // name of spell to cast
      END ELSE
      PATCH_IF ("%type%" = 12) BEGIN               // if shield
        WRITE_ASCII ("%fx_off%" + 0x14) ~cdbreak9~ // name of spell to cast
      END ELSE BEGIN                               // if cloak
        WRITE_ASCII ("%fx_off%" + 0x14) ~cdbreak4~ // name of spell to cast
      END
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Breakable nonmagical shields, armor and helms    \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @106000 DESIGNATED 1060
GROUP @13
REQUIRE_PREDICATE GAME_IS ~tutu tutu_totsc bgee~ @25 // tutu, BGEE only

COPY_EXISTING ~misc59.itm~ ~override/cddelhlm.itm~

// bgee freaks out with misc items in shield/armor/helmet slots
ACTION_IF GAME_IS ~bgee~ BEGIN // bgee

  COPY_EXISTING ~misc57.itm~ ~override~ // broken shield
    WRITE_SHORT 0x1c 12
    WRITE_LONG  0x1e `0
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 target = 1 parameter1 = `0 timing = 2 STR_VAR resource = cdbrak12 END
    BUT_ONLY

  COPY_EXISTING ~misc58.itm~ ~override~ // broken armor
    WRITE_SHORT 0x1c 2
    WRITE_LONG  0x1e `0
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 target = 1 parameter1 = `0 timing = 2 STR_VAR resource = cdbrak2 END
    BUT_ONLY

  COPY_EXISTING ~cddelhlm.itm~ ~override~ // broken helmet
    WRITE_SHORT 0x1c 7
    WRITE_LONG  0x1e `0
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 target = 1 parameter1 = `0 timing = 2 STR_VAR resource = cdbrak7 END
    BUT_ONLY

END

COPY ~bg2_tweaks/spl/cdbreak1.spl~ ~override/cdbrak2.spl~ // armor
  SAY         0x9e @106001

COPY_EXISTING ~cdbrak2.spl~ ~override/cdbrak7.spl~  // helm
  SAY         0x9e @106003
  WRITE_LONG  0xce 6
  WRITE_ASCII 0xde ~cddelhlm~ #8

COPY_EXISTING ~cdbrak2.spl~ ~override/cdbrak12.spl~  // shield
  SAY         0x9e @106002
  WRITE_LONG  0xce 9
  WRITE_ASCII 0xde ~misc57~ #8

ACTION_IF !((GAME_IS ~bgee bg2ee~) OR (MOD_IS_INSTALLED ~TOBEX/TOBEX.TP2~ ~0~)) BEGIN // if no critical hit aversion from tobex, script to remove broken helm item

  EXTEND_TOP ~baldur.bcs~ ~bg2_tweaks/baf/break.baf~
  
  ACTION_IF FILE_EXISTS_IN_GAME ~baldur25.bcs~ THEN BEGIN
  
    EXTEND_TOP ~baldur25.bcs~ ~bg2_tweaks/baf/break.baf~
  
  END

  COPY_EXISTING ~cdbrak7.spl~ ~override~  // create broken misc in inventory
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 122 target = 1 parameter1 = 1 timing = 1 insert_point = 0 STR_VAR resource = misc59 END

END ELSE BEGIN // otherwise add flag to disable critical hit protection for broken helmet

  COPY_EXISTING ~cddelhlm.itm~ ~override~ // broken helmet
    WRITE_BYTE 0x1b THIS | BIT1

END

// give nonmagical iron armor/shield/helmets a chance to break when attacked
ACTION_FOR_EACH file IN
  // helmets
  ~_helm01.itm~ ~_helm08.itm~ ~_helm09.itm~ ~_helm10.itm~ ~_helm11.itm~ ~_helm12.itm~ ~_helm13.itm~
  ~_helm15.itm~ ~_ihelm01.itm~ ~_ihelm10.itm~ ~dhelm01.itm~ ~elfhelm.itm~ ~helm01.itm~ ~helm08.itm~
  ~helm09.itm~ ~helm10.itm~ ~helm11.itm~ ~helm12.itm~ ~helm13.itm~ ~helm15.itm~ ~helm22.itm~
  ~helmskwa.itm~ ~ihelm01.itm~ ~ihelm10.itm~ 
  // shields
  ~_ishld03.itm~ ~_shld01.itm~ ~_shld03.itm~ ~_shld05.itm~ ~_shld08.itm~ ~_shld09.itm~ ~_shld10.itm~
  ~_shld11.itm~ ~_shld12.itm~ ~_shld13.itm~ ~_shld14.itm~ ~_shld15.itm~ ~_shld16.itm~ ~_shld18.itm~
  ~_shld99.itm~ ~dshld01.itm~ ~ishld03.itm~ ~shld01.itm~ ~shld03.itm~ ~shld05.itm~ ~shld08.itm~
  ~shld09.itm~ ~shld10.itm~ ~shld11.itm~ ~shld12.itm~ ~shld13.itm~ ~shld14.itm~ ~shld15.itm~
  ~shld16.itm~ ~shld18.itm~ ~shld99.itm~
  // metal armor
  ~_chan01.itm~ ~_chan04.itm~ ~_ichan01.itm~ ~_ichan04.itm~ ~_iplat01.itm~ ~_plat01.itm~ 
  ~_plat04.itm~ ~_plat07.itm~ ~_plat98.itm~ ~chan01.itm~ ~chan04.itm~ ~ichan01.itm~ ~ichan04.itm~
  ~iplat01.itm~ ~plat01.itm~ ~plat04.itm~ ~plat07.itm~ ~plat98.itm~ ~plat99.itm~ ~vischan1.itm~
  ~vischan2.itm~ ~visplat1.itm~
  BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%file%~ THEN BEGIN

    COPY_EXISTING ~%file%~ ~override~
      READ_SHORT  0x1c type
      LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 232 target = 1 timing = 2 probability1 = 1 STR_VAR resource = EVAL "cdbrak%type%" END
      BUT_ONLY

  END

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Multi-player kickout patch                       \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @107000 DESIGNATED 1070
GROUP @13
//REQUIRE_PREDICATE GAME_IS ~soa tob bgt bgee bg2ee~ @25

ACTION_IF GAME_IS ~bgee~ THEN BEGIN // BGEE

  COMPILE ~bg2_tweaks/dlg/multi_bgee.d~

END ELSE BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~howparty.2da~ THEN BEGIN // iwdee
  
    COMPILE ~bg2_tweaks/dlg/multig_iwdee.d~ // iwdee

  END ELSE BEGIN // non-bgee, non-iwdee
  
    COMPILE ~bg2_tweaks/dlg/multig.d~

  END
    
END

ACTION_IF GAME_IS ~tutu tutu_totsc~ THEN BEGIN // Tutu, removes CC/PP options and adds FAI option

  COMPILE ~bg2_tweaks/dlg/multi22.d~

END

ACTION_IF GAME_IS ~bgt~ THEN BEGIN // BGT, adds EndOfBG checks for CC/PP and adds FAI option

  COMPILE ~bg2_tweaks/dlg/multibgt.d~

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Add Misc Bags of Holding                         \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @108000 DESIGNATED 1080
GROUP @13
REQUIRE_PREDICATE GAME_IS ~tutu tutu_totsc soa tob bgt bgee bg2ee~ @25

ACTION_IF GAME_IS ~tob bg2ee~ THEN BEGIN // if ToB, clone existing items

  // store files
  COPY_EXISTING ~bag05.sto~ ~override/cdammo.sto~   // ammo belt
                ~bag05.sto~ ~override/cdammo2.sto~  // ammo belt #2
                ~bag06.sto~ ~override/cdpotion.sto~ // potion case
                ~bag06.sto~ ~override/cdpotio2.sto~ // potion case #2

  // item files
  COPY_EXISTING ~bag05.itm~ ~override/cdammo.itm~   // ammo belt
                ~bag05.itm~ ~override/cdammo2.itm~  // ammo belt #2
                ~bag06.itm~ ~override/cdpotion.itm~ // potion case
                ~bag06.itm~ ~override/cdpotio2.itm~ // potion case #2

END ELSE BEGIN // if no ToB, create potion case & ammo belt

  // bams
  COPY ~bg2_tweaks/bam/ibag05.bam~ ~override~
       ~bg2_tweaks/bam/ibag06.bam~ ~override~

  ACTION_IF GAME_IS BGEE THEN BEGIN
  
    COPY ~bg2_tweaks/bmp/pbag05.bmp~ ~override~
         ~bg2_tweaks/bmp/pbag06.bmp~ ~override~
         
  END ELSE BEGIN
  
    COPY ~bg2_tweaks/bam/pbag05.bam~ ~override~
         ~bg2_tweaks/bam/pbag06.bam~ ~override~
  
  END

  // ammo belts
  COPY ~bg2_tweaks/sto/cdammo.sto~ ~override~
       ~bg2_tweaks/sto/cdammo.sto~ ~override/cdammo2.sto~
    SAY 0x0c @108001

  COPY ~bg2_tweaks/itm/cdammo.itm~ ~override~
       ~bg2_tweaks/itm/cdammo.itm~ ~override/cdammo2.itm~
    SAY 0x08 @108001
    SAY 0x0c @108001
    SAY 0x50 @108002
    SAY 0x54 @108002

  // potion case
  COPY ~bg2_tweaks/sto/cdpotion.sto~ ~override~
       ~bg2_tweaks/sto/cdpotion.sto~ ~override/cdpotio2.sto~
    SAY 0x0c @108003

  COPY ~bg2_tweaks/itm/cdpotion.itm~ ~override~
       ~bg2_tweaks/itm/cdpotion.itm~ ~override/cdpotio2.itm~
    SAY 0x08 @108003
    SAY 0x0c @108003
    SAY 0x50 @108004
    SAY 0x54 @108004

  ACTION_IF FILE_EXISTS_IN_GAME ~cdt03040.g3~ THEN BEGIN // if bottomless bags

    COPY_EXISTING ~cdammo.sto~   ~override~
                  ~cdammo2.sto~  ~override~
                  ~cdpotion.sto~ ~override~
                  ~cdpotio2.sto~ ~override~
      WRITE_SHORT 0x22 32767
      BUT_ONLY_IF_IT_CHANGES

  END

END

ACTION_IF NOT GAME_IS ~bgee~ BEGIN // non-BGEE

  COPY_EXISTING ~roger.sto~   ~override~ // roger
                ~murcrag.sto~ ~override~ // Cragmoon
                ~shop05.sto~  ~override~ // perter
                ~trmer04.sto~ ~override~ // trademeet blacksmith
    READ_LONG  0x34 "sale_offset"
    READ_LONG  0x38 "sale_number"
    WRITE_LONG 0x38 ("%sale_number%" + 1)
    READ_LONG  0x2c "purchase_offset"
    READ_LONG  0x4c "drink_offset"
    READ_LONG  0x70 "cure_offset"
    INSERT_BYTES  ("%sale_offset%" +        ("%sale_number%" * 0x1c)) 28
      PATCH_IF ("murcrag" STRING_COMPARE_CASE ~%SOURCE_RES%~ = 0) BEGIN // if store is cragmoon add potion case
        WRITE_ASCII ("%sale_offset%" +        ("%sale_number%" * 0x1C)) ~cdpotion~
      END ELSE
      PATCH_IF ("roger" STRING_COMPARE_CASE ~%SOURCE_RES%~ = 0) BEGIN // if store is roger add potion case 2
        WRITE_ASCII ("%sale_offset%" +        ("%sale_number%" * 0x1C)) ~cdpotio2~
      END ELSE
      PATCH_IF ("shop05" STRING_COMPARE_CASE ~%SOURCE_RES%~ = 0) BEGIN // if store is perter add ammo belt 1
        WRITE_ASCII ("%sale_offset%" +        ("%sale_number%" * 0x1c)) ~cdammo~
      END ELSE
      PATCH_IF ("trmer04" STRING_COMPARE_CASE ~%SOURCE_RES%~ = 0) BEGIN // if store is Trademeet blacksmith add ammo belt 2
        WRITE_ASCII ("%sale_offset%" +        ("%sale_number%" * 0x1c)) ~cdammo2~
      END
      WRITE_LONG  ("%sale_offset%" + 0x10 + ("%sale_number%" * 0x1c)) 3 // identified and unstealable
      WRITE_LONG  ("%sale_offset%" + 0x14 + ("%sale_number%" * 0x1c)) 1 // quantity
    PATCH_IF NOT ("%purchase_offset%" < "%sale_offset%") BEGIN
      WRITE_LONG 0x2c ("%purchase_offset%" + 0x1c)
    END
    PATCH_IF NOT ("%drink_offset%" < "%sale_offset%") BEGIN
      WRITE_LONG 0x4c ("%drink_offset%" + 0x1c)
    END
    PATCH_IF NOT ("%cure_offset%" < "%sale_offset%") BEGIN
      WRITE_LONG 0x70 ("%cure_offset%" + 0x1c)
    END
    
END

// if BGT, BGEE, or Tutu game, add bags to BG portion
ACTION_IF GAME_IS ~tutu tutu_totsc bgt bgee~ THEN BEGIN

  COPY_EXISTING ~cdammo.itm~   ~override/c!ammo.itm~   // ammo belt
                ~cdammo.sto~   ~override/c!ammo.sto~
                ~bag02g.itm~   ~override/c!bag02.itm~  // gem bag
                ~bag02g.sto~   ~override/c!bag02.sto~
                ~cdpotion.itm~ ~override/c!potion.itm~ // potion case
                ~cdpotion.sto~ ~override/c!potion.sto~
                ~bag03g.itm~   ~override/c!bag03.itm~  // scroll case
                ~bag03g.sto~   ~override/c!bag03.sto~

  ACTION_IF GAME_IS ~bgee~ BEGIN // missing bgee resources
  
    COPY ~bg2_tweaks/itm/bag04.itm~ ~override/c!bag04.itm~
         ~bg2_tweaks/sto/bag04.sto~ ~override/c!bag04.sto~
         
    // string changes
    COPY_EXISTING ~c!bag02.itm~ ~override~
      WRITE_LONG 0x08 25502
      WRITE_LONG 0x0c 25502
      WRITE_LONG 0x50 25504
      WRITE_LONG 0x54 25504

    COPY_EXISTING ~c!bag02.sto~ ~override~
      WRITE_LONG 0x0c 25502

    COPY_EXISTING ~c!bag03.itm~ ~override~
      WRITE_LONG 0x08 25505
      WRITE_LONG 0x0c 25505
      WRITE_LONG 0x50 25507
      WRITE_LONG 0x54 25507

    COPY_EXISTING ~c!bag03.sto~ ~override~
      WRITE_LONG 0x0c 25505

    COPY_EXISTING ~c!bag04.itm~ ~override~
      SAY 0x08 @108005
      SAY 0x0c @108005
      SAY 0x50 @108006
      SAY 0x54 @108006

    COPY_EXISTING ~c!bag04.sto~ ~override~
      SAY 0x0c @108005

    ACTION_IF FILE_EXISTS_IN_GAME ~cdt03040.g3~ THEN BEGIN // if bottomless bags
  
      COPY_EXISTING ~c!bag04.sto~   ~override~
        WRITE_SHORT 0x22 32767
        BUT_ONLY

    END

  END ELSE BEGIN
  
    COPY_EXISTING ~bag04.itm~    ~override/c!bag04.itm~  // bag of holding
                  ~bag04.sto~    ~override/c!bag04.sto~
  
  END

  COPY_EXISTING_REGEXP GLOB ~^_?friend\.sto$~    ~override~ // FAI
                            ~^[_h]ighhedg\.sto$~ ~override~ // High Hedge
                            ~^_?taerum\.sto$~    ~override~ // Thunderhammer Smithy
                            ~^_?sto4803\.sto$~   ~override~ // Nashkel General Store
                            ~^_?sto0703\.sto$~   ~override~ // Sorcerous Sundries
    READ_LONG  0x34 "sale_offset"
    READ_LONG  0x38 "sale_number"
    WRITE_LONG 0x38 ("%sale_number%" + 1)
    READ_LONG  0x2c "purchase_offset"
    READ_LONG  0x4c "drink_offset"
    READ_LONG  0x70 "cure_offset"
    INSERT_BYTES  ("%sale_offset%" +        ("%sale_number%" * 0x1c)) 28
      PATCH_IF (~%SOURCE_RES%~ STRING_COMPARE_REGEXP ~^_?friend$~ = 0) BEGIN // if store is FAI add c!bag02
        WRITE_ASCII ("%sale_offset%" +        ("%sale_number%" * 0x1c)) ~c!bag02~
      END ELSE
      PATCH_IF (~%SOURCE_RES%~ STRING_COMPARE_REGEXP ~^[_h]ighhedg$~ = 0) BEGIN // if store is High Hedge add c!bag03
        WRITE_ASCII ("%sale_offset%" +        ("%sale_number%" * 0x1c)) ~c!bag03~
      END ELSE
      PATCH_IF (~%SOURCE_RES%~ STRING_COMPARE_REGEXP ~^_?taerum$~ = 0) BEGIN // if store is Thunderhammer Smithy add c!ammo
        WRITE_ASCII ("%sale_offset%" +        ("%sale_number%" * 0x1c)) ~c!ammo~
      END ELSE
      PATCH_IF (~%SOURCE_RES%~ STRING_COMPARE_REGEXP ~^_?sto4803$~ = 0) BEGIN // if store is Nashkel General Store add c!potion
        WRITE_ASCII ("%sale_offset%" +        ("%sale_number%" * 0x1c)) ~c!potion~
      END ELSE BEGIN // else sorcerous sundries
        WRITE_ASCII ("%sale_offset%" +        ("%sale_number%" * 0x1c)) ~c!bag04~// if store is SS add c!bag04
      END
      WRITE_LONG  ("%sale_offset%" + 0x10 + ("%sale_number%" * 0x1c)) 3 // identified and unstealable
      WRITE_LONG  ("%sale_offset%" + 0x14 + ("%sale_number%" * 0x1c)) 1 // quantity
    PATCH_IF NOT ("%purchase_offset%" < "%sale_offset%") BEGIN
      WRITE_LONG 0x2c ("%purchase_offset%" + 0x1c)
    END
    PATCH_IF NOT ("%drink_offset%" < "%sale_offset%") BEGIN
      WRITE_LONG 0x4c ("%drink_offset%" + 0x1c)
    END
    PATCH_IF NOT ("%cure_offset%" < "%sale_offset%") BEGIN
      WRITE_LONG 0x70 ("%cure_offset%" + 0x1c)
    END

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Exotic Items Pack                                \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @109000 DESIGNATED 1090
GROUP @13
REQUIRE_PREDICATE GAME_IS ~tutu tutu_totsc bgt bgee~ @25 // Tutu, bgee or BGT only
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~cdcal040.g3~ @16 // already installed by the calling

// artwork
COPY ~bg2_tweaks/bam/cdiammo.bam~  ~override~
     ~bg2_tweaks/bam/cdipotbg.bam~ ~override~
     ~bg2_tweaks/bam/cdplbag2.bam~ ~override~
     ~bg2_tweaks/bam/cdplqiv1.bam~ ~override~
     ~bg2_tweaks/bam/icdioun1.bam~ ~override~
     ~bg2_tweaks/bam/icdioun2.bam~ ~override~

ACTION_IF GAME_IS ~bgee~ THEN BEGIN

  COPY ~bg2_tweaks/bmp/ccdioun1.bmp~ ~override~
       ~bg2_tweaks/bmp/cdpammo.bmp~  ~override~
       ~bg2_tweaks/bmp/cdplbag1.bmp~ ~override~
       ~bg2_tweaks/bmp/cdplqiv2.bmp~ ~override~
       ~bg2_tweaks/bmp/cdppotbg.bmp~ ~override~

END ELSE BEGIN

  COPY ~bg2_tweaks/bam/ccdioun1.bam~ ~override~
       ~bg2_tweaks/bam/cdpammo.bam~  ~override~
       ~bg2_tweaks/bam/cdplbag1.bam~ ~override~
       ~bg2_tweaks/bam/cdplqiv2.bam~ ~override~
       ~bg2_tweaks/bam/cdppotbg.bam~ ~override~

END

// add and name items
COPY ~bg2_tweaks/itm/cdplquiv.itm~ ~override~ // quiver of plenty +0
  SAY NAME1 @109001
  SAY NAME2 @109001
  SAY IDENTIFIED_DESC @109002
  SAY UNIDENTIFIED_DESC @109002

COPY ~bg2_tweaks/itm/cdplcase.itm~ ~override~ // case of plenty +0
  SAY NAME1 @109003
  SAY NAME2 @109003
  SAY IDENTIFIED_DESC @109004
  SAY UNIDENTIFIED_DESC @109004

COPY ~bg2_tweaks/itm/cdplbag.itm~ ~override~ // bag of plenty +0
  SAY NAME1 @109005
  SAY NAME2 @109005
  SAY IDENTIFIED_DESC @109006
  SAY UNIDENTIFIED_DESC @109006

COPY ~bg2_tweaks/itm/cdioun1.itm~ ~override~  // Deep Purple Ioun Stone
  SAY NAME1 #8181
  SAY NAME2 @109007
  SAY IDENTIFIED_DESC @109008
  SAY UNIDENTIFIED_DESC #5770

COPY ~bg2_tweaks/itm/cdioun2.itm~ ~override~ // Flickering White Ioun Stone
  SAY NAME1 #8181
  SAY NAME2 @109009
  SAY IDENTIFIED_DESC @109010
  SAY UNIDENTIFIED_DESC #5770

COPY ~bg2_tweaks/itm/cdkat2.itm~ ~override~ // Katana +2
  SAY NAME1 @109011
  SAY NAME2 @109013
  SAY IDENTIFIED_DESC @109014
  SAY UNIDENTIFIED_DESC @109012
  SAY 0x19e @109015
  SAY 0x1fe @109016
  SAY 0x25e @109017
  SAY 0x2be @109018
  SAY 0x31e @109019
  SAY 0x37e @109020
  SAY 0x3de @109021

ACTION_IF GAME_IS ~bgee~ THEN BEGIN // BGEE
  
  EXTEND_BOTTOM ~ar1803.bcs~ ~bg2_tweaks/baf/_ar1803.baf~ // adds Contagion scroll to Davaeorn's loot
  EXTEND_BOTTOM ~ar3601.bcs~ ~bg2_tweaks/baf/_ar3601.baf~ // adds Ninja-To +0 to Sirine cave loot

  OUTER_SPRINT "sendai_file" "sendai"
  
  COPY_EXISTING ~cdioun1.itm~ ~override~
                ~cdioun2.itm~ ~override~
    SAY NAME1 @109023
    SAY UNIDENTIFIED_DESC @109024
    
  // since adding tsu-o-shi to krumm overwrites his club +1, find new home for it
  COPY_EXISTING ~caldo.cre~ ~override~ // move club +1 to krumm's companion
    REPLACE_CRE_ITEM ~blun36~ #0 #0 #0 ~NONE~ ~WEAPON1~ EQUIP
    // match proficiency for new weapon
    READ_BYTE 0x72 prof_blunt
    READ_BYTE 0x73 prof_spike
    PATCH_IF prof_spike > prof_blunt BEGIN
      WRITE_BYTE 0x72 prof_spike
    END

END ELSE BEGIN
  
  ACTION_IF GAME_IS ~tutu tutu_totsc~ THEN BEGIN // tutu

    COMPILE ~bg2_tweaks/dlg/exotic_22.d~ // fixes Gullykin cleric's dlg file to open new store

    COPY_EXISTING ~fw3601.are~ ~override~ // adds area script to Sirine cave
      WRITE_ASCII 0x94 ~_ar3601~ #8
  
    EXTEND_BOTTOM ~_ar1803.bcs~ ~bg2_tweaks/baf/_ar1803.baf~ // adds Contagion scroll to Davaeorn's loot
    EXTEND_BOTTOM ~_ar3601.bcs~ ~bg2_tweaks/baf/_ar3601.baf~ // adds Ninja-To +0 to Sirine cave loot

    ACTION_IF NOT FILE_EXISTS_IN_GAME ~_tem4003.sto~ THEN BEGIN // if Gullykin does not have a store already
  
      COPY_EXISTING ~_tem4802.sto~ ~override/_tem4003.sto~ // clones and renames Temple of Helm
        SAY 0x0C @109022

    END

    OUTER_SPRINT "sendai_file" "_sendai"
    
  END ELSE BEGIN // bgt

    COMPILE ~bg2_tweaks/dlg/exotic_bgt.d~ // fixes Gullykin cleric's dlg file to open new store
  
    EXTEND_BOTTOM ~ar8603.bcs~ ~bg2_tweaks/baf/_ar1803.baf~ // adds Contagion scroll to Davaeorn's loot
    EXTEND_BOTTOM ~ar9501.bcs~ ~bg2_tweaks/baf/_ar3601.baf~ // adds Ninja-To +0 to Sirine cave loot

    ACTION_IF NOT FILE_EXISTS_IN_GAME ~tem4003.sto~ THEN BEGIN // if Gullykin does not have a store already
  
      COPY_EXISTING ~tem4802.sto~ ~override/tem4003.sto~ // clones and renames Temple of Helm
        SAY 0x0C @109022

    END

    OUTER_SPRINT "sendai_file" "bgsendai"

  END
  
END

COPY_EXISTING_REGEXP GLOB ~^_?meilum\.cre$~ ~override~ // adds katana +0 to Meilum by replacing primary weapon
  REPLACE_CRE_ITEM ~sw1h43~ #0 #0 #0 ~NONE~ ~WEAPON1~ EQUIP

COPY_EXISTING_REGEXP GLOB ~^_?krumm\.cre$~  ~override~ // adds katana +2 to Krumm by replacing primary weapon
  REPLACE_CRE_ITEM ~cdkat2~ #0 #0 #0 ~NONE~ ~WEAPON1~ EQUIP
  READ_BYTE 0x6e prof_lsword // proficiency update since weapon type changes
  READ_BYTE 0x72 prof_blunt
  PATCH_IF prof_blunt > prof_lsword BEGIN
    WRITE_BYTE 0x6e prof_blunt
  END

COPY_EXISTING_REGEXP GLOB ~^_?davaeo\.cre$~ ~override~ // adds Flickering White Ioun stone to helmet slot
  ADD_CRE_ITEM ~cdioun2~ #0 #0 #0 ~NONE~ ~HELMET~

COPY_EXISTING_REGEXP GLOB ~^_?haseo\.cre$~  ~override~ // adds katana +1 to Haseo by moving primary weapon to offhand
  REPLACE_CRE_ITEM ~sw1h44~ #0 #0 #0 ~NONE~ ~WEAPON1~ EQUIP
  ADD_CRE_ITEM     ~sw1h05~ #0 #0 #0 ~NONE~ ~SHIELD~

COPY_EXISTING_REGEXP GLOB ~^_?desret\.cre$~ ~override~ // adds wakizashi +1 to Desreta by moving primary weapon to offhand
  REPLACE_CRE_ITEM ~sw1h47~ #0 #0 #0 ~NONE~ ~WEAPON1~ EQUIP
  ADD_CRE_ITEM     ~sw1h05~ #0 #0 #0 ~NONE~ ~SHIELD~
  READ_BYTE 0x6e prof_lsword // proficiency update since weapon type changes
  READ_BYTE 0x6f prof_ssword
  PATCH_IF prof_lsword > prof_ssword BEGIN
    WRITE_BYTE 0x6f prof_lsword
  END

COPY_EXISTING_REGEXP GLOB ~^_?hakt\.cre$~   ~override~ // adds wakizashi +0 to Hakt by replacing primary melee weapon
  REPLACE_CRE_ITEM ~sw1h46~ #0 #0 #0 ~NONE~ ~WEAPON2~
  READ_BYTE 0x6e prof_lsword // proficiency update since weapon type changes
  READ_BYTE 0x6f prof_ssword
  PATCH_IF prof_lsword > prof_ssword BEGIN
    WRITE_BYTE 0x6f prof_lsword
  END

COPY_EXISTING_REGEXP GLOB ~^_?maneir\.cre$~ ~override~ // adds scimitar +1 to Maneira by replacing primary melee weapon
  REPLACE_CRE_ITEM ~sw1h22~ #0 #0 #0 ~NONE~ ~WEAPON2~
  READ_BYTE 0x6e prof_lsword // proficiency update since weapon type changes
  READ_BYTE 0x6f prof_ssword
  PATCH_IF prof_ssword > prof_lsword BEGIN
    WRITE_BYTE 0x6e prof_ssword
  END

COPY_EXISTING ~%sendai_file%.cre~ ~override~ // adds scimitar +1 to Sendai Argrim by replacing offhand weapon
  REPLACE_CRE_ITEM ~sw1h22~ #0 #0 #0 ~NONE~ ~SHIELD~

COPY_EXISTING_REGEXP GLOB ~^[_s]toblack\.sto$~ ~override~  // adds Ninja-To +1 to Black Lily's store
  ADD_STORE_ITEM ~sw1h49~ AFTER  ~sw1h07 _sw1h07~ #0 #0 #0 ~IDENTIFIED~ #1

COPY_EXISTING_REGEXP GLOB ~^[_h]ighhedg\.sto$~ ~override~  // adds lev 1, 2 scrolls to High Hedge
                          ~^_?sto0703\.sto$~   ~override~  // adds lev 1, 2 scrolls to Sorcerous Sundries
  ADD_STORE_ITEM ~scrl6d~ AFTER ~scrl70 _scrl70~ #0 #0 #0 ~IDENTIFIED~ #2 // find familiar
  ADD_STORE_ITEM ~scrl5u~ AFTER ~scrl70 _scrl70~ #0 #0 #0 ~IDENTIFIED~ #2 // reflected image
  ADD_STORE_ITEM ~scrla6~ AFTER ~scrl70 _scrl70~ #0 #0 #0 ~IDENTIFIED~ #2 // spook
  ADD_STORE_ITEM ~scrl6e~ AFTER ~scrl89 _scrl89~ #0 #0 #0 ~IDENTIFIED~ #2 // pw: sleep

COPY_EXISTING_REGEXP GLOB ~^_?sto0703\.sto$~ ~override~  // adds lev 3, 4 scrolls to Sorcerous Sundries
  ADD_STORE_ITEM ~scrl6l~ AFTER ~scrl1k _scrl1k~ #0 #0 #0 ~IDENTIFIED~ #2 // hold undead
  ADD_STORE_ITEM ~scrl6i~ AFTER ~scrl1k _scrl1k~ #0 #0 #0 ~IDENTIFIED~ #2 // protection from cold
  ADD_STORE_ITEM ~scrla5~ AFTER ~scrl1k _scrl1k~ #0 #0 #0 ~IDENTIFIED~ #2 // MMM
  ADD_STORE_ITEM ~scrl6r~ AFTER ~scrl5i _scrl5i~ #0 #0 #0 ~IDENTIFIED~ #2 // spider spawn

COPY_EXISTING_REGEXP GLOB ~^[_h]ighhedg\.sto$~ ~override~ // High Hedge
  ADD_STORE_ITEM ~cdioun1~ LAST #0 #0 #0 ~IDENTIFIED~ #1 // purple ioun stone

COPY_EXISTING_REGEXP GLOB ~^_?taerum\.sto$~    ~override~ // Thunderhammer Smithy
  ADD_STORE_ITEM ~cdplcase~ LAST #0 #0 #0 ~IDENTIFIED~ #1 // case of plenty

COPY_EXISTING_REGEXP GLOB ~^_?sto4803\.sto$~   ~override~ // Nashkel General Store
  ADD_STORE_ITEM ~cdplbag~ LAST #0 #0 #0 ~IDENTIFIED~ #1 // bag of plenty

COPY_EXISTING_REGEXP GLOB ~^_?sto4909\.sto$~   ~override~ // Carnival
  ADD_STORE_ITEM ~cdplquiv~ LAST #0 #0 #0 ~IDENTIFIED~ #1 // quiver of plenty

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.sto$~ ~override~  // adds generic scimitars to stores that sell short swords if not present already
  READ_LONG 0x34 "sale_offset"  ELSE 0
  READ_LONG 0x38 "sale_num"     ELSE 0
  READ_LONG 0x2c "item_offset"  ELSE 0
  READ_LONG 0x4c "drink_offset" ELSE 0
  READ_LONG 0x70 "cure_offset"  ELSE 0
  SET "scimitar_exist" = 0
  SET "scimitar_insert" = 0
  FOR (index = 0 ; index < sale_num ; index = index + 1) BEGIN
    READ_ASCII ("%sale_offset%" +        ("%index%" * 0x1c)) "item"
    PATCH_IF (("%item%" STRING_COMPARE_CASE "sw1h07" = 0) OR ("%item%" STRING_COMPARE_CASE "_sw1h07" = 0)) BEGIN // short sword
      READ_ASCII ("%sale_offset%" +        ("%index%" * 0x1c)) "clone" (28) // read short sword entry
      SET "scimitar_insert" = ("%index%" + 1) // sets right after as insert point
    END ELSE
    PATCH_IF (("%item%" STRING_COMPARE_CASE "sw1h20" = 0) OR ("%item%" STRING_COMPARE_CASE "_sw1h20" = 0)) BEGIN // scimitar
      SET "scimitar_exist" = 1 // scimitars are already present
      SET "index" = "%sale_num%" // kills loop
    END
  END
  PATCH_IF (("%scimitar_exist%" = 0) AND ("%scimitar_insert%" > 0)) BEGIN // short swords present but not scimitars
    INSERT_BYTES            ("%sale_offset%" + ("%scimitar_insert%" * 0x1C)) 28
      WRITE_EVALUATED_ASCII ("%sale_offset%" + ("%scimitar_insert%" * 0x1C)) ~%clone%~ // clones short sword entry
      WRITE_ASCII           ("%sale_offset%" + ("%scimitar_insert%" * 0x1C)) ~sw1h20~ #8  // scimitar
    // correcting offsets and such
    WRITE_LONG 0x38 ("%sale_num%" + 1)
    PATCH_IF NOT ("%item_offset%" < "%sale_offset%") BEGIN
      WRITE_LONG 0x2c ("%item_offset%" + 0x1c)
    END
    PATCH_IF NOT ("%drink_offset%" < "%sale_offset%") BEGIN
      WRITE_LONG 0x4c ("%drink_offset%" + 0x1c)
    END
    PATCH_IF NOT ("%cure_offset%" < "%sale_offset%") BEGIN
      WRITE_LONG 0x70 ("%cure_offset%" + 0x1c)
    END
  END
  BUT_ONLY_IF_IT_CHANGES

// if Ashes of Embers is installed, the katana and quivers should be usable by all
ACTION_IF ((FILE_EXISTS_IN_GAME ~j#swsoa.txt~) OR                        // SoA version of AoE (for BGT)
           (FILE_EXISTS_IN_GAME ~J#ToBAoE.txt~) OR                       // Tutu version of AoE
           (FILE_EXISTS_IN_GAME ~j#sensibleweaponsmods.txt~)) THEN BEGIN // mod-only version of AoE

  COPY_EXISTING ~cdplquiv.itm~ ~override~ // quiver of plenty +0
                ~cdplcase.itm~ ~override~ // case of plenty +0
                ~cdplbag.itm~  ~override~ // bag of plenty +0
                ~cdkat2.itm~   ~override~ // katana +2
    WRITE_LONG 0x1e 0 // remove all class/race/align unusabilities
    WRITE_BYTE 0x29 0 // remove all kit unusabilities
    WRITE_BYTE 0x2b 0 // remove all kit unusabilities
    WRITE_BYTE 0x2d 0 // remove all kit unusabilities
    WRITE_BYTE 0x2f 0 // remove all kit unusabilities

  COPY_EXISTING ~cdkat2.itm~ ~override~   // katana +2
    WRITE_BYTE 0x2b 0b00000010 // remove all kit unusabilities except for Beastmaster

END

ACTION_IF GAME_IS ~bgee~ BEGIN // bgee

  COPY_EXISTING ~cdkat2.itm~ ~override~ // Katana +2
    SAY UNIDENTIFIED_DESC @109025 // bgee automatically includes usability block
    SAY IDENTIFIED_DESC @109026

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Reveal City Areas                                \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @110000 DESIGNATED 1100
GROUP @13
REQUIRE_PREDICATE GAME_IS ~tutu tutu_totsc bgee~ @25 // tutu, BGEE only

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt01100.g3~

ACTION_IF FILE_EXISTS_IN_GAME ~_ar0300.bcs~ BEGIN // tutu bugfix

  //fixes Narlen's goofiness
  COPY_EXISTING ~_ar0300.bcs~ ~override~
    DECOMPILE_BCS_TO_BAF
      REPLACE_TEXTUALLY ~TimeGT(20)~ ~TimeGT(20) Global("CDNarlenExists","GLOBAL",0)~
      REPLACE_TEXTUALLY ~TimeLT(4)~  ~TimeLT(4)  Global("CDNarlenExists","GLOBAL",0)~
      REPLACE_TEXTUALLY ~TimeGT(4)~  ~TimeGT(4)  Global("CDNarlenExists","GLOBAL",1)~
      REPLACE_TEXTUALLY ~\bActivate("narlen")~   ~Activate("narlen") SetGlobal("CDNarlenExists","GLOBAL",1)~
      REPLACE_TEXTUALLY ~Deactivate("narlen")~ ~Deactivate("narlen") SetGlobal("CDNarlenExists","GLOBAL",0)~
    COMPILE_BAF_TO_BCS
    BUT_ONLY_IF_IT_CHANGES
    
END

ACTION_FOR_EACH file IN   
  ~_ar0100.bcs~ // NW Baldur's Gate
  ~_ar0200.bcs~ // N Baldur's Gate
  ~_ar0300.bcs~ // NE Baldur's Gate
  ~_ar0600.bcs~ // W Baldur's Gate
  ~_ar0700.bcs~ // Central Baldur's Gate
  ~_ar0800.bcs~ // E Baldur's Gate
  ~_ar1000.bcs~ // Ulgoth's Beard
  ~_ar1100.bcs~ // SW Baldur's Gate
  ~_ar1200.bcs~ // S Baldur's Gate
  ~_ar1300.bcs~ // SE Baldur's Gate
  ~_ar2600.bcs~ // Candlekeep Prologue
  ~_ar2626.bcs~ // Candlekeep Chapter 6
  ~_ar3300.bcs~ // Beregost
  ~_ar4800.bcs~ // Nashkel
  ~ar0100.bcs~  // NW Baldur's Gate
  ~ar0200.bcs~  // N Baldur's Gate
  ~ar0300.bcs~  // NE Baldur's Gate
  ~ar0600.bcs~  // W Baldur's Gate
  ~ar0700.bcs~  // Central Baldur's Gate
  ~ar0800.bcs~  // E Baldur's Gate
  ~ar1000.bcs~  // Ulgoth's Beard
  ~ar1100.bcs~  // SW Baldur's Gate
  ~ar1200.bcs~  // S Baldur's Gate
  ~ar1300.bcs~  // SE Baldur's Gate
  ~ar2600.bcs~  // Candlekeep Prologue
  ~ar2626.bcs~  // Candlekeep Chapter 6
  ~ar3300.bcs~  // Beregost
  ~ar4800.bcs~  // Nashkel
  BEGIN
  
  ACTION_IF FILE_EXISTS_IN_GAME ~%file%~ BEGIN
  
    EXTEND_BOTTOM ~%file%~ ~bg2_tweaks/baf/reveal.baf~
  
  END
  
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Do Not Reveal City Areas                         \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @110100 DESIGNATED 1101
GROUP @13
REQUIRE_PREDICATE NOT GAME_IS ~tutu tutu_totsc bgee~ @25 // for non-tutu, non-BGEE only
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~howparty.2da~ @25 // non-iwdee

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt01101.g3~

ACTION_FOR_EACH file IN
  AR0015 // BGT: Candlekeep Prologue
  AR0300 // bg2: docks
  AR0400 // bg2: slums
  AR0500 // bg2: bridge district
  AR0900 // bg2: temple district
  AR1000 // bg2: government district
  AR2000 // bg2: trademeet
  AR2200 // bg2: ust natha
  AR2800 // bg2: Suldanesselar
  AR3700 // BGT: Nashkel
  AR5000 // bg2: saradush
  AR5500 // bg2: amkethran
  AR6526 // BGT: Candlekeep Chapter 6
  AR6700 // BGT: Beregost
  AR7200 // BGT: NW Baldur's Gate
  AR7300 // BGT: N Baldur's Gate
  AR7400 // BGT: NE Baldur's Gate
  AR7600 // BGT: W Baldur's Gate
  AR7700 // BGT: Central Baldur's Gate
  AR7800 // BGT: E Baldur's Gate
  AR8000 // BGT: SW Baldur's Gate
  AR8100 // BGT: S Baldur's Gate
  AR8200 // BGT: SE Baldur's Gate
  ARU000 // BGT: Ulgoth's Beard
  oh4010 // bg2ee
  oh4290 // bg2ee
  oh5400 // bg2ee
  oh6010 // bg2ee
  oh8000 // bg2ee
  cut01  // promenade destruction cutscene
  cutd1  // promenade destruction cutscene
  cut220a // pp transport to saradush

  BEGIN
  
  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.bcs~ BEGIN

    COPY_EXISTING ~%file%.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~[ %TAB%]Explore()~ ~ ~
      END
      BUT_ONLY
  
  END
  
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Add Map Notes                                    \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @111000 DESIGNATED 1110
GROUP @13
REQUIRE_PREDICATE GAME_IS ~tutu tutu_totsc~ @25 // tutu only

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt01110.g3~

/*
Note Color key:
Gray    - Caves & Dungeon Entrances
Violet  - Inns & Taverns
Green   - Temples
Orange  - Info points
Red     - Open space locations (i.e. city squares)
Blue    - Buildings of note
Dk Blue - People
Lt Gray - Stores
*/

COPY_EXISTING ~fw0100.are~ ~override~ // NW Baldur's Gate
  ADD_MAP_NOTE #1230 #1706 ~blue~   @111010
  ADD_MAP_NOTE #3610 #3007 ~violet~ @111011
  ADD_MAP_NOTE #2940 #1716 ~violet~ @111012
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw0123.are~ ~override~ // Temple of Bhaal
  ADD_MAP_NOTE #1668 #1297 ~blue~ @111096
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw0146.are~ ~override~ // Thieves' Maze
  ADD_MAP_NOTE #583 #1148 ~gray~ @111095
  ADD_MAP_NOTE #414 #2763 ~gray~ @111095
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw0200.are~ ~override~ // N Baldur's Gate
  ADD_MAP_NOTE #1550 #1123 ~green~  @111013
  ADD_MAP_NOTE #200  #1462 ~red~    @111014
  ADD_MAP_NOTE #3581 #1464 ~blue~   @111015
  ADD_MAP_NOTE #1963 #2795 ~blue~   @111016
  ADD_MAP_NOTE #3782 #2418 ~violet~ @111017
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw0300.are~ ~override~ // NE Baldur's Gate
  ADD_MAP_NOTE #144  #1799 ~violet~ @111018
  ADD_MAP_NOTE #541  #2854 ~violet~ @111019
  ADD_MAP_NOTE #1215 #1345 ~green~  @111020
  ADD_MAP_NOTE #2276 #3134 ~blue~   @111021
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw0400.are~ ~override~ // Zombie Farm
  ADD_MAP_NOTE #638 #454 ~blue~ @111058
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw0600.are~ ~override~ // W Baldur's Gate
  ADD_MAP_NOTE #1110 #1080 ~green~ @111083
  ADD_MAP_NOTE #1520 #2289 ~blue~  @111084
  ADD_MAP_NOTE #3439 #873  ~blue~  @111085
  ADD_MAP_NOTE #2914 #551  ~blue~  @111086
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw0700.are~ ~override~ // Central Baldur's Gate
  ADD_MAP_NOTE #2536 #873 ~blue~      @111087
  ADD_MAP_NOTE #730  #567 ~lightgray~ @111088
  ADD_MAP_NOTE #1729 #1230 ~violet~   @111089
  ADD_MAP_NOTE #1993 #720 ~blue~      @111090
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw0800.are~ ~override~ // E Baldur's Gate
  ADD_MAP_NOTE #1780 #2451 ~green~     @111065
  ADD_MAP_NOTE #1055 #2015 ~lightgray~ @111066
  ADD_MAP_NOTE #3151 #1093 ~lightgray~ @111067
  ADD_MAP_NOTE #1073 #3017 ~lightgray~ @111068
  ADD_MAP_NOTE #1728 #1354 ~violet~    @111069
  ADD_MAP_NOTE #3259 #1974 ~lightgray~ @111070
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw0900.are~ ~override~ // Wyrm's Crossing
  ADD_MAP_NOTE #2342 #2028 ~gray~ @111059
  ADD_MAP_NOTE #4093 #708  ~blue~ @111033
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw1100.are~ ~override~ // SW Baldur's Gate
  ADD_MAP_NOTE #3535 #1327 ~blue~      @111079
  ADD_MAP_NOTE #1556 #1385 ~blue~      @111080
  ADD_MAP_NOTE #3255 #2534 ~blue~      @111081
  ADD_MAP_NOTE #2742 #1063 ~lightgray~ @111072
  ADD_MAP_NOTE #2487 #2377 ~lightgray~ @111072
  ADD_MAP_NOTE #2526 #823  ~violet~    @111082
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw1200.are~ ~override~ // S Baldur's Gate
  ADD_MAP_NOTE #2568 #1858 ~blue~   @111074
  ADD_MAP_NOTE #3654 #2976 ~violet~ @111075
  ADD_MAP_NOTE #931  #2415 ~green~  @111076
  ADD_MAP_NOTE #400  #677  ~blue~   @111077
  ADD_MAP_NOTE #2624 #588 ~violet~  @111078
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw1300.are~ ~override~ // SE Baldur's Gate
  ADD_MAP_NOTE #166  #729  ~violet~    @111071
  ADD_MAP_NOTE #2135 #316  ~lightgray~ @111072
  ADD_MAP_NOTE #960  #2561 ~lightgray~ @111072
  ADD_MAP_NOTE #1699 #2399 ~lightgray~ @111073
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw1400.are~ ~override~ // Fishing Village
  ADD_MAP_NOTE #1899 #2402 ~gray~ @111057
  ADD_MAP_NOTE #3504 #2233 ~blue~ @111058
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw1600.are~ ~override~ // Cloakwood Druids
  ADD_MAP_NOTE #2194 #1001 ~gray~ @111034
  ADD_MAP_NOTE #804  #2174 ~blue~ @111062
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw1700.are~ ~override~ // Cloakwood Wyverns
  ADD_MAP_NOTE #4222 #1709 ~gray~ @111034
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw1800.are~ ~override~ // Cloakwood Mines Exterior
  ADD_MAP_NOTE #3247 #793  ~blue~ @111042
  ADD_MAP_NOTE #1375 #1852 ~blue~ @111008
  ADD_MAP_NOTE #1904 #1554 ~blue~ @111004
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw1801.are~ ~override~ // Cloakwood Mines Lvl 1
  ADD_MAP_NOTE #1975 #71   ~orange~ @111063
  ADD_MAP_NOTE #341  #133  ~gray~   @111064
  ADD_MAP_NOTE #1406 #1563 ~gray~   @111050
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw1802.are~ ~override~ // Cloakwood Mines Lvl 3
  ADD_MAP_NOTE #2612 #2916 ~gray~ @111050
  ADD_MAP_NOTE #756  #1219 ~gray~ @111051
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw1803.are~ ~override~ // Cloakwood Mines Lvl 4
  ADD_MAP_NOTE #205  #1177 ~orange~ @111064
  ADD_MAP_NOTE #1711 #209  ~gray~   @111051
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw1804.are~ ~override~ // Cloakwood Mines Lvl 2
  ADD_MAP_NOTE #436  #1564 ~gray~ @111050
  ADD_MAP_NOTE #1157 #110  ~gray~ @111051
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw1900.are~ ~override~ // Bandit Camp
  ADD_MAP_NOTE #2326 #773 ~gray~ @111034
  ADD_MAP_NOTE #3655 #957 ~blue~ @111060
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw2100.are~ ~override~ // Cloakwood Spider Nest
  ADD_MAP_NOTE #1817 #1240 ~blue~ @111061
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw2200.are~ ~override~ // Cloakwood Lodge
  ADD_MAP_NOTE #2477 #1790 ~blue~ @111043
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw2300.are~ ~override~ // Friendly Arm Inn
  ADD_MAP_NOTE #1550 #2970 ~red~    @111005
  ADD_MAP_NOTE #3511 #1903 ~violet~ @111024
  ADD_MAP_NOTE #3873 #2463 ~green~  @111025
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw2600.are~ ~override~ // Candlekeep Prologue
              ~fw2626.are~ ~override~ // Candlekeep Chapter 6
  ADD_MAP_NOTE #1071 #557  ~violet~ @111001
  ADD_MAP_NOTE #2959 #464  ~blue~   @111002
  ADD_MAP_NOTE #3843 #728  ~green~  @111003
  ADD_MAP_NOTE #4303 #1410 ~blue~   @111004
  ADD_MAP_NOTE #4396 #2561 ~red~    @111005
  ADD_MAP_NOTE #2607 #1679 ~blue~   @111006
  ADD_MAP_NOTE #2053 #2696 ~blue~   @111007
  ADD_MAP_NOTE #1569 #2487 ~blue~   @111008
  ADD_MAP_NOTE #3531 #2916 ~blue~   @111009
  ADD_MAP_NOTE #3063 #2981 ~blue~   @111022
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw2615.are~ ~override~ // Candlekeep Catacombs Lvl 1
  ADD_MAP_NOTE #4026 #1712 ~gray~ @111049
  ADD_MAP_NOTE #3826 #921 ~gray~  @111049
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw2619.are~ ~override~ // Candlekeep Catacombs Lvl 2
  ADD_MAP_NOTE #3552 #2308 ~gray~ @111049
  ADD_MAP_NOTE #240  #1040 ~gray~ @111049
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw2800.are~ ~override~ // Coast Way
  ADD_MAP_NOTE #2359 #3335 ~orange~ @111023
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw3100.are~ ~override~ // Shipwreck Coast
  ADD_MAP_NOTE #958 #1646 ~orange~ @111055
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw3200.are~ ~override~ // High Hedge
  ADD_MAP_NOTE #2893 #2671 ~lightgray~ @111032
  ADD_MAP_NOTE #1732 #1583 ~blue~      @111033
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw3300.are~ ~override~ // Beregost
  ADD_MAP_NOTE #3711 #3754 ~violet~    @111026
  ADD_MAP_NOTE #1470 #2731 ~violet~    @111027
  ADD_MAP_NOTE #4612 #2906 ~lightgray~ @111028
  ADD_MAP_NOTE #3403 #2039 ~violet~    @111029
  ADD_MAP_NOTE #2596 #2189 ~violet~    @111030
  ADD_MAP_NOTE #3643 #993  ~blue~      @111031
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw3400.are~ ~override~ // Beregost Temple
  ADD_MAP_NOTE #414  #916 ~green~ @111046
  ADD_MAP_NOTE #1463 #893 ~green~ @111047
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw3600.are~ ~override~ // Lighthouse
  ADD_MAP_NOTE #393 #991  ~gray~   @111034
  ADD_MAP_NOTE #883 #2702 ~orange~ @111056
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw3800.are~ ~override~ // South Beregost Road
  ADD_MAP_NOTE #1861 #2269 ~gray~   @111034
  ADD_MAP_NOTE #3172 #1447 ~orange~ @111035
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw3900.are~ ~override~ // Ulcaster Ruins
  ADD_MAP_NOTE #3159 #663 ~gray~ @111048
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw4000.are~ ~override~ // Gullykin
  ADD_MAP_NOTE #882 #721 ~orange~ @111053
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw4100.are~ ~override~ // Archaelogical Dig
  ADD_MAP_NOTE #3035 #1285 ~gray~   @111034
  ADD_MAP_NOTE #3751 #2173 ~orange~ @111023
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw4200.are~ ~override~ // Fisherman's Lake
  ADD_MAP_NOTE #1379 #714 ~orange~ @111045
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw4400.are~ ~override~ // Lonely Peaks
  ADD_MAP_NOTE #1485 #1199 ~gray~   @111034
  ADD_MAP_NOTE #2711 #2694 ~orange~ @111045
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw4500.are~ ~override~ // Firewine Bridge
  ADD_MAP_NOTE #4027 #1983 ~gray~ @111054
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw4700.are~ ~override~ // Xvart Village
  ADD_MAP_NOTE #4217 #1025 ~gray~ @111034
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw4800.are~ ~override~ // Nashkel
  ADD_MAP_NOTE #2725 #949  ~green~     @111013
  ADD_MAP_NOTE #1308 #674  ~violet~    @111036
  ADD_MAP_NOTE #1665 #902  ~lightgray~ @111037
  ADD_MAP_NOTE #2689 #2441 ~violet~    @111038
  ADD_MAP_NOTE #4765 #558  ~violet~    @111039
  ADD_MAP_NOTE #3331 #1780 ~blue~      @111040
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw5000.are~ ~override~ // Valley of the Tombs
  ADD_MAP_NOTE #944  #1793 ~gray~   @111034
  ADD_MAP_NOTE #1868 #2897 ~gray~   @111034
  ADD_MAP_NOTE #4494 #2855 ~gray~   @111034
  ADD_MAP_NOTE #1771 #886  ~orange~ @111094
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw5100.are~ ~override~ // Gnoll Stronghold
  ADD_MAP_NOTE #591  #2147 ~gray~ @111034
  ADD_MAP_NOTE #152  #3376 ~gray~ @111034
  ADD_MAP_NOTE #818  #2420 ~gray~ @111034
  ADD_MAP_NOTE #2564 #2341 ~red~  @111041
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw5200.are~ ~override~ // Cloudpeak Mountains
  ADD_MAP_NOTE #3163 #2674 ~gray~ @111034
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw5201.are~ ~override~ // Firewine Ruins
  ADD_MAP_NOTE #2256 #1673 ~gray~ @111049
  ADD_MAP_NOTE #538  #215  ~gray~ @111049
  ADD_MAP_NOTE #1052 #1655 ~gray~ @111049
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw5400.are~ ~override~ // Nashkel Mines Exterior
  ADD_MAP_NOTE #1202 #717  ~gray~   @111042
  ADD_MAP_NOTE #2796 #430  ~blue~   @111043
  ADD_MAP_NOTE #641  #2713 ~orange~ @111044
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw5401.are~ ~override~ // Nashkel Mines Lvl 1
  ADD_MAP_NOTE #1333 #187  ~gray~ @111049
  ADD_MAP_NOTE #2076 #1884 ~gray~ @111050
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw5402.are~ ~override~ // Nashkel Mines Lvl 2
  ADD_MAP_NOTE #2306 #2710 ~gray~ @111051
  ADD_MAP_NOTE #3610 #2516 ~gray~ @111050
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw5403.are~ ~override~ // Nashkel Mines Lvl 3
  ADD_MAP_NOTE #1593 #120  ~gray~ @111051
  ADD_MAP_NOTE #3357 #2416 ~gray~ @111050
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw5404.are~ ~override~ // Nashkel Mines Lvl 4
  ADD_MAP_NOTE #424  #1893 ~gray~ @111051
  ADD_MAP_NOTE #1416 #1211 ~gray~ @111034
  ADD_MAP_NOTE #2975 #908  ~gray~ @111049
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw5506.are~ ~override~ // Candlekeep Catacombs Lvl 3
  ADD_MAP_NOTE #689 #124  ~gray~ @111049
  ADD_MAP_NOTE #279 #2107 ~gray~ @111052
  BUT_ONLY_IF_IT_CHANGES

// the following areas are TotSC only
ACTION_IF FILE_EXISTS_IN_GAME ~fw1500.are~ THEN BEGIN // if TotSC is installed

  COPY_EXISTING ~fw0500.are~ ~override~ // Durlag's Tower Exterior
    ADD_MAP_NOTE #2703 #2163 ~gray~ @111093
    BUT_ONLY_IF_IT_CHANGES

  COPY_EXISTING ~fw1000.are~ ~override~ // Ulgoth's Beard
    ADD_MAP_NOTE #2310 #612 ~blue~ @111091
    ADD_MAP_NOTE #350  #626 ~blue~ @111092
    BUT_ONLY_IF_IT_CHANGES

  COPY_EXISTING ~fw1009.are~ ~override~ // Ice Island Dungeon
    ADD_MAP_NOTE #2389 #1670 ~gray~ @111052
    ADD_MAP_NOTE #620  #170  ~gray~ @111052
    BUT_ONLY_IF_IT_CHANGES

  COPY_EXISTING ~fw1500.are~ ~override~ // N Werewolf Island
    ADD_MAP_NOTE #4200 #1683 ~blue~ @111043
    ADD_MAP_NOTE #3697 #1143 ~red~  @111055
    BUT_ONLY_IF_IT_CHANGES

  COPY_EXISTING ~fw2012.are~ ~override~ // Werewolf Caverns
    ADD_MAP_NOTE #974  #1392 ~gray~ @111052
    ADD_MAP_NOTE #2977 #401  ~gray~ @111052
    BUT_ONLY_IF_IT_CHANGES

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Stores Sell Higher Stacks of Items               \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @112000 DESIGNATED 1120
GROUP @13

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt01120.g3~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.sto$~ ~override~
  READ_LONG 0x34 "sale_offset"
  READ_LONG 0x38 "sale_num"
  FOR (index = 0 ; index < sale_num ; index = index + 1) BEGIN
    READ_ASCII ("%sale_offset%" +        ("%index%" * 0x1c)) "item"
    PATCH_IF ("%item%" STRING_COMPARE_REGEXP "^_?\(BOLT\|DART\|AROW\|BULL\)01$" = 0) BEGIN // bolt01, dart01, arow01, bull01
      WRITE_SHORT ("%sale_offset%" + 0x0a + ("%index%" * 0x1c)) 120
    END ELSE
    PATCH_IF ("%item%" STRING_COMPARE_REGEXP "^_?\(DAGG05\|AX1H04\)$" = 0) BEGIN // dagg05, ax1h04
      WRITE_SHORT ("%sale_offset%" + 0x0a + ("%index%" * 0x1c)) 20
    END
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Reset rep at beginning of BG2 (BGT)              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @113000 DESIGNATED 1130
GROUP @13
REQUIRE_PREDICATE GAME_IS ~bgt~ @25
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~cdt01130.g3~ @16 // already installed by BGT tweaks

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt01130.g3~

EXTEND_BOTTOM ~ar0602.bcs~ ~bg2_tweaks/baf/ar0602.baf~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Gems and potions have lore                       \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @114000 DESIGNATED 1140
GROUP @13

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt01140.g3~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
    READ_SHORT 0x1c "type"
    READ_SHORT 0x42 "lore"
    PATCH_IF ((("%type%" = 9)) AND ("%lore%" = 0)) BEGIN // potions or gems
      READ_LONG  0x34 "price"
      READ_LONG  0x64 "abil_off"
      READ_SHORT 0x68 "abil_num"
      // lore
      PATCH_IF ("%price%" > 4025) BEGIN
        WRITE_SHORT 0x42 (60 + (("%price%" - 4025) / 400))
      END ELSE
      PATCH_IF ("%price%" > 2025) BEGIN
        WRITE_SHORT 0x42 (50 + (("%price%" - 2025) / 200))
      END ELSE
      PATCH_IF ("%price%" > 1025) BEGIN
        WRITE_SHORT 0x42 (40 + (("%price%" - 1025) / 100))
      END ELSE
      PATCH_IF ("%price%" > 425) BEGIN
        WRITE_SHORT 0x42 (30 + (("%price%" - 425) / 60))
      END ELSE
      PATCH_IF ("%price%" > 175) BEGIN
        WRITE_SHORT 0x42 (20 + (("%price%" - 225) / 25))
      END ELSE
      PATCH_IF ("%price%" > 25) BEGIN
        WRITE_SHORT 0x42 (5 + (("%price%" - 50) / 10))
      END ELSE
      PATCH_IF ("%price%" > 5) BEGIN
        WRITE_SHORT 0x42 ("%price%" / 5)
      END ELSE BEGIN
        WRITE_SHORT 0x42 1
      END
      // force them to be id'd before use
      FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN
        READ_BYTE ("%abil_off%" +        ("%index%" * 0x38)) "abil_type"
        PATCH_IF ("%abil_type%" = 3) BEGIN // if magical ability
          WRITE_BYTE ("%abil_off%" + 0x01 + ("%index%" * 0x38)) 1 // force identification before use
        END
      END
      // strref adjustments
      READ_LONG 0x0c "name_id"
      READ_LONG 0x54 "desc_id"
      PATCH_IF ("%name_id%" > 2147483646) OR ("%name_id%" < 1) THEN BEGIN // if no id'd name, move un-id strings to id
        READ_LONG  0x08 "name_nid"
        WRITE_LONG 0x0c "%name_nid%"
      END
      PATCH_IF ("%desc_id%" > 2147483646) OR ("%desc_id%" < 1) THEN BEGIN // if no id'd desc, move un-id strings to id
        READ_LONG  0x50 "desc_nid"
        WRITE_LONG 0x54 "%desc_nid%"
      END
      PATCH_IF ("%type%" = 9) BEGIN // potions
        SAY 0x08  #6976
        SAY 0x50 #16264
      END ELSE BEGIN // gems
        SAY 0x08   #7106
        SAY 0x50 @114001
      END
    END
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Shapeshifter rebalancing                         \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @115000 DESIGNATED 1150
GROUP @13

ACTION_IF GAME_IS ~tutu tutu_totsc~ THEN BEGIN // Tutu

  COPY_EXISTING ~_b1-3.itm~    ~override~ // adds magical flag to misc weapons
                ~_b2-16.itm~   ~override~
                ~_b1-20.itm~   ~override~
                ~_drizzts.itm~ ~override~
                ~_fblade.itm~  ~override~
                ~_s1-10.itm~   ~override~
                ~_s1-12.itm~   ~override~
                ~_s2-16.itm~   ~override~
                ~_sarevo.itm~  ~override~
    PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
      READ_BYTE  0x18 "flags"
      WRITE_BYTE 0x18 ("%flags%" BOR 0b01000000)
    END
    BUT_ONLY_IF_IT_CHANGES

END

COPY_EXISTING ~sppr604.spl~ ~override~
  WRITE_SHORT 0x20 16384
  SAY IDENTIFIED_DESC @115001
  BUT_ONLY_IF_IT_CHANGES

COPY ~bg2_tweaks/cre/wwbearwe.cre~ ~override~
  SAY NAME1 @115002
  SAY NAME2 @115002

COPY ~bg2_tweaks/cre/wwbear.cre~ ~override~
  SAY NAME1 @115003
  SAY NAME2 @115003

COPY ~bg2_tweaks/2da/anisum04.2da~ ~override~
     ~bg2_tweaks/2da/anisum05.2da~ ~override~
     ~bg2_tweaks/2da/clabdr03.2da~ ~override~
     ~bg2_tweaks/eff/spanim5.eff~  ~override~
     ~bg2_tweaks/spl/spcl643.spl~  ~override~
     ~bg2_tweaks/spl/spcl644.spl~  ~override~

// if DR > v3  is installed prior to srb
ACTION_IF ((FILE_EXISTS_IN_GAME ~cdnegpp.spl~) AND (FILE_EXISTS ~Divine_Remix/lib/macro_reindex_clab.tph~)) THEN BEGIN

  INCLUDE ~Divine_Remix/lib/macro_reindex_clab.tph~
  INCLUDE ~Divine_Remix/lib/macro_add_druid_spells.tph~
  COPY_EXISTING ~clabdr03.2da~ ~override~
    LAUNCH_PATCH_MACRO ~add_druid_spells~ // add generic druid spells
    LAUNCH_PATCH_MACRO ~reindex_clab~     // re-index lines
    BUT_ONLY_IF_IT_CHANGES

END ELSE BEGIN

  // makes spells castable by rangers and druids
  COPY_EXISTING ~sppr109.spl~ ~override~
                ~sppr203.spl~ ~override~
                ~sppr318.spl~ ~override~
                ~sppr304.spl~ ~override~
                ~sppr513.spl~ ~override~
    WRITE_BYTE 0x21 (THIS & `BIT7)
    BUT_ONLY

  // update spellbooks for druids for new druid-allowed spells
  COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
    READ_LONG 0x1cc joinable ELSE 0
    READ_BYTE 0x273 class
    PATCH_IF ((joinable > 0) AND ((class = 11) OR (class = 16))) BEGIN // check for non-null bio string, druid class
      PATCH_IF (class = 16) BEGIN // fighter-druids
        READ_BYTE 0x235 level
      END ELSE BEGIN              // vanilla druids
        READ_BYTE 0x234 level
      END
      ADD_KNOWN_SPELL ~sppr109~ #0 ~priest~
      PATCH_IF (level > 2) BEGIN
        ADD_KNOWN_SPELL ~sppr203~ #1 ~priest~
      END
      PATCH_IF (level > 4) BEGIN
        ADD_KNOWN_SPELL ~sppr304~ #2 ~priest~
        ADD_KNOWN_SPELL ~sppr318~ #2 ~priest~
      END
      PATCH_IF (level > 8) BEGIN
        ADD_KNOWN_SPELL ~sppr513~ #4 ~priest~
      END
    END
    BUT_ONLY

END

COPY ~bg2_tweaks/itm/wwwere.itm~ ~override~
  SAY IDENTIFIED_DESC @115004

COPY ~bg2_tweaks/itm/wwweregr.itm~ ~override~
  SAY IDENTIFIED_DESC @115005

// if something installed that allows shapeshifters to be non-TN alignments, adjust the paw usability
COPY_EXISTING ~alignmnt.2da~ ~override~
  READ_2DA_ENTRY 42 1 10 "lg"
  READ_2DA_ENTRY 42 2 10 "ln"
  READ_2DA_ENTRY 42 3 10 "le"
  READ_2DA_ENTRY 42 4 10 "ng"
  READ_2DA_ENTRY 42 6 10 "ne"
  READ_2DA_ENTRY 42 7 10 "cg"
  READ_2DA_ENTRY 42 8 10 "cn"
  READ_2DA_ENTRY 42 9 10 "ce"

ACTION_IF (("%lg%" = 1) OR ("%ln%" = 1) OR ("%le%" = 1)) THEN BEGIN // remove lawful flag

  COPY_EXISTING ~wwwere.itm~   ~override~
                ~wwweregr.itm~ ~override~
    READ_BYTE  0x1e "use"
    WRITE_BYTE 0x1e ("%use%" BAND 0b11101111) // removes lawful flag
    BUT_ONLY_IF_IT_CHANGES

END

ACTION_IF (("%cg%" = 1) OR ("%cn%" = 1) OR ("%ce%" = 1)) THEN BEGIN // remove chaotic flag

  COPY_EXISTING ~wwwere.itm~   ~override~
                ~wwweregr.itm~ ~override~
    READ_BYTE  0x1e "use"
    WRITE_BYTE 0x1e ("%use%" BAND 0b11111110) // removes chaotic flag
    BUT_ONLY_IF_IT_CHANGES

END

ACTION_IF (("%lg%" = 1) OR ("%ng%" = 1) OR ("%cg%" = 1)) THEN BEGIN // remove good flag

  COPY_EXISTING ~wwwere.itm~   ~override~
                ~wwweregr.itm~ ~override~
    READ_BYTE  0x1e "use"
    WRITE_BYTE 0x1e ("%use%" BAND 0b11111011) // removes good flag
    BUT_ONLY_IF_IT_CHANGES

END

ACTION_IF (("%le%" = 1) OR ("%ne%" = 1) OR ("%ce%" = 1)) THEN BEGIN // remove evil flag

  COPY_EXISTING ~wwwere.itm~   ~override~
                ~wwweregr.itm~ ~override~
    READ_BYTE  0x1e "use"
    WRITE_BYTE 0x1e ("%use%" BAND 0b11111101) // removes evil flag
    BUT_ONLY_IF_IT_CHANGES

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Multiple Strongholds                             \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// no restrictions                                  \\\\\
/////                                                  \\\\\

BEGIN @116001 DESIGNATED 1160
GROUP @13
SUBCOMPONENT @116000 // multi strongholds
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee~ @25

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt01160.g3~

COMPILE ~bg2_tweaks/dlg/stronghold1.d~  // removes stronghold var
        ~bg2_tweaks/dlg/stronghold2.d~  // removes class checks
        ~bg2_tweaks/dlg/stronghold2a.d~ // allows choice for bard, ranger, druid strongholds

COPY_EXISTING ~ar0900.bcs~ ~override~
              ~ar0901.bcs~ ~override~
              ~ar0902.bcs~ ~override~
              ~ar0904.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(Global("GaalSpoke","AR0900",1)\|Global("TelwynSpawn","GLOBAL",0)\|Global("SainSpawn","GLOBAL",0)\|Global("YarrylSpawn","GLOBAL",0)\)[ %TAB%%LNL%%MNL%%WNL%]+OR([56])[%TAB% %LNL%%MNL%%WNL%]+Class(Player1,CLERIC)[%TAB% %LNL%%MNL%%WNL%]+Class(Player1,FIGHTER_CLERIC)[%TAB% %LNL%%MNL%%WNL%]+Class(Player1,CLERIC_MAGE)[%TAB% %LNL%%MNL%%WNL%]+Class(Player1,CLERIC_THIEF)[%TAB% %LNL%%MNL%%WNL%]+Class(Player1,FIGHTER_MAGE_CLERIC)\([%TAB% %LNL%%MNL%%WNL%]+Class(Player1,CLERIC_RANGER)\)?~
      ~\1~
    REPLACE_TEXTUALLY ~\(Global("TelwynSpawn","GLOBAL",0)\|Global("GaalSpoke","AR0900",1)\)[ %TAB%%LNL%%MNL%%WNL%]+!Class(Player1,CLERIC)[ %TAB%%LNL%%MNL%%WNL%]+!Class(Player1,FIGHTER_CLERIC)[ %TAB%%LNL%%MNL%%WNL%]+!Class(Player1,CLERIC_MAGE)[ %TAB%%LNL%%MNL%%WNL%]+!Class(Player1,CLERIC_THIEF)[ %TAB%%LNL%%MNL%%WNL%]+!Class(Player1,FIGHTER_MAGE_CLERIC)~
      ~False()~
  END

//COPY_EXISTING ~ar0904.bcs~ ~override~
//  DECOMPILE_AND_PATCH BEGIN
//    REPLACE_TEXTUALLY ~Global("YarrylSpawn","GLOBAL",0)[ %TAB%%LNL%%MNL%%WNL%]+OR([56])[%TAB% %LNL%%MNL%%WNL%]+Class(Player1,CLERIC)[%TAB% %LNL%%MNL%%WNL%]+Class(Player1,FIGHTER_CLERIC)[%TAB% %LNL%%MNL%%WNL%]+Class(Player1,CLERIC_MAGE)[%TAB% %LNL%%MNL%%WNL%]+Class(Player1,CLERIC_THIEF)[%TAB% %LNL%%MNL%%WNL%]+Class(Player1,FIGHTER_MAGE_CLERIC)\([%TAB% %LNL%%MNL%%WNL%]+Class(Player1,CLERIC_RANGER)\)?~
//      ~Global("GaalSpoke","AR0900",1)~
//  END


//EXTEND_TOP ~ar0900.bcs~ ~bg2_tweaks/baf/ar0900.baf~
//EXTEND_TOP ~ar0901.bcs~ ~bg2_tweaks/baf/ar0901.baf~
//EXTEND_TOP ~ar0902.bcs~ ~bg2_tweaks/baf/ar0902.baf~
//EXTEND_TOP ~ar0904.bcs~ ~bg2_tweaks/baf/ar0904.baf~

/////                                                  \\\\\
///// keep class restrictions                          \\\\\
/////                                                  \\\\\

BEGIN @116100 DESIGNATED 1161
GROUP @13
SUBCOMPONENT @116000 // multi strongholds
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee~ @25

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt01161.g3~

COMPILE ~bg2_tweaks/dlg/stronghold1.d~  // removes stronghold var
        ~bg2_tweaks/dlg/stronghold1a.d~ // allows choice for bard, ranger, druid strongholds

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Bonus Merchants                                  \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @117000 DESIGNATED 1170
GROUP @13
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee~ @25
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~bpduearc.spl~ @16 // non-BP

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt01170.g3~

EXTEND_TOP ~ar0406.bcs~ ~bg2_tweaks/baf/ar0406.baf~
EXTEND_TOP ~ar0702.bcs~ ~bg2_tweaks/baf/ar0702.baf~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Edwina                                           \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @118000 DESIGNATED 1180
GROUP @13
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee~ @25

COPY ~bg2_tweaks/bmp/royo4l.bmp~ ~override~
     ~bg2_tweaks/bmp/royo4s.bmp~ ~override~

// add portrait change opcodes to edwin transform spells
COPY_EXISTING ~spin662.spl~ ~override~ // return to edwin
              ~spin916.spl~ ~override~ // change to edwina
  READ_LONG  0x64 "abil_off"
  READ_SHORT 0x68 "abil_num"
  READ_LONG  0x6a "fx_off"
  READ_LONG  0x34 "level"
  SET "delta" = 0
  FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN
    READ_SHORT ("%abil_off%" + 0x1e + (0x28 * "%index%")) "abil_fx_num"
    READ_SHORT ("%abil_off%" + 0x20 + (0x28 * "%index%")) "abil_fx_idx"
    SET "abil_fx_idx" = ("%abil_fx_idx%" + "%delta%")
    WRITE_SHORT ("%abil_off%" + 0x20 + (0x28 * "%index%")) "%abil_fx_idx%"
    FOR (index2 = 0 ; index2 < 2 ; index2 = index2 + 1) BEGIN
      INSERT_BYTES  ("%fx_off%" +        (0x30 * "%abil_fx_idx%")) 0x30
        WRITE_SHORT ("%fx_off%" +        (0x30 * "%abil_fx_idx%")) 107        // change portrait
        WRITE_BYTE  ("%fx_off%" + 0x02 + (0x30 * "%abil_fx_idx%")) 2          // target:preset target
        WRITE_LONG  ("%fx_off%" + 0x08 + (0x30 * "%abil_fx_idx%")) "%index2%" // small or large portrait
        WRITE_BYTE  ("%fx_off%" + 0x0c + (0x30 * "%abil_fx_idx%")) 1          // instant/permanent
        WRITE_BYTE  ("%fx_off%" + 0x12 + (0x30 * "%abil_fx_idx%")) 100        // probability
    END
    PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_CASE "spin662" = 0) BEGIN
      WRITE_ASCII ("%fx_off%" + 0x14 + (0x30 * "%abil_fx_idx%")) ~nedwinm~ // portrait resource
      WRITE_ASCII ("%fx_off%" + 0x44 + (0x30 * "%abil_fx_idx%")) ~nedwins~ // portrait resource
    END ELSE BEGIN
      WRITE_ASCII ("%fx_off%" + 0x14 + (0x30 * "%abil_fx_idx%")) ~royo4l~ // portrait resource
      WRITE_ASCII ("%fx_off%" + 0x44 + (0x30 * "%abil_fx_idx%")) ~royo4s~ // portrait resource
    END
    SET "delta" = ("%delta%" + 2)
    WRITE_SHORT ("%abil_off%" + 0x1e + (0x28 * "%index%")) ("%abil_fx_num%" + 2)
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Ease of Use Romance Fixes                        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @119000 DESIGNATED 1190
GROUP @13
REQUIRE_PREDICATE GAME_IS ~soa tob bgt~ @25
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~cdbehbla.pro~ @16 // only if Fixpack not installed

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt01190.g3~

COMPILE ~bg2_tweaks/dlg/jagalvar.d~

// Anomen's title change; effects should be permanent and persist through resurrection
COPY_EXISTING ~spin678.spl~ ~override~
  READ_LONG  0x64 "abil_off"
  READ_SHORT 0x68 "abil_num"
  READ_LONG  0x6a "fx_off"
  WHILE ("%abil_num%" > 0) BEGIN
    SET "abil_num" = ("%abil_num%" - 1)
    READ_SHORT ("%abil_off%" + (0x28 * "%abil_num%")) "type"
    PATCH_IF ("%type%" = 1) BEGIN // if melee
      READ_SHORT ("%abil_off%" + 0x1e + (0x28 * "%abil_num%")) "abil_fx_num"
      READ_SHORT ("%abil_off%" + 0x20 + (0x28 * "%abil_num%")) "abil_fx_idx"
      WHILE ("%abil_fx_num%" > 0) BEGIN
        SET "abil_fx_num" = ("%abil_fx_num%" - 1)
        WRITE_BYTE ("%fx_off%" + 0x0c + (0x30 * ("%abil_fx_idx%" + "%abil_fx_num%"))) 1 // instant/permanent
      END
    END
  END
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~aerie.bcs~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~SetGlobal("HaerDalisRomanceActive","GLOBAL",3)~
      ~SetGlobal("HaerDalisRomanceActive","GLOBAL",3)
      END

      IF
        InParty("HaerDalis")
        !Dead("HaerDalis")
        Global("HaerDalisRomanceActive","GLOBAL",2)
        Global("AerieRomanceActive","GLOBAL",1)
      THEN
        RESPONSE #100
          SetGlobal("HaerDalisRomanceActive","GLOBAL",1)
          Continue()
      END

      IF
        InParty("HaerDalis")
        !Dead("HaerDalis")
        !Global("HaerDalisRomanceActive","GLOBAL",0)
        !Global("HaerDalisRomanceActive","GLOBAL",3)
        Global("AerieRomanceActive","GLOBAL",2)
      THEN
        RESPONSE #100
          SetGlobal("HaerDalisRomanceActive","GLOBAL",3)
          Continue()~
    REPLACE_TEXTUALLY ~AreaType(0)~ ~AreaType(OUTDOOR)~
  COMPILE_BAF_TO_BCS
EXTEND_TOP ~aerie.bcs~ ~bg2_tweaks/baf/romfix_aerie.baf~

COPY_EXISTING ~anomen.bcs~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~SetGlobal("AnomenIsNotKnight","GLOBAL",1)~
      ~SetGlobal("AnomenIsNotKnight","GLOBAL",1)
      ChangeAlignment("Anomen",CHAOTIC_NEUTRAL)~
    REPLACE_TEXTUALLY ~"TALKEDTOCOR","GLOBAL"~ ~"TALKEDCOR","GLOBAL"~
  COMPILE_BAF_TO_BCS
EXTEND_TOP ~anomen.bcs~ ~bg2_tweaks/baf/romfix_anomen.baf~

COPY_EXISTING ~jaheira.bcs~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~GlobalLT("DerminSpawn","GLOBAL",5)~
      ~  GlobalLT("DerminSpawn","GLOBAL",5)
        !InParty(Myself)
      THEN
        RESPONSE #100
          RealSetGlobalTimer("JaheiraRomance","GLOBAL",3600)
          SetGlobal("DerminSpawn","GLOBAL",5)
          SetGlobalTimer("DerminAppear","GLOBAL",17280)
          StartDialogueNoSet([PC])
      END

      IF
        False()~
    REPLACE_TEXTUALLY ~\bSetGlobalTimer("TerminselAppear","GLOBAL",FIVE_DAYS)~
                      ~RealSetGlobalTimer("TerminselAppear","GLOBAL",3600)~ // originally 36000 seems to be more than random.
  COMPILE_BAF_TO_BCS

COPY_EXISTING ~viconia.bcs~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~Global("LoveTalk","LOCALS",71)~ ~False()~
    REPLACE_TEXTUALLY ~Global("ViconiaKeldornFight","GLOBAL",1)~
      ~Global("ViconiaKeldornFight","GLOBAL",1)
      THEN
        RESPONSE #100
          SetGlobal("ViconiaKeldornFight","GLOBAL",0)
      END

      IF
        False()~
  COMPILE_BAF_TO_BCS

// If ToB installed, patch ToB BCS files:
ACTION_IF GAME_IS ~tob bg2ee~ THEN BEGIN

  COPY_EXISTING ~anom25.bcs~ ~override~
    DECOMPILE_BCS_TO_BAF
      REPLACE_TEXTUALLY ~Global("AnomenSummoned","GLOBAL",1)~
      ~Global("AnomenSummoned","GLOBAL",1)
      !Global("AnomenIsKnight","GLOBAL",1)~
    COMPILE_BAF_TO_BCS
  EXTEND_TOP ~anom25.bcs~ ~bg2_tweaks/baf/romfix_anom25.baf~

  COPY_EXISTING ~jahe25.bcs~ ~override~
    DECOMPILE_BCS_TO_BAF
      REPLACE_TEXTUALLY ~OR(2)~ ~~
      REPLACE_TEXTUALLY ~GlobalGT("LoveTalk","LOCALS",50)~ ~~
      REPLACE_TEXTUALLY ~SetGlobal("JaheiraRomanceActive","GLOBAL",2)~ ~~
    COMPILE_BAF_TO_BCS

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Imoen ToB-Dialogue Fixes                         \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @120000 DESIGNATED 1200
GROUP @13
REQUIRE_PREDICATE GAME_IS ~bgt tob~    @25 // ToB required, already fixed in bg2ee
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~cdbehbla.pro~ @16 // only if Fixpack not installed

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt01200.g3~

COMPILE ~bg2_tweaks/dlg/immyfix.d~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Use BG Walking Speeds                            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @121000 DESIGNATED 1210
GROUP @13

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt01210.g3~

COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
  READ_SHORT 0x28 "anim" ELSE 0
  PATCH_IF (
       ("%SOURCE_FILE%" STRING_COMPARE_CASE   "charbase.cre" = 0)
    OR ("%SOURCE_FILE%" STRING_COMPARE_REGEXP "^AERIE[0-9][0-9]?\.cre$" = 0) // aerie comes as a ogre, so gets missed
    OR ("%anim%" = 0x5000)
    OR ("%anim%" = 0x5001)
    OR ("%anim%" = 0x5002)
    OR ("%anim%" = 0x5003)
    OR ("%anim%" = 0x5010)
    OR ("%anim%" = 0x5011)
    OR ("%anim%" = 0x5012)
    OR ("%anim%" = 0x5013)
    OR ("%anim%" = 0x5100)
    OR ("%anim%" = 0x5101)
    OR ("%anim%" = 0x5102)
    OR ("%anim%" = 0x5103)
    OR ("%anim%" = 0x5110)
    OR ("%anim%" = 0x5111)
    OR ("%anim%" = 0x5112)
    OR ("%anim%" = 0x5113)
    OR ("%anim%" = 0x5200)
    OR ("%anim%" = 0x5201)
    OR ("%anim%" = 0x5202)
    OR ("%anim%" = 0x5210)
    OR ("%anim%" = 0x5211)
    OR ("%anim%" = 0x5212)
    OR ("%anim%" = 0x5300)
    OR ("%anim%" = 0x5301)
    OR ("%anim%" = 0x5302)
    OR ("%anim%" = 0x5303)
    OR ("%anim%" = 0x5310)
    OR ("%anim%" = 0x5311)
    OR ("%anim%" = 0x5312)
    OR ("%anim%" = 0x5313)
    OR ("%anim%" = 0x6000)
    OR ("%anim%" = 0x6001)
    OR ("%anim%" = 0x6002)
    OR ("%anim%" = 0x6003)
    OR ("%anim%" = 0x6004)
    OR ("%anim%" = 0x6005)
    OR ("%anim%" = 0x6010)
    OR ("%anim%" = 0x6011)
    OR ("%anim%" = 0x6012)
    OR ("%anim%" = 0x6013)
    OR ("%anim%" = 0x6014)
    OR ("%anim%" = 0x6015)
    OR ("%anim%" = 0x6100)
    OR ("%anim%" = 0x6101)
    OR ("%anim%" = 0x6102)
    OR ("%anim%" = 0x6103)
    OR ("%anim%" = 0x6104)
    OR ("%anim%" = 0x6105)
    OR ("%anim%" = 0x6110)
    OR ("%anim%" = 0x6111)
    OR ("%anim%" = 0x6112)
    OR ("%anim%" = 0x6113)
    OR ("%anim%" = 0x6114)
    OR ("%anim%" = 0x6115)
    OR ("%anim%" = 0x6200)
    OR ("%anim%" = 0x6201)
    OR ("%anim%" = 0x6202)
    OR ("%anim%" = 0x6204)
    OR ("%anim%" = 0x6205)
    OR ("%anim%" = 0x6210)
    OR ("%anim%" = 0x6211)
    OR ("%anim%" = 0x6212)
    OR ("%anim%" = 0x6214)
    OR ("%anim%" = 0x6215)
    OR ("%anim%" = 0x6300)
    OR ("%anim%" = 0x6301)
    OR ("%anim%" = 0x6302)
    OR ("%anim%" = 0x6303)
    OR ("%anim%" = 0x6304)
    OR ("%anim%" = 0x6305)
    OR ("%anim%" = 0x6310)
    OR ("%anim%" = 0x6311)
    OR ("%anim%" = 0x6312)
    OR ("%anim%" = 0x6313)
    OR ("%anim%" = 0x6314)
    OR ("%anim%" = 0x6315)
    OR ("%anim%" = 0x6500)
    OR ("%anim%" = 0x6510)) BEGIN
    READ_BYTE   0x33 fx_flag
    READ_LONG  0x2c4 fx_off
    READ_LONG  0x2c8 fx_num
    WRITE_LONG 0x2c8 (fx_num + 1)
    // fix offsets after inserted effects
    PATCH_FOR_EACH offset IN 0x2a0 0x2a8 0x2b0 0x2b8 0x2bc BEGIN
      READ_LONG offset offset_val
      PATCH_IF (fx_off <= offset_val) BEGIN
        WRITE_LONG offset (offset_val + 0x30 + (fx_flag * 0xd8))
      END
    END
    INSERT_BYTES fx_off (0x30 + (fx_flag * 0xd8))
    WRITE_SHORT (fx_off +        (fx_flag * 0x08)) 176
    WRITE_BYTE  (fx_off + 0x02 + (fx_flag * 0x0a)) 1
    WRITE_LONG  (fx_off + 0x04 + (fx_flag * 0x10)) 0xfffffffd
    WRITE_BYTE  (fx_off + 0x0c + (fx_flag * 0x10)) 9
    WRITE_BYTE  (fx_off + 0x12 + (fx_flag * 0x12)) 100
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Cromwell can upgrade WK items                    \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @122000 DESIGNATED 1220
GROUP @13
REQUIRE_PREDICATE GAME_IS ~tob bgt bg2ee~ @25

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt01220.g3~

STRING_SET 66764 @122043

ACTION_IF NOT MOD_IS_INSTALLED ~bg2_tweaks/setup-bg2_tweaks.tp2~ ~1230~ BEGIN

  EXTEND_BOTTOM ~botsmith.bcs~ ~bg2_tweaks/baf/botsmith.baf~
  
  COPY_EXISTING ~botsmith.dlg~ ~override~
    DECOMPILE_DLG_TO_D
      REPLACE_TEXTUALLY ~OR(2)\([%TAB% %LNL%%MNL%%WNL%]*PartyHasItem("blun14")[%TAB% %LNL%%MNL%%WNL%]*PartyHasItem("blun30c")\)~
                        ~OR(3)\1 PartyHasItem("blun30d")~
    COMPILE_D_TO_DLG
    BUT_ONLY_IF_IT_CHANGES

  COMPILE ~bg2_tweaks/dlg/cespy0.d~ // cespenar FoA head order

END

// compiled in serial so that COPY_TRANS will get the one before it
COMPILE ~bg2_tweaks/dlg/crom1.d~
COMPILE ~bg2_tweaks/dlg/crom2.d~
COMPILE ~bg2_tweaks/dlg/crom3.d~
COMPILE ~bg2_tweaks/dlg/crom4.d~
COMPILE ~bg2_tweaks/dlg/crom5.d~
COMPILE ~bg2_tweaks/dlg/crom6.d~
COMPILE ~bg2_tweaks/dlg/crom7.d~
COMPILE ~bg2_tweaks/dlg/crom8.d~
COMPILE ~bg2_tweaks/dlg/crom9.d~
COMPILE ~bg2_tweaks/dlg/crom10.d~
COMPILE ~bg2_tweaks/dlg/crom11.d~
COMPILE ~bg2_tweaks/dlg/crom12.d~
COMPILE ~bg2_tweaks/dlg/crom13.d~
COMPILE ~bg2_tweaks/dlg/crom14.d~
COMPILE ~bg2_tweaks/dlg/crom15.d~

EXTEND_BOTTOM ~ar0334.bcs~ ~bg2_tweaks/baf/ar0334.baf~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// cespenar gets cromwell recipes                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @123000 DESIGNATED 1230
GROUP @13
REQUIRE_PREDICATE GAME_IS ~tob bgt bg2ee~ @25

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt01230.g3~

ACTION_IF NOT MOD_IS_INSTALLED ~bg2_tweaks/setup-bg2_tweaks.tp2~ ~1220~ BEGIN

  EXTEND_BOTTOM ~botsmith.bcs~ ~bg2_tweaks/baf/botsmith.baf~
  
  COPY_EXISTING ~botsmith.dlg~ ~override~
    DECOMPILE_DLG_TO_D
      REPLACE_TEXTUALLY ~OR(2)\([%TAB% %LNL%%MNL%%WNL%]*PartyHasItem("blun14")[%TAB% %LNL%%MNL%%WNL%]*PartyHasItem("blun30c")\)~
                        ~OR(3)\1 PartyHasItem("blun30d")~
    COMPILE_D_TO_DLG
    BUT_ONLY_IF_IT_CHANGES

  COMPILE ~bg2_tweaks/dlg/cespy0.d~ // cespenar FoA head order

END

// compiled in serial so that COPY_TRANS will get the one before it
COMPILE ~bg2_tweaks/dlg/cespy1.d~ // red & shadow dragon scale (bg2ee already has shadow)
COMPILE ~bg2_tweaks/dlg/cespy2.d~ // ankheg scale
COMPILE ~bg2_tweaks/dlg/cespy3.d~ // crom faeyr
COMPILE ~bg2_tweaks/dlg/cespy4.d~ // equalizer
COMPILE ~bg2_tweaks/dlg/cespy5.d~ // gesen bow
COMPILE ~bg2_tweaks/dlg/cespy6.d~ // wave halberd
COMPILE ~bg2_tweaks/dlg/cespy7.d~ // silver sword
COMPILE ~bg2_tweaks/dlg/cespy8.d~ // mace of disruption

EXTEND_BOTTOM ~botsmith.bcs~ ~bg2_tweaks/baf/cespy.baf~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// PnP Spell Progression Tables                     \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @201000 DESIGNATED 2010
DEPRECATED @18

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Two-Handed Bastard Swords                        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @202000 DESIGNATED 2020
GROUP @9

INCLUDE ~bg2_tweaks/lib/2h_descripts.tpa~  // text replacement macroes

// lookup table
COPY ~bg2_tweaks/2da/cdbastrd.2da~ ~override~
  COUNT_2DA_ROWS 3 cd_count

// this array used to give item charges
ACTION_CLEAR_ARRAY cd_bastard

// search for bastard swords and push them into lookup table if not already listed
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_BYTE 0x31 prof
  READ_BYTE 0x18 flags
  PATCH_IF ((prof = 89) AND ((flags & 0b00000110) = BIT2) AND  // bastard prof, droppable, 1-handed
            (!FILE_CONTAINS_EVALUATED (~cdbastrd.2da~ ~^%SOURCE_RES% ~))) BEGIN // and not already in file
    INNER_ACTION BEGIN

      OUTER_SET cd_count += 1
      APPEND ~cdbastrd.2da~ ~%SOURCE_RES%  cdbs%cd_count%  c!bs%cd_count%~

    END
  END
  BUT_ONLY

// copy lookup table back w/o backup--this means further (re-)installs will
// keep the existing file names and prevent save breakage
COPY + ~override/cdbastrd.2da~ ~bg2_tweaks/2da/cdbastrd.2da~
  COUNT_2DA_ROWS 3 rows
  FOR (index = 0 ; index < rows ; ++index) BEGIN
    READ_2DA_ENTRY index 0 3 orig
    READ_2DA_ENTRY index 1 3 1h
    READ_2DA_ENTRY index 2 3 2h
    INNER_ACTION BEGIN

      ACTION_IF FILE_EXISTS_IN_GAME ~%orig%.itm~ THEN BEGIN

        COPY_EXISTING ~%orig%.itm~ ~override~
          READ_SHORT  0x42 lore
          PATCH_IF (lore = 0) BEGIN
            SPRINT 1h ~%orig%~ // if original has no lore, don't bother with a second 1h item
          END
          READ_SHORT  0x68 abil_num
          WRITE_SHORT 0x68 (abil_num + 1)
          READ_LONG   0x6a fx_off
          WRITE_LONG  0x6a (fx_off + 0x38)
          PATCH_IF (abil_num > 0) BEGIN
            READ_LONG   0x64 abil_off
            READ_SHORT (abil_off + 0x1e + ((abil_num - 1) * 0x38)) last_fx_num
            READ_SHORT (abil_off + 0x20 + ((abil_num - 1) * 0x38)) last_fx_idx
            SET new_fx = (last_fx_idx + last_fx_num)
          END ELSE BEGIN
            READ_SHORT  0x70 new_fx
          END
          // because create inventory item doesn't have a way to set charges, create new and delete current
          INSERT_BYTES   (fx_off +        (new_fx * 0x30)) 0x30 // new effect
            WRITE_SHORT  (fx_off +        (new_fx * 0x30)) 122  // create inventory item
            WRITE_BYTE   (fx_off + 0x02 + (new_fx * 0x30)) 1    // target: self
            WRITE_BYTE   (fx_off + 0x0c + (new_fx * 0x30)) 1    // instant/permanent till death
            WRITE_BYTE   (fx_off + 0x12 + (new_fx * 0x30)) 100  // probability
            WRITE_ASCIIE (fx_off + 0x14 + (new_fx * 0x30)) ~%2h%~ #8
          INSERT_BYTES   (fx_off       ) 0x38         // new ability
            WRITE_BYTE   (fx_off       ) 3            // magical
            WRITE_BYTE   (fx_off + 0x01) 1            // ID to use
            WRITE_SHORT  (fx_off + 0x02) 3            // in item slots
            WRITE_ASCII  (fx_off + 0x04) ~isw2h01~ #8 // item bam
            WRITE_SHORT  (fx_off + 0x0c) 5            // target: caster
            WRITE_SHORT  (fx_off + 0x0e) 1            // range: 1
            WRITE_SHORT  (fx_off + 0x1e) 1            // num effects: 1
            WRITE_SHORT  (fx_off + 0x20) new_fx       // fx index
            WRITE_SHORT  (fx_off + 0x22) 1            // num charges
            WRITE_BYTE   (fx_off + 0x24) 1            // vanishes when drained
            WRITE_SHORT  (fx_off + 0x2a) 1            // projectile: none
          BUT_ONLY

        ACTION_IF (abil_num > 3) BEGIN // don't worry about charges, just fix tooltips for loaded items
        
          ACTION_IF (FILE_CONTAINS_EVALUATED (~tooltip.2da~ ~^%SOURCE_RES% ~)) BEGIN

            COPY_EXISTING ~tooltip.2da~ ~override~
              REPLACE_TEXTUALLY ~^\(%orig%\)\([ %TAB%]+.+\)~ ~\1\2
%1h%\2
%2h%\2~

          END
        
        END ELSE BEGIN // build charge array, add/fix tooltips

          ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_bastard BEGIN "%orig%" => "%abil_num%" END
          
          ACTION_IF (FILE_CONTAINS_EVALUATED (~tooltip.2da~ ~^%orig% ~)) BEGIN // fix existing tooltips

            COPY_EXISTING ~tooltip.2da~ ~override~
              PATCH_IF (abil_num = 0) BEGIN
                REPLACE_TEXTUALLY ~^\(%orig%\)\([ %TAB%]+\)\(-?[0-9]+\)\(.+\)~ ~\1\2CDTWOHANDED\3
%1h%\2CDTWOHANDED\3
%2h%\2CDONEHANDED\3~
              END ELSE
              PATCH_IF (abil_num = 1) BEGIN
                REPLACE_TEXTUALLY ~^\(%orig%\)\([ %TAB%]+-?[0-9]+[ %TAB%]+\)\(-?[0-9]+\)\(.+\)~ ~\1\2CDTWOHANDED\3
%1h%\2CDTWOHANDED\3
%2h%\2CDONEHANDED\3~
              END ELSE
              PATCH_IF (abil_num = 2) BEGIN
                REPLACE_TEXTUALLY ~^\(%orig%\)\([ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+\)\(-?[0-9]+\)~ ~\1\2CDTWOHANDED
%1h%\2CDTWOHANDED
%2h%\2CDONEHANDED~
              END

          END ELSE BEGIN // or add as new tooltips
            
            ACTION_IF (abil_num = 0) BEGIN
              APPEND ~tooltip.2da~ ~%orig% CDTWOHANDED -1 -1
%2h% CDONEHANDED -1 -1~
              APPEND ~tooltip.2da~ ~%1h% CDTWOHANDED -1 -1~ UNLESS ~^%1h% ~ // separate to avoid duplicate entries for 0 lore
            END
            
            ACTION_IF (abil_num = 1) BEGIN
              APPEND ~tooltip.2da~ ~%orig% -1 CDTWOHANDED -1
%2h% -1 CDONEHANDED -1~
              APPEND ~tooltip.2da~ ~%1h% -1 CDTWOHANDED -1~ UNLESS ~^%1h% ~
            END
            
            ACTION_IF (abil_num = 2) BEGIN
              APPEND ~tooltip.2da~ ~%orig% -1 -1 CDTWOHANDED
%2h% -1 -1 CDONEHANDED~
              APPEND ~tooltip.2da~ ~%1h% -1 -1 CDTWOHANDED~ UNLESS ~^%1h% ~
            END
          
          END

          COPY_EXISTING ~tooltip.2da~ ~override~
            REPLACE ~CDONEHANDED~ @202001
            REPLACE ~CDTWOHANDED~ @202002
            PRETTY_PRINT_2DA

        END

        COPY_EXISTING ~%orig%.itm~ ~override/%2h%.itm~
                      ~%orig%.itm~ ~override/%1h%.itm~
          WRITE_SHORT 0x42 0 // no lore

        COPY_EXISTING ~%2h%.itm~ ~override~
          WRITE_ASCIIE (fx_off + 0x4c + (new_fx * 0x30)) ~%1h%~ #8 // exploit the fact that fx_off and new_fx carry over
          WRITE_ASCII  (fx_off + 0x04) ~sw1h01~ #8                 // item bam
          WRITE_BYTE  0x18 THIS | BIT1                           // add two-handed flag
          WRITE_ASCII 0x22 S2                                    // paperdoll animation
          FOR (loops = 0; loops < abil_num; ++loops) BEGIN
            READ_BYTE (abil_off + (loops * 0x38)) type
            PATCH_IF (type = 1) BEGIN // adjust melee abilities to 2h sword mix of attacks
              WRITE_SHORT (abil_off + 0x2c + (loops * 0x38)) 60 // overhand attacks
              WRITE_SHORT (abil_off + 0x2e + (loops * 0x38)) 40 // slashing attacks
              WRITE_SHORT (abil_off + 0x30 + (loops * 0x38))  0 // thrusting attacks
            END
          END
          // update descriptions
          PATCH_FOR_EACH offset IN 0x50 0x54 BEGIN
            READ_LONG ~%offset%~ desc_strref
            PATCH_IF (desc_strref >= 0 && desc_strref < 2147483646) BEGIN
              READ_STRREF ~%offset%~ desc
              INNER_PATCH_SAVE desc ~%desc%~ BEGIN
                LAUNCH_PATCH_MACRO ~2h_descripts~
              END
              SAY_EVALUATED ~%offset%~ ~%desc%~ // write changes
            END
          END

      END

    END
  END

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.[ac]re$~ ~override~
  PATCH_IF ("%SOURCE_FILE%" STRING_COMPARE_REGEXP "^.+\.cre$" = 0) BEGIN // if creature
    READ_LONG 0x2bc itm_off ELSE 0
    READ_LONG 0x2c0 itm_num ELSE 0
  END ELSE BEGIN
    READ_LONG  0x78 itm_off ELSE 0
    READ_SHORT 0x76 itm_num ELSE 0
  END
  FOR (index = 0 ; index < itm_num ; ++index) BEGIN
    READ_ASCII (itm_off +        (0x14 * index)) item ELSE ""
    PHP_EACH cd_bastard AS itemname => charge BEGIN
      PATCH_IF ("%item%" STRING_COMPARE_CASE "%itemname%" = 0) BEGIN
        WRITE_SHORT (itm_off + 0x0a + (charge * 0x02) + (0x14 * index)) 1 // add charge
      END
    END
  END
  BUT_ONLY

// add charges to items being sold
COPY_EXISTING_REGEXP GLOB ~^.+\.sto$~ ~override~
  READ_LONG 0x34 sale_off  ELSE 0
  READ_LONG 0x38 sale_num  ELSE 0
  FOR (index = 0 ; index < sale_num ; ++index) BEGIN
    READ_ASCII (sale_off +        (index * 0x1c)) item
    PHP_EACH cd_bastard AS itemname => charge BEGIN
      PATCH_IF ("%item%" STRING_COMPARE_CASE "%itemname%" = 0) BEGIN
        WRITE_SHORT (sale_off + 0x0a + (charge * 0x02) + (0x1c * index)) 1 // add charge
      END
    END
  END
  BUT_ONLY

// if breakable weapons from tutufix installed or BGEE/BGT breakable weapons
ACTION_IF ((FILE_EXISTS_IN_GAME ~_sw1h01.spl~) OR (FILE_EXISTS_IN_GAME ~sw1h01.spl~)) THEN BEGIN

  INCLUDE ~bg2_tweaks/lib/functions.tpa~
    
  ACTION_IF FILE_EXISTS_IN_GAME ~_sw1h01.spl~ THEN BEGIN
  
    OUTER_SPRINT break_spell "_sw1h01"
    
  END ELSE BEGIN

    OUTER_SPRINT break_spell "sw1h01"
    
  END

  ACTION_FOR_EACH res IN cdbs1 cdbs2 BEGIN
    
    ACTION_IF FILE_EXISTS_IN_GAME ~%res%.itm~ BEGIN

      // change existing breakage spell to point to new spell
      COPY_EXISTING ~%resource%.itm~ ~override~
        LPF ITEM_ALTER_EFFECT INT_VAR headers = 1 opcode = 146 STR_VAR resource = EVAL "%res%" END

      // make new breaking spell
      COPY_EXISTING ~%break_spell%.spl~ ~override/%res%.spl~ // create breakage spell for new swords
        LPF SPELL_ALTER_EFFECT INT_VAR opcode = 112 STR_VAR resource = EVAL "%res%" END

    END

  END

END

// cespenar can use alternate items for upgrades
ACTION_IF FILE_EXISTS_IN_GAME botsmith.dlg THEN BEGIN

  COMPILE ~bg2_tweaks/dlg/2Hbastard.d~

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Two-Handed Katanas                               \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @203000 DESIGNATED 2030
GROUP @9

INCLUDE ~bg2_tweaks/lib/2h_descripts.tpa~  // text replacement macroes

// lookup table
COPY ~bg2_tweaks/2da/cdkatana.2da~ ~override~
  COUNT_2DA_ROWS 3 cd_count

// this array used to give item charges (re-used from bastard, makes no diff)
ACTION_CLEAR_ARRAY cd_bastard

// search for bastard swords and push them into lookup table if not already listed
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_BYTE 0x31 prof
  READ_BYTE 0x18 flags
  PATCH_IF ((prof = 94) AND ((flags & 0b00000110) = BIT2) AND  // katana prof, droppable, 1-handed
            (!FILE_CONTAINS_EVALUATED (~cdkatana.2da~ ~^%SOURCE_RES% ~))) BEGIN // and not already in file
    INNER_ACTION BEGIN

      OUTER_SET cd_count += 1
      APPEND ~cdkatana.2da~ ~%SOURCE_RES%  cdkt%cd_count%  c!kt%cd_count%~
    
    END
  END
  BUT_ONLY

// copy lookup table back w/o backup--this means further (re-)installs will
// keep the existing file names and prevent save breakage
COPY + ~override/cdkatana.2da~ ~bg2_tweaks/2da/cdkatana.2da~
  COUNT_2DA_ROWS 3 rows
  FOR (index = 0 ; index < rows ; ++index) BEGIN
    READ_2DA_ENTRY index 0 3 orig
    READ_2DA_ENTRY index 1 3 1h
    READ_2DA_ENTRY index 2 3 2h
    INNER_ACTION BEGIN

      ACTION_IF FILE_EXISTS_IN_GAME ~%orig%.itm~ THEN BEGIN

        COPY_EXISTING ~%orig%.itm~ ~override~
          READ_ASCII  0x3a bam (8)
          READ_SHORT  0x42 lore
          PATCH_IF (lore = 0) BEGIN
            SPRINT 1h ~%orig%~ // if original has no lore, don't bother with a second 1h item
          END
          READ_SHORT  0x68 abil_num
          WRITE_SHORT 0x68 (abil_num + 1)
          READ_LONG   0x6a fx_off
          WRITE_LONG  0x6a (fx_off + 0x38)
          PATCH_IF (abil_num > 0) BEGIN
            READ_LONG   0x64 abil_off
            READ_SHORT (abil_off + 0x1e + ((abil_num - 1) * 0x38)) last_fx_num
            READ_SHORT (abil_off + 0x20 + ((abil_num - 1) * 0x38)) last_fx_idx
            SET new_fx = (last_fx_idx + last_fx_num)
          END ELSE BEGIN
            READ_SHORT  0x70 new_fx
          END
          // because create inventory item doesn't have a way to set charges, create new and delete current
          INSERT_BYTES   (fx_off +        (new_fx * 0x30)) 0x30 // new effect
            WRITE_SHORT  (fx_off +        (new_fx * 0x30)) 122  // create inventory item
            WRITE_BYTE   (fx_off + 0x02 + (new_fx * 0x30)) 1    // target: self
            WRITE_BYTE   (fx_off + 0x0c + (new_fx * 0x30)) 1    // instant/permanent till death
            WRITE_BYTE   (fx_off + 0x12 + (new_fx * 0x30)) 100  // probability
            WRITE_ASCIIE (fx_off + 0x14 + (new_fx * 0x30)) ~%2h%~ #8
          INSERT_BYTES   (fx_off       ) 0x38         // new ability
            WRITE_BYTE   (fx_off       ) 3            // magical
            WRITE_BYTE   (fx_off + 0x01) 1            // ID to use
            WRITE_SHORT  (fx_off + 0x02) 3            // in item slots
            WRITE_ASCII  (fx_off + 0x04) ~isw2h01~ #8 // item bam
            WRITE_SHORT  (fx_off + 0x0c) 5            // target: caster
            WRITE_SHORT  (fx_off + 0x0e) 1            // range: 1
            WRITE_SHORT  (fx_off + 0x1e) 1            // num effects: 1
            WRITE_SHORT  (fx_off + 0x20) new_fx       // fx index
            WRITE_SHORT  (fx_off + 0x22) 1            // num charges
            WRITE_BYTE   (fx_off + 0x24) 1            // vanishes when drained
            WRITE_SHORT  (fx_off + 0x2a) 1            // projectile: none
          BUT_ONLY

        ACTION_IF (abil_num > 3) BEGIN // don't worry about charges, just fix tooltips for loaded items
        
          ACTION_IF (FILE_CONTAINS_EVALUATED (~tooltip.2da~ ~^%SOURCE_RES% ~)) BEGIN

            COPY_EXISTING ~tooltip.2da~ ~override~
              REPLACE_TEXTUALLY ~^\(%orig%\)\([ %TAB%]+.+\)~ ~\1\2
%1h%\2
%2h%\2~

          END
        
        END ELSE BEGIN // build charge array, add/fix tooltips

          ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_bastard BEGIN "%orig%" => "%abil_num%" END
          
          ACTION_IF (FILE_CONTAINS_EVALUATED (~tooltip.2da~ ~^%orig% ~)) BEGIN // fix existing tooltips

            COPY_EXISTING ~tooltip.2da~ ~override~
              PATCH_IF (abil_num = 0) BEGIN
                REPLACE_TEXTUALLY ~^\(%orig%\)\([ %TAB%]+\)\(-?[0-9]+\)\(.+\)~ ~\1\2CDTWOHANDED\3
%1h%\2CDTWOHANDED\3
%2h%\2CDONEHANDED\3~
              END ELSE
              PATCH_IF (abil_num = 1) BEGIN
                REPLACE_TEXTUALLY ~^\(%orig%\)\([ %TAB%]+-?[0-9]+[ %TAB%]+\)\(-?[0-9]+\)\(.+\)~ ~\1\2CDTWOHANDED\3
%1h%\2CDTWOHANDED\3
%2h%\2CDONEHANDED\3~
              END ELSE
              PATCH_IF (abil_num = 2) BEGIN
                REPLACE_TEXTUALLY ~^\(%orig%\)\([ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+\)\(-?[0-9]+\)~ ~\1\2CDTWOHANDED
%1h%\2CDTWOHANDED
%2h%\2CDONEHANDED~
              END

          END ELSE BEGIN // or add as new tooltips
            
            ACTION_IF (abil_num = 0) BEGIN
              APPEND ~tooltip.2da~ ~%orig% CDTWOHANDED -1 -1
%2h% CDONEHANDED -1 -1~
              APPEND ~tooltip.2da~ ~%1h% CDTWOHANDED -1 -1~ UNLESS ~^%1h% ~ // separate to avoid duplicate entries for 0 lore
            END
            
            ACTION_IF (abil_num = 1) BEGIN
              APPEND ~tooltip.2da~ ~%orig% -1 CDTWOHANDED -1
%2h% -1 CDONEHANDED -1~
              APPEND ~tooltip.2da~ ~%1h% -1 CDTWOHANDED -1~ UNLESS ~^%1h% ~
            END
            
            ACTION_IF (abil_num = 2) BEGIN
              APPEND ~tooltip.2da~ ~%orig% -1 -1 CDTWOHANDED
%2h% -1 -1 CDONEHANDED~
              APPEND ~tooltip.2da~ ~%1h% -1 -1 CDTWOHANDED~ UNLESS ~^%1h% ~
            END
          
          END

          COPY_EXISTING ~tooltip.2da~ ~override~
            REPLACE ~CDONEHANDED~ @202001
            REPLACE ~CDTWOHANDED~ @202002
            PRETTY_PRINT_2DA

        END

        COPY_EXISTING ~%orig%.itm~ ~override/%2h%.itm~
                      ~%orig%.itm~ ~override/%1h%.itm~
          WRITE_SHORT 0x42 0 // no lore

        COPY_EXISTING ~%2h%.itm~ ~override~
          WRITE_ASCIIE (fx_off + 0x4c + (new_fx * 0x30)) ~%1h%~ #8 // exploit the fact that fx_off and new_fx carry over
          WRITE_ASCII  (fx_off + 0x04) ~sw1h43~ #8                 // item bam
          WRITE_BYTE  0x18 THIS | BIT1                           // add two-handed flag
          WRITE_ASCII 0x22 S2                                    // paperdoll animation
          FOR (loops = 0; loops < abil_num; ++loops) BEGIN
            READ_BYTE (abil_off + (loops * 0x38)) type
            PATCH_IF (type = 1) BEGIN // adjust melee abilities to 2h sword mix of attacks
              WRITE_SHORT (abil_off + 0x2c + (loops * 0x38)) 60 // overhand attacks
              WRITE_SHORT (abil_off + 0x2e + (loops * 0x38)) 40 // slashing attacks
              WRITE_SHORT (abil_off + 0x30 + (loops * 0x38))  0 // thrusting attacks
            END
          END
          // update descriptions
          PATCH_FOR_EACH offset IN 0x50 0x54 BEGIN
            READ_LONG ~%offset%~ desc_strref
            PATCH_IF (desc_strref >= 0 && desc_strref < 2147483646) BEGIN
              READ_STRREF ~%offset%~ desc
              INNER_PATCH_SAVE desc ~%desc%~ BEGIN
                LAUNCH_PATCH_MACRO ~2h_descripts~
              END
              SAY_EVALUATED ~%offset%~ ~%desc%~ // write changes
            END
          END

      END

    END
  END

// add charges to items in game
PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.[ac]re$~ ~override~
  PATCH_IF ("%SOURCE_FILE%" STRING_COMPARE_REGEXP "^.+\.cre$" = 0) BEGIN // if creature
    READ_LONG 0x2bc itm_off ELSE 0
    READ_LONG 0x2c0 itm_num ELSE 0
  END ELSE BEGIN
    READ_LONG  0x78 itm_off ELSE 0
    READ_SHORT 0x76 itm_num ELSE 0
  END
  FOR (index = 0 ; index < itm_num ; ++index) BEGIN
    READ_ASCII (itm_off +        (0x14 * index)) item ELSE ""
    PHP_EACH cd_bastard AS itemname => charge BEGIN
      PATCH_IF ("%item%" STRING_COMPARE_CASE "%itemname%" = 0) BEGIN
        WRITE_SHORT (itm_off + 0x0a + (charge * 0x02) + (0x14 * index)) 1 // add charge
      END
    END
  END
  BUT_ONLY

// add charges to items being sold
COPY_EXISTING_REGEXP GLOB ~^.+\.sto$~ ~override~
  READ_LONG 0x34 sale_off  ELSE 0
  READ_LONG 0x38 sale_num  ELSE 0
  FOR (index = 0 ; index < sale_num ; ++index) BEGIN
    READ_ASCII (sale_off +        (index * 0x1c)) item
    PHP_EACH cd_bastard AS itemname => charge BEGIN
      PATCH_IF ("%item%" STRING_COMPARE_CASE "%itemname%" = 0) BEGIN
        WRITE_SHORT (sale_off + 0x0a + (charge * 0x02) + (0x1c * index)) 1 // add charge
      END
    END
  END
  BUT_ONLY

// if breakable weapons from tutufix installed or BGEE/BGT breakable weapons
ACTION_IF FILE_EXISTS_IN_GAME ~sw1h43.spl~ THEN BEGIN

  INCLUDE ~bg2_tweaks/lib/functions.tpa~

  // change existing breakage spell to point to new spell
  COPY_EXISTING ~cdkt1.itm~ ~override~
    LPF ITEM_ALTER_EFFECT INT_VAR headers = 1 opcode = 146 STR_VAR resource = "cdkt1" END

  // make new breaking spell
  COPY_EXISTING ~sw1h43.spl~ ~override/cdkt1.spl~ // create breakage spell for new swords
    LPF SPELL_ALTER_EFFECT INT_VAR opcode = 112 STR_VAR resource = "cdkt1" END

END

// cespenar can use alternate items for upgrades
ACTION_IF FILE_EXISTS_IN_GAME botsmith.dlg THEN BEGIN

  COMPILE ~bg2_tweaks/dlg/2Hkatanas.d~
  
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Two-Handed Axes                                  \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @203500 DESIGNATED 2035
GROUP @9

INCLUDE ~bg2_tweaks/lib/functions.tpa~
INCLUDE ~bg2_tweaks/lib/2h_descripts.tpa~  // text replacement macroes

// lookup table
COPY ~bg2_tweaks/2da/cdaxe.2da~ ~override~
  COUNT_2DA_ROWS 3 cd_count

// this array used to give item charges (re-used from bastard, makes no diff)
ACTION_CLEAR_ARRAY cd_bastard

// search for axes and push them into lookup table if not already listed
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_BYTE 0x31 prof
  READ_BYTE 0x18 flags
  SET melee = 0
  READ_LONG  0x64 abil_off
  READ_SHORT 0x68 abil_num
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    READ_SHORT (abil_off +        (index * 0x38)) type
    PATCH_IF (type = 1) BEGIN
      SET melee = 1
    END
    PATCH_IF (type = 2) BEGIN
      SET melee = 0
      SET index = abil_num // kills loop, prevents patching if throwing axe
    END
  END
  PATCH_IF ((melee = 1) AND (prof = 92) AND ((flags & 0b00000110) = BIT2) AND  // aaxe prof, droppable, 1-handed, only if it has melee ability
            (!FILE_CONTAINS_EVALUATED (~cdaxe.2da~ ~^%SOURCE_RES% ~))) BEGIN // and not already in file
    INNER_ACTION BEGIN

      OUTER_SET cd_count += 1
      APPEND ~cdaxe.2da~ ~%SOURCE_RES%  cdax%cd_count%  c!ax%cd_count%~

    END
  END
  BUT_ONLY

// copy lookup table back w/o backup--this means further (re-)installs will 
// keep the existing file names and prevent save breakage
COPY + ~override/cdaxe.2da~ ~bg2_tweaks/2da/cdaxe.2da~
  COUNT_2DA_ROWS 3 rows
  FOR (index = 0 ; index < rows ; ++index) BEGIN
    READ_2DA_ENTRY index 0 3 orig
    READ_2DA_ENTRY index 1 3 1h
    READ_2DA_ENTRY index 2 3 2h
    INNER_ACTION BEGIN

      ACTION_IF FILE_EXISTS_IN_GAME ~%orig%.itm~ THEN BEGIN

        COPY_EXISTING ~%orig%.itm~ ~override~
          READ_SHORT  0x42 lore
          PATCH_IF (lore = 0) BEGIN
            SPRINT 1h ~%orig%~ // if original has no lore, don't bother with a second 1h item
          END
          READ_SHORT  0x68 abil_num
          WRITE_SHORT 0x68 (abil_num + 1)
          READ_LONG   0x6a fx_off
          WRITE_LONG  0x6a (fx_off + 0x38)
          PATCH_IF (abil_num > 0) BEGIN
            READ_LONG   0x64 abil_off
            READ_SHORT (abil_off + 0x1e + ((abil_num - 1) * 0x38)) last_fx_num
            READ_SHORT (abil_off + 0x20 + ((abil_num - 1) * 0x38)) last_fx_idx
            SET new_fx = (last_fx_idx + last_fx_num)
          END ELSE BEGIN
            READ_SHORT  0x70 new_fx
          END
          // because create inventory item doesn't have a way to set charges, create new and delete current
          INSERT_BYTES   (fx_off +        (new_fx * 0x30)) 0x30 // new effect
            WRITE_SHORT  (fx_off +        (new_fx * 0x30)) 122  // create inventory item
            WRITE_BYTE   (fx_off + 0x02 + (new_fx * 0x30)) 1    // target: self
            WRITE_BYTE   (fx_off + 0x0c + (new_fx * 0x30)) 1    // instant/permanent till death
            WRITE_BYTE   (fx_off + 0x12 + (new_fx * 0x30)) 100  // probability
            WRITE_ASCIIE (fx_off + 0x14 + (new_fx * 0x30)) ~%2h%~ #8
          INSERT_BYTES   (fx_off       ) 0x38         // new ability
            WRITE_BYTE   (fx_off       ) 3            // magical
            WRITE_BYTE   (fx_off + 0x01) 1            // ID to use
            WRITE_SHORT  (fx_off + 0x02) 3            // in item slots
            WRITE_ASCII  (fx_off + 0x04) ~ihalb01~ #8 // item bam
            WRITE_SHORT  (fx_off + 0x0c) 5            // target: caster
            WRITE_SHORT  (fx_off + 0x0e) 1            // range: 1
            WRITE_SHORT  (fx_off + 0x1e) 1            // num effects: 1
            WRITE_SHORT  (fx_off + 0x20) new_fx       // fx index
            WRITE_SHORT  (fx_off + 0x22) 1            // num charges
            WRITE_BYTE   (fx_off + 0x24) 1            // vanishes when drained
            WRITE_SHORT  (fx_off + 0x2a) 1            // projectile: none
          BUT_ONLY

        ACTION_IF (abil_num > 3) BEGIN // don't worry about charges, just fix tooltips for loaded items
        
          ACTION_IF (FILE_CONTAINS_EVALUATED (~tooltip.2da~ ~^%SOURCE_RES% ~)) BEGIN

            COPY_EXISTING ~tooltip.2da~ ~override~
              REPLACE_TEXTUALLY ~^\(%orig%\)\([ %TAB%]+.+\)~ ~\1\2
%1h%\2
%2h%\2~

          END
        
        END ELSE BEGIN // build charge array, add/fix tooltips

          ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_bastard BEGIN "%orig%" => "%abil_num%" END
          
          ACTION_IF (FILE_CONTAINS_EVALUATED (~tooltip.2da~ ~^%orig% ~)) BEGIN // fix existing tooltips

            COPY_EXISTING ~tooltip.2da~ ~override~
              PATCH_IF (abil_num = 0) BEGIN
                REPLACE_TEXTUALLY ~^\(%orig%\)\([ %TAB%]+\)\(-?[0-9]+\)\(.+\)~ ~\1\2CDTWOHANDED\3
%1h%\2CDTWOHANDED\3
%2h%\2CDONEHANDED\3~
              END ELSE
              PATCH_IF (abil_num = 1) BEGIN
                REPLACE_TEXTUALLY ~^\(%orig%\)\([ %TAB%]+-?[0-9]+[ %TAB%]+\)\(-?[0-9]+\)\(.+\)~ ~\1\2CDTWOHANDED\3
%1h%\2CDTWOHANDED\3
%2h%\2CDONEHANDED\3~
              END ELSE
              PATCH_IF (abil_num = 2) BEGIN
                REPLACE_TEXTUALLY ~^\(%orig%\)\([ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+\)\(-?[0-9]+\)~ ~\1\2CDTWOHANDED
%1h%\2CDTWOHANDED
%2h%\2CDONEHANDED~
              END

          END ELSE BEGIN // or add as new tooltips
            
            ACTION_IF (abil_num = 0) BEGIN
              APPEND ~tooltip.2da~ ~%orig% CDTWOHANDED -1 -1
%2h% CDONEHANDED -1 -1~
              APPEND ~tooltip.2da~ ~%1h% CDTWOHANDED -1 -1~ UNLESS ~^%1h% ~ // separate to avoid duplicate entries for 0 lore
            END
            
            ACTION_IF (abil_num = 1) BEGIN
              APPEND ~tooltip.2da~ ~%orig% -1 CDTWOHANDED -1
%2h% -1 CDONEHANDED -1~
              APPEND ~tooltip.2da~ ~%1h% -1 CDTWOHANDED -1~ UNLESS ~^%1h% ~
            END
            
            ACTION_IF (abil_num = 2) BEGIN
              APPEND ~tooltip.2da~ ~%orig% -1 -1 CDTWOHANDED
%2h% -1 -1 CDONEHANDED~
              APPEND ~tooltip.2da~ ~%1h% -1 -1 CDTWOHANDED~ UNLESS ~^%1h% ~
            END
          
          END

          COPY_EXISTING ~tooltip.2da~ ~override~
            REPLACE ~CDONEHANDED~ @202001
            REPLACE ~CDTWOHANDED~ @202002
            PRETTY_PRINT_2DA

        END

        COPY_EXISTING ~%orig%.itm~ ~override/%2h%.itm~
                      ~%orig%.itm~ ~override/%1h%.itm~
          WRITE_SHORT 0x42 0 // no lore

        COPY_EXISTING ~%2h%.itm~ ~override~
          WRITE_ASCIIE (fx_off + 0x4c + (new_fx * 0x30)) ~%1h%~ #8 // exploit the fact that fx_off and new_fx carry over
          WRITE_ASCII  (fx_off + 0x04) ~iax1h01~ #8                // item bam
          WRITE_BYTE  0x18 THIS | BIT1                             // add two-handed flag
          WRITE_ASCII 0x22 HB                                      // paperdoll animation
          FOR (loops = 0; loops < abil_num; ++loops) BEGIN
            READ_BYTE (abil_off + (loops * 0x38)) type
            PATCH_IF ((type = 1) OR (type = 2)) BEGIN // adjust melee abilities to halberd mix of attacks
              WRITE_SHORT (abil_off + 0x2c + (loops * 0x38))  0 // overhand attacks
              WRITE_SHORT (abil_off + 0x2e + (loops * 0x38)) 35 // slashing attacks
              WRITE_SHORT (abil_off + 0x30 + (loops * 0x38)) 65 // thrusting attacks
            END
          END
          // update descriptions
          PATCH_FOR_EACH offset IN 0x50 0x54 BEGIN
            READ_LONG ~%offset%~ desc_strref
            PATCH_IF (desc_strref >= 0 && desc_strref < 2147483646) BEGIN
              READ_STRREF ~%offset%~ desc
              INNER_PATCH_SAVE desc ~%desc%~ BEGIN
                LAUNCH_PATCH_MACRO ~2h_descripts~
              END
              SAY_EVALUATED ~%offset%~ ~%desc%~ // write changes
            END
          END

      END

    END
  END

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.[ac]re$~ ~override~
  PATCH_IF ("%SOURCE_FILE%" STRING_COMPARE_REGEXP "^.+\.cre$" = 0) BEGIN // if creature
    READ_LONG 0x2bc itm_off ELSE 0
    READ_LONG 0x2c0 itm_num ELSE 0
  END ELSE BEGIN
    READ_LONG  0x78 itm_off ELSE 0
    READ_SHORT 0x76 itm_num ELSE 0
  END
  FOR (index = 0 ; index < itm_num ; ++index) BEGIN
    READ_ASCII (itm_off +        (0x14 * index)) item ELSE ""
    PHP_EACH cd_bastard AS itemname => charge BEGIN
      PATCH_IF ("%item%" STRING_COMPARE_CASE "%itemname%" = 0) BEGIN
        WRITE_SHORT (itm_off + 0x0a + (charge * 0x02) + (0x14 * index)) 1 // add charge
      END
    END
  END
  BUT_ONLY

// add charges to items being sold
COPY_EXISTING_REGEXP GLOB ~^.+\.sto$~ ~override~
  READ_LONG 0x34 sale_off  ELSE 0
  READ_LONG 0x38 sale_num  ELSE 0
  FOR (index = 0 ; index < sale_num ; ++index) BEGIN
    READ_ASCII (sale_off +        (index * 0x1c)) item
    PHP_EACH cd_bastard AS itemname => charge BEGIN
      PATCH_IF ("%item%" STRING_COMPARE_CASE "%itemname%" = 0) BEGIN
        WRITE_SHORT (sale_off + 0x0a + (charge * 0x02) + (0x1c * index)) 1 // add charge
      END
    END
  END
  BUT_ONLY

// if breakable weapons from tutufix installed or BGEE/BGT breakable weapons
ACTION_IF ((FILE_EXISTS_IN_GAME ~_ax1h01.spl~) OR (FILE_EXISTS_IN_GAME ~ax1h01.spl~)) THEN BEGIN

  INCLUDE ~bg2_tweaks/lib/functions.tpa~
    
  ACTION_IF FILE_EXISTS_IN_GAME ~_ax1h01.spl~ THEN BEGIN
  
    OUTER_SPRINT break_spell "_ax1h01"
    
  END ELSE BEGIN

    OUTER_SPRINT break_spell "ax1h01"
    
  END

  ACTION_FOR_EACH res IN cdax1 cdax14 BEGIN
    
    ACTION_IF FILE_EXISTS_IN_GAME ~%res%.itm~ BEGIN

      // change existing breakage spell to point to new spell
      COPY_EXISTING ~%resource%.itm~ ~override~
        LPF ITEM_ALTER_EFFECT INT_VAR headers = 1 opcode = 146 STR_VAR resource = EVAL "%res%" END

      // make new breaking spell
      COPY_EXISTING ~%break_spell%.spl~ ~override/%res%.spl~ // create breakage spell for new swords
        LPF SPELL_ALTER_EFFECT INT_VAR opcode = 112 STR_VAR resource = EVAL "%res%" END

    END

  END

END

// tutorial obe also destroys illusionary alternate axes
ACTION_IF FILE_EXISTS_IN_GAME obe.dlg THEN BEGIN

  COMPILE ~bg2_tweaks/dlg/2Haxes_obe.d~
  
END

// cespenar can use alternate items for upgrades
ACTION_IF FILE_EXISTS_IN_GAME botsmith.dlg THEN BEGIN

  COMPILE ~bg2_tweaks/dlg/2Haxes.d~

END

// cespenar can use alternate items for upgrades
ACTION_IF FILE_EXISTS_IN_GAME dedion.dlg THEN BEGIN

  COMPILE ~bg2_tweaks/dlg/2haxes_iwd.d~
  
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Universal Clubs                                  \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @204000 DESIGNATED 2040
GROUP @9

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02040.g3~

// this does the work of our description patching when fed a few regular expressions and replacements
DEFINE_PATCH_FUNCTION ~REMOVE_MAGE_LINE_FROM_USABILITIES~
  INT_VAR
    offset = 0 // offset to write description to
  STR_VAR
    desc = ~~ // original description
    unusable_regexp = ~~ // regexp that matches the Unusable By/Not Usable By line
    unusable_replacement = ~~ // replacement for the Unusable By line
    mage_regexp = ~~ // regexp that matches the Mage line in the usability section
    class_prefix = ~~ // in the usability section, this precedes all class listings (ex: in english, this is one space)
BEGIN
  INNER_PATCH_SAVE desc ~%desc%~ BEGIN
    REPLACE_TEXTUALLY ~%unusable_regexp%~ ~%unusable_replacement%~
  END
  PATCH_IF ((~%desc%~ STRING_CONTAINS_REGEXP ~^%unusable_replacement%~) == 0) BEGIN
    // extract usability info
    INNER_PATCH_SAVE usab_block ~%desc%~ BEGIN
      REPLACE_TEXTUALLY ~\(.*[%LNL%%MNL%%WNL%]\)*%unusable_replacement%~ ~~
    END
    // update info
    INNER_PATCH_SAVE usab_block_new ~%usab_block%~ BEGIN
      REPLACE_TEXTUALLY ~[%LNL%%MNL%%WNL%][- %TAB%]*$~ ~~ // remove any empty lines
      REPLACE_TEXTUALLY ~^[- %TAB%]*~ ~%class_prefix%~ // make sure entries are indented by one space
      REPLACE_TEXTUALLY ~%mage_regexp%~ ~~ // remove mage line
    END
    // replace old usability info with new
    INNER_PATCH_SAVE desc ~%desc%~ BEGIN
      REPLACE_TEXTUALLY EXACT_MATCH ~%usab_block%~ ~%usab_block_new%~
    END
    
    // check if mage was the only thing that couldn't use
    INNER_PATCH_SAVE compare ~%desc%~ BEGIN 
      REPLACE_TEXTUALLY ~^%unusable_replacement%.*[%LNL%%MNL%%WNL%]%class_prefix%[^- %TAB%]~ ~~
    END
    // if it was...
    PATCH_IF (~%desc%~ STRING_EQUAL ~%compare%~) BEGIN
      INNER_PATCH_SAVE desc ~%desc%~ BEGIN
        // remove 'not usable by' line, so it won't be sitting all by its lonesome now
        REPLACE_TEXTUALLY ~[%LNL%%MNL%%WNL%]%unusable_replacement%.*~ ~~
      END
    END

    // write changes
    SAY_EVALUATED ~%offset%~ ~%desc%~
  END
END

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
    READ_BYTE 0x31 "prof" ELSE 0
    PATCH_IF ("%prof%" = 115) BEGIN
      READ_BYTE 0x1f "fighter"
      PATCH_IF (("%fighter%" BAND "0b00001000") = "0b00000000") BEGIN // if usable by single-class fighter
        READ_BYTE   0x20 "mage"
        WRITE_BYTE 0x20 ("%mage%" BAND "0b11111011")
        
        // update descriptions
        PATCH_FOR_EACH offset IN
          ~0x50~
          ~0x54~
        BEGIN
          READ_LONG ~%offset%~ desc_strref
          PATCH_IF (desc_strref >= 0 && desc_strref < 2147483646) BEGIN
            READ_STRREF ~%offset%~ desc
            // remove mage line from matching english descriptions
            LAUNCH_PATCH_FUNCTION ~REMOVE_MAGE_LINE_FROM_USABILITIES~
              STR_VAR
                unusable_regexp = EVALUATE_BUFFER ~^[ %TAB%]*\(Unusable By\|Not Usable By\).*~
                unusable_replacement = ~Not Usable By:~
                mage_regexp = EVALUATE_BUFFER ~[%LNL%%MNL%%WNL%].*\bMage.*~
                class_prefix = ~ ~
            END
            // remove mage line from matching german descriptions
            LAUNCH_PATCH_FUNCTION ~REMOVE_MAGE_LINE_FROM_USABILITIES~
              STR_VAR
                unusable_regexp = EVALUATE_BUFFER ~^[ %TAB%]*Kann nicht verwendet werden von[ %TAB%]*:.*~
                unusable_replacement = ~Kann nicht verwendet werden von:~
                mage_regexp = EVALUATE_BUFFER ~[%LNL%%MNL%%WNL%][- %TAB%]*Magiern?.*~
                class_prefix = ~- ~
            END
            // remove mage line from matching italian descriptions
            LAUNCH_PATCH_FUNCTION ~REMOVE_MAGE_LINE_FROM_USABILITIES~
              STR_VAR
                unusable_regexp = EVALUATE_BUFFER ~^[ %TAB%]*Non[ %TAB%]*utilizzabile[ %TAB%]*da[ %TAB%]*:.*~
                unusable_replacement = ~Non utilizzabile da:~
                mage_regexp = EVALUATE_BUFFER ~[%LNL%%MNL%%WNL%][- %TAB%]*Mago.*~
                class_prefix = ~ ~
            END
            // remove mage line from matching spanish descriptions
            LAUNCH_PATCH_FUNCTION ~REMOVE_MAGE_LINE_FROM_USABILITIES~
              STR_VAR
                unusable_regexp = EVALUATE_BUFFER ~^[ %TAB%]*No \(lo puede usar\|utilizable por\|usable por\|permitido a\)[ %TAB%]*:.*~
                unusable_replacement = ~No permitido a:~
                mage_regexp = EVALUATE_BUFFER ~[%LNL%%MNL%%WNL%][- %TAB%]*\(El \)?Mago.*~
                class_prefix = ~ ~
            END
          END
        END
      END
    END
  END
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~weapprof.2da~ ~override~ // allows mage proficiency in clubs
  FOR (column = 22; column < 30; column = column + 1) BEGIN
    SET_2DA_ENTRY_LATER ~weapprof~ 20 column ~1~ // Specialist Mage
  END
  SET_2DA_ENTRY_LATER ~weapprof~ 20  4 ~1~ // Mage
  SET_2DA_ENTRY_LATER ~weapprof~ 20 53 ~1~ // Wild Mage
  SET_2DA_ENTRIES_NOW ~weapprof~ 1
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Universal Clubs description updates              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @205000 DESIGNATED 2050
DEPRECATED @18

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Weapon Styles for All (Idobek)                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @206000 DESIGNATED 2060
GROUP @9

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02060.g3~

OUTER_SET mage_kit_index = 0
COPY_EXISTING ~kitlist.2da~ ~override~
  READ_2DA_ENTRIES_NOW ~r2en_kitlist~ 9
  FOR (i = 0; i < r2en_kitlist; i += 1) BEGIN
    READ_2DA_ENTRY_FORMER ~r2en_kitlist~ i 8 parent_class
    PATCH_IF (~%parent_class%~ STRING_EQUAL ~1~) BEGIN // mage
      READ_2DA_ENTRY_FORMER ~r2en_kitlist~ i 1 kitname
      SPRINT $mage_kit(~%mage_kit_index%~) ~%kitname%~
      SET mage_kit_index += 1
    END
  END
  BUT_ONLY

PRINT @1
COPY_EXISTING ~weapprof.2da~ ~override~
  READ_2DA_ENTRIES_NOW ~r2en2_weapprof~ 3
  READ_2DA_ENTRY_FORMER ~r2en2_weapprof~ 30 4  mage   // sword and shield style check for mages
  READ_2DA_ENTRY_FORMER ~r2en2_weapprof~ 30 32 kensai // sword and shield style check for kensais
  FOR (i = 0; VARIABLE_IS_SET EVALUATE_BUFFER "r2en2_weapprof_1_%i%"; i+=1) BEGIN END // stolen from bg-style proficiencies component
  SET classcnt = i
  FOR (class = 4; class < classcnt; class += 1) BEGIN // for each class
    SET_2DA_ENTRY_LATER ~s2el_weapprof~ 29 class ~2~
    SET_2DA_ENTRY_LATER ~s2el_weapprof~ 30 class ~2~
    SET_2DA_ENTRY_LATER ~s2el_weapprof~ 31 class ~2~
    SET_2DA_ENTRY_LATER ~s2el_weapprof~ 32 class ~3~
    PATCH_IF (mage == 0) BEGIN // if mages can't use shields (as usual)...
      READ_2DA_ENTRY_FORMER ~r2en2_weapprof~ 0 (class - 1) classname
      FOR (j = 0; j < mage_kit_index; j += 1) BEGIN // don't let mage kits put points in sword & shield
        SPRINT kitname $mage_kit(~%j%~)
        PATCH_IF (~%classname%~ STRING_EQUAL_CASE ~%kitname%~) BEGIN
          SET_2DA_ENTRY_LATER ~s2el_weapprof~ 30 class ~0~
          SET j = mage_kit_index // exit loop
        END
      END
    END
  END
  // reset monk
  SET_2DA_ENTRY_LATER ~s2el_weapprof~ 29 51 ~0~
  SET_2DA_ENTRY_LATER ~s2el_weapprof~ 30 51 ~0~
  SET_2DA_ENTRY_LATER ~s2el_weapprof~ 32 51 ~0~
  // reset mage if sword and shield style not allowed
  PATCH_IF (mage == 0) BEGIN
    SET_2DA_ENTRY_LATER ~s2el_weapprof~ 30 4 ~0~
  END
  // reset kensai if sword and shield style not allowed
  PATCH_IF (kensai == 0) BEGIN
    SET_2DA_ENTRY_LATER ~s2el_weapprof~ 30 32 ~0~
  END
  SET_2DA_ENTRIES_NOW ~s2el_weapprof~ 3
  REPLACE_EVALUATE ~\(^ID  *\)~ BEGIN
    SPACES somespace ~%MATCH1%~
  END ~%somespace%%MATCH1%~
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Druids Use Cleric Level and Spell Progression    \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @207000 DESIGNATED 2070
DEPRECATED @18

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Delay High Level Abilities                       \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @208000 DESIGNATED 2080
GROUP @9
REQUIRE_PREDICATE GAME_IS ~tob bg2ee bgt~   @25 // ToB required

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02080.g3~

COPY_EXISTING ~lunumab.2da~ ~override~
  FOR (row = 3; row < 33; row = row + 1) BEGIN
    READ_2DA_ENTRY row 1 1 level
    PATCH_IF (level < 21) BEGIN
      SET_2DA_ENTRY_LATER ~lunumab~ row 1 ~21~
    END
  END
  SET_2DA_ENTRIES_NOW ~lunumab~ 1
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Change XP Cap                                    \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// remove cap                                       \\\\\
/////                                                  \\\\\

BEGIN @209001 DESIGNATED 2090 // remove xp cap
GROUP @9
SUBCOMPONENT @209000 // change xp cap

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02090.g3~

COPY  ~bg2_tweaks/2da/xpcap.2da~   ~override~

COPY  ~bg2_tweaks/2da/xplevel.2da~   ~override~
  PATCH_IF FILE_EXISTS_IN_GAME ~cdt02070.g3~ THEN BEGIN // if druid/cleric xp progression installed
    REPLACE_TEXTUALLY
~CLERIC \(.*\)
DRUID \(.*\)~
~CLERIC \1
DRUID  \1~
  END

// extend bard, ranger pickpocketing to 200 and levels to 50
INCLUDE ~bg2_tweaks/lib/thieving_to_50.tpa~

ACTION_CLEAR_ARRAY  cd_extend_tables
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_extend_tables BEGIN
  lvlmodwm => 0 // wild mage level modulation for wild surges
  monkfist => 0 // monk fist progression
  mxsplbrd => 0 // bard spell table
  mxspldd  => 0 // dragon disciple spell table
  mxspldru => 0 // druid spell table
  mxsplpal => 0 // paladin spell table
  mxsplprs => 0 // priest spell table
  mxsplran => 0 // ranger spell table
  mxsplsrc => 0 // sorcerer spell table
  mxsplwiz => 0 // mage spell table
  raisdead => 0 // raise dead temple prices
  splsrckn => 0 // socerer spell table
END

// extend hp tables for ee games
ACTION_IF FILE_EXISTS_IN_GAME hpclass.2da THEN BEGIN

  // add hp tables to list of tables to extend
  COPY_EXISTING ~hpclass.2da~ ~override~
    COUNT_2DA_ROWS ~2~ "rows"
    FOR ( index = 1 ; index < rows ; index = index + 1 ) BEGIN
      SET "exists" = 0
      READ_2DA_ENTRY "%index%" 1 2 "table"
      DEFINE_ASSOCIATIVE_ARRAY cd_extend_tables BEGIN "%table%" => 0 END
    END

END

// easy replacements for 2das with levels as rows
ACTION_PHP_EACH cd_extend_tables AS file => foo BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.2da~ THEN BEGIN

    COPY_EXISTING ~%file%.2da~ ~override~
      PATCH_IF (SOURCE_SIZE > 0) BEGIN
        REPLACE_TEXTUALLY ~^40\([ %TAB%].*\)~
          ~40\1
41\1
42\1
43\1
44\1
45\1
46\1
47\1
48\1
49\1
50\1~
      END
      UNLESS ~^50[ %TAB%]~
      BUT_ONLY_IF_IT_CHANGES
  END

END

// extend level-based abilities to 50; 2das with a zero column
ACTION_FOR_EACH file IN backstab crippstr layhands savemonk saveprs saverog savewar savewiz sneakatt thac0 xpbonus BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.2da~ THEN BEGIN

    COPY_EXISTING ~%file%.2da~ ~override~
       REPLACE_TEXTUALLY ~^\(.+[ %TAB%]+[0-9]+[ %TAB%]+[0-9]+[ %TAB%]+[0-9]+[ %TAB%]+[0-9]+\)\([ %TAB%]+[0-9]+\)~
        ~\1\2\2\2\2\2\2\2\2\2\2\2~
       REPLACE_TEXTUALLY ~\([ %TAB%]+\)40[ %TAB%]+40[ %TAB%]+40[ %TAB%]+40[ %TAB%]+40[ %TAB%]+40[ %TAB%]+40[ %TAB%]+40[ %TAB%]+40[ %TAB%]+40[ %TAB%]+40~
        ~\140\141\142\143\144\145\146\147\148\149\150~  
      UNLESS ~41[ %TAB%]+42[ %TAB%]+43[ %TAB%]+44[ %TAB%]+45[ %TAB%]+46[ %TAB%]+47[ %TAB%]+48[ %TAB%]+49[ %TAB%]+50~
      BUT_ONLY_IF_IT_CHANGES
      
  END

END

// extend level-based abilities to 50; wspatck
COPY_EXISTING ~wspatck.2da~ ~override~ // warrior bonus attacks for proficiencies and levels
  // for iwdee, extend from 31 to 40
  REPLACE_TEXTUALLY ~\(1[ %TAB%]+2[ %TAB%]+3[ %TAB%]+4[ %TAB%]+5[ %TAB%]+6[ %TAB%]+7[ %TAB%]+8[ %TAB%]+9[ %TAB%]+10[ %TAB%]+11[ %TAB%]+12[ %TAB%]+13[ %TAB%]+14[ %TAB%]+15[ %TAB%]+16[ %TAB%]+17[ %TAB%]+18[ %TAB%]+19[ %TAB%]+20[ %TAB%]+21[ %TAB%]+22[ %TAB%]+23[ %TAB%]+24[ %TAB%]+25[ %TAB%]+26[ %TAB%]+27[ %TAB%]+28[ %TAB%]+29[ %TAB%]+30\)\([ %TAB%]+\)31~
    ~\1\231\232\233\234\235\236\237\238\239\240~
  REPLACE_TEXTUALLY ~^\([0-5]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+\)\([ %TAB%]+-?[0-9]+\)~
    ~\1\2\2\2\2\2\2\2\2\2\2~
  // everything else, extend from 41 to 50  
  REPLACE_TEXTUALLY ~\(1[ %TAB%]+2[ %TAB%]+3[ %TAB%]+4[ %TAB%]+5[ %TAB%]+6[ %TAB%]+7[ %TAB%]+8[ %TAB%]+9[ %TAB%]+10[ %TAB%]+11[ %TAB%]+12[ %TAB%]+13[ %TAB%]+14[ %TAB%]+15[ %TAB%]+16[ %TAB%]+17[ %TAB%]+18[ %TAB%]+19[ %TAB%]+20[ %TAB%]+21[ %TAB%]+22[ %TAB%]+23[ %TAB%]+24[ %TAB%]+25[ %TAB%]+26[ %TAB%]+27[ %TAB%]+28[ %TAB%]+29[ %TAB%]+30[ %TAB%]+31[ %TAB%]+32[ %TAB%]+33[ %TAB%]+34[ %TAB%]+35[ %TAB%]+36[ %TAB%]+37[ %TAB%]+38[ %TAB%]+39\)\([ %TAB%]+\)40~
    ~\1\240\241\242\243\244\245\246\247\248\249\250~
  REPLACE_TEXTUALLY ~^\([0-5]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+\)\([ %TAB%]+-?[0-9]+\)~
    ~\1\2\2\2\2\2\2\2\2\2\2\2~
  UNLESS ~41[ %TAB%]+42[ %TAB%]+43[ %TAB%]+44[ %TAB%]+45[ %TAB%]+46[ %TAB%]+47[ %TAB%]+48[ %TAB%]+49[ %TAB%]+50~
  BUT_ONLY_IF_IT_CHANGES

ACTION_CLEAR_ARRAY  cd_extend_clabs
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_extend_clabs BEGIN // start with class tables
  clabba01 => 0 // generic bard abilities
  clabdr01 => 0 // generic druid abilities
  clabfi01 => 0 // generic fighter abilities
  clabmo01 => 0 // generic monk abilities
  clabpa01 => 0 // generic paladin abilities
  clabpa05 => 0 // fallen paladin abilities
  clabpr01 => 0 // generic priest abilities
  clabrn01 => 0 // generic ranger abilities
  clabrn05 => 0 // fallen ranger abilities
  clabth01 => 0 // generic thief abilities
END

// add kit tables dynamically by reading kitlist
COPY_EXISTING ~kitlist.2da~ ~override~
  COUNT_2DA_ROWS ~9~ "rows"
  FOR ( index = 1 ; index < rows ; index = index + 1 ) BEGIN
    SET "exists" = 0
    READ_2DA_ENTRY "%index%" 5 9 "clab"
    DEFINE_ASSOCIATIVE_ARRAY cd_extend_clabs BEGIN "%clab%" => 0 END
  END
  BUT_ONLY_IF_IT_CHANGES

// extend clab abilities to 50
ACTION_PHP_EACH cd_extend_clabs AS file => foo BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.2da~ THEN BEGIN

    COPY_EXISTING ~%file%.2da~ ~override~
      PATCH_IF (SOURCE_SIZE > 0) BEGIN
        REPLACE_TEXTUALLY ~[%WNL%%MNL%]+~ ~%LNL%~
        REPLACE_TEXTUALLY ~\(1[ %TAB%]+2[ %TAB%]+3[ %TAB%]+4[ %TAB%]+5[ %TAB%]+6[ %TAB%]+7[ %TAB%]+8[ %TAB%]+9[ %TAB%]+10[ %TAB%]+11[ %TAB%]+12[ %TAB%]+13[ %TAB%]+14[ %TAB%]+15[ %TAB%]+16[ %TAB%]+17[ %TAB%]+18[ %TAB%]+19[ %TAB%]+20[ %TAB%]+21[ %TAB%]+22[ %TAB%]+23[ %TAB%]+24[ %TAB%]+25[ %TAB%]+26[ %TAB%]+27[ %TAB%]+28[ %TAB%]+29[ %TAB%]+30[ %TAB%]+31[ %TAB%]+32[ %TAB%]+33[ %TAB%]+34[ %TAB%]+35[ %TAB%]+36[ %TAB%]+37[ %TAB%]+38[ %TAB%]+39\)\([ %TAB%]+\)40[ %TAB%]*~
          ~\1\240\241\242\243\244\245\246\247\248\249\250~
        REPLACE_TEXTUALLY ~^\(ABILITY[0-9A-Z]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+\)\([ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%%LNL%]+\)[ %TAB%]*$~
          ~\1\2\2~
      END
      UNLESS ~41[ %TAB%]+42[ %TAB%]+43[ %TAB%]+44[ %TAB%]+45[ %TAB%]+46[ %TAB%]+47[ %TAB%]+48[ %TAB%]+49[ %TAB%]+50~
      BUT_ONLY

  END

END

// for EE
ACTION_FOR_EACH file IN startare startbp BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.2da~ THEN BEGIN

    COPY_EXISTING ~%file%.2da~ ~override~
      REPLACE_TEXTUALLY ~\(START_XP_CAP[ %TAB%]+\)[0-9]+~ ~\1-1~
      BUT_ONLY

  END

END

/////                                                  \\\\\
///// cap at level 20                                  \\\\\
/////                                                  \\\\\

BEGIN @209100 DESIGNATED 2091
GROUP @9
SUBCOMPONENT @209000 // change xp cap

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02091.g3~

COPY  ~bg2_tweaks/2da/xpcap.2da~   ~override~

COPY ~bg2_tweaks/2da/xplevel20.2da~ ~override/xplevel.2da~
  PATCH_IF FILE_EXISTS_IN_GAME ~cdt02070.g3~ THEN BEGIN // if druid/cleric xp progression installed
    REPLACE_TEXTUALLY
~CLERIC \(.*\)
DRUID \(.*\)~
~CLERIC \1
DRUID  \1~
  END

/////                                                  \\\\\
///// cap at level 30                                  \\\\\
/////                                                  \\\\\

BEGIN @209200 DESIGNATED 2092
GROUP @9
SUBCOMPONENT @209000 // change xp cap

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02092.g3~

COPY  ~bg2_tweaks/2da/xpcap.2da~   ~override~

COPY ~bg2_tweaks/2da/xplevel30.2da~ ~override/xplevel.2da~
  PATCH_IF FILE_EXISTS_IN_GAME ~cdt02070.g3~ THEN BEGIN // if druid/cleric xp progression installed
    REPLACE_TEXTUALLY
~CLERIC \(.*\)
DRUID \(.*\)~
~CLERIC \1
DRUID  \1~
  END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Allow Thief Skills and Stealth in Heavy Armor    \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @210000 DESIGNATED 2100
GROUP @9

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02100.g3~

ACTION_CLEAR_ARRAY cd_armor
INCLUDE ~bg2_tweaks/lib/thieving_descripts.tpa~ // text replacement macroes
INCLUDE ~bg2_tweaks/lib/common_armor_list.tpa~  // common armor types needed for both components
INCLUDE ~bg2_tweaks/lib/thieving_to_50.tpa~     // extend bard, ranger pickpocketing to 200 and levels to 50

// to get unarmored bonuses to work, bonuses are added in the race bonus tables
// all non-robe armor offsets these
COPY_EXISTING ~skillrac.2da~ ~override~
  COUNT_2DA_ROWS 8 rows
  FOR (index = 0 ; index < rows ; ++index) BEGIN
    READ_2DA_ENTRY index 1 8 val
    SET_2DA_ENTRY  index 1 8 (val + 5) // pickpocket
    READ_2DA_ENTRY index 4 8 val
    SET_2DA_ENTRY  index 4 8 (val + 10) // move silently
    READ_2DA_ENTRY index 5 8 val
    SET_2DA_ENTRY  index 5 8 (val + 5) // HiS
  END
  BUT_ONLY
  
ACTION_PHP_EACH cd_armor AS item => type BEGIN

  ACTION_IF ((type > 104) AND (FILE_EXISTS_IN_GAME ~%item%.itm~)) BEGIN // ignore shields
  
    ACTION_IF ((type < 201) OR (type = 300) OR (type = 400)) BEGIN // leather, other misc armors that only need to offset unarmored bonuses

      // for leather or padded, add penalties to offset unarmored bonuses from race table
      COPY_EXISTING ~%item%.itm~ ~override~
        READ_SHORT 0x1c sanity_check
        PATCH_IF sanity_check = 2 BEGIN
          LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 275 target = 1 parameter1 =  "-5" timing = 2 END // HiS
          LPF ADD_ITEM_EQEFFECT INT_VAR opcode =  92 target = 1 parameter1 =  "-5" timing = 2 END // pickpocketing
          LPF ADD_ITEM_EQEFFECT INT_VAR opcode =  59 target = 1 parameter1 = "-10" timing = 2 END // move silently
        END
        BUT_ONLY 

    END ELSE BEGIN

      COPY_EXISTING ~%item%.itm~ ~override~
        READ_SHORT 0x1c sanity_check
        PATCH_IF sanity_check = 2 BEGIN
          SET patch_hide = 0
          PATCH_IF ((type = 201) OR (type = 210) OR (type = 211) OR (type = 212) OR
                    (type = 214) OR (type = 301) OR (type = 310)) BEGIN // studded leather
            SET penalty_hide = 20
            SET penalty_pickpocket = 30
            SET penalty_lock = 10
            SET penalty_trap = 10
            SET penalty_silent = 20
          END ELSE
          PATCH_IF (type = 215) BEGIN // studded leather, gets an extra -15 to move silently (squeaky leather from bgee)
            SET penalty_hide = 20
            SET penalty_pickpocket = 30
            SET penalty_lock = 10
            SET penalty_trap = 10
            SET penalty_silent = 35
          END ELSE
          PATCH_IF ((type = 202) OR (type = 302)) BEGIN // hide
            SET penalty_hide = 20
            SET penalty_pickpocket = 60
            SET penalty_lock = 50
            SET penalty_trap = 50
            SET penalty_silent = 30
          END ELSE
          PATCH_IF ((type = 203) OR (type = 303) OR (type = 330)) BEGIN // chain mail
            SET penalty_hide = 30
            SET penalty_pickpocket = 40
            SET penalty_lock = 15
            SET penalty_trap = 15
            SET penalty_silent = 40
          END ELSE
          PATCH_IF ((type = 204) OR (type = 304)) BEGIN // elven chain mail
            SET penalty_hide = 10
            SET penalty_pickpocket = 20
            SET penalty_lock = 5
            SET penalty_trap = 5
            SET penalty_silent = 10
          END
          PATCH_IF ((type = 205) OR (type = 305)) BEGIN // scale mail
            SET penalty_hide = 50
            SET penalty_pickpocket = 50
            SET penalty_lock = 20
            SET penalty_trap = 20
            SET penalty_silent = 60
          END ELSE
          PATCH_IF ((type = 206) OR (type = 207) OR (type = 306) OR (type = 307)) BEGIN // splint, banded mail
            SET penalty_hide = 30
            SET penalty_pickpocket = 40
            SET penalty_lock = 15
            SET penalty_trap = 25
            SET penalty_silent = 40
          END ELSE
          PATCH_IF ((type = 208) OR (type = 308)) BEGIN // plate mail
            SET penalty_hide = 75
            SET penalty_pickpocket = 75
            SET penalty_lock = 40
            SET penalty_trap = 40
            SET penalty_silent = 80
          END ELSE
          PATCH_IF ((type = 209) OR (type = 309)) BEGIN // full plate
            SET penalty_hide = 95
            SET penalty_pickpocket = 95
            SET penalty_lock = 80
            SET penalty_trap = 80
            SET penalty_silent = 95
          END ELSE
          PATCH_IF (type = 213) BEGIN // studded leather, use regular penalties plus an extra 25%
            SET penalty_hide = 45
            SET penalty_pickpocket = 55
            SET penalty_lock = 35
            SET penalty_trap = 35
            SET penalty_silent = 45
          END
          PATCH_IF ((type = 211) OR (type = 212) OR (type = 310) OR (type = 330)) BEGIN // armor that shouln't get HiS changes
            SET penalty_hide = 255
          END
  //        PATCH_IF (type = 0) BEGIN // armor that shouln't get pickpocket changes
  //          SET penalty_pickpocket = 255
  //        END
          PATCH_IF ((type = 212) OR (type = 214)) BEGIN // armor that shouln't get open locks changes
            SET penalty_lock = 255
          END
          PATCH_IF (type = 214) BEGIN // armor that shouln't get find traps changes
            SET penalty_trap = 255
          END
          PATCH_IF (type = 210) BEGIN // armor that shouln't get MS changes
            SET penalty_silent = 255
          END
          LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = 144 END // delete disable thieving buttons
          PATCH_IF (penalty_hide != 255) BEGIN
            LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = 275 END
            LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 275 target = 1 parameter1 = (0 - penalty_hide - 5) timing = 2 END // extra 5 to offset unarmored bonus
          END ELSE BEGIN
            LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 275 target = 1 parameter1 = "-5"                   timing = 2 END // extra 5 to offset unarmored bonus
          END
          PATCH_IF (penalty_pickpocket != 255) BEGIN
            LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = 92 END
            LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 92 target = 1 parameter1 = (0 - penalty_pickpocket - 5) timing = 2 END // extra 5 to offset unarmored bonus
          END ELSE BEGIN
            LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 92 target = 1 parameter1 = "-5"                         timing = 2 END // extra 5 to offset unarmored bonus
          END
          PATCH_IF (penalty_trap != 255) BEGIN
            LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = 91 END
            LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 91  target = 1 parameter1 = (0 - penalty_trap)           timing = 2 END
          END
          PATCH_IF (penalty_lock != 255) BEGIN
            LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = 90 END
            LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 90  target = 1 parameter1 = (0 - penalty_lock)           timing = 2 END
          END
          PATCH_IF (penalty_silent != 255) BEGIN
            LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = 59 END
            LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 59  target = 1 parameter1 = (0 - penalty_silent - 10)    timing = 2 END // extra 10 to offset unarmored bonus
          END ELSE BEGIN
            LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 59 target = 1 parameter1 = "-10"                         timing = 2 END // extra 10 to offset unarmored bonus
          END
          // adjust descriptions
          PATCH_FOR_EACH offset IN 0x50 0x54 BEGIN
            READ_LONG ~%offset%~ desc_strref
            PATCH_IF (desc_strref >= 0 && desc_strref < 2147483646) BEGIN
              READ_STRREF ~%offset%~ desc
              INNER_PATCH_SAVE desc ~%desc%~ BEGIN
                LAUNCH_PATCH_MACRO ~thieving_descripts~
              END
              SAY_EVALUATED ~%offset%~ ~%desc%~ // write changes
            END
          END
        END
        BUT_ONLY

    END

  END
  
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Adjust item descriptions for thieving skills               \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @211000 DESIGNATED 2110
DEPRECATED @18

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Allow Arcane Spellcasting in Armor               \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @212000 DESIGNATED 2120
GROUP @9

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02120.g3~

ACTION_CLEAR_ARRAY cd_armor
INCLUDE ~bg2_tweaks/lib/arcane_descripts.tpa~  // text replacement macroes
INCLUDE ~bg2_tweaks/lib/common_armor_list.tpa~ // common armor types needed for both components

ACTION_PHP_EACH cd_armor AS item => type BEGIN

  ACTION_IF ((type < 300) AND (FILE_EXISTS_IN_GAME ~%item%.itm~)) BEGIN

    COPY_EXISTING ~%item%.itm~ ~override~
      READ_SHORT 0x1c sanity_check
      PATCH_IF ((sanity_check = 2) OR (sanity_check = 12)) BEGIN
        LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete =  60 END
        LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = 145 END
        PATCH_IF ((type = 100) OR (type = 101)) BEGIN // buckler, small shield
          SET patch_miscast = 5
        END ELSE
        PATCH_IF ((type = 199) OR (type = 200)) BEGIN // leather, padded armor
          SET patch_miscast = 10
        END ELSE
        PATCH_IF ((type = 102) OR (type = 103) OR (type = 110) OR (type = 201) OR (type > 209)) BEGIN // medium/large shield, studded leather
          SET patch_miscast = 15
        END ELSE
        PATCH_IF (type = 202) BEGIN // hide armor
          SET patch_miscast = 20
        END ELSE
        PATCH_IF (type = 205) BEGIN // scale armor
          SET patch_miscast = 25
        END ELSE
        PATCH_IF ((type = 130) OR (type = 203)) BEGIN // chain armor
          SET patch_miscast = 30
        END ELSE
        PATCH_IF (type = 209) BEGIN // full plate
          SET patch_miscast = 35
        END ELSE
        PATCH_IF ((type = 206) OR (type = 207) OR (type = 208)) BEGIN // plate mail, banded mail, splint mail
          SET patch_miscast = 40
        END ELSE BEGIN // tower shield
          SET patch_miscast = 50
        END
        LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 60 target = 1 parameter1 = patch_miscast timing = 2 END
        // adjust descriptions
        PATCH_FOR_EACH offset IN 0x50 0x54 BEGIN
          READ_LONG ~%offset%~ desc_strref
          PATCH_IF (desc_strref >= 0 && desc_strref < 2147483646) BEGIN
            READ_STRREF ~%offset%~ desc
            INNER_PATCH_SAVE desc ~%desc%~ BEGIN
              LAUNCH_PATCH_MACRO ~arcane_descripts~
            END
            SAY_EVALUATED ~%offset%~ ~%desc%~ // write changes
          END
        END
      END
      BUT_ONLY

  END
  
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Adjust item descriptions for arcane casting                \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @213000 DESIGNATED 2130
DEPRECATED @18

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// More dual-classes                                          \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @214000 DESIGNATED 2140
GROUP @9

INCLUDE ~bg2_tweaks/lib/kit_ids_fixer.tpa~
INCLUDE ~bg2_tweaks/lib/tob2soa.tpa~

// first it replaces WILDMAGE and BARBARIAN values in these files if they're present
COPY_EXISTING ~dualclas.2da~ ~override~
  REPLACE_TEXTUALLY
    ~\bWILDMAGE\b.*~
    ~WILDMAGE                1       1       0       1       0       0~
  REPLACE_TEXTUALLY
    ~\bBARBARIAN\b.*~
    ~BARBARIAN               0       1       1       1       1       0~
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~abdcdsrq.2da~ ~override~
  REPLACE_TEXTUALLY
    ~\bWILDMAGE\b.*~
    ~WILDMAGE                0       0       0       17      0       0~
  REPLACE_TEXTUALLY
    ~\bBARBARIAN\b.*~
    ~BARBARIAN               17      0       0       0       0       0~
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~abdcscrq.2da~ ~override~
  REPLACE_TEXTUALLY
    ~\bWILDMAGE\b.*~
    ~WILDMAGE                0       0       0       15      0       0~
  REPLACE_TEXTUALLY
    ~\bBARBARIAN\b.*~
    ~BARBARIAN               15      0       0       0       0       0~
  BUT_ONLY_IF_IT_CHANGES

// if they're not then it appends them to the files
APPEND ~dualclas.2da~ ~WILDMAGE                1       1       0       1       0       0~
  UNLESS ~\bWILDMAGE\b~

APPEND ~abdcdsrq.2da~ ~WILDMAGE                0       0       0       17      0       0~
  UNLESS ~\bWILDMAGE\b~

APPEND ~abdcscrq.2da~ ~WILDMAGE                0       0       0       15      0       0~
  UNLESS ~\bWILDMAGE\b~

APPEND ~dualclas.2da~ ~BARBARIAN               0       1       1       1       1       0~
  UNLESS ~\bBARBARIAN\b~

APPEND ~abdcdsrq.2da~ ~BARBARIAN               17      0       0       0       0       0~
  UNLESS ~\bBARBARIAN\b~

APPEND ~abdcscrq.2da~ ~BARBARIAN               15      0       0       0       0       0~
  UNLESS ~\bBARBARIAN\b~

COPY ~bg2_tweaks/spl/cdtitle0.spl~ ~override~ // spell to fix title and class name for d/c barbarians. Calls one of the EFFs below as appropriate

COPY ~bg2_tweaks/eff/cdtitle1.eff~ ~override~
  SAY 0x1C @214001

COPY_EXISTING ~cdtitle1.eff~ ~override/cdtitle2.eff~
  SAY 0x1C @214002

COPY_EXISTING ~cdtitle1.eff~ ~override/cdtitle3.eff~
  SAY 0x1C @214003

COPY_EXISTING ~cdtitle1.eff~ ~override/cdtitle4.eff~
  SAY 0x1C @214004

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
    READ_BYTE 0x1f "usa2"
    READ_BYTE 0x29 "kit1"
    PATCH_IF ((("%kit1%" BAND 0b01000000) = 0b01000000) AND // if unusable by fighters and barbarians
              (("%usa2%" BAND 0b00001000) = 0b00001000)) THEN BEGIN
      WRITE_BYTE 0x29 ("%kit1%" BAND 0b10111111) //remove redundant barb flag
    END
  END
  BUT_ONLY_IF_IT_CHANGES


ACTION_IF FILE_EXISTS ~Refinements/Hlab/Thief/clab/CLABEMPT.2DA~ AND NOT FILE_EXISTS ~Refinements/lib/kit.tpa~ THEN BEGIN

  ADD_KIT ~barbtF~
    ~barbtF   1 1 1 1 1 0 0 0~
    ~barbtF  0 1 0 0 1 0 0 1 0 1 1 0 0 1 1 1 0 1 0 0 0 0 1 1 0 1 1 1 1 1 1 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
    ~barbtF     0       9       0       0       0       0~
    ~barbtF     0       0       0       0       0       0~
    ~barbtF     0       17      0       0       0       0~
    ~barbtF     0       15      0       0       0       0~
    ~barbtF     0       1       1       1       1       1       1       1       1~
    ~barbtF     1       1       1       0       0       0~
    ~Refinements/Hlab/Thief/clab/CLABEMPT.2DA~
    ~~
    ~0x40000000 2~
    ~BRB~
    ~* * * * * * * * * * * * * * * * * * * *~
    SAY #45855
    SAY #45859
    SAY #45869

  ADD_KIT ~barbtw~
    ~barbtw   1 1 1 1 1 0 0 0~
    ~barbtw  0 1 0 0 1 0 0 1 0 1 1 0 0 1 1 1 0 1 0 0 0 0 1 1 0 1 1 1 1 1 1 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
    ~barbtw     0       9       0       0       0       0~
    ~barbtw     0       0       0       0       0       0~
    ~barbtw     0       17      0       0       0       0~
    ~barbtw     0       15      0       0       0       0~
    ~barbtw     0       1       1       1       1       1       1       1       1~
    ~barbtw     1       1       1       0       0       0~
    ~Refinements/Hlab/Thief/clab/CLABTEMP.2DA~
    ~~
    ~0x40000000 2~
    ~BRB~
    ~* * * * * * * * * * * * * * * * * * * *~
    SAY #45855
    SAY #45859
    SAY #45869

  COPY_EXISTING ~kitlist.2da~ ~override~
    REPLACE_TEXTUALLY ~45855 45859 45869 CLABTEMP ~     ~45855 45859 45869 CLABFI05 ~
    SET_2DA_ENTRY 34 7 1 0x400000020

  EXTEND_TOP ~dplayer.bcs~  ~bg2_tweaks/baf/dplayer.baf~
  EXTEND_TOP ~dplayer2.bcs~ ~bg2_tweaks/baf/dplayer.baf~
  EXTEND_TOP ~dplayer3.bcs~ ~bg2_tweaks/baf/dplayer.baf~
  //EXTEND_BOTTOM ~baldur.bcs~   ~bg2_tweaks/baf/Runcheck.baf~
  //EXTEND_BOTTOM ~baldur25.bcs~ ~bg2_tweaks/baf/Runcheck.baf~
  EXTEND_BOTTOM ~li#stri.bcs~  ~bg2_tweaks/baf/li#stri.baf~

  COPY_EXISTING ~weapprof.2da~ ~override~
  // Full compatibility with all weapprof modifiers (like, say, AOE)
    FOR (row = 3; row < 35; row = row + 1) BEGIN
      READ_2DA_ENTRY "%row%" 52 1 "barb"
      SET_2DA_ENTRY "%row%" (22+"%barbtF%") 1 "%barb%"
      SET_2DA_ENTRY "%row%" (22+"%barbtw%") 1 "%barb%"
    END

  COPY_EXISTING  ~KITLIST.2DA~ ~override~
    READ_2DA_ENTRY 34 2 1 "lower"
    READ_2DA_ENTRY 34 3 1 "mixed"
    READ_2DA_ENTRY 34 4 1 "help"
    SET_2DA_ENTRY "%barbtF%"+3 2 1 "%lower%"
    SET_2DA_ENTRY "%barbtw%"+3 2 1 "%lower%"
    SET_2DA_ENTRY "%barbtF%"+3 3 1 "%mixed%"
    SET_2DA_ENTRY "%barbtw%"+3 3 1 "%mixed%"
    SET_2DA_ENTRY "%barbtF%"+3 4 1 "%help%"
    SET_2DA_ENTRY "%barbtw%"+3 4 1 "%help%"

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// wear multiple protection items                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// PnP restrictions                                 \\\\\
/////                                                  \\\\\

BEGIN @215001 DESIGNATED 2150 // PnP restrictions
GROUP @9
SUBCOMPONENT @215000 // wear multiple protection items

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02150.g3~

INCLUDE ~bg2_tweaks/lib/pnp_descripts.tpa~  // text replacement macroes

// lookup table
COPY ~bg2_tweaks/2da/cdpnp.2da~ ~override~
  COUNT_2DA_ROWS 2 cd_count

// this array used to give item charges (re-used from bastard, makes no diff)
ACTION_CLEAR_ARRAY cd_bastard

COPY_EXISTING ~itemexcl.2da~ ~override~
  COUNT_2DA_ROWS 2 rows
  FOR (index = 0 ; index < rows ; ++index) BEGIN
    READ_2DA_ENTRY index 0 2 item
    INNER_ACTION BEGIN

      // don't bother if file isn't present or if it's already on the list
      ACTION_IF (FILE_EXISTS_IN_GAME ~%item%.itm~ AND (!FILE_CONTAINS_EVALUATED (~cdpnp.2da~ ~^%item% ~))) THEN BEGIN

        COPY_EXISTING ~%item%.itm~ ~override~
          READ_SHORT 0x1c type
          SET add = 0
          PATCH_IF (type != 2) BEGIN // don't care if armor
            READ_LONG  0x6a fx_off
            READ_SHORT 0x70 fx_num
            FOR (index2 = 0 ; index2 < fx_num ; ++index2) BEGIN
              READ_SHORT (fx_off + (0x30 * index2)) opcode
              PATCH_IF (opcode = 0) BEGIN
                SET add = 1
              END
            END
          END
          BUT_ONLY

        ACTION_IF (add = 1) BEGIN

          OUTER_SET cd_count += 1
          APPEND ~cdpnp.2da~ ~%SOURCE_RES%  cdpp%cd_count%  c!pp%cd_count%~

        END

      END

    END
  END
  BUT_ONLY

COPY + ~override/cdpnp.2da~ ~bg2_tweaks/2da/cdpnp.2da~
  COUNT_2DA_ROWS 3 rows
  FOR (index = 0 ; index < rows ; ++index) BEGIN
    READ_2DA_ENTRY index 0 3 orig
    READ_2DA_ENTRY index 1 3 no_ac
    READ_2DA_ENTRY index 2 3 0_lore
    INNER_ACTION BEGIN

      ACTION_IF FILE_EXISTS_IN_GAME ~%orig%.itm~ THEN BEGIN

        COPY_EXISTING ~%orig%.itm~ ~override~
          READ_ASCII  0x3a bam (8)
          READ_SHORT  0x42 lore
          PATCH_IF (lore = 0) BEGIN
            SPRINT 0_lore ~%orig%~ // if original has no lore, don't bother with a second new item
          END
          READ_SHORT  0x68 abil_num
          WRITE_SHORT 0x68 (abil_num + 1)
          READ_LONG   0x6a fx_off
          WRITE_LONG  0x6a (fx_off + 0x38)
          PATCH_IF (abil_num > 0) BEGIN
            READ_LONG   0x64 abil_off
            READ_SHORT (abil_off + 0x1e + ((abil_num - 1) * 0x38)) last_fx_num
            READ_SHORT (abil_off + 0x20 + ((abil_num - 1) * 0x38)) last_fx_idx
            SET new_fx = (last_fx_idx + last_fx_num)
          END ELSE BEGIN
            READ_SHORT  0x70 new_fx
          END
          // because create inventory item doesn't have a way to set charges, create new and delete current
          INSERT_BYTES   (fx_off +        (new_fx * 0x30)) 0x30 // new effect
            WRITE_SHORT  (fx_off +        (new_fx * 0x30)) 122  // create inventory item
            WRITE_BYTE   (fx_off + 0x02 + (new_fx * 0x30)) 1    // target: self
            WRITE_BYTE   (fx_off + 0x0c + (new_fx * 0x30)) 1    // instant/permanent till death
            WRITE_BYTE   (fx_off + 0x12 + (new_fx * 0x30)) 100  // probability
            WRITE_ASCIIE (fx_off + 0x14 + (new_fx * 0x30)) ~%no_ac%~ #8
          INSERT_BYTES   (fx_off       ) 0x38         // new ability
            WRITE_BYTE   (fx_off       ) 3            // magical
            WRITE_BYTE   (fx_off + 0x01) 1            // ID to use
            WRITE_SHORT  (fx_off + 0x02) 3            // in item slots
            WRITE_ASCIIE (fx_off + 0x04) ~%bam%~ #8   // item bam
            WRITE_SHORT  (fx_off + 0x0c) 5            // target: caster
            WRITE_SHORT  (fx_off + 0x0e) 1            // range: 1
            WRITE_SHORT  (fx_off + 0x1e) 1            // num effects: 1
            WRITE_SHORT  (fx_off + 0x20) new_fx       // fx index
            WRITE_SHORT  (fx_off + 0x22) 1            // num charges
            WRITE_BYTE   (fx_off + 0x24) 1            // vanishes when drained
            WRITE_SHORT  (fx_off + 0x2a) 1            // projectile: none
          BUT_ONLY

        ACTION_IF (abil_num < 3) BEGIN // don't bother if new ability is #4 (charges only for first 3 abils)

          ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_bastard BEGIN "%orig%" => "%abil_num%" END
        
        END

        COPY_EXISTING ~%orig%.itm~ ~override/%no_ac%.itm~
                      ~%orig%.itm~ ~override/%0_lore%.itm~
          WRITE_SHORT 0x42 0 // no lore

        COPY_EXISTING ~%no_ac%.itm~ ~override~
          WRITE_ASCIIE (fx_off + 0x4c + (new_fx * 0x30)) ~%0_lore%~ #8 // exploit the fact that fx_off and new_fx carry over
          LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = 0 END
          // update descriptions
          PATCH_FOR_EACH offset IN  0x54 BEGIN
            READ_LONG ~%offset%~ desc_strref
            PATCH_IF (desc_strref >= 0 && desc_strref < 2147483646) BEGIN
              READ_STRREF ~%offset%~ desc
              INNER_PATCH_SAVE desc ~%desc%~ BEGIN
                LAUNCH_PATCH_MACRO ~pnp_descripts~
              END
              SAY_EVALUATED ~%offset%~ ~%desc%~ // write changes
            END
          END

      END

    END
  END

// add charges to items in game
COPY_EXISTING_REGEXP GLOB ~^.+\.[ac]re$~ ~override~
  PATCH_IF ("%SOURCE_FILE%" STRING_COMPARE_REGEXP "^.+\.cre$" = 0) BEGIN // if creature
    READ_LONG 0x2bc itm_off ELSE 0
    READ_LONG 0x2c0 itm_num ELSE 0
  END ELSE BEGIN
    READ_LONG  0x78 itm_off ELSE 0
    READ_SHORT 0x76 itm_num ELSE 0
  END
  FOR (index = 0 ; index < itm_num ; ++index) BEGIN
    READ_ASCII (itm_off +        (0x14 * index)) item ELSE ""
    PHP_EACH cd_bastard AS itemname => charge BEGIN
      PATCH_IF ("%item%" STRING_COMPARE_CASE "%itemname%" = 0) BEGIN
        WRITE_SHORT (itm_off + 0x0a + (charge * 0x02) + (0x14 * index)) 1 // add charge
      END
    END
  END
  BUT_ONLY

// add charges to items being sold
COPY_EXISTING_REGEXP GLOB ~^.+\.sto$~ ~override~
  READ_LONG 0x34 sale_off  ELSE 0
  READ_LONG 0x38 sale_num  ELSE 0
  FOR (index = 0 ; index < sale_num ; ++index) BEGIN
    READ_ASCII (sale_off +        (index * 0x1c)) item
    PHP_EACH cd_bastard AS itemname => charge BEGIN
      PATCH_IF ("%item%" STRING_COMPARE_CASE "%itemname%" = 0) BEGIN
        WRITE_SHORT (sale_off + 0x0a + (charge * 0x02) + (0x1c * index)) 1 // add charge
      END
    END
  END
  BUT_ONLY
  
// cespenar can use alternate items for upgrades
ACTION_IF FILE_EXISTS_IN_GAME botsmith.dlg THEN BEGIN

  COMPILE ~bg2_tweaks/dlg/cdpnp.d~
  
END

/////                                                  \\\\\
///// no restrictions                                  \\\\\
/////                                                  \\\\\

BEGIN @215100 DESIGNATED 2151 // no restrictions
GROUP @9
SUBCOMPONENT @215000 // wear multiple protection items

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02151.g3~

COPY  ~bg2_tweaks/2da/itemexcl.2da~ ~override~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Altered weapon proficiencies                     \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// Rebalanced weapon proficiencies                  \\\\\
/////                                                  \\\\\

BEGIN @216000 DESIGNATED 2160
GROUP @9
SUBCOMPONENT @216006

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02160.g3~

INCLUDE ~bg2_tweaks/lib/prof_descripts.tpa~  // text replacement macroes

ACTION_IF FILE_EXISTS_IN_GAME ~misc9q.itm~ THEN BEGIN // ToB

  // animation corrections
  COPY_EXISTING ~misc9q.itm~ ~override~
    WRITE_ASCII 0x22 "sc" #2

END

ACTION_IF GAME_IS ~tob bg2ee~ THEN BEGIN // ToB

  COPY_EXISTING ~sw1h66.itm~ ~override~
    WRITE_ASCII 0x22 "ss" #2

  COPY_EXISTING ~sw1h67.itm~ ~override~
    WRITE_ASCII 0x22 "s1" #2

  COPY_EXISTING ~sw1h68a.itm~ ~override~
    WRITE_ASCII 0x22 "sc" #2

END

// since ee uses funky encoding, we'll read in the weapon names dynamically
ACTION_GET_STRREF  6337 mace
ACTION_GET_STRREF  6704 flail_temp
ACTION_GET_STRREF  6745 ssword_temp
ACTION_GET_STRREF  6853 mstar
ACTION_GET_STRREF  7523 katana
ACTION_GET_STRREF  7526 waki
ACTION_GET_STRREF  7529 ninjato
ACTION_GET_STRREF 11052 scimitar

ACTION_IF GAME_IS ~bgee~ BEGIN // ee has different strrefs for eastern stuff
  ACTION_GET_STRREF 26716 waki
  ACTION_GET_STRREF 26711 katana
  ACTION_GET_STRREF 26721 ninjato
END

OUTER_INNER_PATCH_SAVE ssword ~%ssword_temp%~ BEGIN // generic short sword string has trailing space, messing up matching below
  REPLACE_TEXTUALLY ~\(.+\) *$~ ~\1~ // remove trailing space
END

OUTER_INNER_PATCH_SAVE flail ~%flail_temp%~ BEGIN // generic flail string has trailing space, messing up matching below
  REPLACE_TEXTUALLY ~\([^ ]+\) *~ ~\1~ // remove trailing space
END

// since morning star can also be morningstar, we dance a little for the matches
ACTION_IF ("%LANGUAGE%" STRING_COMPARE_CASE "ENGLISH" = 0) BEGIN
  OUTER_SPRINT mstar1 "Morning"
  OUTER_SPRINT mstar2 "star"
END ELSE BEGIN
  ACTION_GET_STRREF  6853 mstar1
  OUTER_SPRINT mstar2 ""
END


//item changes
PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~ // item changes
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
    READ_ASCII 0x22 "anim" (2)
    READ_BYTE  0x31 "prof"
    PATCH_IF ("%prof%" = 100 AND (("%anim%" STRING_COMPARE_CASE "ms" = 0) OR ("%anim%" STRING_COMPARE_CASE "mc" = 0))) BEGIN // morningstars
      SET "prof" = 101
      WRITE_BYTE 0x31 "%prof%" // mace prof
    END ELSE
    PATCH_IF (("%prof%" = 95) AND ("%anim%" STRING_COMPARE_CASE "s1" = 0)) BEGIN // ninja-tos
      SET "prof" = 91
      WRITE_BYTE 0x31 "%prof%" // short sword prof
    END ELSE
    PATCH_IF (("%prof%" = 95) AND ("%anim%" STRING_COMPARE_CASE "ss" = 0)) BEGIN // wakizashis
      SET "prof" = 94
      WRITE_BYTE 0x31 "%prof%" // katana prof
    END
    PATCH_IF ((prof = 91) OR (prof = 94) OR (prof = 95) OR (prof = 100) OR (prof = 101)) BEGIN
      // update descriptions
      PATCH_FOR_EACH offset IN 0x50 0x54 BEGIN
        READ_LONG ~%offset%~ desc_strref
        PATCH_IF (desc_strref >= 0 && desc_strref < 2147483646) BEGIN
          READ_STRREF ~%offset%~ desc
          INNER_PATCH_SAVE desc ~%desc%~ BEGIN
            LAUNCH_PATCH_MACRO ~prof_descripts~
          END
          SAY_EVALUATED ~%offset%~ ~%desc%~ // write changes
        END
      END
    END
  END
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~weapprof.2da~ ~override~
  SET_2DA_ENTRY 11 3 51 ~cdssdescript~
  SET_2DA_ENTRY 14 3 51 ~cdkatdescript~
  SET_2DA_ENTRY 15 3 51 ~cdscimdescript~
  SET_2DA_ENTRY 21 3 51 ~cdfldescript~
  SET_2DA_ENTRY 22 3 51 ~cdmacedescript~
  REPLACE ~cdssdescript~   @216001
  REPLACE ~cdkatdescript~  @216002
  REPLACE ~cdscimdescript~ @216003
  REPLACE ~cdfldescript~   @216004
  REPLACE ~cdmacedescript~ @216005
  BUT_ONLY_IF_IT_CHANGES

// if Maurolava's samurai kit is installed, old version
// only wants katanas and wakizashis; since they're now combined need to remove scimitars for the kit
ACTION_IF FILE_EXISTS_IN_GAME ~samurai.2da~ THEN BEGIN

  // find samurai kit in kitlist
  COPY_EXISTING ~kitlist.2da~ ~override~
    COUNT_2DA_ROWS ~9~ "rows"
    FOR ( index = 31 ; index < rows ; index = index + 1 ) BEGIN
      READ_2DA_ENTRY "%index%" 5 9 "clab"
      PATCH_IF ("%clab%" STRING_COMPARE_CASE "samurai" = 0) BEGIN
        SET "samurai" = "%index%"
        SET "rows" = 0 // kills loop
      END
    END
    BUT_ONLY_IF_IT_CHANGES
    
  COPY_EXISTING ~weapprof.2da~ ~override~
    SET_2DA_ENTRY 14 ("%samurai%" + 22) ("%samurai%" + 23) 1
    BUT_ONLY_IF_IT_CHANGES

END

// if Maurolava's samurai kit is installed, new version
// only wants katanas and wakizashis; since they're now combined need to remove scimitars for the kit
ACTION_IF FILE_EXISTS_IN_GAME ~mlsamura.2da~ THEN BEGIN

  // find samurai kit in kitlist
  COPY_EXISTING ~kitlist.2da~ ~override~
    COUNT_2DA_ROWS ~9~ "rows"
    FOR ( index = 31 ; index < rows ; index = index + 1 ) BEGIN
      READ_2DA_ENTRY "%index%" 5 9 "clab"
      PATCH_IF ("%clab%" STRING_COMPARE_CASE "mlsamura" = 0) BEGIN
        SET "samurai" = "%index%"
        SET "rows" = 0 // kills loop
      END
    END
    BUT_ONLY_IF_IT_CHANGES
    
  COPY_EXISTING ~weapprof.2da~ ~override~
    SET_2DA_ENTRY 14 ("%samurai%" + 22) ("%samurai%" + 23) 1
    BUT_ONLY_IF_IT_CHANGES

END

/////                                                  \\\\\
///// BG Weapon profs with weapon styles               \\\\\
/////                                                  \\\\\

BEGIN @216100 DESIGNATED 2161
GROUP @9
SUBCOMPONENT @216006

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02161.g3~

INCLUDE ~bg2_tweaks/lib/bg_style_profs.tpa~
OUTER_SET "tb#tutu_realloc_style" = 0
LAUNCH_MACRO_ACTION "tb#tutu_weapprof"

/////                                                  \\\\\
///// BG Weapon profs without weapon styles            \\\\\
/////                                                  \\\\\

BEGIN @216200 DESIGNATED 2162
GROUP @9
SUBCOMPONENT @216006

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02162.g3~

INCLUDE ~bg2_tweaks/lib/bg_style_profs.tpa~
OUTER_SET "tb#tutu_realloc_style" = 1
LAUNCH_MACRO_ACTION "tb#tutu_weapprof"

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Cast Spells from Scrolls at Character Level      \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @217000 DESIGNATED 2170
GROUP @9

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02170.g3~

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_LONG  0x64 "abil_off" ELSE 0
  READ_SHORT 0x68 "abil_num" ELSE 0
  READ_LONG  0x6a "fx_off"   ELSE 0
  FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN
    READ_BYTE ("%abil_off%" + (0x38 * "%index%")) "type"
    PATCH_IF ("%type%" = 3) BEGIN // magical
      READ_SHORT ("%abil_off%" + 0x1e + (0x38 * "%index%")) "abil_fx_num"
      READ_SHORT ("%abil_off%" + 0x20 + (0x38 * "%index%")) "abil_fx_idx"
      FOR (index2 = 0 ; index2 < abil_fx_num ; index2 = index2 + 1) BEGIN
        READ_SHORT ("%fx_off%" +        (0x30 * ("%abil_fx_idx%" + "%index2%"))) "opcode"
        PATCH_IF (("%opcode%" = 146) OR ("%opcode%" = 148)) BEGIN // cast spell
          WRITE_LONG ("%fx_off%" + 0x04 + (0x30 * ("%abil_fx_idx%" + "%index2%"))) 0 // cast at character's level
        END
      END
    END
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Un-Nerfed THAC0 Table, Saving Throws, etc.       \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @218000 DESIGNATED 2180
DEPRECATED @18

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// limited storekeeper id ability                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// only mages and bards                             \\\\\
/////                                                  \\\\\

BEGIN @219001 DESIGNATED 2190 // only mages n bards
GROUP @9
SUBCOMPONENT @219000 // limited storekeeper id ability

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02190.g3~

// builds cdstores.2da
INCLUDE ~bg2_tweaks/lib/cdstores.tpa~

  // with cdstores.2da built, use it as a lookup table
COPY_EXISTING ~cdstores.2da~ ~override~
  COUNT_2DA_ROWS ~2~ "rows"
  FOR ( index = 1 ; index < rows ; index = index + 1 ) BEGIN
    READ_2DA_ENTRY "%index%" 0 1 "store"
    READ_2DA_ENTRY "%index%" 1 1 "creature"
    PATCH_IF FILE_EXISTS_IN_GAME ~%store%~ THEN BEGIN

      INNER_PATCH_FILE ~%creature%~ BEGIN
        READ_BYTE 0x273 "class"
        PATCH_IF (("%class%" =   1) OR     // mage
                  ("%class%" =   5) OR     // bard
                  ("%class%" =   7) OR     // fighter-mage
                  ("%class%" =  10) OR     // fighter-mage-thief
                  ("%class%" =  13) OR     // mage-thief
                  ("%class%" =  14) OR     // cleric-mage
                  ("%class%" =  17) OR     // fighter-mage-cleric
                  ("%class%" =  19) OR     // sorcerer
                  ("%class%" = 202) OR     // mage-all
                  ("%class%" = 206)) BEGIN // bard-all
          SET "lore" = 200
        END ELSE BEGIN
          SET "lore" = 0
        END
      END // end inner_patch_file

      INNER_ACTION BEGIN

        COPY_EXISTING ~%store%~ ~override~
          PATCH_IF (SOURCE_SIZE > 0x9a) BEGIN
            WRITE_LONG 0x3c "%lore%" // lore value
          END
          BUT_ONLY_IF_IT_CHANGES

      END
    END // end patch_if
  END
  BUT_ONLY_IF_IT_CHANGES

/////                                                  \\\\\
///// based on shopkeeper                              \\\\\
/////                                                  \\\\\

BEGIN @219100 DESIGNATED 2191 // only mages n bards
GROUP @9
SUBCOMPONENT @219000 // limited storekeeper id ability

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02191.g3~

// builds cdstores.2da
INCLUDE ~bg2_tweaks/lib/cdstores.tpa~

  // with cdstores.2da built, use it as a lookup table
COPY_EXISTING ~cdstores.2da~ ~override~
  COUNT_2DA_ROWS ~2~ "rows"
  FOR (index = 1 ; index < rows ; index = index + 1 ) BEGIN
    READ_2DA_ENTRY "%index%" 0 1 "store"
    READ_2DA_ENTRY "%index%" 1 1 "creature"
    PATCH_IF FILE_EXISTS_IN_GAME ~%store%~ THEN BEGIN

      INNER_PATCH_FILE ~%creature%~ BEGIN
        READ_BYTE 0x234 "level1" // level of primary class
        READ_BYTE 0x235 "level2" // Highest attained level in secondary class (0-100)
        READ_BYTE 0x236 "level3" // Highest attained level in tertiary class (0-100)
        READ_BYTE 0x273 "class"  // corresponds to values in class.ids

        // lore due to class and level
        PATCH_IF ("%class%" = 5) BEGIN // bard
          SET "lore" = (10 * "%level1%")
        END ELSE
        PATCH_IF (("%class%" =  1) OR     // single-class mage
                  ("%class%" =  4) OR     // single-class thief
                  ("%class%" = 19)) BEGIN // single-class sorcerer
          SET "lore" = (3 * "%level1%")
        END ELSE
        PATCH_IF (("%class%" =  7) OR     // multi-dual fighter-thief
                  ("%class%" =  9) OR     // multi-dual fighter-mage
                  ("%class%" = 14) OR     // multi-dual cleric-mage
                  ("%class%" = 15)) BEGIN // multi-dual cleric-thief
          PATCH_IF ((3 * "%level2%") < ("%level1%")) BEGIN
            SET "lore" = "%level1%"
          END ELSE BEGIN
            SET "lore" = (3 * "%level2%")
          END
        END ELSE
        PATCH_IF (("%class%" =  13)) BEGIN // multi-dual mage-thief
          PATCH_IF (("%level2%") < ("%level1%")) BEGIN
            SET "lore" = (3 * "%level1%")
          END ELSE BEGIN
            SET "lore" = (3 * "%level2%")
          END
        END ELSE
        PATCH_IF (("%class%" =  10)) BEGIN // fighter-mage-thief
          PATCH_IF (("%level2%") < ("%level3%")) BEGIN
            SET "lore" = (3 * "%level3%")
          END ELSE BEGIN
            SET "lore" = (3 * "%level2%")
          END
          PATCH_IF ("%lore%" < "%level1%") BEGIN
            SET "lore" = "%level1%"
          END
        END ELSE
        PATCH_IF (("%class%" =  17)) BEGIN // fighter-mage-cleric
          PATCH_IF (("%level1%") < ("%level3%")) BEGIN
            SET "lore" = "%level3%"
          END ELSE BEGIN
            SET "lore" = "%level1%"
          END
          PATCH_IF ("%lore%" < (3 * "%level2%")) BEGIN
            SET "lore" = (3 * "%level2%")
          END
        END ELSE BEGIN // any other single, multi, or dual class non-mage, non-thief, non-bard
          PATCH_IF ("%level1%" > "%level2%") BEGIN
            SET "lore" = "%level1%"
          END ELSE BEGIN
            SET "lore" = "%level2%"
          END
          PATCH_IF ("%lore%" < "%level3%") BEGIN
            SET "lore" = "%level3%"
          END
        END

        SET "lore" = (40 + "%lore%") // occupational bonus--also, just enough for Ribald to ID everything in game

        // assign bonuses for high/low int and wis
        FOR (index2 = 0 ; index2 < 2 ; index2 = index2 + 1) BEGIN
          READ_BYTE (0x23a + "%index2%") "attr" // int, wis on the two loops
          PATCH_IF (("%attr%" > 9) AND ("%attr%" < 15)) BEGIN // kills sequence if of average value
          END ELSE
          PATCH_IF ("%attr%" < 7) BEGIN
            SET "lore" = "%lore%" - 20
          END ELSE
          PATCH_IF (("%attr%" > 6) AND ("%attr%" < 10)) BEGIN
            SET "lore" = "%lore%" - 10
          END ELSE
          PATCH_IF (("%attr%" > 14) AND ("%attr%" < 17)) BEGIN
            SET "lore" = "%lore%" + 5
          END ELSE
          PATCH_IF ("%attr%" = 17) BEGIN
            SET "lore" = "%lore%" + 7
          END ELSE
          PATCH_IF ("%attr%" = 18) BEGIN
            SET "lore" = "%lore%" + 10
          END ELSE
          PATCH_IF ("%attr%" = 19) BEGIN
            SET "lore" = "%lore%" + 12
          END ELSE
          PATCH_IF ("%attr%" = 20) BEGIN
            SET "lore" = "%lore%" + 15
          END ELSE
          PATCH_IF ("%attr%" = 21) BEGIN
            SET "lore" = "%lore%" + 20
          END ELSE
          PATCH_IF ("%attr%" = 22) BEGIN
            SET "lore" = "%lore%" + 25
          END ELSE
          PATCH_IF ("%attr%" = 23) BEGIN
            SET "lore" = "%lore%" + 30
          END ELSE
          PATCH_IF ("%attr%" = 24) BEGIN
            SET "lore" = "%lore%" + 35
          END ELSE
          PATCH_IF ("%attr%" = 25) BEGIN
            SET "lore" = "%lore%" + 40
          END
        END

        // enforce min-max limits of 0, 200
        PATCH_IF ("%lore%" < 0) BEGIN
          SET "lore" = 0
        END ELSE
        PATCH_IF ("%lore%" > 200) BEGIN
          SET "lore" = 200
        END
      END // end inner_patch_file

      INNER_ACTION BEGIN

        COPY_EXISTING ~%store%~ ~override~
          READ_LONG 0x3c "old_lore" ELSE 0
          PATCH_IF ("%old_lore%" > 0) BEGIN
            WRITE_LONG 0x3c "%lore%" // lore value
          END
          BUT_ONLY_IF_IT_CHANGES

      END
    END
  END
  BUT_ONLY_IF_IT_CHANGES

/////                                                  \\\\\
///// hybrid approach                                  \\\\\
/////                                                  \\\\\

BEGIN @219200 DESIGNATED 2192 // only mages n bards
GROUP @9
SUBCOMPONENT @219000 // limited storekeeper id ability

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02192.g3~

// builds cdstores.2da
INCLUDE ~bg2_tweaks/lib/cdstores.tpa~

  // with cdstores.2da built, use it as a lookup table
COPY_EXISTING ~cdstores.2da~ ~override~
  COUNT_2DA_ROWS ~2~ "rows"
  FOR (index = 1 ; index < rows ; index = index + 1 ) BEGIN
    READ_2DA_ENTRY "%index%" 0 1 "store"
    READ_2DA_ENTRY "%index%" 1 1 "creature"
    PATCH_IF FILE_EXISTS_IN_GAME ~%store%~ THEN BEGIN

      INNER_PATCH_FILE ~%creature%~ BEGIN
        READ_BYTE 0x273 "class"  // corresponds to values in class.ids
        PATCH_IF (("%class%" =   1) OR     // mage
                  ("%class%" =   5) OR     // bard
                  ("%class%" =   7) OR     // fighter-mage
                  ("%class%" =  10) OR     // fighter-mage-thief
                  ("%class%" =  13) OR     // mage-thief
                  ("%class%" =  14) OR     // cleric-mage
                  ("%class%" =  17) OR     // fighter-mage-cleric
                  ("%class%" =  19) OR     // sorcerer
                  ("%class%" = 202) OR     // mage-all
                  ("%class%" = 206)) BEGIN // bard-all
          SET "lore" = 200
        END ELSE BEGIN
          READ_BYTE 0x234 "level1"  // level of primary class
          READ_BYTE 0x235 "level2" // Highest attained level in secondary class (0-100)
          READ_BYTE 0x236 "level3" // Highest attained level in tertiary class (0-100)
          PATCH_IF ("%class%" =  4) BEGIN // single-class thief
            SET "lore" = (3 * "%level1%")
          END ELSE
          PATCH_IF (("%class%" =  9) OR     // multi-dual fighter-thief
                    ("%class%" = 15)) BEGIN // multi-dual cleric-thief
            PATCH_IF ((3 * "%level2%") < ("%level1%")) BEGIN
              SET "lore" = "%level1%"
            END ELSE BEGIN
              SET "lore" = (3 * "%level2%")
            END
          END ELSE BEGIN // any other single, multi, or dual class non-mage, non-thief, non-bard
            PATCH_IF ("%level1%" > "%level2%") BEGIN
              SET "lore" = "%level1%"
            END ELSE BEGIN
              SET "lore" = "%level2%"
            END
            PATCH_IF ("%lore%" < "%level3%") BEGIN
              SET "lore" = "%level3%"
            END
          END

          SET "lore" = (40 + "%lore%") // occupational bonus--also, just enough for Ribald to ID everything in game

          // assign bonuses for high/low int and wis
          FOR (index2 = 0 ; index2 < 2 ; index2 = index2 + 1) BEGIN
            READ_BYTE (0x23a + "%index2%") "attr" // int, wis on the two loops
            PATCH_IF (("%attr%" > 9) AND ("%attr%" < 15)) BEGIN // kills sequence if of average value
            END ELSE
            PATCH_IF ("%attr%" < 7) BEGIN
              SET "lore" = "%lore%" - 20
            END ELSE
            PATCH_IF (("%attr%" > 6) AND ("%attr%" < 10)) BEGIN
              SET "lore" = "%lore%" - 10
            END ELSE
            PATCH_IF (("%attr%" > 14) AND ("%attr%" < 17)) BEGIN
              SET "lore" = "%lore%" + 5
            END ELSE
            PATCH_IF ("%attr%" = 17) BEGIN
              SET "lore" = "%lore%" + 7
            END ELSE
            PATCH_IF ("%attr%" = 18) BEGIN
              SET "lore" = "%lore%" + 10
            END ELSE
            PATCH_IF ("%attr%" = 19) BEGIN
              SET "lore" = "%lore%" + 12
            END ELSE
            PATCH_IF ("%attr%" = 20) BEGIN
              SET "lore" = "%lore%" + 15
            END ELSE
            PATCH_IF ("%attr%" = 21) BEGIN
              SET "lore" = "%lore%" + 20
            END ELSE
            PATCH_IF ("%attr%" = 22) BEGIN
              SET "lore" = "%lore%" + 25
            END ELSE
            PATCH_IF ("%attr%" = 23) BEGIN
              SET "lore" = "%lore%" + 30
            END ELSE
            PATCH_IF ("%attr%" = 24) BEGIN
              SET "lore" = "%lore%" + 35
            END ELSE
            PATCH_IF ("%attr%" > 24) BEGIN
              SET "lore" = "%lore%" + 40
            END
          END

          // enforce min-max limits of 0, 200
          PATCH_IF ("%lore%" < 0) BEGIN
            SET "lore" = 0
          END ELSE
          PATCH_IF ("%lore%" > 200) BEGIN
            SET "lore" = 200
          END
        END
      END // end inner_patch_file

      INNER_ACTION BEGIN

        COPY_EXISTING ~%store%~ ~override~
          READ_LONG 0x3c "old_lore" ELSE 0
          PATCH_IF ("%old_lore%" > 0) BEGIN
            WRITE_LONG 0x3c "%lore%" // lore value
          END
          BUT_ONLY_IF_IT_CHANGES

      END
    END
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Multi-Class Grand Mastery                        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @220000 DESIGNATED 2200
GROUP @9

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02200.g3~

PRINT @1
COPY_EXISTING ~weapprof.2da~  ~override~
  FOR (column = 12; column < 21; column = column + 1) BEGIN
    FOR (row = 11; row < 31; row = row + 1) BEGIN
      READ_2DA_ENTRY row column 1 stars
      PATCH_IF (stars > 1) BEGIN
        SET_2DA_ENTRY_LATER ~multigrand~ row column ~5~
      END
    END
  END
  SET_2DA_ENTRIES_NOW ~multigrand~ 1
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// True Grand Mastery                               \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @221000 DESIGNATED 2210
GROUP @9

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02210.g3~

COPY ~bg2_tweaks/2da/wspatck.2da~  ~override~
     ~bg2_tweaks/2da/wspecial.2da~ ~override~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Change Magically Created Weapons to Zero Weight  \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @222000 DESIGNATED 2220
GROUP @9

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02220.g3~

SPRINT weight_temp @212001 // use existing weight string from arcane magic in heavy armor, but...
OUTER_INNER_PATCH_SAVE weight ~%weight_temp%~ BEGIN
  REPLACE_TEXTUALLY ~\[0-9\]\+~ ~~ // we'll remove actual numerical weight from match
END

ACTION_CLEAR_ARRAY  cd_magical_items
ACTION_DEFINE_ARRAY cd_magical_items BEGIN END

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
                          ~^.+\.spl$~ ~override~
  PATCH_IF ("%SOURCE_FILE%" STRING_COMPARE_REGEXP "^.+\.spl$" = 0) BEGIN
    SET "abil_length" = 0x28
  END ELSE BEGIN
    SET "abil_length" = 0x38
  END
  READ_LONG  0x64 abil_off ELSE 0
  READ_SHORT 0x68 abil_num ELSE 0
  READ_LONG  0x6a fx_off   ELSE 0
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    READ_SHORT  (abil_off + 0x1e + (abil_length * index)) abil_fx_num
    READ_SHORT  (abil_off + 0x20 + (abil_length * index)) abil_fx_idx
    FOR (index2 = 0 ; index2 < abil_fx_num ; ++index2) BEGIN
      READ_SHORT (fx_off +        (0x30 * (abil_fx_idx + index2))) opcode
      PATCH_IF ((opcode = 111) OR (opcode = 255)) BEGIN // create magic item
        READ_ASCII (fx_off + 0x14 + (0x30 * (abil_fx_idx + index2))) item
        DEFINE_ASSOCIATIVE_ARRAY cd_magical_items BEGIN "%item%" => 0 END
      END
    END
  END
  BUT_ONLY

ACTION_PHP_EACH cd_magical_items AS item => foo BEGIN
      
  ACTION_IF FILE_EXISTS_IN_GAME ~%item%.itm~ THEN BEGIN

    COPY_EXISTING ~%item%.itm~ ~override~
      READ_LONG 0x4c weight_itm
      PATCH_IF (weight_itm != 0) BEGIN // don't bother if not non-zero
        WRITE_LONG 0x4c 0        
        // adjust descriptions
        PATCH_FOR_EACH offset IN 0x50 0x54 BEGIN
          READ_LONG ~%offset%~ desc_strref
          PATCH_IF (desc_strref >= 0 && desc_strref < 2147483646) BEGIN
            READ_STRREF ~%offset%~ desc
            INNER_PATCH_SAVE desc ~%desc%~ BEGIN
              REPLACE_TEXTUALLY ~%weight%[0-9]+~  ~\10~
            END
            SAY_EVALUATED ~%offset%~ ~%desc%~ // write changes
          END
        END
      END  
      BUT_ONLY

  END

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Make +x/+y weapons consistent                              \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @223000 DESIGNATED 2230
GROUP @9
REQUIRE_PREDICATE GAME_IS ~soa tob bgt~ @25
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~cdbehbla.pro~ @16
FORBID_COMPONENT ~item_rev/item_rev.tp2~ ~0~ @16

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02230.g3~
  
// get enchantments from lookup table
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_enchantment BEGIN

  blun10.itm => 3 // Root of the Problem +1/+3 vs unnatural
  blun23.itm => 3 // Bone Club +2/+3 vs undead
  hamm04.itm => 4 // hammer +1/+4 vs giantkin
  sw1h03.itm => 3 // bastard sword +1/+3 vs shapeshifters
  sw1h31.itm => 4 // daystar
  sw1h54.itm => 3 // Equalizer
  wamace.itm => 5 // jerrod's mace

END

ACTION_PHP_EACH cd_enchantment AS item => ench BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%item%.itm~ THEN BEGIN

    COPY_EXISTING ~%item%.itm~ ~override~ // daystar
      WRITE_LONG 0x60 ench
      BUT_ONLY

  END

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Description updates for consistent +x/+y weapons           \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @223100 DESIGNATED 2231
GROUP @9
REQUIRE_COMPONENT ~bg2_tweaks/setup-bg2_tweaks.tp2~ ~2230~ @17
REQUIRE_PREDICATE (("%LANGUAGE%" STRING_COMPARE_CASE "chinese" = 0) OR
                   ("%LANGUAGE%" STRING_COMPARE_CASE "english" = 0) OR
                   ("%LANGUAGE%" STRING_COMPARE_CASE "french" = 0) OR
                   ("%LANGUAGE%" STRING_COMPARE_CASE "german" = 0) OR 
                   ("%LANGUAGE%" STRING_COMPARE_CASE "italian" = 0) OR 
                   ("%LANGUAGE%" STRING_COMPARE_CASE "spanish" = 0)) @7 // chinese/english/french/german/italian/spanish only

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02231.g3~

ACTION_IF FILE_EXISTS_IN_GAME ~bola01.itm~ THEN BEGIN // if AoE installed

  ACTION_IF FILE_EXISTS_IN_GAME ~blun10.itm~ BEGIN
    COPY_EXISTING ~blun10.itm~ ~override~ // Root of the Problem +1/+3 vs unnatural
      SAY IDENTIFIED_DESC @99057
      BUT_ONLY_IF_IT_CHANGES
  END

  ACTION_IF FILE_EXISTS_IN_GAME ~blun23.itm~ BEGIN
    COPY_EXISTING ~blun23.itm~ ~override~ // Bone Club +2/+3 vs undead
      SAY IDENTIFIED_DESC @99058
      BUT_ONLY_IF_IT_CHANGES
  END

  ACTION_IF FILE_EXISTS_IN_GAME ~hamm04.itm~ BEGIN
    COPY_EXISTING ~hamm04.itm~ ~override~ // Hammer +1/+4 vs Giantkin
      SAY IDENTIFIED_DESC @99059
      BUT_ONLY_IF_IT_CHANGES
  END

  ACTION_IF FILE_EXISTS_IN_GAME ~sw1h03.itm~ BEGIN
    COPY_EXISTING ~sw1h03.itm~ ~override~ // bastard sword +1/+3 vs shapeshifters
      SAY IDENTIFIED_DESC @99060
      BUT_ONLY_IF_IT_CHANGES
  END

  ACTION_IF FILE_EXISTS_IN_GAME ~sw1h31.itm~ BEGIN
    COPY_EXISTING ~sw1h31.itm~ ~override~ // Daystar
      SAY IDENTIFIED_DESC @99061
      BUT_ONLY_IF_IT_CHANGES
  END

  ACTION_IF FILE_EXISTS_IN_GAME ~sw1h54.itm~ BEGIN
    COPY_EXISTING ~sw1h54.itm~ ~override~ // Equalizer
      SAY IDENTIFIED_DESC @99062
      BUT_ONLY_IF_IT_CHANGES
  END

END ELSE BEGIN // if no AoE

  ACTION_IF FILE_EXISTS ~override/cdt02040.g3~ THEN BEGIN // if universal clubs

    ACTION_IF FILE_EXISTS_IN_GAME ~blun10.itm~ BEGIN
      COPY_EXISTING ~blun10.itm~ ~override~ // Root of the Problem +1/+3 vs unnatural
        SAY IDENTIFIED_DESC @99063
        BUT_ONLY_IF_IT_CHANGES
    END

    ACTION_IF FILE_EXISTS_IN_GAME ~blun23.itm~ BEGIN
      COPY_EXISTING ~blun23.itm~ ~override~ // Bone Club +2/+3 vs undead
        SAY IDENTIFIED_DESC @99064
        BUT_ONLY_IF_IT_CHANGES
    END

  END ELSE BEGIN // if

    ACTION_IF FILE_EXISTS_IN_GAME ~blun10.itm~ BEGIN
      COPY_EXISTING ~blun10.itm~ ~override~ // Root of the Problem +1/+3 vs unnatural
        SAY IDENTIFIED_DESC @99051
        BUT_ONLY_IF_IT_CHANGES
    END

    ACTION_IF FILE_EXISTS_IN_GAME ~blun23.itm~ BEGIN
      COPY_EXISTING ~blun23.itm~ ~override~ // Bone Club +2/+3 vs undead
        SAY IDENTIFIED_DESC @99052
        BUT_ONLY_IF_IT_CHANGES
    END

  END

  ACTION_IF FILE_EXISTS_IN_GAME ~hamm04.itm~ BEGIN
    COPY_EXISTING ~hamm04.itm~ ~override~ // Hammer +1/+4 vs Giantkin
      SAY IDENTIFIED_DESC @99053
      BUT_ONLY_IF_IT_CHANGES
  END

  ACTION_IF FILE_EXISTS_IN_GAME ~sw1h03.itm~ BEGIN
    COPY_EXISTING ~sw1h03.itm~ ~override~ // bastard sword +1/+3 vs shapeshifters
      SAY IDENTIFIED_DESC @99054
      BUT_ONLY_IF_IT_CHANGES
  END

  ACTION_IF FILE_EXISTS_IN_GAME ~sw1h31.itm~ BEGIN
    COPY_EXISTING ~sw1h31.itm~ ~override~ // Daystar
      SAY IDENTIFIED_DESC @99055
      BUT_ONLY_IF_IT_CHANGES
  END

  ACTION_IF FILE_EXISTS_IN_GAME ~sw1h54.itm~ BEGIN
    COPY_EXISTING ~sw1h54.itm~ ~override~ // Equalizer
      SAY IDENTIFIED_DESC @99056
      BUT_ONLY_IF_IT_CHANGES
  END

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Alter THAC0 Table                                          \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @224000 DESIGNATED 2240
GROUP @9
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~bpduearc.spl~ @16      // non-BP
REQUIRE_PREDICATE NOT FILE_EXISTS         ~data/rot-rule.bif~ @16 // non-RoT
REQUIRE_PREDICATE NOT FILE_EXISTS         ~data/tdd-rule.bif~ @16 // non-TDD

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02240.g3~

COPY ~bg2_tweaks/2da/thac0.2da~ ~override~

/////                                                            \\\\\
///// capped at -10                                              \\\\\
/////                                                            \\\\\

BEGIN @224100 DESIGNATED 2241
DEPRECATED @18

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Alter sorcerer spell progression                           \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @225000 DESIGNATED 2250
GROUP @9
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~bpduearc.spl~ @16      // non-BP
REQUIRE_PREDICATE NOT FILE_EXISTS         ~data/rot-rule.bif~ @16 // non-RoT
REQUIRE_PREDICATE NOT FILE_EXISTS         ~data/tdd-rule.bif~ @16 // non-TDD

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02250.g3~

COPY ~bg2_tweaks/2da/un_mxsplsrc.2da~ ~override/mxsplsrc.2da~

ACTION_IF FILE_EXISTS_IN_GAME mxspldd.2da THEN BEGIN

  COPY_EXISTING ~mxsplsrc.2da~ ~override/mxspldd.2da~
    FOR (row = 0 ; row < 50 ; ++row) BEGIN
      FOR (col = 1 ; col < 10 ; ++col) BEGIN
        READ_2DA_ENTRY row col 10 spells
        PATCH_IF (spells > 2) BEGIN
          SET_2DA_ENTRY row col 10 (spells - 2)
        END
      END
    END

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Alter Mage Spell Progression Table                         \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                            \\\\\
///// un-nerfed                                                  \\\\\
/////                                                            \\\\\

BEGIN @226001 DESIGNATED 2260
GROUP @9
SUBCOMPONENT @226000 // alter mage progression
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~bpduearc.spl~ @16      // non-BP
REQUIRE_PREDICATE NOT FILE_EXISTS         ~data/rot-rule.bif~ @16 // non-RoT
REQUIRE_PREDICATE NOT FILE_EXISTS         ~data/tdd-rule.bif~ @16 // non-TDD

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02260.g3~

COPY ~bg2_tweaks/2da/un_mxsplwiz.2da~ ~override/mxsplwiz.2da~

ACTION_IF GAME_IS ~tob bgt bg2ee~ THEN BEGIN

  // add mage alchemy, scribe scrolls
  COPY_EXISTING ~lucm0.2da~ ~override~
                ~luma0.2da~ ~override~
    COUNT_2DA_ROWS ~9~ "rows"
    SET "patch" = 0
    FOR ( index = 1 ; index < rows ; index = index + 1 ) BEGIN
      READ_2DA_ENTRY "%index%" 1 9 "abil"
      PATCH_IF ("%abil%" STRING_COMPARE_CASE "*" = 0) BEGIN
        PATCH_IF ("%patch%" = 0) BEGIN
          SET_2DA_ENTRY "%index%" 1 9 ~GA_GBSCRBMG~
          SET_2DA_ENTRY "%index%" 4 9 ~1~
          SET_2DA_ENTRY "%index%" 5 9 ~99~
          SET_2DA_ENTRY "%index%" 6 9 ~99~
          SET "patch" = 1
        END ELSE BEGIN
          SET_2DA_ENTRY "%index%" 1 9 ~GA_GBALCHMG~
          SET_2DA_ENTRY "%index%" 4 9 ~1~
          SET_2DA_ENTRY "%index%" 5 9 ~99~
          SET_2DA_ENTRY "%index%" 6 9 ~99~
          SET "index" = "%rows%" // kills loop
        END
      END
    END
    BUT_ONLY_IF_IT_CHANGES

  COPY ~bg2_tweaks/spl/gbalchmg.spl~ ~override~
    SAY 0x08 @218001
    SAY 0x50 @218004

  COPY ~bg2_tweaks/spl/gbscrbmg.spl~ ~override~
    SAY 0x08 @218007
    SAY 0x50 @218010

  ACTION_IF NOT FILE_EXISTS_IN_GAME ~cdt02280.g3~ THEN BEGIN // if un-nerfed cleric progression hasn't already made this change...

    COPY_EXISTING ~spcl918.spl~ ~override~
      SAY        0x08 @218003
      SAY        0x50 @218006
      READ_LONG  0x64 "abil_off"
      READ_SHORT 0x68 "abil_num"
      READ_LONG  0x6a "fx_off"
      FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN
        READ_SHORT ("%abil_off%" + 0x1e + (0x28 * "%index%")) "abil_fx_num"
        READ_SHORT ("%abil_off%" + 0x20 + (0x28 * "%index%")) "abil_fx_idx"
        FOR (index2 = 0 ; index2 < abil_fx_num ; index2 = index2 + 1) BEGIN
          READ_SHORT ("%fx_off%" +        (0x30 * ("%abil_fx_idx%" + "%index2%"))) "opcode"
          PATCH_IF ("%opcode%" = 122) BEGIN // create item
            READ_ASCII ("%fx_off%" + 0x14 + (0x30 * ("%abil_fx_idx%" + "%index2%"))) "item"
            PATCH_IF ("%item%" STRING_COMPARE_CASE "potn52" = 0) BEGIN // potion of extra healing
              WRITE_ASCII ("%fx_off%" + 0x14 + (0x30 * ("%abil_fx_idx%" + "%index2%"))) ~potn10~ #8 // potion of invis
            END ELSE
            PATCH_IF ("%item%" STRING_COMPARE_CASE "potn36" = 0) BEGIN // potion of master thieving
              WRITE_ASCII ("%fx_off%" + 0x14 + (0x30 * ("%abil_fx_idx%" + "%index2%"))) ~potn37~ #8 // potion of mind focusing
            END
          END
        END
      END
      BUT_ONLY_IF_IT_CHANGES

    COPY_EXISTING ~spcl919.spl~ ~override~
      SAY 0x008 @218009
      SAY 0x050 @218012

  END

END

/////                                                            \\\\\
///// pnp                                                        \\\\\
/////                                                            \\\\\

BEGIN @226100 DESIGNATED 2261
GROUP @9
SUBCOMPONENT @226000 // alter mage progression

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02261.g3~

COPY ~bg2_tweaks/2da/mxsplwiz.2da~ ~override~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Alter bard Spell Progression Table                         \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                            \\\\\
///// un-nerfed                                                  \\\\\
/////                                                            \\\\\

BEGIN @226001 DESIGNATED 2270
GROUP @9
SUBCOMPONENT @227000 // alter bard progression
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~bpduearc.spl~ @16      // non-BP
REQUIRE_PREDICATE NOT FILE_EXISTS         ~data/rot-rule.bif~ @16 // non-RoT
REQUIRE_PREDICATE NOT FILE_EXISTS         ~data/tdd-rule.bif~ @16 // non-TDD

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02270.g3~

COPY ~bg2_tweaks/2da/un_mxsplbrd.2da~ ~override/mxsplbrd.2da~

/////                                                            \\\\\
///// pnp                                                        \\\\\
/////                                                            \\\\\

BEGIN @226100 DESIGNATED 2271
GROUP @9
SUBCOMPONENT @227000 // alter bard progression

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02271.g3~

COPY ~bg2_tweaks/2da/mxsplbrd.2da~ ~override~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Alter cleric Spell Progression Table                       \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                            \\\\\
///// un-nerfed                                                  \\\\\
/////                                                            \\\\\

BEGIN @226001 DESIGNATED 2280
GROUP @9
SUBCOMPONENT @228000 // alter cleric progression
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~bpduearc.spl~ @16      // non-BP
REQUIRE_PREDICATE NOT FILE_EXISTS         ~data/rot-rule.bif~ @16 // non-RoT
REQUIRE_PREDICATE NOT FILE_EXISTS         ~data/tdd-rule.bif~ @16 // non-TDD

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02280.g3~

COPY ~bg2_tweaks/2da/un_mxsplprs.2da~ ~override/mxsplprs.2da~

ACTION_IF GAME_IS ~tob bgt bg2ee~ THEN BEGIN

  // add clerical alchemy, scribe scrolls
  COPY_EXISTING ~lucl0.2da~ ~override~
                ~lucm0.2da~ ~override~
    COUNT_2DA_ROWS ~9~ "rows"
    SET "patch" = 0
    FOR ( index = 1 ; index < rows ; index = index + 1 ) BEGIN
      READ_2DA_ENTRY "%index%" 1 9 "abil"
      PATCH_IF ("%abil%" STRING_COMPARE_CASE "*" = 0) BEGIN
        PATCH_IF ("%patch%" = 0) BEGIN
          SET_2DA_ENTRY "%index%" 1 9 ~GA_GBSCRBPR~
          SET_2DA_ENTRY "%index%" 4 9 ~1~
          SET_2DA_ENTRY "%index%" 5 9 ~99~
          SET_2DA_ENTRY "%index%" 6 9 ~99~
          SET "patch" = 1
        END ELSE BEGIN
          SET_2DA_ENTRY "%index%" 1 9 ~GA_GBALCHPR~
          SET_2DA_ENTRY "%index%" 4 9 ~1~
          SET_2DA_ENTRY "%index%" 5 9 ~99~
          SET_2DA_ENTRY "%index%" 6 9 ~99~
          SET "index" = "%rows%" // kills loop
        END
      END
    END
    BUT_ONLY_IF_IT_CHANGES

  COPY ~bg2_tweaks/spl/gbalchpr.spl~ ~override~
    SAY 0x08 @218002
    SAY 0x50 @218005

  COPY ~bg2_tweaks/spl/gbscrbpr.spl~ ~override~
    SAY 0x08 @218008
    SAY 0x50 @218011

  ACTION_IF NOT FILE_EXISTS_IN_GAME ~cdt02260.g3~ THEN BEGIN // if un-nerfed mage progression hasn't already made this change...

    COPY_EXISTING ~spcl918.spl~ ~override~
      SAY        0x08 @218003
      SAY        0x50 @218006
      READ_LONG  0x64 "abil_off"
      READ_SHORT 0x68 "abil_num"
      READ_LONG  0x6a "fx_off"
      FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN
        READ_SHORT ("%abil_off%" + 0x1e + (0x28 * "%index%")) "abil_fx_num"
        READ_SHORT ("%abil_off%" + 0x20 + (0x28 * "%index%")) "abil_fx_idx"
        FOR (index2 = 0 ; index2 < abil_fx_num ; index2 = index2 + 1) BEGIN
          READ_SHORT ("%fx_off%" +        (0x30 * ("%abil_fx_idx%" + "%index2%"))) "opcode"
          PATCH_IF ("%opcode%" = 122) BEGIN // create item
            READ_ASCII ("%fx_off%" + 0x14 + (0x30 * ("%abil_fx_idx%" + "%index2%"))) "item"
            PATCH_IF ("%item%" STRING_COMPARE_CASE "potn52" = 0) BEGIN // potion of extra healing
              WRITE_ASCII ("%fx_off%" + 0x14 + (0x30 * ("%abil_fx_idx%" + "%index2%"))) ~potn10~ #8 // potion of invis
            END ELSE
            PATCH_IF ("%item%" STRING_COMPARE_CASE "potn36" = 0) BEGIN // potion of master thieving
              WRITE_ASCII ("%fx_off%" + 0x14 + (0x30 * ("%abil_fx_idx%" + "%index2%"))) ~potn37~ #8 // potion of mind focusing
            END
          END
        END
      END
      BUT_ONLY_IF_IT_CHANGES

    COPY_EXISTING ~spcl919.spl~ ~override~
      SAY 0x008 @218009
      SAY 0x050 @218012

  END

END

/////                                                            \\\\\
///// pnp                                                        \\\\\
/////                                                            \\\\\

BEGIN @226100 DESIGNATED 2281
GROUP @9
SUBCOMPONENT @228000 // alter cleric progression

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02281.g3~

COPY ~bg2_tweaks/2da/mxsplprs.2da~ ~override~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Alter druid Spell/level Progression Table                  \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                            \\\\\
///// druid level, un-nerfed druid spells                        \\\\\
/////                                                            \\\\\

BEGIN @229001 DESIGNATED 2290
GROUP @9
SUBCOMPONENT @229000 // alter druid progression
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~bpduearc.spl~ @16      // non-BP
REQUIRE_PREDICATE NOT FILE_EXISTS         ~data/rot-rule.bif~ @16 // non-RoT
REQUIRE_PREDICATE NOT FILE_EXISTS         ~data/tdd-rule.bif~ @16 // non-TDD

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02290.g3~

COPY ~bg2_tweaks/2da/un_mxspldru.2da~ ~override/mxspldru.2da~

/////                                                            \\\\\
///// druid level, pnp cleric/druid spells                       \\\\\
/////                                                            \\\\\

BEGIN @229100 DESIGNATED 2291
GROUP @9
SUBCOMPONENT @229000 // alter druid progression

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02291.g3~

COPY ~bg2_tweaks/2da/mxsplprs.2da~ ~override/mxspldru.2da~

/////                                                            \\\\\
///// cleric level, normal druid spells                          \\\\\
/////                                                            \\\\\

BEGIN @229200 DESIGNATED 2292
GROUP @9
SUBCOMPONENT @229000 // alter druid progression


// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02292.g3~

// use cleric level progression
INCLUDE ~bg2_tweaks/lib/druid_2_cleric_levels.tpa~

/////                                                            \\\\\
///// cleric level, un-nerfed druid spells                       \\\\\
/////                                                            \\\\\

BEGIN @229300 DESIGNATED 2293
GROUP @9
SUBCOMPONENT @229000 // alter druid progression


// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02293.g3~

COPY ~bg2_tweaks/2da/un_mxspldru.2da~ ~override/mxspldru.2da~

// use cleric level progression
INCLUDE ~bg2_tweaks/lib/druid_2_cleric_levels.tpa~

/////                                                            \\\\\
///// cleric level, pnp cleric/druid spells                      \\\\\
/////                                                            \\\\\

BEGIN @229400 DESIGNATED 2294
GROUP @9
SUBCOMPONENT @229000 // alter druid progression


// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02294.g3~

COPY ~bg2_tweaks/2da/mxsplprs.2da~ ~override/mxspldru.2da~

// use cleric level progression
INCLUDE ~bg2_tweaks/lib/druid_2_cleric_levels.tpa~

/////                                                            \\\\\
///// cleric level, normal cleic spells                          \\\\\
/////                                                            \\\\\

BEGIN @229500 DESIGNATED 2295
GROUP @9
SUBCOMPONENT @229000 // alter druid progression


// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02295.g3~

COPY_EXISTING ~mxsplprs.2da~ ~override/mxspldru.2da~

// use cleric level progression
INCLUDE ~bg2_tweaks/lib/druid_2_cleric_levels.tpa~

/////                                                            \\\\\
///// cleric level, un-nerfed cleric spells                      \\\\\
/////                                                            \\\\\

BEGIN @229600 DESIGNATED 2296
GROUP @9
SUBCOMPONENT @229000 // alter druid progression


// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02296.g3~

COPY ~bg2_tweaks/2da/un_mxsplprs.2da~ ~override/mxspldru.2da~

// use cleric level progression
INCLUDE ~bg2_tweaks/lib/druid_2_cleric_levels.tpa~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// triple-class HLA tables                           \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @230000 DESIGNATED 2300
GROUP @9
REQUIRE_PREDICATE     FILE_EXISTS_IN_GAME ~cdt02090.g3~ @21 // xp cap must be removed
REQUIRE_PREDICATE     GAME_IS ~tob bgt bg2ee~   @25 // ToB required

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02300.g3~

// gives f/m/c and f/m/t unique HLAs; tables created below
COPY_EXISTING ~LUABBR.2DA~ ~override~
  SET_2DA_ENTRY 14 1 1 ~FMT~
  SET_2DA_ENTRY 19 1 1 ~FMC~
  BUT_ONLY

ACTION_IF (NOT FILE_EXISTS_IN_GAME ~lufmc.2da~) BEGIN

  COPY ~bg2_tweaks/2da/lufmc.2da~ ~override~

END ELSE BEGIN

  COPY_EXISTING ~lufmc.2da~ ~override~
    READ_2DA_ENTRIES_NOW ~r2en_lufmc~ 10
    SET planetar = 0
    SET dark_planetar = 0
    SET improved_alacrity = 0
    SET dragons_breath = 0
    SET comet = 0
    FOR (i = 0; i < r2en_lufmc; i += 1) BEGIN // for each row
      READ_2DA_ENTRY_FORMER ~r2en_lufmc~ i 1 old_ability
      SPRINT new_ability ~~
      PATCH_IF (~%old_ability%~ STRING_EQUAL_CASE ~GA_SPCL902~) BEGIN // deathblow
        PATCH_IF (planetar == 0) BEGIN
          SPRINT new_ability ~GA_SPWI923~ // summon planetar
          SET planetar = 1
        END
      END ELSE
      PATCH_IF (~%old_ability%~ STRING_EQUAL_CASE ~GA_SPCL903~) BEGIN // greater deathblow
        PATCH_IF (dark_planetar == 0) BEGIN
          SPRINT new_ability ~GA_SPWI924~ // summon dark planetar
          SET dark_planetar = 1
        END
      END ELSE
      PATCH_IF (~%old_ability%~ STRING_EQUAL_CASE ~GA_SPCL908~) BEGIN // war cry
        PATCH_IF (improved_alacrity == 0) BEGIN
          SPRINT new_ability ~GA_SPWI921~ // improved alacrity
          SET improved_alacrity = 1
        END
      END ELSE
      PATCH_IF (~%old_ability%~ STRING_EQUAL_CASE ~*~) BEGIN // unused
        PATCH_IF (dragons_breath == 0) BEGIN
          SPRINT new_ability ~GA_SPWI922~ // dragon's breath
          SET dragons_breath = 1
        END ELSE
        PATCH_IF (comet == 0) BEGIN
          SPRINT new_ability ~GA_SPWI925~ // comet
          SET comet = 1
        END
      END
      PATCH_IF (STRING_LENGTH ~%new_ability%~ > 0) BEGIN // ability to be replaced here
        SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 1 ~%new_ability%~ // ability
        SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 2 ~*~ // icon
        SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 3 ~*~ // strref
        SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 4 ~32~ // min_level
        SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 5 ~99~ // max_level
        SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 6 ~1~ // num_allowed
        SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 7 ~*~ // prerequisite
        PATCH_IF (~%new_ability%~ STRING_EQUAL_CASE ~GA_SPWI923~) BEGIN // summon planetar restrictions
          SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 8 ~GA_SPWI924~ // excluded_by
          SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 9 ~ALL_EVIL~ // alignment_restrict
        END ELSE
        PATCH_IF (~%new_ability%~ STRING_EQUAL_CASE ~GA_SPWI924~) BEGIN // summon dark planetar restrictions
          SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 8 ~GA_SPWI923~ // excluded_by
          SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 9 ~ALL_GOOD~ // alignment_restrict
        END ELSE BEGIN // restrictions for others
          SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 8 ~*~ // excluded_by
          SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 9 ~*~ // alignment_restrict
        END
      END
    END
    SET_2DA_ENTRIES_NOW ~s2el_lufmc~ 10 // actually write changes!
    PRETTY_PRINT_2DA

END

ACTION_IF (NOT FILE_EXISTS_IN_GAME ~lufmt.2da~) BEGIN

  COPY ~bg2_tweaks/2da/lufmt.2da~ ~override~

END ELSE BEGIN

  COPY_EXISTING ~lufmt.2da~ ~override~
    READ_2DA_ENTRIES_NOW ~r2en_lufmt~ 10
    SET planetar = 0
    SET dark_planetar = 0
    SET improved_alacrity = 0
    SET dragons_breath = 0
    SET energy_blades = 0
    SET comet = 0
    FOR (i = 0; i < r2en_lufmt; i += 1) BEGIN // for each row
      READ_2DA_ENTRY_FORMER ~r2en_lufmt~ i 1 old_ability
      SPRINT new_ability ~~
      PATCH_IF (~%old_ability%~ STRING_EQUAL_CASE ~GA_SPCL902~) BEGIN // deathblow
        PATCH_IF (planetar == 0) BEGIN
          SPRINT new_ability ~GA_SPWI923~ // summon planetar
          SET planetar = 1
        END
      END ELSE
      PATCH_IF (~%old_ability%~ STRING_EQUAL_CASE ~GA_SPCL903~) BEGIN // greater deathblow
        PATCH_IF (dark_planetar == 0) BEGIN
          SPRINT new_ability ~GA_SPWI924~ // summon dark planetar
          SET dark_planetar = 1
        END
      END ELSE
      PATCH_IF (~%old_ability%~ STRING_EQUAL_CASE ~GA_SPCL908~) BEGIN // war cry
        PATCH_IF (improved_alacrity == 0) BEGIN
          SPRINT new_ability ~GA_SPWI921~ // improved alacrity
          SET improved_alacrity = 1
        END
      END ELSE
      PATCH_IF (~%old_ability%~ STRING_EQUAL_CASE ~GA_SPCL918~) BEGIN // alchemy
        PATCH_IF (energy_blades == 0) BEGIN
          SPRINT new_ability ~GA_SPWI920~ // energy blades
          SET energy_blades = 1
        END
      END ELSE
      PATCH_IF (~%old_ability%~ STRING_EQUAL_CASE ~GA_SPCL919~) BEGIN // scribe scrolls
        PATCH_IF (comet == 0) BEGIN
          SPRINT new_ability ~GA_SPWI925~ // comet
          SET comet = 1
        END
      END ELSE
      PATCH_IF (~%old_ability%~ STRING_EQUAL_CASE ~*~) BEGIN // unused
        PATCH_IF (dragons_breath == 0) BEGIN
          SPRINT new_ability ~GA_SPWI922~ // dragon's breath
          SET dragons_breath = 1
        END
      END
      PATCH_IF (STRING_LENGTH ~%new_ability%~ > 0) BEGIN // ability to be replaced here
        SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 1 ~%new_ability%~ // ability
        SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 2 ~*~ // icon
        SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 3 ~*~ // strref
        SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 4 ~32~ // min_level
        SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 5 ~99~ // max_level
        SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 6 ~1~ // num_allowed
        SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 7 ~*~ // prerequisite
        PATCH_IF (~%new_ability%~ STRING_EQUAL_CASE ~GA_SPWI923~) BEGIN // summon planetar restrictions
          SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 8 ~GA_SPWI924~ // excluded_by
          SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 9 ~ALL_EVIL~ // alignment_restrict
        END ELSE
        PATCH_IF (~%new_ability%~ STRING_EQUAL_CASE ~GA_SPWI924~) BEGIN // summon dark planetar restrictions
          SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 8 ~GA_SPWI923~ // excluded_by
          SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 9 ~ALL_GOOD~ // alignment_restrict
        END ELSE BEGIN // restrictions for others
          SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 8 ~*~ // excluded_by
          SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 9 ~*~ // alignment_restrict
        END
      END
    END
    SET_2DA_ENTRIES_NOW ~s2el_lufmt~ 10 // actually write changes!
    PRETTY_PRINT_2DA

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// save penalties for powerful spellcasters                   \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                            \\\\\
///// arcane only                                                \\\\\
/////                                                            \\\\\

BEGIN @231001 DESIGNATED 2310
GROUP @9
SUBCOMPONENT @231000 // arcane save penalties

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02310.g3~

INCLUDE ~bg2_tweaks/lib/saves_macro.tpa~

COPY_EXISTING_REGEXP GLOB ~^.+\.spl$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
    READ_SHORT 0x1c "spell_type" ELSE 0
    PATCH_IF ("%spell_type%" = 1) BEGIN // arcane only
      LAUNCH_PATCH_MACRO ~save_via_level~ // contains rest of patch
    END
  END
  BUT_ONLY_IF_IT_CHANGES

/////                                                            \\\\\
///// divine only                                                \\\\\
/////                                                            \\\\\

BEGIN @231100 DESIGNATED 2311
GROUP @9
SUBCOMPONENT @231000 // divine

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02311.g3~

INCLUDE ~bg2_tweaks/lib/saves_macro.tpa~

COPY_EXISTING_REGEXP GLOB ~^.+\.spl$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
    READ_SHORT 0x1c "spell_type" ELSE 0
    PATCH_IF ("%spell_type%" = 2) BEGIN // divine only
      LAUNCH_PATCH_MACRO ~save_via_level~ // contains rest of patch
    END
  END
  BUT_ONLY_IF_IT_CHANGES

/////                                                            \\\\\
///// arcane & divine                                            \\\\\
/////                                                            \\\\\

BEGIN @231200 DESIGNATED 2312
GROUP @9
SUBCOMPONENT @231000 // both

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02312.g3~

INCLUDE ~bg2_tweaks/lib/saves_macro.tpa~

// first fix spin692 so it won't muck up the search
ACTION_IF FILE_EXISTS_IN_GAME spin692.spl THEN BEGIN

  COPY_EXISTING ~spin692.spl~ ~override~
    PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
      READ_LONG 0x64 "abil_off"
      WRITE_SHORT ("%abil_off%" + 0x10) 1 // set first ability header to have minimum level of 1
    END
    BUT_ONLY_IF_IT_CHANGES

END

COPY_EXISTING_REGEXP GLOB ~^.+\.spl$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
    READ_SHORT 0x1c "spell_type" ELSE 0
    PATCH_IF (("%spell_type%" = 1) OR ("%spell_type%" = 2)) BEGIN // arcane & divine only
      LAUNCH_PATCH_MACRO ~save_via_level~ // contains rest of patch
    END
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// trap cap removal                                           \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @232000 DESIGNATED 2320
GROUP @9

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02320.g3~

ACTION_IF FILE_EXISTS_IN_GAME ~traplimt.2da~ THEN BEGIN 

  COPY_EXISTING ~traplimt.2da~ ~override~
    COUNT_2DA_ROWS 2 rows
    FOR (row = 0 ; row < rows ; ++row) BEGIN
      READ_2DA_ENTRY row 1 2 limit
      PATCH_IF IS_AN_INT limit BEGIN // sanity check
        SET_2DA_ENTRY row 1 2 999
      END
    END
    BUT_ONLY

END ELSE BEGIN

  COPY_EXISTING ~trapsnar.pro~ ~override/ag#snar.pro~
  ADD_PROJECTILE ~override/ag#snar.pro~
  
  COPY_EXISTING ~spcl411.spl~ ~override~
                ~spcl415.spl~ ~override~
    READ_LONG  0x64 ab_off
    READ_SHORT 0x68 ab_num
    FOR(i=0; i<ab_num; i+=1) BEGIN
      WRITE_SHORT (ab_off+i*0x28+0x26) ag#snar
    END
    BUT_ONLY_IF_IT_CHANGES
  
  ACTION_IF GAME_IS ~tob bgt bg2ee~ THEN BEGIN // if ToB
  
    COPY_EXISTING ~trapboom.pro~ ~override/ag#boom.pro~
    COPY_EXISTING ~trapspik.pro~ ~override/ag#spik.pro~
    COPY_EXISTING ~traptime.pro~ ~override/ag#time.pro~
  
    ADD_PROJECTILE ~override/ag#boom.pro~
    ADD_PROJECTILE ~override/ag#spik.pro~
    ADD_PROJECTILE ~override/ag#time.pro~
  
    COPY_EXISTING ~spcl910b.spl~ ~override~
      READ_LONG  0x64 ab_off
      READ_SHORT 0x68 ab_num
      FOR(i=0; i<ab_num; i+=1) BEGIN
        WRITE_SHORT (ab_off+i*0x28+0x26) ag#spik
      END
  
    COPY_EXISTING ~spcl911b.spl~ ~override~
      READ_LONG  0x64 ab_off
      READ_SHORT 0x68 ab_num
      FOR(i=0; i<ab_num; i+=1) BEGIN
        WRITE_SHORT (ab_off+i*0x28+0x26) ag#boom
      END
      BUT_ONLY_IF_IT_CHANGES
  
    COPY_EXISTING ~spcl912b.spl~ ~override~
      READ_LONG  0x64 ab_off
      READ_SHORT 0x68 ab_num
      FOR(i=0; i<ab_num; i+=1) BEGIN
        WRITE_SHORT (ab_off+i*0x28+0x26) ag#time
      END
      BUT_ONLY_IF_IT_CHANGES

  END

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// remove delay on magical traps                              \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @233000 DESIGNATED 2330
GROUP @9

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02330.g3~

ACTION_FOR_EACH proj IN dfirebl iceglyp idpro96 trapglyp trapskul BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%proj%.pro~ THEN BEGIN
  
    COPY_EXISTING ~%proj%.pro~ ~override~
      WRITE_SHORT 0x210 1
      BUT_ONLY

  END

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// summoning cap removal                                      \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @233900 DESIGNATED 2339
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~summlimt.2da~ @25 // ToB required
GROUP @9

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02339.g3~

COPY_EXISTING ~summlimt.2da~ ~override~
  REPLACE_TEXTUALLY ~NORMAL[ %TAB%]+[0-9]+~ ~NORMAL 999~
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// celestial cap removal                                      \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @234000 DESIGNATED 2340
REQUIRE_PREDICATE     GAME_IS ~tob bg2ee~   @25 // ToB required
GROUP @9

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02340.g3~

ACTION_IF FILE_EXISTS_IN_GAME ~summlimt.2da~ THEN BEGIN

  COPY_EXISTING ~summlimt.2da~ ~override~
    REPLACE_TEXTUALLY ~CELESTIAL[ %TAB%]+[0-9]+~ ~CELESTIAL 999~
    BUT_ONLY

END ELSE BEGIN

  COPY_EXISTING ~devagood.cre~ ~override/ag#dgood.cre~
                ~devaevil.cre~ ~override/ag#devil.cre~
                ~plangood.cre~ ~override/ag#pgood.cre~
                ~planevil.cre~ ~override/ag#pevil.cre~

  COPY_EXISTING ~spdeva.eff~ ~override~
    WRITE_ASCII 0x30 ag#dgood

  COPY_EXISTING ~spdeva2.eff~ ~override~
    WRITE_ASCII 0x30 ag#devil

  COPY_EXISTING ~spplan.eff~ ~override~
    WRITE_ASCII 0x30 ag#pgood

  COPY_EXISTING ~spplan2.eff~ ~override~
    WRITE_ASCII 0x30 ag#pevil

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Alter Multiclass Restrictions                              \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                            \\\\\
///// Allow humans to multiclass                                 \\\\\
/////                                                            \\\\\

BEGIN @235001 DESIGNATED 2350
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~clsrcreq.2da~ @25 // ToB required
GROUP @9
SUBCOMPONENT @235000 // Higher HP on Level Up

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02350.g3~

COPY_EXISTING ~clsrcreq.2da~ ~override~
  COUNT_2DA_ROWS 8 rows
  FOR (row = 0 ; row < rows ; ++row) BEGIN
    READ_2DA_ENTRY row 0 8 entry
    PATCH_IF (("%entry%" STRING_COMPARE_CASE "FIGHTER_MAGE"        = 0) OR
              ("%entry%" STRING_COMPARE_CASE "FIGHTER_CLERIC"      = 0) OR
              ("%entry%" STRING_COMPARE_CASE "FIGHTER_THIEF"       = 0) OR
              ("%entry%" STRING_COMPARE_CASE "FIGHTER_MAGE_THIEF"  = 0) OR
              ("%entry%" STRING_COMPARE_CASE "MAGE_THIEF"          = 0) OR
              ("%entry%" STRING_COMPARE_CASE "CLERIC_MAGE"         = 0) OR
              ("%entry%" STRING_COMPARE_CASE "CLERIC_THIEF"        = 0) OR
              ("%entry%" STRING_COMPARE_CASE "FIGHTER_DRUID"       = 0) OR
              ("%entry%" STRING_COMPARE_CASE "FIGHTER_MAGE_CLERIC" = 0) OR
              ("%entry%" STRING_COMPARE_CASE "CLERIC_RANGER"       = 0)) BEGIN // allow human multi-classes in these by default
      SET_2DA_ENTRY  row 1 8 1
    END
  END
  PRETTY_PRINT_2DA
  BUT_ONLY

/////                                                            \\\\\
///// Allow non-humans every mult-class combo                    \\\\\
/////                                                            \\\\\

BEGIN @235100 DESIGNATED 2351
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~clsrcreq.2da~ @25 // ToB required
GROUP @9
SUBCOMPONENT @235000 // Higher HP on Level Up

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02351.g3~

COPY_EXISTING ~clsrcreq.2da~ ~override~
  COUNT_2DA_ROWS 8 rows
  FOR (row = 0 ; row < rows ; ++row) BEGIN
    READ_2DA_ENTRY row 0 8 entry
    PATCH_IF (("%entry%" STRING_COMPARE_CASE "FIGHTER_MAGE"        = 0) OR
              ("%entry%" STRING_COMPARE_CASE "FIGHTER_CLERIC"      = 0) OR
              ("%entry%" STRING_COMPARE_CASE "FIGHTER_THIEF"       = 0) OR
              ("%entry%" STRING_COMPARE_CASE "FIGHTER_MAGE_THIEF"  = 0) OR
              ("%entry%" STRING_COMPARE_CASE "MAGE_THIEF"          = 0) OR
              ("%entry%" STRING_COMPARE_CASE "CLERIC_MAGE"         = 0) OR
              ("%entry%" STRING_COMPARE_CASE "CLERIC_THIEF"        = 0) OR
              ("%entry%" STRING_COMPARE_CASE "FIGHTER_DRUID"       = 0) OR
              ("%entry%" STRING_COMPARE_CASE "FIGHTER_MAGE_CLERIC" = 0) OR
              ("%entry%" STRING_COMPARE_CASE "CLERIC_RANGER"       = 0)) BEGIN
      FOR (col = 2; col < 8 ; ++col) BEGIN
        SET_2DA_ENTRY  row col 8 1
      END
    END
  END
  PRETTY_PRINT_2DA
  BUT_ONLY

/////                                                            \\\\\
///// multiclass madness                                         \\\\\
/////                                                            \\\\\

BEGIN @235200 DESIGNATED 2352
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~clsrcreq.2da~ @25 // ToB required
GROUP @9
SUBCOMPONENT @235000 // Higher HP on Level Up

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02352.g3~

COPY_EXISTING ~clsrcreq.2da~ ~override~
  COUNT_2DA_ROWS 8 rows
  FOR (row = 0 ; row < rows ; ++row) BEGIN
    READ_2DA_ENTRY row 0 8 entry
    PATCH_IF (("%entry%" STRING_COMPARE_CASE "FIGHTER_MAGE"        = 0) OR
              ("%entry%" STRING_COMPARE_CASE "FIGHTER_CLERIC"      = 0) OR
              ("%entry%" STRING_COMPARE_CASE "FIGHTER_THIEF"       = 0) OR
              ("%entry%" STRING_COMPARE_CASE "FIGHTER_MAGE_THIEF"  = 0) OR
              ("%entry%" STRING_COMPARE_CASE "MAGE_THIEF"          = 0) OR
              ("%entry%" STRING_COMPARE_CASE "CLERIC_MAGE"         = 0) OR
              ("%entry%" STRING_COMPARE_CASE "CLERIC_THIEF"        = 0) OR
              ("%entry%" STRING_COMPARE_CASE "FIGHTER_DRUID"       = 0) OR
              ("%entry%" STRING_COMPARE_CASE "FIGHTER_MAGE_CLERIC" = 0) OR
              ("%entry%" STRING_COMPARE_CASE "CLERIC_RANGER"       = 0)) BEGIN
      FOR (col = 1; col < 8 ; ++col) BEGIN
        SET_2DA_ENTRY row col 8 1
      END
    END
  END
  PRETTY_PRINT_2DA
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Remove Racial Restrictions for Single Classes              \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @236000 DESIGNATED 2360
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~clsrcreq.2da~ @25 // ToB required
GROUP @9

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02360.g3~

COPY_EXISTING ~clsrcreq.2da~ ~override~
  COUNT_2DA_ROWS 8 rows
  FOR (row = 0 ; row < rows ; ++row) BEGIN
    READ_2DA_ENTRY row 0 8 entry
    PATCH_IF (("%entry%" STRING_COMPARE_CASE "MAGE"      = 0) OR
              ("%entry%" STRING_COMPARE_CASE "FIGHTER"   = 0) OR
              ("%entry%" STRING_COMPARE_CASE "BARBARIAN" = 0) OR
              ("%entry%" STRING_COMPARE_CASE "CLERIC"    = 0) OR
              ("%entry%" STRING_COMPARE_CASE "THIEF"     = 0) OR
              ("%entry%" STRING_COMPARE_CASE "BARD"      = 0) OR
              ("%entry%" STRING_COMPARE_CASE "PALADIN"   = 0) OR
              ("%entry%" STRING_COMPARE_CASE "DRUID"     = 0) OR
              ("%entry%" STRING_COMPARE_CASE "RANGER"    = 0) OR
              ("%entry%" STRING_COMPARE_CASE "SORCERER"  = 0) OR
              ("%entry%" STRING_COMPARE_CASE "MONK"      = 0)) BEGIN
      FOR (col = 2; col < 8 ; ++col) BEGIN
        SET_2DA_ENTRY row col 8 1
      END
    END
  END
  PRETTY_PRINT_2DA
  BUT_ONLY

COPY_EXISTING ~mgsrcreq.2da~ ~override~
  FOR (col = 2; col < 9 ; ++col) BEGIN
    SET_2DA_ENTRY 0 col 9 1
  END
  PRETTY_PRINT_2DA
  BUT_ONLY

// let dwarves get 17 CHR to be paladins
COPY_EXISTING ~abracerq.2da~ ~override~
  READ_2DA_ENTRY 1 12 13 foo
  PATCH_IF (foo < 18) THEN BEGIN
    SET_2DA_ENTRY  1 12 13 18
  END
  PRETTY_PRINT_2DA
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Alter Dual-class Restrictions                              \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                            \\\\\
///// Humans can no longer dual-class                            \\\\\
/////                                                            \\\\\

BEGIN @237001 DESIGNATED 2370
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~clsrcreq.2da~ @25 // ToB required
GROUP @9
SUBCOMPONENT @237000 // Higher HP on Level Up

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02370.g3~

COPY_EXISTING ~clsrcreq.2da~ ~override~
  REPLACE_TEXTUALLY ~^DUALCLASS[ %TAB%]+1~ ~DUALCLASS 0~
  PRETTY_PRINT_2DA
  BUT_ONLY

/////                                                            \\\\\
///// Allow Non-Humans to Dual-Class                             \\\\\
/////                                                            \\\\\

BEGIN @237100 DESIGNATED 2371
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~clsrcreq.2da~ @25 // ToB required
GROUP @9
SUBCOMPONENT @237000 // Higher HP on Level Up

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02371.g3~

COPY_EXISTING ~clsrcreq.2da~ ~override~
  REPLACE_TEXTUALLY ~^\(DUALCLASS[ %TAB%]+[0-9]+[ %TAB%]+\).+$~ ~\1 1 1 1 1 1 1~
  PRETTY_PRINT_2DA
  BUT_ONLY

/////                                                            \\\\\
///// Allow both                                                 \\\\\
/////                                                            \\\\\

BEGIN @235200 DESIGNATED 2372
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~clsrcreq.2da~ @25 // ToB required
GROUP @9
SUBCOMPONENT @237000 // Higher HP on Level Up

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02372.g3~

COPY_EXISTING ~clsrcreq.2da~ ~override~
  REPLACE_TEXTUALLY ~^\(DUALCLASS[ %TAB%]+\).+$~ ~\1 0 1 1 1 1 1 1~
  PRETTY_PRINT_2DA
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// open all kits to all races                                 \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @238000 DESIGNATED 2380
GROUP @9

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02370.g3~

COPY_EXISTING ~clsrcreq.2da~ ~override~
  COUNT_2DA_ROWS 8 rows
  FOR (row = 0 ; row < rows ; ++row) BEGIN
    READ_2DA_ENTRY row 0 8 entry
    PATCH_IF NOT (("%entry%" STRING_COMPARE_CASE "FIGHTER_MAGE"        = 0) OR
                  ("%entry%" STRING_COMPARE_CASE "FIGHTER_CLERIC"      = 0) OR
                  ("%entry%" STRING_COMPARE_CASE "FIGHTER_THIEF"       = 0) OR
                  ("%entry%" STRING_COMPARE_CASE "FIGHTER_MAGE_THIEF"  = 0) OR
                  ("%entry%" STRING_COMPARE_CASE "MAGE_THIEF"          = 0) OR
                  ("%entry%" STRING_COMPARE_CASE "CLERIC_MAGE"         = 0) OR
                  ("%entry%" STRING_COMPARE_CASE "CLERIC_THIEF"        = 0) OR
                  ("%entry%" STRING_COMPARE_CASE "FIGHTER_DRUID"       = 0) OR
                  ("%entry%" STRING_COMPARE_CASE "FIGHTER_MAGE_CLERIC" = 0) OR
                  ("%entry%" STRING_COMPARE_CASE "CLERIC_RANGER"       = 0) OR
                  ("%entry%" STRING_COMPARE_CASE "MAGE"                = 0) OR
                  ("%entry%" STRING_COMPARE_CASE "FIGHTER"             = 0) OR
                  ("%entry%" STRING_COMPARE_CASE "BARBARIAN"           = 0) OR
                  ("%entry%" STRING_COMPARE_CASE "CLERIC"              = 0) OR
                  ("%entry%" STRING_COMPARE_CASE "THIEF"               = 0) OR
                  ("%entry%" STRING_COMPARE_CASE "BARD"                = 0) OR
                  ("%entry%" STRING_COMPARE_CASE "PALADIN"             = 0) OR
                  ("%entry%" STRING_COMPARE_CASE "DRUID"               = 0) OR
                  ("%entry%" STRING_COMPARE_CASE "RANGER"              = 0) OR
                  ("%entry%" STRING_COMPARE_CASE "SORCERER"            = 0) OR
                  ("%entry%" STRING_COMPARE_CASE "MONK"                = 0) OR
                  ("%entry%" STRING_COMPARE_CASE "DUALCLASS"           = 0)) BEGIN
      FOR (col = 1; col < 8 ; ++col) BEGIN
        SET_2DA_ENTRY row col 8 1
      END
    END
  END
  PRETTY_PRINT_2DA
  BUT_ONLY

COPY_EXISTING ~mgsrcreq.2da~ ~override~
  COUNT_2DA_ROWS 9 rows
  FOR (row = 1 ; row < rows ; ++row) BEGIN
    FOR (col = 2; col < 9 ; ++col) BEGIN
      PATCH_IF col = 7 BEGIN SET col += 1 END // skip gnome column
      SET_2DA_ENTRY row col 9 1
    END
  END
  PRETTY_PRINT_2DA
  BUT_ONLY

ACTION_IF NOT FILE_EXISTS_IN_GAME k_mn_h.2da BEGIN

  COPY ~bg2_tweaks/2da/k_mn_h.2da~ ~override~
  
END
  
ACTION_FOR_EACH race IN d e g he hl ho BEGIN

  ACTION_IF NOT FILE_EXISTS_IN_GAME ~k_mn_%race%.2da~ THEN BEGIN
  
    COPY_EXISTING ~k_mn_h.2da~ ~override/k_mn_%race%.2da~
    
  END

END

COPY_EXISTING ~kitlist.2da~ ~override~
  COUNT_2DA_ROWS 9 rows
  FOR (row = 0 ; row < rows ; ++row) BEGIN
    READ_2DA_ENTRY row 0 9 kitno
    READ_2DA_ENTRY row 8 9 class
    PATCH_IF ((IS_AN_INT class) AND (kitno != 30) AND (kitno != 31)) BEGIN // this is to screen out header junk and barbarians and wild mages
      SPRINT letter this_is_a_fake // just in case none of these match
      PATCH_IF class =  2 BEGIN SPRINT letter  f END
      PATCH_IF class =  6 BEGIN SPRINT letter  p END
      PATCH_IF class = 12 BEGIN SPRINT letter  r END
      PATCH_IF class =  4 BEGIN SPRINT letter  t END
      PATCH_IF class =  5 BEGIN SPRINT letter  b END
      PATCH_IF class = 11 BEGIN SPRINT letter  d END
      PATCH_IF class =  3 BEGIN SPRINT letter  c END
      PATCH_IF class =  1 BEGIN SPRINT letter  m END
      PATCH_IF class = 19 BEGIN SPRINT letter  s END
      PATCH_IF class = 20 BEGIN SPRINT letter mn END
      SET available = 0
      PATCH_FOR_EACH race IN d e g he hl ho BEGIN // first loop is to check and see if the kit is available to any race so we don't expose internal mod kits
        PATCH_IF FILE_EXISTS_IN_GAME ~k_%letter%_%race%.2da~ BEGIN
          INNER_ACTION BEGIN

            COPY_EXISTING ~k_%letter%_%race%.2da~ ~override~
              COUNT_2DA_ROWS 2 rows_inner
              FOR (row_inner = 0 ; row_inner < rows_inner ; ++row_inner) BEGIN
                READ_2DA_ENTRY row_inner 0 2 index
                READ_2DA_ENTRY row_inner 1 2 kitcheck
                PATCH_IF IS_AN_INT kitcheck BEGIN // sanity check
                  PATCH_IF kitcheck = kitno BEGIN
                    SET available = 1
                    SET row_inner = rows_inner // kill loop
                  END
                END
              END
              BUT_ONLY
          END
        END
      END
      PATCH_IF available = 1 THEN BEGIN // only if it actually is available do we add it to all races
        PATCH_FOR_EACH race IN d e g he hl ho BEGIN
          PATCH_IF FILE_EXISTS_IN_GAME ~k_%letter%_%race%.2da~ BEGIN
            INNER_ACTION BEGIN

              COPY_EXISTING ~k_%letter%_%race%.2da~ ~override~
                SET added = 0
                COUNT_2DA_ROWS 2 rows_inner
                FOR (row_inner = 0 ; row_inner < rows_inner ; ++row_inner) BEGIN
                  READ_2DA_ENTRY row_inner 0 2 index
                  READ_2DA_ENTRY row_inner 1 2 kitcheck
                  PATCH_IF IS_AN_INT kitcheck BEGIN // sanity check
                    PATCH_IF kitcheck = kitno BEGIN
                      SET added = 1
                      SET row_inner = rows_inner // kill loop
                    END
                  END
                END
                BUT_ONLY
    
              ACTION_IF added = 0 THEN BEGIN
                OUTER_SET index += 1
                APPEND ~k_%letter%_%race%.2da~ ~%index% %kitno%~
              END
  
            END
          END
        END
      END
    END
  END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Higher HP on Level Up                            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// maximum                                          \\\\\
/////                                                  \\\\\

BEGIN @300001 DESIGNATED 3000 // maximum
GROUP @4
SUBCOMPONENT @300000 // Higher HP on Level Up

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03000.g3~

ACTION_IF FILE_EXISTS_IN_GAME hpclass.2da THEN BEGIN // if tobex/ee, look up and patch tables

  ACTION_CLEAR_ARRAY cd_hp_tables
  ACTION_DEFINE_ARRAY cd_hp_tables BEGIN END

  COPY_EXISTING ~hpclass.2da~ ~override~
    COUNT_2DA_ROWS 2 rows2
    FOR (index2 = 0 ; index2 < rows2 ; ++index2) BEGIN
      READ_2DA_ENTRY index2 1 2 table
      PATCH_IF FILE_EXISTS_IN_GAME ~%table%.2da~ BEGIN
        DEFINE_ASSOCIATIVE_ARRAY cd_hp_tables BEGIN "%table%" => 0 END
      END
    END
    BUT_ONLY

  ACTION_PHP_EACH cd_hp_tables AS tables => foo BEGIN

    COPY_EXISTING ~%tables%.2da~ ~override~
      COUNT_2DA_ROWS 4 rows
      FOR (index = 0 ; index < rows ; ++index) BEGIN
        READ_2DA_ENTRY index 2 4 rolls
        PATCH_IF (rolls != 0) BEGIN
          READ_2DA_ENTRY index 1 4 sides
          READ_2DA_ENTRY index 3 4 modifier
          SET_2DA_ENTRY  index 2 4 0
          SET_2DA_ENTRY  index 3 4 ((rolls * sides) + modifier)
        END
      END
      PRETTY_PRINT_2DA
      BUT_ONLY

  END

END ELSE BEGIN // otherwise use fixed list

  ACTION_FOR_EACH table IN hpbarb hpmonk hpprs hprog hpwiz hpwar BEGIN
  
    ACTION_IF FILE_EXISTS_IN_GAME ~%table%.2da~ BEGIN
  
      COPY_EXISTING ~%table%.2da~ ~override~
        COUNT_2DA_ROWS 4 rows
        FOR (index = 0 ; index < rows ; ++index) BEGIN
          READ_2DA_ENTRY index 2 4 rolls
          PATCH_IF (rolls != 0) BEGIN
            READ_2DA_ENTRY index 1 4 sides
            READ_2DA_ENTRY index 3 4 modifier
            SET_2DA_ENTRY  index 2 4 0
            SET_2DA_ENTRY  index 3 4 ((rolls * sides) + modifier)
          END
        END
        PRETTY_PRINT_2DA
        BUT_ONLY
  
    END
  
  END

END

/////                                                  \\\\\
///// nwn-style                                        \\\\\
/////                                                  \\\\\

BEGIN @300100 DESIGNATED 3001 // nwn-style
GROUP @4
SUBCOMPONENT @300000 // Higher HP on Level Up

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03001.g3~

ACTION_IF FILE_EXISTS_IN_GAME hpclass.2da THEN BEGIN // if tobex/ee, look up and patch tables

  ACTION_CLEAR_ARRAY cd_hp_tables
  ACTION_DEFINE_ARRAY cd_hp_tables BEGIN END

  COPY_EXISTING ~hpclass.2da~ ~override~
    COUNT_2DA_ROWS 2 rows2
    FOR (index2 = 0 ; index2 < rows2 ; ++index2) BEGIN
      READ_2DA_ENTRY index2 1 2 table
      PATCH_IF FILE_EXISTS_IN_GAME ~%table%.2da~ BEGIN
        DEFINE_ASSOCIATIVE_ARRAY cd_hp_tables BEGIN "%table%" => 0 END
      END
    END
    BUT_ONLY

  ACTION_PHP_EACH cd_hp_tables AS tables => foo BEGIN

    COPY_EXISTING ~%tables%.2da~ ~override~
      COUNT_2DA_ROWS 4 rows
      FOR (index = 0 ; index < rows ; ++index) BEGIN
        READ_2DA_ENTRY index 2 4 rolls
        PATCH_IF (rolls != 0) BEGIN
          READ_2DA_ENTRY index 1 4 sides
          SET_2DA_ENTRY  index 1 4 2
          SET_2DA_ENTRY  index 2 4 ((rolls * sides) / 2)
        END
      END
      PRETTY_PRINT_2DA
      BUT_ONLY

  END

END ELSE BEGIN

  ACTION_FOR_EACH table IN hpbarb hpmonk hpprs hprog hpwiz hpwar BEGIN

    ACTION_IF FILE_EXISTS_IN_GAME ~%table%.2da~ BEGIN

      COPY_EXISTING ~%table%.2da~ ~override~
        COUNT_2DA_ROWS 4 rows
        FOR (index = 0 ; index < rows ; ++index) BEGIN
          READ_2DA_ENTRY index 2 4 rolls
          PATCH_IF (rolls != 0) BEGIN
            READ_2DA_ENTRY index 1 4 sides
            SET_2DA_ENTRY  index 1 4 2
            SET_2DA_ENTRY  index 2 4 ((rolls * sides) / 2)
          END
        END
        PRETTY_PRINT_2DA
        BUT_ONLY

    END

  END

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Maximum HP for NPCs                              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// all npcs                                         \\\\\
/////                                                  \\\\\

BEGIN @301001 DESIGNATED 3010 // maximum hp for npcs
GROUP @4
SUBCOMPONENT @301000 // all npcs

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03010.g3~

// load patch macro
INCLUDE ~bg2_tweaks/lib/max_hp_creatures.tpa~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN
    PATCH_IF (NOT ~%SOURCE_RES%~ STRING_EQUAL_CASE ~riftcr04~) BEGIN // skip creatures meant to have lower hp
      READ_SHORT 0x24 "currenthp" ELSE 0
      READ_SHORT 0x26 "maxhp"     ELSE 0
      PATCH_IF (("%maxhp%" > 0) AND ("%currenthp%" > 0)) BEGIN
        LAUNCH_PATCH_MACRO ~max_hp_creatures~ // contains rest of patch
      END
    END
  END
  BUT_ONLY_IF_IT_CHANGES

/////                                                  \\\\\
///// Maximum HP for Non-Party-Joinable NPCs           \\\\\
/////                                                  \\\\\

BEGIN @301100 DESIGNATED 3011 // maximum hp for npcs
GROUP @4
SUBCOMPONENT @301000 // all npcs

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03011.g3~

// load patch macro
INCLUDE ~bg2_tweaks/lib/max_hp_creatures.tpa~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN
    PATCH_IF (NOT ~%SOURCE_RES%~ STRING_EQUAL_CASE ~riftcr04~) BEGIN // skip creatures meant to have lower hp
      READ_SHORT 0x24  "currenthp" ELSE 0
      READ_SHORT 0x26  "maxhp"     ELSE 0
      READ_LONG  0x1cc "biography" ELSE 0
      PATCH_IF (("%maxhp%" > 0) AND ("%currenthp%" > 0) AND (("%biography%" > 2147483646) OR ("%biography%" < 1))) BEGIN
        LAUNCH_PATCH_MACRO ~max_hp_creatures~ // contains rest of patch
      END
    END
  END
  BUT_ONLY_IF_IT_CHANGES

/////                                                  \\\\\
///// Maximum HP for Party-Joinable NPCs               \\\\\
/////                                                  \\\\\

BEGIN @301200 DESIGNATED 3012 // maximum hp for npcs
GROUP @4
SUBCOMPONENT @301000 // all npcs

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03012.g3~

// load patch macro
INCLUDE ~bg2_tweaks/lib/max_hp_creatures.tpa~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN
    READ_SHORT 0x24  "currenthp" ELSE 0
    READ_SHORT 0x26  "maxhp"     ELSE 0
    READ_LONG  0x1cc "biography" ELSE 0
    PATCH_IF (("%maxhp%" > 0) AND ("%currenthp%" > 0) AND ("%biography%" < 2147483647) AND ("%biography%" > 0)) BEGIN
      LAUNCH_PATCH_MACRO ~max_hp_creatures~ // contains rest of patch
    END
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Identify All Items                               \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @302000 DESIGNATED 3020
GROUP @4

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03020.g3~

// SoS item needs to be un-ID'd for one quest - part 1
ACTION_IF FILE_EXISTS_IN_GAME ~cbltcnt2.itm~ THEN BEGIN

  COPY_EXISTING ~cbltcnt2.itm~ ~override~
    READ_SHORT 0x42 "sos_lore"
    BUT_ONLY_IF_IT_CHANGES

END

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
    WRITE_SHORT 0x42 0
  END
  BUT_ONLY_IF_IT_CHANGES

// SoS item needs to be un-ID'd for one quest - part 2
ACTION_IF FILE_EXISTS_IN_GAME ~cbltcnt2.itm~ THEN BEGIN

  COPY_EXISTING ~cbltcnt2.itm~ ~override~
    WRITE_SHORT 0x42 "%sos_lore%"
    BUT_ONLY_IF_IT_CHANGES

END

COPY_EXISTING_REGEXP GLOB ~^.+\.[ac]re$~ ~override~
  PATCH_IF ("%SOURCE_FILE%" STRING_COMPARE_REGEXP "^.+\.cre$" = 0) BEGIN // if creature
    READ_LONG 0x2bc "itm_off" ELSE 0
    READ_LONG 0x2c0 "itm_num" ELSE 0
  END ELSE BEGIN
    READ_LONG  0x78 "itm_off" ELSE 0
    READ_SHORT 0x76 "itm_num" ELSE 0
  END
  FOR (index = 0 ; index < itm_num ; index = index + 1) BEGIN
    READ_BYTE  ("%itm_off%" + 0x10 + (0x14 * "%index%")) "flag"
    WRITE_BYTE ("%itm_off%" + 0x10 + (0x14 * "%index%")) ("%flag%" BOR 0b00000001) // adds identified flag
  END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Easy Spell Learning                              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// 100% learn spells                                \\\\\
/////                                                  \\\\\

BEGIN @303001 DESIGNATED 3030 // 100% learn spells
GROUP @4
SUBCOMPONENT @303000 // easy spell learning

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03030.g3~

PRINT @1
COPY_EXISTING ~intmod.2da~ ~override~
  COUNT_2DA_ROWS ~5~ "rows"
  FOR (index = 3; index < rows ; index = index + 1) BEGIN
    SET_2DA_ENTRY_LATER ~intmod~ index 1 ~150~
  END
  SET_2DA_ENTRIES_NOW ~intmod~ 1
  BUT_ONLY_IF_IT_CHANGES

/////                                                  \\\\\
///// 100% learn spells & no spell caps                \\\\\
/////                                                  \\\\\

BEGIN @303100 DESIGNATED 3031 // 100% learn spells, remove caps
GROUP @4
SUBCOMPONENT @303000 // easy spell learning

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03031.g3~

PRINT @1
COPY_EXISTING ~intmod.2da~ ~override~
  COUNT_2DA_ROWS ~5~ "rows"
  FOR (index = 3; index < rows ; index = index + 1) BEGIN
    SET_2DA_ENTRY_LATER ~intmod~ index 1 ~150~
    SET_2DA_ENTRY_LATER ~intmod~ index 2 ~9~
    SET_2DA_ENTRY_LATER ~intmod~ index 3 ~99~
  END
  SET_2DA_ENTRIES_NOW ~intmod~ 1
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Make Misc Bags of Holding Bottomless             \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @304000 DESIGNATED 3040
GROUP @4

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03040.g3~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.sto$~ ~override~
  READ_LONG 0x08 "itemtype" ELSE 0
  PATCH_IF ("%itemtype%" = 5) BEGIN
    WRITE_SHORT 0x22 32767
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Remove fatigue from restoration spells           \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @305000 DESIGNATED 3050
GROUP @4
REQUIRE_PREDICATE NOT GAME_IS ~tutu tutu_totsc~ @25 // non-Tutu

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03050.g3~

// remove fatigue from restoration spells
ACTION_FOR_EACH spell IN 
  ~sppr417.spl~
  ~sppr713.spl~
  ~spwish07.spl~
  ~spwish46.spl~
  BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%spell%~ BEGIN

    COPY_EXISTING ~%spell%~ ~override~
      LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 93 END
      BUT_ONLY
      
  END
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Remove "You Must Gather..."                      \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @306000 DESIGNATED 3060
GROUP @4
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~howparty.2da~ @25 // non-iwdee

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03060.g3~

COPY  ~bg2_tweaks/wav/error01.wav~ ~override~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Store-rep tweaks                                 \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// discounts for low rep                            \\\\\
/////                                                  \\\\\

BEGIN @307000 DESIGNATED 3070 // low-rep discounts
GROUP @4
SUBCOMPONENT @307001 // store-rep tweaks

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03070.g3~

COPY ~bg2_tweaks/2da/repmodst.2da~ ~override~

/////                                                  \\\\\
///// no rep effect, 100%                              \\\\\
/////                                                  \\\\\

BEGIN @307100 DESIGNATED 3071 // low-rep discounts
GROUP @4
SUBCOMPONENT @307001 // store-rep tweaks

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03071.g3~

COPY ~bg2_tweaks/2da/repmodst_100.2da~ ~override/repmodst.2da~

/////                                                  \\\\\
///// no rep effect, 80%                               \\\\\
/////                                                  \\\\\

BEGIN @307200 DESIGNATED 3072 // low-rep discounts
GROUP @4
SUBCOMPONENT @307001 // store-rep tweaks

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03072.g3~

COPY ~bg2_tweaks/2da/repmodst_080.2da~ ~override/repmodst.2da~

/////                                                  \\\\\
///// no rep effect, 60%                               \\\\\
/////                                                  \\\\\

BEGIN @307300 DESIGNATED 3073 // low-rep discounts
GROUP @4
SUBCOMPONENT @307001 // store-rep tweaks

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03073.g3~

COPY ~bg2_tweaks/2da/repmodst_060.2da~ ~override/repmodst.2da~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Unlimited Ammo Stacks                            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @308000 DESIGNATED 3080
GROUP @4

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03080.g3~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~ // looking for ammo
  READ_SHORT  0x1C "type" ELSE 0
  READ_SHORT  0x38 "max"  ELSE 0
  PATCH_IF (
             ("%max%" > 1) AND // if item can already stack and is of the type...
             (
               ("%type%" =  5) OR // arrows,
               ("%type%" = 14) OR // bullets,
               ("%type%" = 29) OR // spears,
               ("%type%" = 31) OR // bolts,
               ("%type%" = 24) OR // darts,
               ("%type%" = 25) OR // axe, or
               ("%type%" = 16)    // dagger
             )
           ) BEGIN
    WRITE_SHORT 0x38 999
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Unlimited Gem and Jewelry Stacking               \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @309000 DESIGNATED 3090
GROUP @4

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03090.g3~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~ // looking for gems and jewelry
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
    READ_SHORT 0x1c "type"     ELSE 0
    READ_SHORT 0x68 "max_abil" ELSE 0
    READ_SHORT 0x70 "max_fx"   ELSE 0
    PATCH_IF (
               ("%max_abil%" = 0) AND // maximum abilities must be zero and
               ("%max_fx%" = 0) AND   // maximum effects must be zero and type must be one of
               (
                 ("%type%" =  1) OR // amulets
                 ("%type%" = 34) OR // gems
                 ("%type%" = 33) OR // gold
                 ("%type%" = 10)    // or rings
               )
             ) BEGIN
      WRITE_SHORT 0x38 999
    END
  END
  BUT_ONLY_IF_IT_CHANGES

// bandit scalps, winter wolf pelts too
ACTION_FOR_EACH item IN _misc01 _misc86 misc01 misc86 BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%item%.itm~ BEGIN

    COPY_EXISTING ~%item%.itm~ ~override~
      WRITE_SHORT 0x38 9999
      BUT_ONLY

  END

END

//alter nashkel shopkeep to only take one wolf pelt at a time instead of the whole stack
ACTION_FOR_EACH dialog IN shopkn _shopkn BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%dialog%.dlg~ BEGIN

    COPY_EXISTING ~%dialog%.dlg~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~TakePartyItem("\(_?MISC01\)")~ ~TakePartyItemNum("\1",1)~
      END
  
  END

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Unlimited Potion Stacking                        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @310000 DESIGNATED 3100
GROUP @4

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03100.g3~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~ // looking for potions
  READ_SHORT  0x1C "type" ELSE 0
  PATCH_IF ("%type%" = 9) BEGIN // potions
    WRITE_SHORT 0x38 999
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Unlimited Scroll Stacking                        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @311000 DESIGNATED 3110
GROUP @4

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03110.g3~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~ // looking for scrolls
  READ_SHORT  0x1C "type" ELSE 0
  PATCH_IF ("%type%" = 11) BEGIN // scrolls
    WRITE_SHORT 0x38 999
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Happy patch                                      \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// never angry                                      \\\\\
/////                                                  \\\\\

BEGIN @312001 DESIGNATED 3120
GROUP @4
SUBCOMPONENT @312000

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03120.g3~

// stop rep complaints
COPY_EXISTING ~happy.2da~ ~override~
  REPLACE_TEXTUALLY ~-[0-9]+~ ~0~
  PRETTY_PRINT_2DA
  BUT_ONLY

// remove NPC conflicts
//INCLUDE ~bg2_tweaks/lib/happy.tpa~

/////                                                  \\\\\
///// angry but never leave                            \\\\\
/////                                                  \\\\\

BEGIN @312100 DESIGNATED 3121
GROUP @4
SUBCOMPONENT @312000

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03121.g3~

// stop rep complaints
COPY_EXISTING ~happy.2da~ ~override~  
  REPLACE_EVALUATE ~-\([0-9]+\)~ BEGIN
    PATCH_IF ("%MATCH1%" > 160) BEGIN SET new = 160 END ELSE BEGIN SET new = "%MATCH1%" END
  END ~-%new%~
  PRETTY_PRINT_2DA
  BUT_ONLY

// remove NPC conflicts
//INCLUDE ~bg2_tweaks/lib/happy.tpa~

/////                                                  \\\\\
///// always neutral                                   \\\\\
/////                                                  \\\\\

BEGIN @312200 DESIGNATED 3122
GROUP @4
SUBCOMPONENT @312000

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03122.g3~

// stop rep complaints
COPY_EXISTING ~happy.2da~ ~override~
  FOR (col = 1 ; col < 4 ; col = col + 1) BEGIN
    FOR (row = 0 ; row < 20 ; row = row + 1) BEGIN
      SET_2DA_ENTRY %row% %col% 4 "0"
    END
  END
  PRETTY_PRINT_2DA
  BUT_ONLY_IF_IT_CHANGES

// remove NPC conflicts
//INCLUDE ~bg2_tweaks/lib/happy.tpa~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// NPCs don't fight                                 \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @312300 DESIGNATED 3123
GROUP @4
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~howparty.2da~ @25 // non-iwdee

// remove NPC conflicts
INCLUDE ~bg2_tweaks/lib/happy.tpa~


/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Stop Haer'Dalis-Aerie romance from starting      \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @312400 DESIGNATED 3124
GROUP @4
REQUIRE_PREDICATE GAME_IS ~bgt soa tob bg2ee~ @25 // non-Tutu

COPY_EXISTING ~haerdali.bcs~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~Global("RomanceConflict","GLOBAL",0)~ ~False()~
  COMPILE_BAF_TO_BCS
  BUT_ONLY_IF_IT_CHANGES
EXTEND_TOP ~haerdali.bcs~ ~bg2_tweaks/baf/haerdali.baf~ 


/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Happy neutrals                                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @312500 DESIGNATED 3125 // happy neutrals
GROUP @4

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03125.g3~

COPY_EXISTING ~happy.2da~ ~override~
  PATCH_IF GAME_IS ~bgee~ THEN BEGIN
    FOR (row = 6; row < 20; row = row + 1) BEGIN
      SET happy = (74 + row)
      SET_2DA_ENTRY "%row%" 2 4 ~happy~
    END
  END ELSE BEGIN  
    FOR (row = 6; row < 12; row = row + 1) BEGIN
      SET_2DA_ENTRY "%row%" 2 4 ~80~
    END
  END  
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// No traps or locks                                \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @313000 DESIGNATED 3130
GROUP @4

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03130.g3~

COPY_EXISTING_REGEXP GLOB ~^.+\.are$~ ~override~
  READ_SHORT 0x5a "trig_num"
  READ_LONG  0x5c "trig_off"
  READ_SHORT 0x74 "cont_num"
  READ_LONG  0x70 "cont_off"
  READ_SHORT 0xa4 "door_num"
  READ_LONG  0xa8 "door_off"
  FOR (index = 0 ; index < trig_num ; index = index + 1) BEGIN // cycle through triggers
    READ_SHORT ("%trig_off%" + 0x6a + (0xc4 * "%index%")) "trap_diff"
    PATCH_IF (("%trap_diff%" > 0) AND ("%trap_diff%" < 100)) BEGIN
      WRITE_SHORT ("%trig_off%" + 0x6c + (0xc4 * "%index%")) 0 // is not a trap
    END
  END
  FOR (index2 = 0 ; index2 < cont_num ; index2 = index2 + 1) BEGIN // cycle through containers
    WRITE_LONG  ("%cont_off%" + 0x26 + ("%index2%" * 0xc0)) 0 // lock difficulty
    WRITE_SHORT ("%cont_off%" + 0x30 + ("%index2%" * 0xc0)) 0 // is not a trap
  END
  FOR (index3 = 0 ; index3 < door_num ; index3 = index3 + 1) BEGIN // cycle through doors
    WRITE_SHORT ("%door_off%" + 0x70 + ("%index3%" * 0xc8)) 0 // is not a trap
    WRITE_LONG  ("%door_off%" + 0x88 + ("%index3%" * 0xc8)) 0 // detect diff
    WRITE_LONG  ("%door_off%" + 0x8c + ("%index3%" * 0xc8)) 0 // lock diff
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Faster chapter 1&2 cutscenes and dreams          \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// original EoU stuff                               \\\\\
/////                                                  \\\\\

BEGIN @314001 DESIGNATED 3140 // original EoU
GROUP @4
SUBCOMPONENT @314000 // faster chap 1&2 cutscenes
REQUIRE_PREDICATE GAME_IS ~soa tob bg2ee~ @25 // non-Tutu, non-BGT
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~mrimrin1.itm~ @24 // imoen romance compatibility code

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03140.g3~

COMPILE ~bg2_tweaks/baf/newgame.baf~
        ~bg2_tweaks/baf/cut01c.baf~
        ~bg2_tweaks/baf/cut01d.baf~
        ~bg2_tweaks/baf/cut01e.baf~
        ~bg2_tweaks/baf/cut01f.baf~
        ~bg2_tweaks/baf/cut01g.baf~

INCLUDE ~bg2_tweaks/lib/faster_start.tpa~

/////                                                  \\\\\
///// non-silly version                                \\\\\
/////                                                  \\\\\

BEGIN @314100 DESIGNATED 3141 // original EoU
GROUP @4
SUBCOMPONENT @314000 // faster chap 1&2 cutscenes
REQUIRE_PREDICATE GAME_IS ~soa tob bg2ee~ @25 // non-Tutu, non-BGT
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~mrimrin1.itm~ @24 // imoen romance compatibility code

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03141.g3~

COPY ~bg2_tweaks/baf/newgame_nonsilly.baf~ ~override/newgame.bcs~
     ~bg2_tweaks/baf/cut01c_nonsilly.baf~  ~override/cut01c.bcs~
     ~bg2_tweaks/baf/cut01d_nonsilly.baf~  ~override/cut01d.bcs~
     ~bg2_tweaks/baf/cut01e_nonsilly.baf~  ~override/cut01e.bcs~
     ~bg2_tweaks/baf/cut01f_nonsilly.baf~  ~override/cut01f.bcs~
     ~bg2_tweaks/baf/cut01g_nonsilly.baf~  ~override/cut01g.bcs~
  COMPILE_BAF_TO_BCS

INCLUDE ~bg2_tweaks/lib/faster_start.tpa~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Remove Cloak-of-Mirroring & Spell-Trap Animation \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @315000 DESIGNATED 3150
GROUP @4
REQUIRE_PREDICATE NOT GAME_IS ~tutu tutu_totsc~ @25 // non-Tutu

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03150.g3~

COPY ~bg2_tweaks/bam/spmagglo.bam~ ~override~
     ~bg2_tweaks/bam/spmagglo.bam~ ~override/spsturni.bam~
     ~bg2_tweaks/bam/spmagglo.bam~ ~override/spturni2.bam~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Keep Drizzt's Loot, Disable Malchor Harpell      \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @316000 DESIGNATED 3160
GROUP @4
REQUIRE_PREDICATE NOT GAME_IS ~tutu tutu_totsc bgee~ @25 // non-Tutu
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~howparty.2da~ @25 // non-iwdee

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03160.g3~

COPY_EXISTING ~baldur.bcs~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~!Exists("c6harp")~ ~False()~
  COMPILE_BAF_TO_BCS
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// No Drow Avatars On Party In Underdark            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @317000 DESIGNATED 3170
GROUP @4
REQUIRE_PREDICATE NOT GAME_IS ~tutu tutu_totsc bgee~ @25 // non-Tutu
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~howparty.2da~ @25 // non-iwdee

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03170.g3~

COPY_EXISTING ~ar2102.bcs~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~ActionOverride(Player[1-6],ApplySpell(Myself,DROW_CHANGE))~ ~~
  COMPILE_BAF_TO_BCS
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Romance Cheats                                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @318300 DESIGNATED 3183
GROUP @4
REQUIRE_PREDICATE GAME_IS ~bgt soa tob bg2ee~ @25 // non-Tutu
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~howparty.2da~ @25 // non-iwdee

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt3183.g3~

// make array for standard bg2 romances
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_romances BEGIN
  Aerie   => aeri25
  Anomen  => anom25
  Jaheira => jahe25
  Viconia => vico25
END

ACTION_IF GAME_IS ~bg2ee~ THEN BEGIN

  // add bg2ee npcs
  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_romances BEGIN
    Dorn    => dorn25
    Hexxat  => hexxa25
    Neera   => neer25
    Rasaad  => rasa25
  END

END

// fixes for Anomen's join dialogue
COMPILE ~bg2_tweaks/dlg/ano_rom_fix.d~

// questions to set options
OUTER_SET "proceed" = 0
OUTER_SET "req_race" = 0
OUTER_SET "req_gender" = 0
OUTER_SET "multiple" = 0
OUTER_SET "no_kill" = 0
OUTER_SET "start_tob" = 0

OUTER_WHILE ("%proceed%" STRING_COMPARE_REGEXP "[Aa]") BEGIN
  OUTER_WHILE ("%req_race%" STRING_COMPARE_REGEXP "[AaBb]") BEGIN
    PRINT @318301
    ACTION_READLN req_race
  END
  OUTER_WHILE ("%req_gender%" STRING_COMPARE_REGEXP "[AaBb]") BEGIN
    PRINT @318303
    ACTION_READLN req_gender
  END
  OUTER_WHILE ("%multiple%" STRING_COMPARE_REGEXP "[AaBb]") BEGIN
    PRINT @318305
    ACTION_READLN multiple
  END
  ACTION_IF ("%multiple%" STRING_COMPARE_CASE "a" = 0) THEN BEGIN // only offer no kill if multi-romance selected
    OUTER_WHILE ("%no_kill%" STRING_COMPARE_REGEXP "[AaBb]") BEGIN
      PRINT @318307
      ACTION_READLN no_kill
    END
  END
  ACTION_IF GAME_IS ~tob bg2ee~ THEN BEGIN // tob
    OUTER_WHILE ("%start_tob%" STRING_COMPARE_REGEXP "[AaBb]") BEGIN
      PRINT @318311
      ACTION_READLN start_tob
    END
  END
  // print summary of options before proceeding
  PRINT @318309
  ACTION_IF ("%req_race%" STRING_COMPARE_CASE "a" = 0) THEN BEGIN
    PRINT @318302
  END
  ACTION_IF ("%req_gender%" STRING_COMPARE_CASE "a" = 0) THEN BEGIN
    PRINT @318304
  END
  ACTION_IF ("%multiple%" STRING_COMPARE_CASE "a" = 0) THEN BEGIN
    PRINT @318306
  END
  ACTION_IF ("%no_kill%" STRING_COMPARE_CASE "a" = 0) THEN BEGIN
    PRINT @318308
  END
  ACTION_IF ("%start_tob%" STRING_COMPARE_CASE "a" = 0) THEN BEGIN
    PRINT @318312
  END
  OUTER_WHILE ("%proceed%" STRING_COMPARE_REGEXP "[AaBb]") BEGIN
    PRINT @318310
    ACTION_READLN proceed
  END
  ACTION_IF ("%proceed%" STRING_COMPARE_CASE "b" = 0) THEN BEGIN
    OUTER_SET "proceed" = 0
    OUTER_SET "req_race" = 0
    OUTER_SET "req_gender" = 0
    OUTER_SET "multiple" = 0
    OUTER_SET "no_kill" = 0
    OUTER_SET "start_tob" = 0
  END
END

// take care of tob_start
// this will also take care of any romance requirement adjustments on the ToB side while we're here
ACTION_IF ("%start_tob%" STRING_COMPARE_CASE "a" = 0) THEN BEGIN

  EXTEND_TOP ~aeri25.bcs~ ~bg2_tweaks/baf/aeri25_start_tob.baf~

  ACTION_IF GAME_IS ~bg2ee~ THEN BEGIN
    COMPILE ~bg2_tweaks/dlg/rom_start_tob_bg2ee.d~ // order is different
  END ELSE BEGIN
    COMPILE ~bg2_tweaks/dlg/rom_start_tob.d~
  END

  ACTION_IF ("%req_gender%" STRING_COMPARE_CASE "b" = 0) THEN BEGIN // if gender reqs intact

    COMPILE ~bg2_tweaks/dlg/rom_start_tob_gender.d~

  END

  ACTION_IF ("%req_race%" STRING_COMPARE_CASE "b" = 0) THEN BEGIN // if  race reqs intact

    COMPILE ~bg2_tweaks/dlg/rom_start_tob_race.d~

  END

  ACTION_IF ("%multiple%" STRING_COMPARE_CASE "b" = 0) THEN BEGIN // if no multi allowed

    COMPILE ~bg2_tweaks/dlg/rom_start_tob_multi.d~

  END

END

ACTION_IF (("%req_race%" STRING_COMPARE_CASE "a" = 0) OR ("%req_gender%" STRING_COMPARE_CASE "a" = 0)) BEGIN

  ACTION_PHP_EACH cd_romances AS npc => tob BEGIN
    
    EXTEND_TOP ~%npc%.bcs~ ~bg2_tweaks/baf/%npc%_rom_reqs.baf~
  
  END
      
  ACTION_IF ("%req_race%" STRING_COMPARE_CASE "a") BEGIN // if racial requirements intact (ee romances have no racial reqs)
  
    COPY_EXISTING ~aerie.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~\(Global("CDRomanceFoo","LOCALS",0)\)~
        ~\1 OR(5) Race(Player1,HUMAN) Race(Player1,HALF_ELF) Race(Player1,ELF) Race(Player1,HALFLING) Race(Player1,GNOME)~
      END
      BUT_ONLY_IF_IT_CHANGES
  
    COPY_EXISTING ~anomen.bcs~  ~override~
                  ~jaheira.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~\(Global("CDRomanceFoo","LOCALS",0)\)~
        ~\1 OR(4) Race(Player1,HUMAN) Race(Player1,HALF_ELF) Race(Player1,ELF) Race(Player1,HALFLING)~
      END
      BUT_ONLY_IF_IT_CHANGES
  
    COPY_EXISTING ~viconia.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~\(Global("CDRomanceFoo","LOCALS",0)\)~
        ~\1 OR(4) Race(Player1,HUMAN) Race(Player1,HALF_ELF) Race(Player1,HALFLING) Race(Player1,HALFORC)~
      END
      BUT_ONLY_IF_IT_CHANGES
  
  END
      
  ACTION_IF ("%req_gender%" STRING_COMPARE_CASE "a") BEGIN // if gender requirements intact
  
    ACTION_FOR_EACH npc IN aerie jaheira neera viconia BEGIN
  
      ACTION_IF FILE_EXISTS_IN_GAME ~%npc%.bcs~ THEN BEGIN
  
        COPY_EXISTING ~%npc%.bcs~ ~override~
          DECOMPILE_AND_PATCH BEGIN
            REPLACE_TEXTUALLY ~\(Global("CDRomanceFoo","LOCALS",0)\)~
            ~\1 Gender(Player1,MALE)~
          END
          BUT_ONLY_IF_IT_CHANGES
      
      END
    
    END
  
    ACTION_FOR_EACH npc IN anomen hexxat rasaad BEGIN
  
      ACTION_IF FILE_EXISTS_IN_GAME ~%npc%.bcs~ THEN BEGIN
  
        COPY_EXISTING ~%npc%.bcs~ ~override~
          DECOMPILE_AND_PATCH BEGIN
            REPLACE_TEXTUALLY ~\(Global("CDRomanceFoo","LOCALS",0)\)~
            ~\1 Gender(Player1,FEMALE)~
          END
          BUT_ONLY_IF_IT_CHANGES
      
      END
    
    END
  
  END
  
  ACTION_PHP_EACH cd_romances AS npc => tob BEGIN // cleanup dummy var
    
    COPY_EXISTING ~%npc%.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~Global("CDRomanceFoo","LOCALS",0)~ ~~
      END
      BUT_ONLY_IF_IT_CHANGES
  
  END

END

// allow multiple romances
ACTION_IF ("%multiple%" STRING_COMPARE_CASE "a" = 0) THEN BEGIN

  ACTION_PHP_EACH cd_romances AS npc => tob BEGIN
    
    COPY_EXISTING ~%npc%.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        PATCH_PHP_EACH cd_romances AS npc2 => tob2 BEGIN
          PATCH_IF ("%npc%" STRING_COMPARE_CASE "%npc2%") BEGIN // don't self-match
            REPLACE_TEXTUALLY ~\b\(Global("%npc2%RomanceActive","GLOBAL",[12])\)~ ~False()~
          END
        END
        PATCH_IF ("%no_kill%" STRING_COMPARE_CASE "a" = 0) BEGIN // nothing kiills romance option
          APPEND_FILE ~bg2_tweaks/baf/%npc%_rom3.baf~
        END
      END
      BUT_ONLY_IF_IT_CHANGES

    ACTION_IF GAME_IS ~tob bg2ee~ THEN BEGIN
    
      COPY_EXISTING ~%tob%.bcs~ ~override~
        DECOMPILE_AND_PATCH BEGIN
          PATCH_PHP_EACH cd_romances AS npc2 => tob2 BEGIN
            PATCH_IF ("%npc%" STRING_COMPARE_CASE "%npc2%") BEGIN // don't self-match
              REPLACE_TEXTUALLY ~\b\(Global("%npc2%RomanceActive","GLOBAL",[12])\)~ ~False()~
            END
          END
          REPLACE_TEXTUALLY ~GlobalGT("WraithPunish","GLOBAL",0)~ ~False()~
          APPEND_FILE ~bg2_tweaks/baf/%tob%_rom2.baf~
          PATCH_IF ("%no_kill%" STRING_COMPARE_CASE "a" = 0) BEGIN // nothing kiills romance option
            APPEND_FILE ~bg2_tweaks/baf/%tob%_rom3.baf~
          END
        END
        BUT_ONLY_IF_IT_CHANGES

    END

  END

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// rest anywhere                                    \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @319000 DESIGNATED 3190
GROUP @4

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03190.g3~

COPY_EXISTING_REGEXP GLOB ~^.+\.are$~ ~override~
  READ_BYTE  0x48 "flags"
  WRITE_BYTE 0x48 ("%flags%" BOR 0b10000000)
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// sellable items                                             \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @320000 DESIGNATED 3200
GROUP @4

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03200.g3~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_SHORT 0x1c "type"  ELSE 0 // 17 - mace, 18 - sling, 26 - staff, 37 - book
  READ_LONG  0x34 "price" ELSE 0
  PATCH_IF ((("%type%" = 17) OR ("%type%" = 18) OR ("%type%" = 26) OR ("%type%" = 37)) AND ("%price%" = 0)) BEGIN
    WRITE_LONG 0x34 1 // set price to 1 gp
  END
  BUT_ONLY_IF_IT_CHANGES

// stores which buy arrows now buy bolts (mainly affects BGT/Tutu)
COPY_EXISTING_REGEXP GLOB ~^.+\.sto$~ ~override~
  READ_LONG 0x2c "buy_off"   ELSE 0
  READ_LONG 0x30 "buy_num"   ELSE 0
  READ_LONG 0x34 "sale_off"  ELSE 0
  READ_LONG 0x4c "drink_off" ELSE 0
  READ_LONG 0x70 "cure_off"  ELSE 0
  SET "bolts" = 0
  FOR (index = 0 ; index < buy_num ; index = index + 1) BEGIN
    READ_LONG ("%buy_off%" + ("%index%" * 0x04)) "item"
    PATCH_IF ("%item%" = 5) BEGIN // arrows
      SET "bolts" = 1
    END ELSE
    PATCH_IF ("%item%" = 31) BEGIN // bolts
      SET "bolts" = 2
      SET "index" = "%buy_num%" // kills loop
    END
  END
  PATCH_IF ("%bolts%" = 1) BEGIN // if arrows but no bolts
    INSERT_BYTES "%buy_off%" 0x04
      WRITE_LONG "%buy_off%" 31
    WRITE_LONG 0x30 ("%buy_num%" + 1)
    // adjust offsets for older-formatted stores
    PATCH_IF ("%sale_off%" > "%buy_off%") BEGIN
      WRITE_LONG 0x34 ("%sale_off%" + 0x04)
    END
      PATCH_IF ("%drink_off%" > "%buy_off%") BEGIN
    WRITE_LONG 0x4c ("%drink_off%" + 0x04)
    END
    PATCH_IF ("%cure_off%" > "%buy_off%") BEGIN
      WRITE_LONG 0x70 ("%cure_off%" + 0x04)
    END
  END
  BUT_ONLY_IF_IT_CHANGES
  
// get prices from lookup table
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_prices BEGIN
  _blun01  =>    1 // club, tutu
  _blun10  => 2750 // root of the problem, Tutu
  _staf01  =>    1 // generic staff, tutu
  _sw1h13  =>  100 // xan's moonblade, tutu
  blun01   =>    1 // club
  blun10   => 2750 // root of the problem
  deck     => 2500 // deck of many things
  flolth   =>  575 // drow flail +3
  key20    =>   20 // king strohm iii's burial mask
  kuobolt  =>   30 // kuo-toa bolts
  kuobolt2 =>   30 // kuo-toa bolts
  kuobolt3 =>   30 // kuo-toa bolts
  misc3a1  => 3200 // book of infinite spells
  misc3a2  => 3200 // book of infinite spells
  misc3a3  => 3200 // book of infinite spells
  misc3a4  => 3200 // book of infinite spells
  misc3a5  => 3200 // book of infinite spells
  misc3a6  => 3200 // book of infinite spells
  misc3a7  => 3200 // book of infinite spells
  misc3a8  => 3200 // book of infinite spells
  misc3a9  =>  800 // book of infinite spells, final page
  misc3aa  => 3200 // book of infinite spells, 0 lore copy from BG2FP
  misc3c   => 2000 // efreeti bottle
  misc3h   => 1000 // horn of blasting
  misc3l   => 2000 // horn of silence
  misc3o   => 1000 // methlid's harp
  misc9q   =>   55 // habib's scimitar
  npmisc1  =>  200 // jan jansen's goggles
  npmisc2  =>  150 // jan jansen's gloves
  npshld   => 2000 // anomen's shield
  sahbolt  =>   30 // sahuagin bolts
  staf01   =>    1 // generic staff
  sw1h13   =>  100 // xan's moonblade
END

ACTION_PHP_EACH cd_prices AS item => price BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME "%item%.itm" BEGIN

    COPY_EXISTING "%item%.itm" ~override~
      READ_LONG  0x34 current
      PATCH_IF (current = 0) BEGIN // in case someone else sets a price
        WRITE_LONG 0x34 price
      END
      BUT_ONLY
  
  END

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Min Stats Tweak                                  \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @321000 DESIGNATED 3210
GROUP @4

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03210.g3~

OUTER_SET "min_stat" = 0
OUTER_WHILE ((NOT IS_AN_INT "%min_stat%") OR ("%min_stat%" < 6) OR ("%min_stat%" > 15)) BEGIN
    PRINT @321001
    ACTION_READLN min_stat
  END

COPY_EXISTING ~abclasrq.2da~ ~override~
  COUNT_2DA_ROWS ~7~ "rows"
  FOR (index = 0 ; index < rows ; index = index + 1) BEGIN
    FOR (index2 = 1 ; index2 < 7 ; index2 = index2 + 1) BEGIN
      READ_2DA_ENTRY index index2 7 "min"
      PATCH_IF ("%min%" < "%min_stat%") BEGIN
        SET_2DA_ENTRY index index2 7 "%min_stat%"
      END
    END
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Sensible Entrance Points                         \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @322000 DESIGNATED 3220
GROUP @4
REQUIRE_PREDICATE GAME_IS ~tutu tutu_totsc bgt bgee soa tob~ @25

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03220.g3~

ACTION_IF GAME_IS ~tutu tutu_totsc bgt bgee~ THEN BEGIN // BGT/Tutu/BGEE changes

  OUTER_SPRINT beregost "AR3300" // ee
  OUTER_SPRINT nfai     "AR1400" // ee
  OUTER_SPRINT zfarm    "AR0400" // ee
  OUTER_SPRINT shipw    "AR3100" // ee
  OUTER_SPRINT lhouse   "AR3600" // ee
  ACTION_IF (GAME_IS BGT) THEN BEGIN
    OUTER_SPRINT beregost "AR6700"
    OUTER_SPRINT nfai     "AR8300"
    OUTER_SPRINT zfarm    "AR7500"
    OUTER_SPRINT shipw    "AR9200"
    OUTER_SPRINT lhouse   "AR9500"
  END
  ACTION_IF (GAME_IS ~tutu tutu_totsc~) THEN BEGIN
    OUTER_SPRINT beregost "FW3300"
    OUTER_SPRINT nfai     "FW1400"
    OUTER_SPRINT zfarm    "FW0400"
    OUTER_SPRINT shipw    "FW3100"
    OUTER_SPRINT lhouse   "FW3600"
  END

  COPY_EXISTING ~worldmap.wmp~ ~override~
    READ_LONG 0x0c mos_off
    READ_LONG (mos_off + 0x20) area_num
    READ_LONG (mos_off + 0x24) area_off
    READ_LONG (mos_off + 0x28) link_off
    READ_LONG (mos_off + 0x2c) link_num
    FOR (index = 0 ; index < area_num ; ++index) BEGIN
      READ_ASCII (area_off + 0x08 + (index * 0xf0)) "area"
      PATCH_IF ("%area%" STRING_COMPARE_CASE "%beregost%" = 0) BEGIN // beregost
        SET bere_idx = index
      END
      PATCH_IF ("%area%" STRING_COMPARE_CASE "%nfai%" = 0) BEGIN // farm north of fai
        SET nfai_idx = index
      END
      PATCH_IF ("%area%" STRING_COMPARE_CASE "%zfarm%" = 0) BEGIN // zombie farm
        SET zfarm_idx = index
      END
      PATCH_IF ("%area%" STRING_COMPARE_CASE "%shipw%" = 0) BEGIN // shipwreck
        SET shipw_idx = index
      END
      PATCH_IF ("%area%" STRING_COMPARE_CASE "%lhouse%" = 0) BEGIN // lighthouse
        SET lhouse_idx = index
      END
    END
    // read links, adjust entry point for links to beregost
    FOR (index = 0 ; index < link_num ; ++index) BEGIN
      READ_LONG (link_off +        (index * 0xd8)) target
      PATCH_IF (target = lhouse_idx) BEGIN // if travel link to lighthouse
        READ_ASCII (link_off + 0x04 + (index * 0xd8)) entrance_name
        READ_BYTE  (link_off + 0x28 + (index * 0xd8)) target
        PATCH_IF (("%entrance_name%" STRING_COMPARE_CASE "" = 0) AND
                  (target = 1)) BEGIN // unnamed entrance point, north
          WRITE_ASCII (link_off + 0x04 + (index * 0xd8)) ~CDNorthExit~
        END
      END
      PATCH_IF (target = shipw_idx) BEGIN // if travel link to shipwreck
        READ_ASCII (link_off + 0x04 + (index * 0xd8)) entrance_name
        READ_BYTE  (link_off + 0x28 + (index * 0xd8)) target
        PATCH_IF (("%entrance_name%" STRING_COMPARE_CASE "" = 0) AND
                  (target = 4)) BEGIN // unnamed entrance point, south
          WRITE_ASCII (link_off + 0x04 + (index * 0xd8)) ~CDSouthExit~
        END
      END
      PATCH_IF (target = bere_idx) BEGIN // if travel link to beregost
        READ_ASCII (link_off + 0x04 + (index * 0xd8)) entrance_name
        READ_BYTE  (link_off + 0x28 + (index * 0xd8)) target
        PATCH_IF (("%entrance_name%" STRING_COMPARE_CASE "" = 0) AND
                  (target = 2)) BEGIN // unnamed entrance point, east
          WRITE_ASCII (link_off + 0x04 + (index * 0xd8)) ~CDEastExit~
        END
      END
      PATCH_IF (target = nfai_idx) BEGIN // if travel link to north fai
        READ_ASCII (link_off + 0x04 + (index * 0xd8)) entrance_name
        READ_BYTE  (link_off + 0x28 + (index * 0xd8)) target
        PATCH_IF (("%entrance_name%" STRING_COMPARE_CASE "" = 0) AND
                  (target = 4)) BEGIN // unnamed entrance point, south
          WRITE_ASCII (link_off + 0x04 + (index * 0xd8)) ~CDSouthExit~
        END
      END
      PATCH_IF (target = zfarm_idx) BEGIN // if travel link to zombie farm
        READ_ASCII (link_off + 0x04 + (index * 0xd8)) entrance_name
        READ_BYTE  (link_off + 0x28 + (index * 0xd8)) target
        PATCH_IF (("%entrance_name%" STRING_COMPARE_CASE "" = 0) AND
                  (target = 4)) BEGIN // unnamed entrance point, south
          WRITE_ASCII (link_off + 0x04 + (index * 0xd8)) ~CDSouthExit~
        END
      END
    END
    BUT_ONLY
  
  COPY_EXISTING ~%beregost%.are~ ~override~
    LPF fj_are_structure
      INT_VAR
      fj_loc_x       = 4950
      fj_loc_y       = 3085
      fj_orientation = 4   //W
      STR_VAR
      fj_structure_type = entrance
      fj_name           = CDEastExit
    END
  
  COPY_EXISTING ~%nfai%.are~ ~override~
    LPF fj_are_structure
      INT_VAR
      fj_loc_x       = 791
      fj_loc_y       = 3615
      fj_orientation = 9   //N
      STR_VAR
      fj_structure_type = entrance
      fj_name           = CDSouthExit
    END
  
  COPY_EXISTING ~%zfarm%.are~ ~override~
    LPF fj_are_structure
      INT_VAR
      fj_loc_x       = 1638
      fj_loc_y       = 3715
      fj_orientation = 8   //N
      STR_VAR
      fj_structure_type = entrance
      fj_name           = CDSouthExit
    END
  
  COPY_EXISTING ~%shipw%.are~ ~override~
    LPF fj_are_structure
      INT_VAR
      fj_loc_x       = 4957
      fj_loc_y       = 3516
      fj_orientation = 4   //N
      STR_VAR
      fj_structure_type = entrance
      fj_name           = CDSouthExit
    END
  
  COPY_EXISTING ~%lhouse%.are~ ~override~
    LPF fj_are_structure
      INT_VAR
      fj_loc_x       = 4941
      fj_loc_y       = 688
      fj_orientation = 4   //N
      STR_VAR
      fj_structure_type = entrance
      fj_name           = CDNorthExit
    END

END

ACTION_IF (GAME_IS ~BGT SOA TOB~) THEN BEGIN // BG2/BGT; skip for bg2ee

  DECOMPRESS_BIFF AREA090A.BIF
  
  INCLUDE ~bg2_tweaks/lib/functions.tpa~

  OUTER_SET width = 2368
  OUTER_SET sm_width = 148

  COPY_EXISTING ~ar0900.are~ ~override~
    LPF AREA_ALTER_ENTRANCE INT_VAR x_coord = 3259 y_coord = 3451 orient =  2 STR_VAR entry_name = "Exit0903" END // exchange entrances
    LPF AREA_ALTER_ENTRANCE INT_VAR x_coord = 4147 y_coord = 3342 orient = 14 STR_VAR entry_name = "Exi0903b" END
  
  ACTION_FOR_EACH file IN ar0903 BEGIN // in case other mods clone ar0903, we can drop them in this list

    ACTION_IF FILE_EXISTS_IN_GAME ~%file%.are~ THEN BEGIN

      COPY_EXISTING ~%file%.are~ ~override~
        LPF AREA_ALTER_ENTRANCE INT_VAR x_coord = 1964 y_coord = 1520 orient =  6 STR_VAR entry_name = "Exit0900" END // exchange entrances
        LPF AREA_ALTER_ENTRANCE INT_VAR x_coord =  476 y_coord = 1581 orient = 10 STR_VAR entry_name = "Exi0900b" END
        READ_LONG  0x54 act_off
        READ_SHORT 0x58 act_num
        FOR (index = 0 ; index < act_num ; ++index) BEGIN
          WRITE_SHORT (act_off + 0x20 + (index * 0x110)) (width - THIS) // current x
          WRITE_SHORT (act_off + 0x24 + (index * 0x110)) (width - THIS) // travel x
          WRITE_SHORT (act_off + 0x34 + (index * 0x110)) (16 - THIS)   // orientation
        END
        READ_SHORT 0x5a trig_num
        READ_LONG  0x5c trig_off
        FOR (index = 0 ; index < trig_num ; ++index) BEGIN
          READ_SHORT  (trig_off + 0x22 + (index * 0xc4)) left  // flip bounding box
          READ_SHORT  (trig_off + 0x26 + (index * 0xc4)) right // flip bounding box
          WRITE_SHORT (trig_off + 0x22 + (index * 0xc4)) (width - right) // flip bounding box
          WRITE_SHORT (trig_off + 0x26 + (index * 0xc4)) (width - left)  // flip bounding box
          WRITE_SHORT (trig_off + 0x70 + (index * 0xc4)) (width - THIS) // launch point
          WRITE_SHORT (trig_off + 0x84 + (index * 0xc4)) (width - THIS) // activation point
        END
        READ_LONG  0x70 cont_off
        READ_SHORT 0x74 cont_num
        FOR (index = 0 ; index < cont_num ; ++index) BEGIN
          WRITE_SHORT (cont_off + 0x20 + (index * 0xc0)) (width - THIS) // location
          WRITE_SHORT (cont_off + 0x34 + (index * 0xc0)) (width - THIS) // launch point
          READ_SHORT  (cont_off + 0x38 + (index * 0xc0)) left  // flip bounding box
          READ_SHORT  (cont_off + 0x3c + (index * 0xc0)) right // flip bounding box
          WRITE_SHORT (cont_off + 0x38 + (index * 0xc0)) (width - right) // flip bounding box
          WRITE_SHORT (cont_off + 0x3c + (index * 0xc0)) (width - left)  // flip bounding box
        END
        READ_SHORT 0x82 amb_num
        READ_LONG  0x84 amb_off
        FOR (index = 0 ; index < amb_num ; ++index) BEGIN
          WRITE_SHORT (amb_off + 0x20 + (index * 0xd4)) (width - THIS) // location
          // go ahead and enable horse ambient while we're here
          READ_ASCII  (amb_off +        (index * 0xd4)) name //
          PATCH_IF ("%name%" STRING_COMPARE_CASE "SS_Horse" = 0) BEGIN
            WRITE_BYTE (amb_off + 0x90 + (index * 0xd4)) (THIS | (BIT0 + BIT3)) // enabled, random order
          END
        END
        READ_LONG  0xac anim_num
        READ_LONG  0xb0 anim_off
        FOR (index = 0 ; index < anim_num ; ++index) BEGIN
          WRITE_SHORT (anim_off + 0x20 + (index * 0x4c)) (width - THIS) // location
        END
        READ_SHORT 0x80 vert_num
        READ_LONG  0x7c vert_off
        FOR (index = 0 ; index < vert_num ; ++index) BEGIN
          WRITE_SHORT (vert_off +        (index * 0x04)) (width - THIS) // location
        END
        READ_LONG  0x64 spwn_num
        READ_LONG  0x60 spwn_off
        FOR (index = 0 ; index < spwn_num ; ++index) BEGIN
          WRITE_SHORT (spwn_off + 0x20 + (index * 0xc8)) (width - THIS) // location
        END
        READ_LONG  0xc8 note_num
        READ_LONG  0xc4 note_off
        FOR (index = 0 ; index < note_num ; ++index) BEGIN
          WRITE_SHORT (note_off +        (index * 0x34)) (width - THIS) // location
        END
        READ_LONG  0xd0 proj_num
        READ_LONG  0xcc proj_off
        FOR (index = 0 ; index < proj_num ; ++index) BEGIN
          WRITE_SHORT (proj_off + 0x14 + (index * 0x1c)) (width - THIS) // location
        END
        // doors get special treatment
        READ_LONG 0xa4 door_num
        READ_LONG 0xa8 door_off
        FOR (index = 0 ; index < door_num ; ++index) BEGIN
          READ_SHORT  (door_off + 0x38 + (index * 0xc8)) left  // flip bounding box (open)
          READ_SHORT  (door_off + 0x3c + (index * 0xc8)) right // flip bounding box (open)
          WRITE_SHORT (door_off + 0x38 + (index * 0xc8)) (width - right) // flip bounding box (open)
          WRITE_SHORT (door_off + 0x3c + (index * 0xc8)) (width - left)  // flip bounding box (open)
          READ_SHORT  (door_off + 0x40 + (index * 0xc8)) left  // flip bounding box (closed)
          READ_SHORT  (door_off + 0x44 + (index * 0xc8)) right // flip bounding box (closed)
          WRITE_SHORT (door_off + 0x40 + (index * 0xc8)) (width - right) // flip bounding box (closed)
          WRITE_SHORT (door_off + 0x44 + (index * 0xc8)) (width - left)  // flip bounding box (closed)
          WRITE_SHORT (door_off + 0x74 + (index * 0xc8)) (width - THIS) // launch point
          WRITE_SHORT (door_off + 0x90 + (index * 0xc8)) (width - THIS) // open location
          WRITE_SHORT (door_off + 0x94 + (index * 0xc8)) (width - THIS) // closed location
          // since impeded vertices use search map coordinates, need to undo the adjustment above and re-do
          READ_LONG  (door_off + 0x48 + (index * 0xc8)) vert_idx  // impeded/open
          READ_SHORT (door_off + 0x4c + (index * 0xc8)) vert_num  // impeded/open
          FOR (index2 = vert_idx ; index2 < (vert_idx + vert_num) ; ++index2) BEGIN
            WRITE_SHORT (vert_off +        (index2 * 0x04)) (sm_width - (width - THIS)) // location
          END
          READ_LONG  (door_off + 0x50 + (index * 0xc8)) vert_idx  // impeded/closed
          READ_SHORT (door_off + 0x4e + (index * 0xc8)) vert_num  // impeded/closed
          FOR (index2 = vert_idx ; index2 < (vert_idx + vert_num) ; ++index2) BEGIN
            WRITE_SHORT (vert_off +        (index2 * 0x04)) (sm_width - (width - THIS)) // location
          END
        END
        LPF fj_are_structure
          INT_VAR
          fj_loc_x          = 1723
          fj_loc_y          = 1553
          fj_dest_x         = 1723
          fj_dest_y         = 1553
          fj_animation      = 45312 // horse
          fj_orientation    = 6      //SSE
          STR_VAR
          fj_structure_type = actor
          fj_name           = "Atta Girl"
          fj_cre_resref     = cdhorse1
        END
        LPF fj_are_structure
          INT_VAR
          fj_loc_x          = 2000
          fj_loc_y          = 1349
          fj_dest_x         = 2000
          fj_dest_y         = 1349
          fj_animation      = 45312 //horse
          fj_orientation    = 6      //SSE
          STR_VAR
          fj_structure_type = actor
          fj_name           = Angel
          fj_cre_resref     = cdhorse2
        END

    END

  END

  COPY_EXISTING ~horse.cre~ ~override/cdhorse1.cre~
    SAY 0x08 @322001
    SAY 0x0c @322001

  COPY_EXISTING ~horse.cre~ ~override/cdhorse2.cre~
    SAY 0x08 @322002
    SAY 0x0c @322002

  ACTION_FOR_EACH file IN ar0903 cut11a c6spwn1 c6spwn2 c#bjcut1 BEGIN // compatibility

    ACTION_IF FILE_EXISTS_IN_GAME ~%file%.bcs~ THEN BEGIN

      COPY_EXISTING ~%file%.bcs~ ~override~
        DECOMPILE_AND_PATCH BEGIN
          REPLACE_EVALUATE ~\[\([0-9]+\)\.\([0-9]+\)\]~
            BEGIN SET "RESULT" = (width - %MATCH1%) END
            ~[%RESULT%.%MATCH2%]~
          REPLACE_EVALUATE ~Color(\[\([0-9]+\)\.\([0-9]+\)\]~ // don't mess up area fades
            BEGIN SET "RESULT" = (width - %MATCH1%) END
            ~Color([%RESULT%.%MATCH2%]~
        END
    END

  END
  
  // re compatibiity
  ACTION_IF FILE_EXISTS_IN_GAME ~arre07.are~ THEN BEGIN

    COPY_EXISTING ~arre07.are~ ~override~
      READ_LONG  0xac anim_num
      READ_LONG  0xb0 anim_off
      FOR (index = 0 ; index < anim_num ; ++index) BEGIN
        WRITE_SHORT (anim_off + 0x20 + (index * 0x4c)) (width - THIS) // location
      END

  END

  COMPILE ~bg2_tweaks/dlg/ar0903.d~
  
  LAF HANDLE_TILESETS END // decompresses ar0903.tiz from bg2_tweaks/tiz to the override

  COPY ~bg2_tweaks/ar0903~ ~override~

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Adjust Evil joinable NPC reaction rolls          \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @400000 DESIGNATED 4000
GROUP @0
REQUIRE_PREDICATE GAME_IS ~tutu tutu_totsc bgt bgee~ @25 // Tutu or BGT only

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt04000.g3~

ACTION_IF GAME_IS ~bgt~ THEN BEGIN // BGT

  COMPILE ~bg2_tweaks/dlg/bgt_evilnpc.d~

END ELSE BEGIN // Tutu

  ACTION_IF GAME_IS ~tutu tutu_totsc~ THEN BEGIN // tutu

    COMPILE ~bg2_tweaks/dlg/evilnpc.d~
    
  END ELSE BEGIN // bgee
  
    COMPILE ~bg2_tweaks/dlg/bgee_evilnpc.d~

  END

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Improved Fate Spirit Summoning                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @401000 DESIGNATED 4010
GROUP @0
REQUIRE_PREDICATE     GAME_IS ~tob bg2ee~   @25 // ToB required

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt04010.g3~

// end of SoA cutscenes set extra variable
COPY_EXISTING ~cut100a.bcs~ ~override~
              ~cut100b.bcs~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~MultiPlayerSync()~ ~SetGlobal("G#G3.CompletedSoA","GLOBAL",1) MultiPlayerSync()~
  COMPILE_BAF_TO_BCS
  UNLESS ~SetGlobal("G#G3.CompletedSoA","GLOBAL",1)~
  BUT_ONLY_IF_IT_CHANGES

ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_fate_spirit BEGIN
  ~aerie~    => ~aerie~
  ~anomen~   => ~anomen~
  ~cernd~    => ~cernd~
  ~d#silver~ => ~silverstar~
  ~d0alassa~ => ~alassa~
  ~dorn~     => ~dorn_fsp~
  ~edwin~    => ~fsp_edwin~
  ~fwghar~   => ~ghareth~
  ~haerdali~ => ~haer~
  ~hexxat~   => ~hexxat~
  ~imoen~    => ~imoen~
  ~imoen2~   => ~imoen~
  ~j#klsy~   => ~kelsey~
  ~jaheira~  => ~jaheira~
  ~jan~      => ~jan~
  ~jcbru~    => ~bruce_jc~
  ~keldorn~  => ~keldorn~
  ~kido~     => ~kido~
  ~kindrek~  => ~kindrek~
  ~korgan~   => ~fsp_korgan~
  ~mazzy~    => ~mazzy~
  ~minsc~    => ~fsp_minsc~
  ~nalia~    => ~nalia~
  ~neera~    => ~neera~
  ~o#xans~   => ~xan~
  ~p#deh~    => ~deheriana~
  ~p#kiv01~  => ~fsp_kivan~
  ~rasaad~   => ~rasaad~
  ~sc#hub~   => ~hubelpot~
  ~sola~     => ~sola~
  ~subru~    => ~bruce_bons~
  ~tashia~   => ~tashia~
  ~valygar~  => ~fsp_valygar~
  ~vanim~    => ~vanim~
  ~viconia~  => ~viconia~
END

ACTION_PHP_EACH cd_fate_spirit AS script => extension BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME "%script%.bcs" BEGIN

    EXTEND_BOTTOM ~%script%.bcs~    ~bg2_tweaks/baf/%extension%.baf~
    
  END

END

COPY_EXISTING ~fatesp.dlg~ ~override~
  DECOMPILE_DLG_TO_D
    REPLACE_TEXTUALLY ~~~~~!Dead("\([^"]+\)")[ |%TAB%|%LNL%|%MNL%|%WNL%]*Global("\([^"]+\)","GLOBAL",0)~ THEN REPLY #\([0-9]+\)~~~~~
    ~~~~~Dead("\1")~ THEN REPLY #\3 DO ~SetGlobal("\2","GLOBAL",2)~ GOTO 7
    IF ~!Dead("\1") Global("\2","GLOBAL",0)~ THEN REPLY #\3~~~~~
  COMPILE_D_TO_DLG
  BUT_ONLY_IF_IT_CHANGES

// add extra triggers to fate spirit dialogue
COMPILE ~bg2_tweaks/dlg/fatesp.d~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// ToB-Style NPCs                                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @402000 DESIGNATED 4020
GROUP @0

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt04020.g3~

// load patch macro
INCLUDE ~bg2_tweaks/lib/tob_style_npcs.tpa~

/*
ACTION_CLEAR_ARRAY cd_tob_style_npcs
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_tob_style_npcs BEGIN
  ~_ajanti4~  =>  ~_ajanti~
  ~_ajanti6~  =>  ~_ajanti~
  ~_alora6~   =>  ~_alora~
  ~_branwe5~  =>  ~_branwe~
  ~_coran5~   =>  ~_coran~
  ~_dynahe2~  =>  ~_dynahe~
  ~_dynahe4~  =>  ~_dynahe~
  ~_dynahe6~  =>  ~_dynahe~
  ~_edwin2~   =>  ~_edwin~
  ~_edwin4~   =>  ~_edwin~
  ~_edwin6~   =>  ~_edwin~
  ~_faldor5~  =>  ~_faldor~
  ~_garric2~  =>  ~_garric~
  ~_garric4~  =>  ~_garric~
  ~_garric6~  =>  ~_garric~
  ~_imoen2~   =>  ~_imoen1~
  ~_imoen4~   =>  ~_imoen1~
  ~_imoen6~   =>  ~_imoen1~
  ~_jaheir2~  =>  ~_jaheir~
  ~_jaheir4~  =>  ~_jaheir~
  ~_jaheir6~  =>  ~_jaheir~
  ~_kagain2~  =>  ~_kagain~
  ~_kagain4~  =>  ~_kagain~
  ~_kagain6~  =>  ~_kagain~
  ~_khalid2~  =>  ~_khalid~
  ~_khalid4~  =>  ~_khalid~
  ~_khalid6~  =>  ~_khalid~
  ~_kivan4~   =>  ~_kivan~
  ~_kivan6~   =>  ~_kivan~
  ~_minsc2~   =>  ~_minsc~
  ~_minsc4~   =>  ~_minsc~
  ~_minsc6~   =>  ~_minsc~
  ~_montar2~  =>  ~_montar~
  ~_montar4~  =>  ~_montar~
  ~_montar6~  =>  ~_montar~
  ~_quayle4~  =>  ~_quayle~
  ~_quayle6~  =>  ~_quayle~
  ~_safana4~  =>  ~_safana~
  ~_safana6~  =>  ~_safana~
  ~_sharte4~  =>  ~_sharte~
  ~_sharte6~  =>  ~_sharte~
  ~_skie6~    =>  ~_skie~
  ~_tiax4~    =>  ~_tiax~
  ~_tiax6~    =>  ~_tiax~
  ~_viconi4~  =>  ~_viconi~
  ~_viconi6~  =>  ~_viconi~
  ~_xan4~     =>  ~_xan~
  ~_xan6~     =>  ~_xan~
  ~_xzar2~    =>  ~_xzar~
  ~_xzar4~    =>  ~_xzar~
  ~_xzar6~    =>  ~_xzar~
  ~_yeslic5~  =>  ~_yeslic~
  ~aerie10~   =>  ~aerie6~
  ~aerie11~   =>  ~aerie6~
  ~aerie12~   =>  ~aerie6~
  ~aerie7~    =>  ~aerie6~
  ~aerie9~    =>  ~aerie6~
  ~ajanti4~   =>  ~ajanti~
  ~ajanti6~   =>  ~ajanti~
  ~alora6~    =>  ~alora~
  ~anomen10~  =>  ~anomen6~
  ~anomen12~  =>  ~anomen6~
  ~anomen7~   =>  ~anomen6~
  ~anomen8~   =>  ~anomen6~
  ~anomen9~   =>  ~anomen6~
  ~branwe5~   =>  ~branwe~
  ~c!dela11~  =>  ~c!dela10~
  ~c!dela13~  =>  ~c!dela10~
  ~c!dela15~  =>  ~c!dela10~
  ~cernd12~   =>  ~cernd10~
  ~cernd13~   =>  ~cernd10~
  ~cernd13b~  =>  ~cernd10~
  ~cernd14~   =>  ~cernd10~
  ~chloe13~   =>  ~chloe7~
  ~chloe15~   =>  ~chloe7~
  ~chloe9~    =>  ~chloe7~
  ~coran5~    =>  ~coran~
  ~d0alas25~  =>  ~d0alassa~
  ~dorn10~    =>  ~dorn8~
  ~dorn12~    =>  ~dorn8~
  ~dorn14~    =>  ~dorn8~
  ~dorn2~     =>  ~dorn~
  ~dorn4~     =>  ~dorn~
  ~dorn6~     =>  ~dorn~
  ~dorn9~     =>  ~dorn8~
  ~dynahe2~   =>  ~dynahe~
  ~dynahe4~   =>  ~dynahe~
  ~dynahe6~   =>  ~dynahe~
  ~edwin11~   =>  ~edwin7~
  ~edwin12~   =>  ~edwin7~
  ~edwin13~   =>  ~edwin7~
  ~edwin15~   =>  ~edwin7~
  ~edwin2~    =>  ~edwin~
  ~edwin4~    =>  ~edwin~
  ~edwin6~    =>  ~edwin~
  ~edwin9~    =>  ~edwin7~
  ~faldor5~   =>  ~faldor~
  ~fwghar25~  =>  ~fwghar04~
  ~garric2~   =>  ~garric~
  ~garric4~   =>  ~garric~
  ~garric6~   =>  ~garric~
  ~haer11~    =>  ~haer10~
  ~haer13~    =>  ~haer10~
  ~haer15~    =>  ~haer10~
  ~haer19~    =>  ~haer10~
  ~imoen15~   =>  ~imoen10~
  ~imoen2~    =>  ~imoen1~
  ~imoen211~  =>  ~imoen10~
  ~imoen213~  =>  ~imoen10~
  ~imoen4~    =>  ~imoen1~
  ~imoen61~   =>  ~imoen1~
  ~j#indi03~  =>  ~j#indi01~
  ~j#indi04~  =>  ~j#indi01~
  ~j#klsy11~  =>  ~j#klsy09~
  ~j#klsy12~  =>  ~j#klsy09~
  ~j#klsy13~  =>  ~j#klsy09~
  ~j#klsy25~  =>  ~j#klsy09~
  ~jahei14~   =>  ~jaheir7~
  ~jaheir11~  =>  ~jaheir7~
  ~jaheir12~  =>  ~jaheir7~
  ~jaheir2~   =>  ~jaheir~
  ~jaheir4~   =>  ~jaheir~
  ~jaheir6~   =>  ~jaheir~
  ~jaheir8~   =>  ~jaheir7~
  ~jan10~     =>  ~jan8~
  ~jan11~     =>  ~jan8~
  ~jan12~     =>  ~jan8~
  ~jan15~     =>  ~jan8~
  ~jcbruce2~  =>  ~jcbruce~
  ~kagain2~   =>  ~kagain~
  ~kagain4~   =>  ~kagain~
  ~kagain6~   =>  ~kagain~
  ~keldor10~  =>  ~keldor8~
  ~keldor12~  =>  ~keldor8~
  ~keldor14~  =>  ~keldor8~
  ~keldor9~   =>  ~keldor8~
  ~keto10~    =>  ~keto08~
  ~keto12~    =>  ~keto08~
  ~khalid2~   =>  ~khalid~
  ~khalid4~   =>  ~khalid~
  ~khalid6~   =>  ~khalid~
  ~kido25~    =>  ~kido~
  ~kindre25~  =>  ~kindrek~
  ~kivan4~    =>  ~kivan~
  ~kivan6~    =>  ~kivan~
  ~korgan11~  =>  ~korgan8~
  ~korgan12~  =>  ~korgan8~
  ~korgan15~  =>  ~korgan8~
  ~korgan9~   =>  ~korgan8~
  ~m#ambr12~  =>  ~m#ambr10~
  ~m#ambr14~  =>  ~m#ambr10~
  ~m#ambr15~  =>  ~m#ambr10~
  ~mazzy11~   =>  ~mazzy8~
  ~mazzy12~   =>  ~mazzy8~
  ~mazzy15~   =>  ~mazzy8~
  ~mazzy9~    =>  ~mazzy8~
  ~mgwika25~  =>  ~mgwika~
  ~minsc10~   =>  ~minsc7~
  ~minsc12~   =>  ~minsc7~
  ~minsc15~   =>  ~minsc7~
  ~minsc2~    =>  ~minsc~
  ~minsc4~    =>  ~minsc~
  ~minsc6~    =>  ~minsc~
  ~minsc8~    =>  ~minsc7~
  ~minsc9~    =>  ~minsc7~
  ~montar2~   =>  ~montar~
  ~montar4~   =>  ~montar~
  ~montar6~   =>  ~montar~
  ~nalia10~   =>  ~nalia8~
  ~nalia11~   =>  ~nalia8~
  ~nalia13~   =>  ~nalia8~
  ~nalia15~   =>  ~nalia8~
  ~nalia18~   =>  ~nalia8~
  ~nath08~    =>  ~nath07~
  ~nath10~    =>  ~nath07~
  ~neera10~   =>  ~neera8~
  ~neera11~   =>  ~neera8~
  ~neera12~   =>  ~neera8~
  ~neera13~   =>  ~neera8~
  ~neera15~   =>  ~neera8~
  ~neera2~    =>  ~neera~
  ~neera4~    =>  ~neera~
  ~neera6~    =>  ~neera~
  ~o#xan11~   =>  ~o#xan09~
  ~o#xan12~   =>  ~o#xan09~
  ~o#xan13~   =>  ~o#xan09~
  ~ohhfak10~  =>  ~ohhfak8~
  ~ohhfak11~  =>  ~ohhfak8~
  ~ohhfak13~  =>  ~ohhfak8~
  ~ohhfak15~  =>  ~ohhfak8~
  ~ohhfak9~   =>  ~ohhfak8~
  ~ohhhex10~  =>  ~ohhhex8~
  ~ohhhex11~  =>  ~ohhhex8~
  ~ohhhex13~  =>  ~ohhhex8~
  ~ohhhex15~  =>  ~ohhhex8~
  ~ohhhex25~  =>  ~ohhhex8~
  ~ohhhex9~   =>  ~ohhhex8~
  ~quayle4~   =>  ~bgquayle~
  ~quayle6~   =>  ~bgquayle~
  ~r!kit09~   =>  ~r!kit07~
  ~r!kit11~   =>  ~r!kit07~
  ~r!kit13~   =>  ~r!kit07~
  ~r!kit15~   =>  ~r!kit07~
  ~rasaad10~  =>  ~rasaad8~
  ~rasaad11~  =>  ~rasaad8~
  ~rasaad12~  =>  ~rasaad8~
  ~rasaad13~  =>  ~rasaad8~
  ~rasaad2~   =>  ~rasaad~
  ~rasaad4~   =>  ~rasaad~
  ~rasaad6~   =>  ~rasaad~
  ~rasaad9~   =>  ~rasaad8~
  ~safana4~   =>  ~safana~
  ~safana6~   =>  ~safana~
  ~sc#hub25~  =>  ~sc#hub~
  ~sdnpc18~   =>  ~sdnpc12~
  ~sharte4~   =>  ~sharte~
  ~sharte6~   =>  ~sharte~
  ~sk#neht3~  =>  ~sk#neht1~
  ~sk#neht5~  =>  ~sk#neht1~
  ~skie6~     =>  ~skie~
  ~sola10~    =>  ~sola5~
  ~sola12~    =>  ~sola5~
  ~sola15~    =>  ~sola5~
  ~sola17~    =>  ~sola5~
  ~sola7~     =>  ~sola5~
  ~subru09~   =>  ~subru05~
  ~subru17~   =>  ~subru05~
  ~sufinch2~  =>  ~sufinch1~
  ~sufinch4~  =>  ~sufinch1~
  ~tiax4~     =>  ~tiax~
  ~tiax6~     =>  ~tiax~
  ~tlxantob~  =>  ~tlxan~
  ~valyg11~   =>  ~valyg8~
  ~valyg12~   =>  ~valyg8~
  ~valyg14~   =>  ~valyg8~
  ~valyg9~    =>  ~valyg8~
  ~viconi11~  =>  ~viconi6~
  ~viconi13~  =>  ~viconi6~
  ~viconi16~  =>  ~viconi6~
  ~viconi4~   =>  ~viconi~
  ~viconi61~  =>  ~viconi~
  ~viconi8~   =>  ~viconi6~
  ~viconi9~   =>  ~viconi6~
  ~willyb2~   =>  ~willyb~
  ~xan4~      =>  ~bgxan~
  ~xan6~      =>  ~bgxan~
  ~xzar2~     =>  ~bgxzar~
  ~xzar4~     =>  ~bgxzar~
  ~xzar6~     =>  ~bgxzar~
  ~yeslic5~   =>  ~yeslic~
  ~yoshi10~   =>  ~yoshi7~
  ~yoshi11~   =>  ~yoshi7~
  ~yoshi12~   =>  ~yoshi7~
  ~yoshi8~    =>  ~yoshi7~
END

// avoid BGEE, BGT namespace collisions
ACTION_IF GAME_IS ~bgee~ BEGIN

  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_tob_style_npcs BEGIN
    imoen6   => imoen1
    quayle4  => quayle
    quayle6  => quayle
    viconi6  => viconi
    xan4     => xan
    xan6     => xan
    xzar2    => xzar
    xzar4    => xzar
    xzar6    => xzar
  END

END

ACTION_PHP_EACH cd_tob_style_npcs AS dest => orig BEGIN

  ACTION_IF ((FILE_EXISTS_IN_GAME ~%orig%.cre~) AND (FILE_EXISTS_IN_GAME ~%dest%.cre~)) THEN BEGIN

    COPY_EXISTING ~%orig%.cre~ ~override/%dest%.cre~
      LAUNCH_PATCH_MACRO ~tob_style_npcs~
      BUT_ONLY_IF_IT_CHANGES

  END

END
*/

ACTION_IF GAME_IS ~tob bg2ee~ BEGIN

  OUTER_SET col_min = 3
  
END ELSE BEGIN

  OUTER_SET col_min = 2

END

COPY_EXISTING ~interdia.2da~ ~override~
  COUNT_2DA_ROWS col_min rows
  FOR (index = 0 ; index < rows ; ++index) BEGIN
    READ_2DA_ENTRY index 0 col_min curdv //
    DEFINE_ASSOCIATIVE_ARRAY cd_dv_array BEGIN "%curdv%" => "cdtob_%curdv%" END
    INNER_ACTION BEGIN
    
      COPY ~bg2_tweaks/lib/tob_lookup.txt~ ~override/cdtob_%curdv%.txt~

    END
  END
  BUT_ONLY

COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
  READ_LONG 0x1cc bio ELSE "-1"
  PATCH_IF (bio >= 0) BEGIN // don't bother if no bio
    READ_ASCII 0x280 "dvcre" (32) NULL
    PHP_EACH cd_dv_array AS dv => file BEGIN
      PATCH_IF ("%dv%" STRING_COMPARE_CASE "%dvcre%" = 0) BEGIN
        READ_LONG 0x18 xp
        INNER_ACTION BEGIN
          APPEND ~%file%.txt~ ~%SOURCE_RES% %xp%~
        END
      END
    END
  END
  BUT_ONLY


ACTION_PHP_EACH cd_dv_array AS dv => file BEGIN

  COPY_EXISTING ~%file%.txt~ ~override~
    COUNT_2DA_ROWS 2 rows
    PATCH_IF (rows > 1) BEGIN // don't bother if only one creature
      READ_2DA_ENTRY 0 0 2 orig
      READ_2DA_ENTRY 0 1 2 xp
      FOR (index = 1 ; index < rows ; ++index) BEGIN
        READ_2DA_ENTRY index 1 2 xp_test
        PATCH_IF (xp_test < xp) BEGIN
          READ_2DA_ENTRY index 0 2 orig
          SET xp = xp_test
        END
      END
      FOR (index = 0 ; index < rows ; ++index) BEGIN
        READ_2DA_ENTRY index 0 2 dest
        INNER_ACTION BEGIN
  
          ACTION_IF ((FILE_EXISTS_IN_GAME ~%orig%.cre~) AND (FILE_EXISTS_IN_GAME ~%dest%.cre~)) THEN BEGIN
  
            COPY_EXISTING ~%orig%.cre~ ~override/%dest%.cre~
              PATCH_PRINT " -- copying %orig% to %dest%..."
              LAUNCH_PATCH_MACRO ~tob_style_npcs~
              BUT_ONLY_IF_IT_CHANGES
  
          END
          
        END
      END
    END
    BUT_ONLY

  MOVE ~override/%file%.txt~ ~bg2_tweaks/lib~

END


/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Stat changes: Edwin                              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// BG Values                                        \\\\\
/////                                                  \\\\\

BEGIN @403001 DESIGNATED 4030 // Use BG Stats
GROUP @0
SUBCOMPONENT @403000 // Stat Changes: Edwin
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~howparty.2da~ @25

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt04030.g3~

COPY_EXISTING_REGEXP GLOB ~^_?edwin[0-9]*\.cre$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x2d3) THEN BEGIN // protects against invalid files
    WRITE_BYTE 0x238 9 // 9 STR
    WRITE_BYTE 0x23b 9 // 9 WIS
  END
  BUT_ONLY_IF_IT_CHANGES

/////                                                  \\\\\
///// BG2 Values                                       \\\\\
/////                                                  \\\\\

BEGIN @403100 DESIGNATED 4031 // Use BG2 Stats
GROUP @0
SUBCOMPONENT @403000 // Stat Changes: Edwin
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~howparty.2da~ @25

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt04031.g3~

COPY_EXISTING_REGEXP GLOB ~^_?edwin[0-9]*\.cre$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x2d3) THEN BEGIN // protects against invalid files
    WRITE_BYTE 0x238 10 // 10 STR
    WRITE_BYTE 0x23b 10 // 10 WIS
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Stat changes: Jaheira                            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// BG Values                                        \\\\\
/////                                                  \\\\\

BEGIN @403001 DESIGNATED 4040 // Use BG Stats
GROUP @0
SUBCOMPONENT @404000 // Stat Changes: Jaheira
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~howparty.2da~ @25

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt04040.g3~

COPY_EXISTING_REGEXP GLOB ~^_?jahei.*\.cre$~ ~override~
  READ_LONG 0x08 "strref" ELSE 0
  PATCH_IF (("%strref%" = 9456) OR ("%strref%" = 9475)) THEN BEGIN // exclude harper cre files
    WRITE_BYTE 0x23c 14 // 14 DEX
  END
  BUT_ONLY_IF_IT_CHANGES

ACTION_IF FILE_EXISTS_IN_GAME misc5x.itm BEGIN

  COPY_EXISTING ~misc5x.itm~ ~override~ // harper pin
    WRITE_BYTE 0x2c 14 // min dex
    BUT_ONLY_IF_IT_CHANGES

END

/////                                                  \\\\\
///// BG2 Values                                       \\\\\
/////                                                  \\\\\

BEGIN @403100 DESIGNATED 4041 // Use BG2 Stats
GROUP @0
SUBCOMPONENT @404000 // Stat Changes: Jaheira
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~howparty.2da~ @25

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt04041.g3~

COPY_EXISTING_REGEXP GLOB ~^_?jahei.*\.cre$~ ~override~
  READ_LONG 0x08 "strref" ELSE 0
  PATCH_IF (("%strref%" = 9456) OR ("%strref%" = 9475)) THEN BEGIN // exclude harper cre files
    WRITE_BYTE 0x23c 17 // 17 DEX
  END
  BUT_ONLY_IF_IT_CHANGES

ACTION_IF FILE_EXISTS_IN_GAME misc5x.itm BEGIN

  COPY_EXISTING ~misc5x.itm~ ~override~ // harper pin
    WRITE_BYTE 0x2c 17 // min dex
    BUT_ONLY_IF_IT_CHANGES

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Change Jaheira to NG                             \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @405000 DESIGNATED 4050
GROUP @0
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~howparty.2da~ @25

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt04050.g3~

COPY_EXISTING_REGEXP GLOB ~^_?jahei.*\.cre$~ ~override~
  READ_LONG 0x08  "strref" ELSE 0
  READ_BYTE 0x27b "align"  ELSE 0
  PATCH_IF ((("%strref%" = 9456) OR ("%strref%" = 9475)) AND ("%align%" = 34)) THEN BEGIN // exclude harper cre files
    WRITE_BYTE 0x27b 33 // neutral good
  END
  BUT_ONLY_IF_IT_CHANGES

ACTION_IF FILE_EXISTS_IN_GAME misc5x.itm BEGIN

  COPY_EXISTING ~misc5x.itm~ ~override~ // harper pin
    READ_BYTE  0x1e "use1"
    READ_BYTE  0x21 "use2"
    WRITE_BYTE 0x1e (("%use1%" BOR 0b00001000) BAND 0b11111011) // adds GE-Neutral flag, removes good flag
    WRITE_BYTE 0x21 ("%use2%" BOR 0b10000000) // adds half-orc flag
    BUT_ONLY_IF_IT_CHANGES

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Stat changes: Minsc                              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// BG Values                                        \\\\\
/////                                                  \\\\\

BEGIN @403001 DESIGNATED 4060 // Use BG Stats
GROUP @0
SUBCOMPONENT @406000 // Stat Changes: Minsc
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~howparty.2da~ @25

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt04060.g3~

COPY_EXISTING_REGEXP GLOB ~^_?minsc[0-9]*\.cre$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x2d3) THEN BEGIN // protects against invalid files
    WRITE_BYTE 0x23c 15 // 15 DEX
    WRITE_BYTE 0x23d 15 // 15 CON
  END
  BUT_ONLY_IF_IT_CHANGES

/////                                                  \\\\\
///// BG2 Values                                       \\\\\
/////                                                  \\\\\

BEGIN @403100 DESIGNATED 4061 // Use BG2 Stats
GROUP @0
SUBCOMPONENT @406000 // Stat Changes: Minsc
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~howparty.2da~ @25

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt04061.g3~

COPY_EXISTING_REGEXP GLOB ~^_?minsc[0-9]*\.cre$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x2d3) THEN BEGIN // protects against invalid files
    WRITE_BYTE 0x23c 16 // 16 DEX
    WRITE_BYTE 0x23d 16 // 16 CON
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Stat changes: Viconia                            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// BG Values                                        \\\\\
/////                                                  \\\\\

BEGIN @403001 DESIGNATED 4070 // Use BG Stats
GROUP @0
SUBCOMPONENT @407000 // Stat Changes: Viconia
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~howparty.2da~ @25

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt04070.g3~

COPY_EXISTING_REGEXP GLOB ~^\(bg\|_\)?viconi[0-9]*\.cre$~ ~override~
  READ_BYTE  0x5d  "mr" ELSE 50
  PATCH_IF ("%mr%" != 50) BEGIN
    WRITE_BYTE 0x5d 50
  END
  READ_BYTE  0x23b "wis" ELSE 15
  PATCH_IF ("%wis%" != 15) BEGIN
    WRITE_BYTE 0x23b 15 // 15 WIS
    READ_LONG  0x2a8 "mem_info"
    FOR (index = 1 ; index < 4 ; index = index + 1) BEGIN // adds level 2, 3, 4 bonus spell for 15 >> 18 WIS
      READ_LONG ("%mem_info%" + 0x04 + ("%index%" * 0x10)) "spells"
      PATCH_IF ("%spells%" > 0) BEGIN
        WRITE_LONG ("%mem_info%" + 0x04 + ("%index%" * 0x10)) ("%spells%" + 1)
      END
    END
  END
  BUT_ONLY_IF_IT_CHANGES

/////                                                  \\\\\
///// BG2 Values                                       \\\\\
/////                                                  \\\\\

BEGIN @403100 DESIGNATED 4071 // Use BG2 Stats
GROUP @0
SUBCOMPONENT @407000 // Stat Changes: Viconia
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~howparty.2da~ @25

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt04071.g3~

COPY_EXISTING_REGEXP GLOB ~^\(bg\|_\)?viconi[0-9]*\.cre$~ ~override~
  READ_BYTE  0x5d  "mr" ELSE 65
  PATCH_IF ("%mr%" != 65) BEGIN
    WRITE_BYTE 0x5d 65
  END
  READ_BYTE  0x23b "wis" ELSE 18
  PATCH_IF ("%wis%" != 18) BEGIN
    WRITE_BYTE 0x23b 18 // 18 WIS
    READ_LONG  0x2a8 "mem_info"
    FOR (index = 1 ; index < 4 ; index = index + 1) BEGIN // removes level 2, 3, 4 bonus spell for 18 >> 15 WIS
      READ_LONG ("%mem_info%" + 0x04 + ("%index%" * 0x10)) "spells"
      PATCH_IF ("%spells%" > 0) BEGIN
        WRITE_LONG ("%mem_info%" + 0x04 + ("%index%" * 0x10)) ("%spells%" - 1)
      END
    END
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Make Khalid a Fighter-Mage                       \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @408000 DESIGNATED 4080
GROUP @0
REQUIRE_PREDICATE GAME_IS ~tutu tutu_totsc bgt bgee~ @25 // Tutu or BGT only

OUTER_SPRINT tutu_var ~~
ACTION_IF (GAME_IS ~tutu tutu_totsc~) BEGIN
  OUTER_SPRINT tutu_var ~_~
END

ACTION_IF NOT GAME_IS ~tutu tutu_totsc~ THEN BEGIN // BGT, BGEE

  // this file does nothing, it just allows other mods to detect this component
  COPY_EXISTING ~khalid2.cre~ ~override/cdt04080.g3~
    READ_ASCII 0xa4 "soundset" (396)

  COPY ~bg2_tweaks/cre/p#khali1.cre~ ~override/khalid.cre~
       ~bg2_tweaks/cre/p#khali2.cre~ ~override/khalid2.cre~
       ~bg2_tweaks/cre/p#khali3.cre~ ~override/khalid4.cre~
       ~bg2_tweaks/cre/p#khali4.cre~ ~override/khalid6.cre~
    SAY NAME1 #3138
    SAY NAME2 #3138
    WRITE_EVALUATED_ASCII 0xa4 "%soundset%" #396
    SAY BIO @408001
    WRITE_ASCII 0x280 ~khalid~  // death variable
    WRITE_ASCII 0x248 ~khalid~  // override script
    WRITE_ASCII 0x250 ~shout~   // class script
    WRITE_ASCII 0x260 ~wtasight~// general script
    WRITE_ASCII 0x268 ~dplayer~ // default script
    WRITE_ASCII 0x2cc ~khalid~  // dialogue file
    ADD_CRE_ITEM ~BOW03~  #0  #0 #0 ~IDENTIFIED~ ~WEAPON2~ EQUIP TWOHANDED
    ADD_CRE_ITEM ~AROW01~ #40 #0 #0 ~IDENTIFIED~ ~QUIVER2~
    ADD_CRE_ITEM ~HELM01~ #0  #0 #0 ~IDENTIFIED~ ~HELMET~
    ADD_CRE_ITEM ~SW1H01~ #0  #0 #0 ~IDENTIFIED~ ~INV5~
    ADD_CRE_ITEM ~POTN08~ #0  #0 #0 ~IDENTIFIED~ ~QITEM1~

  COPY_EXISTING ~khalid2.cre~ ~override~
                ~khalid4.cre~ ~override~
                ~khalid6.cre~ ~override~
    ADD_CRE_ITEM ~BRAC03~ #0 #0 #0 ~IDENTIFIED~ ~GLOVES~

  COPY_EXISTING ~khalid4.cre~ ~override~
                ~khalid6.cre~ ~override~
    ADD_CRE_ITEM ~CLCK14~ #0 #0 #0 ~IDENTIFIED~ ~CLOAK~

  COPY_EXISTING ~khalid6.cre~ ~override~
    ADD_CRE_ITEM ~RING06~ #0 #0 #0 ~IDENTIFIED~ ~LRING~

  COMPILE ~bg2_tweaks/dlg/p#khalid_bgt.d~ USING ~bg2_tweaks/languages/%s/p#khalid.tra~

  ACTION_IF GAME_IS ~bgee~ THEN BEGIN // BGEE
    EXTEND_BOTTOM ~ar2301.bcs~ ~bg2_tweaks/baf/p#fw2301.baf~ EVALUATE_BUFFER
  END ELSE BEGIN // bgt
    EXTEND_BOTTOM ~ar6801.bcs~ ~bg2_tweaks/baf/p#fw2301.baf~ EVALUATE_BUFFER
  END

  EXTEND_TOP    ~khalid.bcs~ ~bg2_tweaks/baf/p#khalid.baf~

END ELSE BEGIN // Tutu

  // this file does nothing, it just allows other mods to detect this component
  // extra code is to detect walking speeds from tutufix
  COPY_EXISTING ~_KHALID2.cre~ ~override/cdt04080.g3~
    READ_ASCII 0xa4 "soundset" (396) // copies existing soundset
    READ_BYTE  0x33 "eff_version"
    READ_LONG  0x2c4 "effects_offset"
    READ_LONG  0x2c8 "fx_num"
    PATCH_IF ("%eff_version%" = 0) BEGIN
      FOR (loops = 0 ; loops < fx_num ; loops = loops + 1) BEGIN
        READ_SHORT ("%effects_offset%" + ("%loops%" * 0x30)) "type"
        PATCH_IF (("%type%" = 176) OR ("%type%" = 126)) BEGIN
          SET "loops" = "%fx_num%"
          SET "eff_version" = 4
        END
      END
    END ELSE BEGIN
      FOR (loops = 0 ; loops < fx_num ; loops = loops + 1) BEGIN
        READ_LONG ("%effects_offset%" + 8 + ("%loops%" * 264)) "type"
        PATCH_IF (("%type%" = 176) OR ("%type%" = 126)) BEGIN
          SET "loops" = "%fx_num%"
          SET "eff_version" = 4
        END
      END
    END

  COPY ~bg2_tweaks/cre/p#khali1.cre~ ~override/_khalid.cre~
       ~bg2_tweaks/cre/p#khali2.cre~ ~override/_khalid2.cre~
       ~bg2_tweaks/cre/p#khali3.cre~ ~override/_khalid4.cre~
       ~bg2_tweaks/cre/p#khali4.cre~ ~override/_khalid6.cre~
    SAY NAME1 #3138
    SAY NAME2 #3138
    WRITE_EVALUATED_ASCII 0xa4 "%soundset%" #396
    SAY BIO @408001
    WRITE_ASCII 0x280 ~khalid~   // death variable
    WRITE_ASCII 0x248 ~_khalid~  // override script
    WRITE_ASCII 0x250 ~_shout~   // class script
    WRITE_ASCII 0x260 ~_tasight~ // general script
    WRITE_ASCII 0x268 ~_dplayer~ // default script
    WRITE_ASCII 0x2cc ~_khalid~  // dialogue file
    ADD_CRE_ITEM ~_bow03~  #0  #0 #0 ~IDENTIFIED~ ~WEAPON2~ EQUIP TWOHANDED
    ADD_CRE_ITEM ~_arow01~ #40 #0 #0 ~IDENTIFIED~ ~QUIVER2~
    ADD_CRE_ITEM ~_helm01~ #0  #0 #0 ~IDENTIFIED~ ~HELMET~
    ADD_CRE_ITEM ~_sw1h01~ #0  #0 #0 ~IDENTIFIED~ ~INV5~
    ADD_CRE_ITEM ~_potn08~ #0  #0 #0 ~IDENTIFIED~ ~QITEM1~
    PATCH_IF ("%eff_version%" = 4) BEGIN
      READ_LONG  0x2b0 "known_offset"
      READ_LONG  0x2b8 "slots_offset"
      READ_LONG  0x2bc "items_offset"
      READ_LONG  0x2c4 "effects_offset"
      READ_LONG  0x2c8 "fx_num"
      WRITE_LONG 0x2c8 ("%fx_num%" + 1)
      PATCH_IF ("%known_offset%" >= "%effects_offset%") BEGIN
        WRITE_LONG 0x2b0 ("%known_offset%" + 264)
      END
      PATCH_IF ("%slots_offset%" >= "%effects_offset%") BEGIN
        WRITE_LONG 0x2b8 ("%slots_offset%" + 264)
      END
      PATCH_IF ("%items_offset%" >= "%effects_offset%") BEGIN
        WRITE_LONG 0x2bc ("%items_offset%" + 264)
      END
      INSERT_BYTES  ("%effects_offset%"     ) 264
        WRITE_LONG  ("%effects_offset%" +  8) 176
        WRITE_LONG  ("%effects_offset%" + 12) 1
        WRITE_LONG  ("%effects_offset%" + 20) 0xfffffffd
        WRITE_LONG  ("%effects_offset%" + 24) 0
        WRITE_LONG  ("%effects_offset%" + 28) 9
        WRITE_SHORT ("%effects_offset%" + 36) 100
    END

  COPY_EXISTING ~_khalid2.cre~ ~override~
                ~_khalid4.cre~ ~override~
                ~_khalid6.cre~ ~override~
    ADD_CRE_ITEM ~_brac03~ #0 #0 #0 ~IDENTIFIED~ ~GLOVES~

  COPY_EXISTING ~_khalid4.cre~ ~override~
                ~_khalid6.cre~ ~override~
    ADD_CRE_ITEM ~_clck14~ #0 #0 #0 ~IDENTIFIED~ ~CLOAK~

  COPY_EXISTING ~_khalid6.cre~ ~override~
    ADD_CRE_ITEM ~_ring06~ #0 #0 #0 ~IDENTIFIED~ ~LRING~

  COMPILE ~bg2_tweaks/dlg/p#khalid.d~

  EXTEND_BOTTOM ~_ar2301.bcs~ ~bg2_tweaks/baf/p#fw2301.baf~ EVALUATE_BUFFER  
  EXTEND_TOP    ~_khalid.bcs~ ~bg2_tweaks/baf/p#khalid.baf~

END

// if bg-style proficiencies are installed, adjust proficiencies to match new system
ACTION_IF FILE_EXISTS_IN_GAME ~cdt02161.g3~ OR FILE_EXISTS_IN_GAME ~cdt02162.g3~ THEN BEGIN
  
  COPY_EXISTING ~%tutu_var%khalid.cre~  ~override~
                ~%tutu_var%khalid2.cre~ ~override~
                ~%tutu_var%khalid4.cre~ ~override~
                ~%tutu_var%khalid6.cre~ ~override~
    PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN
      SET_BG2_PROFICIENCY ~UNASSIGNED104~ 0
      SET_BG2_PROFICIENCY ~PROFICIENCYLARGESWORD~ 2
    END
    BUT_ONLY
  
  COPY_EXISTING ~%tutu_var%khalid2.cre~ ~override~
                ~%tutu_var%khalid4.cre~ ~override~
                ~%tutu_var%khalid6.cre~ ~override~
    PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN
      SET_BG2_PROFICIENCY ~UNASSIGNED106~ 0
      SET_BG2_PROFICIENCY ~PROFICIENCYMISSILEWEAPONS~ 1
    END
    BUT_ONLY
END

// if tob-style npcs are installed, then adjust higher level creatures
ACTION_IF FILE_EXISTS_IN_GAME ~cdt04020.g3~ THEN BEGIN

  // load patch macro
  INCLUDE ~bg2_tweaks/lib/tob_style_npcs.tpa~
  
  ACTION_CLEAR_ARRAY cd_tob_style_npcs
  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_tob_style_npcs BEGIN
    ~_khalid~  => ~_khalid2~
    ~_khalid~  => ~_khalid4~
    ~_khalid~  => ~_khalid6~
    ~khalid~   => ~khalid2~
    ~khalid~   => ~khalid4~
    ~khalid~   => ~khalid6~
  END
  
  ACTION_PHP_EACH cd_tob_style_npcs AS orig => dest BEGIN
  
    ACTION_IF ((FILE_EXISTS_IN_GAME ~%orig%.cre~) AND (FILE_EXISTS_IN_GAME ~%dest%.cre~)) THEN BEGIN

      COPY_EXISTING ~%orig%.cre~ ~override/%dest%.cre~
        LAUNCH_PATCH_MACRO ~tob_style_npcs~
        BUT_ONLY_IF_IT_CHANGES
  
    END

  END

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Make Montaron an Assassin                        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @409000 DESIGNATED 4090
GROUP @0
REQUIRE_PREDICATE GAME_IS ~tutu tutu_totsc bgt bgee~ @25 // Tutu or BGT only

ACTION_IF NOT GAME_IS ~tutu tutu_totsc~ THEN BEGIN // BGT, BGEE

  // this file does nothing, it just allows other mods to detect this component
  COPY_EXISTING ~montar.cre~ ~override/cdt04090.g3~
    READ_ASCII 0xa4 "soundset" (396)

  COPY ~bg2_tweaks/cre/_montar.cre~  ~override/montar.cre~
       ~bg2_tweaks/cre/_montar2.cre~ ~override/montar2.cre~
       ~bg2_tweaks/cre/_montar4.cre~ ~override/montar4.cre~
       ~bg2_tweaks/cre/_montar6.cre~ ~override/montar6.cre~
    SAY NAME1 #2425
    SAY NAME2 #2425
    WRITE_EVALUATED_ASCII 0xa4 "%soundset%" #396
    WRITE_ASCII 0x34  ~montars~ #8   // small portrait
    WRITE_ASCII 0x3c  ~montarm~ #8   // large portrait
    WRITE_ASCII 0x248 ~montaron~     // Override Script
    WRITE_ASCII 0x250 ~shout~        // Class Script
    WRITE_ASCII 0x258 ~~ #8          // Race Script
    WRITE_ASCII 0x260 ~wtasight~     // General Script
    WRITE_ASCII 0x268 ~dplayer~ #8   // default Script
    WRITE_ASCII 0x280 ~montaron~ #18 // Death Variable
    WRITE_ASCII 0x2cc ~montar~ #8    // Dialogue file
    // use BGT item references instead of Tutu
    READ_LONG  0x2bc "itm_off" ELSE 0
    READ_LONG  0x2c0 "itm_num" ELSE 0
    FOR (index = 0 ; index < itm_num ; index = index + 1) BEGIN // searches through items
      READ_ASCII ("%itm_off%" + (0x14 * "%index%")) "item"
      PATCH_IF ("%item%" STRING_COMPARE_CASE "_leat04" = 0) BEGIN // find invalid resref
        WRITE_ASCII ("%itm_off%" + (0x14 * "%index%")) "leat04" #8 // corrected resref
      END ELSE
      PATCH_IF ("%item%" STRING_COMPARE_CASE "_sw1h07" = 0) BEGIN // find invalid resref
        WRITE_ASCII ("%itm_off%" + (0x14 * "%index%")) "sw1h07" #8 // corrected resref
      END ELSE
      PATCH_IF ("%item%" STRING_COMPARE_CASE "_potn08" = 0) BEGIN // find invalid resref
        WRITE_ASCII ("%itm_off%" + (0x14 * "%index%")) "potn08" #8 // corrected resref
      END ELSE
      PATCH_IF ("%item%" STRING_COMPARE_CASE "_potn14" = 0) BEGIN // find invalid resref
        WRITE_ASCII ("%itm_off%" + (0x14 * "%index%")) "potn14" #8 // corrected resref
      END
    END

END ELSE BEGIN // Tutu

  // this file does nothing, it just allows other mods to detect this component
  // extra code is to detect walking speeds from tutufix
  COPY_EXISTING ~_montar.cre~ ~override/cdt04090.g3~
    READ_ASCII 0xa4 "soundset" (396)
    READ_BYTE  0x33 "eff_version"
    READ_LONG  0x2c4 "effects_offset"
    READ_LONG  0x2c8 "fx_num"
    PATCH_IF ("%eff_version%" = 0) BEGIN
      FOR (loops = 0 ; loops < fx_num ; loops = loops + 1) BEGIN
        READ_SHORT ("%effects_offset%" + ("%loops%" * 0x30)) "type"
        PATCH_IF (("%type%" = 176) OR ("%type%" = 126)) BEGIN
          SET "loops" = "%fx_num%"
          SET "eff_version" = 4
        END
      END
    END ELSE BEGIN
      FOR (loops = 0 ; loops < fx_num ; loops = loops + 1) BEGIN
        READ_LONG ("%effects_offset%" + 8 + ("%loops%" * 264)) "type"
        PATCH_IF (("%type%" = 176) OR ("%type%" = 126)) BEGIN
          SET "loops" = "%fx_num%"
          SET "eff_version" = 4
        END
      END
    END

  COPY ~bg2_tweaks/cre/_montar.cre~  ~override~
       ~bg2_tweaks/cre/_montar2.cre~ ~override~
       ~bg2_tweaks/cre/_montar4.cre~ ~override~
       ~bg2_tweaks/cre/_montar6.cre~ ~override~
    SAY NAME1 #2425
    SAY NAME2 #2425
    WRITE_EVALUATED_ASCII 0xa4 "%soundset%" #396
    WRITE_ASCII 0x34 ~_ontars~   // small portrait
    WRITE_ASCII 0x3c ~_ontarl~   // large portrait
    WRITE_ASCII 0x248 ~_ontaron~ // Override Script
    WRITE_ASCII 0x250 ~_shout~   // Class Script
    WRITE_ASCII 0x258 ~~ #8      // Race Script
    WRITE_ASCII 0x260 ~_tasight~ // General Script
    WRITE_ASCII 0x268 ~_dplayer~ // defalt Script
    WRITE_ASCII 0x280 ~montaron~ // Death Variable
    WRITE_ASCII 0x2cc ~_montar~  // Dialogue file
    PATCH_IF ("%eff_version%" = 4) BEGIN
      READ_LONG  0x2b0 "known_offset"
      READ_LONG  0x2b8 "slots_offset"
      READ_LONG  0x2bc "items_offset"
      READ_LONG  0x2c4 "effects_offset"
      READ_LONG  0x2c8 "fx_num"
      WRITE_LONG 0x2c8 ("%fx_num%" + 1)
      PATCH_IF ("%known_offset%" >= "%effects_offset%") BEGIN
        WRITE_LONG 0x2b0 ("%known_offset%" + 264)
      END
      PATCH_IF ("%slots_offset%" >= "%effects_offset%") BEGIN
        WRITE_LONG 0x2b8 ("%slots_offset%" + 264)
      END
      PATCH_IF ("%items_offset%" >= "%effects_offset%") BEGIN
        WRITE_LONG 0x2bc ("%items_offset%" + 264)
      END
      INSERT_BYTES  ("%effects_offset%"     ) 264
        WRITE_LONG  ("%effects_offset%" +  8) 176
        WRITE_LONG  ("%effects_offset%" + 12) 1
        WRITE_LONG  ("%effects_offset%" + 20) 0xfffffffd
        WRITE_LONG  ("%effects_offset%" + 24) 0
        WRITE_LONG  ("%effects_offset%" + 28) 9
        WRITE_SHORT ("%effects_offset%" + 36) 100
    END

END

// if bg-style proficiencies are installed, adjust proficiencies to match new system
ACTION_IF FILE_EXISTS_IN_GAME ~cdt02161.g3~ OR FILE_EXISTS_IN_GAME ~cdt02162.g3~ THEN BEGIN
  OUTER_SPRINT tutu_var ~~
  ACTION_IF (GAME_IS ~tutu tutu_totsc~) BEGIN
    OUTER_SPRINT tutu_var ~_~
  END
  
  COPY_EXISTING ~%tutu_var%montar.cre~  ~override~
                ~%tutu_var%montar2.cre~ ~override~
                ~%tutu_var%montar4.cre~ ~override~
                ~%tutu_var%montar6.cre~ ~override~
    PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN
      SET_BG2_PROFICIENCY ~PROFICIENCYSPEAR~ 0
      SET_BG2_PROFICIENCY ~PROFICIENCYLARGESWORD~ 1
    END
    BUT_ONLY
  
  ACTION_IF FILE_EXISTS_IN_GAME ~cdt02162.g3~ THEN BEGIN // no weapon styles version
    COPY_EXISTING ~%tutu_var%montar4.cre~ ~override~
                  ~%tutu_var%montar6.cre~ ~override~
      PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN
        SET_BG2_PROFICIENCY ~UNASSIGNED113~ 0
        SET_BG2_PROFICIENCY ~PROFICIENCYMISSILEWEAPONS~ 1
      END
      BUT_ONLY
  END
END

// if tob-style npcs are installed, then adjust higher level creatures
ACTION_IF FILE_EXISTS_IN_GAME ~cdt04020.g3~ THEN BEGIN

  // load patch macro
  INCLUDE ~bg2_tweaks/lib/tob_style_npcs.tpa~
  
  ACTION_CLEAR_ARRAY cd_tob_style_npcs
  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_tob_style_npcs BEGIN
    _montar2 => _montar
    _montar4 => _montar
    _montar6 => _montar
    montar2  => montar
    montar4  => montar
    montar6  => montar
  END

  ACTION_PHP_EACH cd_tob_style_npcs AS dest => orig BEGIN

    ACTION_IF ((FILE_EXISTS_IN_GAME ~%orig%.cre~) AND (FILE_EXISTS_IN_GAME ~%dest%.cre~)) THEN BEGIN

      COPY_EXISTING ~%orig%.cre~ ~override/%dest%.cre~
        LAUNCH_PATCH_MACRO ~tob_style_npcs~
        BUT_ONLY_IF_IT_CHANGES

    END

  END

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Change Korgan to NE                              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @410000 DESIGNATED 4100
GROUP @0
REQUIRE_PREDICATE GAME_IS ~bgt soa tob bg2ee~ @25 // bg2 only

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt04100.g3~

COPY_EXISTING_REGEXP GLOB ~^korgan[0-9]*.*\.cre$~ ~override~
  WRITE_BYTE 0x27b 35 // neutral evil
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Kagain legal CON                                 \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @411000 DESIGNATED 4110
GROUP @0
REQUIRE_PREDICATE GAME_IS ~tutu tutu_totsc bgt bgee~ @25 // Tutu or BGT only

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt04110.g3~

COPY_EXISTING_REGEXP GLOB ~^_?kagain[0-9]*.*\.cre$~ ~override~
  WRITE_BYTE 0x23d 19 // constitution
  BUT_ONLY_IF_IT_CHANGES
  